
5 PCLEAR 1
10 DIM M(50),T(13)
14 '****************
15 '** note table **
16 '****************
20 FORB=1TO13:READT(B):NEXT
30 DATA 89,108,125,133,147,159,170,176
40 DATA 99,117,140,153,165
44 '***********************
45 '** keyboard graphics **
46 '***********************
50 W$=CHR$(207):WL$=CHR$(202):WR$=CHR$(197):W2$=W$+W$:W3$=W$+W2$:B$=CHR$(128):V$=CHR$(175)
60 C$=W2$+WL$+B$+WR$+W$+WL$+B$+WR$+W2$:D$=W3$+V$+W3$+V$+W3$
70 F$=W2$+WL$+B$+WR$+W$+WL$+B$+WR$+W$+WL$+B$+WR$+W2$:G$=W3$+V$+W3$+V$+W3$+V$+W3$:H$=W2$+WL$
80 Y$=CHR$(159):R$=CHR$(255):GR$=CHR$(223)
89 '***********************
90 '** introduction page **
91 '***********************
100 CLS0:PRINT@10,"*SIMON SAYS*";
110 PRINT@40,"*PLAY THIS TUNE*";
120 PRINT@96," A TUNE WILL BE DEVELOPED ONE    NOTE AT A TIME. YOU ARE TO      CORRECTLY PLAY BACK THE TUNE    BY PRESSING THE CORRECT KEYS."
130 PRINT@256," YOU MUST ANSWER QUICKLY!"
140 PRINT@326," ENTER YOUR NAME : ";:PRINT@352,;:INPUTN$
150 PRINT@450," PRESS K FOR KEYBOARD SETUP ";
160 PRINT@485," PRESS C TO CONTINUE ";
170 K$=INKEY$:M(1)=RND(13):IFK$=""THEN170
180 IFK$="K"THENC=1:GOTO196
190 IFK$="C"THENC=0:GOTO200
195 GOTO170
196 GOSUB800
197 '******************
198 '** level select **
199 '******************
200 CLS4
210 PRINT@10,"*SIMON SAYS*";
220 PRINT@40,"*PLAY THIS TUNE*";
230 PRINT@96," SELECT FROM THE FOLLOWING:";
240 PRINT@163," 1) NOVICE - - DIATONIC   ";
245 PRINT@195," 2) INTERMED - DIATONIC   ";
250 PRINT@227," 3) EXPERT - - DIATONIC   ";
255 PRINT@259," 4) NOVICE - - CHROMATIC  ";
260 PRINT@291," 5) INTERMED - CHROMATIC  ";
265 PRINT@323," 6) EXPERT - - CHROMATIC  ";
280 PRINT@122,;:INPUTR
290 IFR<1ORR>6THEN230
360 IFC=1THENC=0
370 GOSUB800
380 X=1
389 '**************************
390 '** computer note select **
391 '**************************
400 PRINT@38,STRING$(20,159);
410 PRINT@70,Y$+Y$+Y$+Y$"***LISTEN***"Y$+Y$+Y$+Y$;
420 PRINT@102,STRING$(20,159);
430 IFR<4THENM(X)=RND(8)ELSEM(X)=RND(13)
432 IFX>1ANDR=2THENGOSUB1500
433 IFX>1ANDR=5THENGOSUB1550
435 FORZ=1TO960:NEXT
440 FORY=1TOX
450 IFX=1ORR=1ORR=4THEN460ELSE470
460 GOSUB1000
470 SOUNDT(M(Y)),8
480 IFX=1ORR=1ORR=4THEN490ELSE495
490 GOSUB1050
495 NEXTY
497 '*******************
498 '** answer select **
499 '*******************
500 PRINT@38,STRING$(20,223);
510 PRINT@70,GR$+GR$;"***PLAY BACK!***"GR$+GR$;
520 PRINT@102,STRING$(20,223);
530 FORY=1TOX
540 T=1
550 K$=INKEY$:T=T+1:IFT>250THEN1200
560 IFK$=""THEN550
570 IFK$="Z"THENK=1:GOTO700
580 IFK$="X"THENK=2:GOTO700
590 IFK$="C"THENK=3:GOTO700
600 IFK$="V"THENK=4:GOTO700
610 IFK$="B"THENK=5:GOTO700
620 IFK$="N"THENK=6:GOTO700
630 IFK$="M"THENK=7:GOTO700
640 IFK$=","THENK=8:GOTO700
650 IFK$="S"THENK=9:GOTO700
660 IFK$="D"THENK=10:GOTO700
670 IFK$="G"THENK=11:GOTO700
680 IFK$="H"THENK=12:GOTO700
690 IFK$="J"THENK=13
700 IFM(Y)=K THEN710ELSE1200
710 GOSUB1000
720 SOUNDT(K),7
730 GOSUB1050
740 NEXTY
750 X=X+1
755 IFX=51THEN1600
760 GOTO400
797 '**************************
798 '** print piano keyboard **
799 '**************************
800 CLS3
810 PRINT@10,"*SIMON SAYS*";
820 PRINT@40,"*PLAY THIS TUNE*";
840 FORX=1TO4:Y=X*32+129:Z=X*32+257
850 PRINT@Y,C$;:PRINT@Y+12,F$;:PRINT@Y+28,H$;
860 PRINT@Z,D$;:PRINT@Z+12,G$;:PRINT@Z+28,W3$;:NEXT
870 IFC=1THENGOSUB900
880 RETURN
897 '***********************
898 '** computer keyboard **
899 '***********************
900 PRINT@100," COMPUTER KEYBOARD SETUP ";
910 PRINT@228,"S";:PRINT@232,"D";:PRINT@240,"G";:PRINT@244,"H";:PRINT@248,"J";
920 PRINT@354,"Z";:PRINT@358,"X";:PRINT@362,"C";:PRINT@366,"V";:PRINT@370,"B";:PRINT@374,"N";:PRINT@378,"M";:PRINT@382,",";
930 PRINT@485," PRESS C TO CONTINUE ";
940 K$=INKEY$:IFK$=""THEN940
950 IFK$="C"THEN RETURNELSE940
989 '************************
990 '** notes being played **
991 '************************
1000 IF M(Y)>10THEN1040
1010 IF M(Y)>8THEN1030
1020 PRINT@M(Y)*4+318,R$;:PRINT@M(Y)*4+350,R$;:RETURN
1030 PRINT@M(Y)*4+160,R$;:PRINT@M(Y)*4+192,R$;:RETURN
1040 PRINT@M(Y)*4+164,R$;:PRINT@M(Y)*4+196,R$;:RETURN
1047 '*********************
1048 '** return keyboard **
1049 '*********************
1050 IFM(Y)>10THEN1090
1060 IFM(Y)>8THEN1080
1070 PRINT@M(Y)*4+318,W$;:PRINT@M(Y)*4+350,W$;:RETURN
1080 PRINT@M(Y)*4+160,B$;:PRINT@M(Y)*4+192,B$;:RETURN
1090 PRINT@M(Y)*4+164,B$;:PRINT@M(Y)*4+196,B$;:RETURN
1197 '******************
1198 '** wrong answer **
1199 '******************
1200 PRINT@36,STRING$(24,255);
1210 PRINT@68,R$+R$+R$"  THAT NOTE WAS:  "R$+R$+R$;
1220 PRINT@100,STRING$(24,255);
1240 FORZ=1TO10
1250 GOSUB1000
1260 SOUND T(M(Y)),1
1270 GOSUB 1050
1280 SOUND T(M(Y)),1
1290 NEXT
1295 GOSUB1000
1297 '***********
1298 '** score **
1299 '***********
1300 IFX<6THEN1350
1310 IFX>5ANDX<11THEN1375
1315 IFX>10ANDX<16THEN1380
1317 IFX>15ANDX<21THEN1390
1320 IFX>20ANDX<26THEN1410
1330 IFX>25ANDX<31THEN1420
1340 IFX>30ANDX<41THEN1430
1345 IFX>40THEN1435
1350 PRINT@416," "N$", YOU DID POORLY!"
1360 PRINT@448," YOU ONLY MADE IT TO "X-1
1370 GOTO1440
1375 PRINT@416," "N$", YOU CAN DO BETTER.":GOTO1400
1380 PRINT@416," "N$",THAT WAS O.K. ":GOTO1400
1390 PRINT@416," "N$",THAT WAS GOOD!"
1400 PRINT@448," YOU MADE IT TO"X-1:GOTO1440
1410 PRINT@416," "N$", THAT WAS EXCELLENT!":GOTO1400
1420 PRINT@416," "N$", YOU ARE GREAT!":GOTO1400
1430 PRINT@416," "N$", YOU ARE FANTASTIC!":GOTO1400
1435 PRINT@416," "N$", **UNBELIEVABLE!!**":GOTO1400
1440 PRINT@485," PRESS P TO PLAY AGAIN ";
1450 K$=INKEY$:IFK$=""THEN1450
1460 IFK$="P"THEN200ELSE1450
1497 '***********************
1498 '** intermed routines **
1499 '***********************
1500 MX=M(X)-M(X-1)
1510 IFMX>4THENM(X)=M(X)-5
1520 IFMX<-4THENM(X)=M(X)+5
1530 RETURN
1550 MX=RND(3)
1560 IFMX=3THEN1580
1570 IFM(X)>8THENM(X)=M(X)-5
1580 RETURN
1597 '***************
1598 '** maximum # **
1599 '***************
1600 PRINT@416," "N$", *I GIVE UP!!*"
1610 FORZ=1TO250STEP5:SOUNDZ,1:NEXTZ
1620 FORZ=1TO10:SOUND240,1:SOUND250,1:NEXTZ
1630 GOTO1400
