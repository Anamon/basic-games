
1 'MATHPAL (32K VERSION)
2 'COPYRIGHT 1982 BY DAVE HOOPER
9 GOTO500
10 CLS:PRINT@489,"INSTRUCTIONS":PRINT:GOSUB2600:GOSUB2600:PRINT" PRESS 'E' TO ENTER YOUR ANSWER":GOSUB2600:PRINT" PRESS 'C' TO CLEAR YOUR ANSWER":GOSUB2600
11 PRINT" PRESS 'A' TO DISPLAY ANSWER":GOSUB2600:PRINT" PRESS '^' TO RETURN TO MENU":GOSUB2600:PRINT" PRESS 'I' TO RETURN HERE":PRINT" ENTER ALL ANSWERS FROM RIGHT":GOSUB2600:PRINT" TO LEFT":PRINT:GOSUB2600:PRINT:GOSUB2600:PRINT" PRESS SPACE BAR TO CON
12 IF PEEK(345)=247THENSCREEN1,0:RETURNELSE12
21 PUT(A,B)-(C,D),J,PSET:RETURN
22 PUT(A,B)-(C,D),A,PSET:RETURN
23 PUT(A,B)-(C,D),B,PSET:RETURN
24 PUT(A,B)-(C,D),C,PSET:RETURN
25 PUT(A,B)-(C,D),D,PSET:RETURN
26 PUT(A,B)-(C,D),E,PSET:RETURN
27 PUT(A,B)-(C,D),F,PSET:RETURN
28 PUT(A,B)-(C,D),G,PSET:RETURN
29 PUT(A,B)-(C,D),H,PSET:RETURN
30 PUT(A,B)-(C,D),I,PSET:RETURN
35 PLAY"V31;T255;L255;O4CDEFGABO5CDEFGABC"
40 PUT(0,G)-(24,V),M,PSET
45 G=G-40:V=V-40:RETURN
50 PCLS(3):G=172:V=192
55 ZT=INT(TT/10):Z1=TT-ZT*10:ZZ$="":RESTORE
60 FOR X=1 TO 10
62 READ B$
64 IF X=ZT THEN ZZ$=B$+ZZ$
66 IF X=Z1 OR (X=10 AND Z1=0) THEN ZZ$=ZZ$+B$
68 NEXT X
69 DRAW"S4;BM104,144"+ZZ$:PAINT(118,146),2,2
70 IF ZT>0 THEN PAINT(162,146),2,2
75 DRAW"S4;BM72,12"+SC$+SD$
80 FORH=18TO225STEP16
85 PUT(H-14,80)-(H+30,96),L,PSET:FORX=1TO100:NEXT:PLAY"V31;L255;T255;O1D"
100 PSET(H-12,96,2)
105 NEXTH
110 A$="V31;T30;O4CL2DEL1CP1CL2DEL1CP1L2CDECDECDEL1CP2"
115 B$="V31;T30;O4FL2GAL1FP1FL2GAL1FP1L2FGAFGAFGAL1FP2"
120 PLAYA$+B$:PLAYA$
125 C$="V31;T255;L255;O1CDEFGABO2CDEFGABO3CDEFGABO4CDEFGABO5CDEFGAB"
130 PLAYC$+C$:PLAYC$
135 RETURN
137 ON KK GOSUB 21,22,23,24,25,26,27,28,29,30:RETURN
140 T=0:X=0:Y=0:W=0
143 IF PEEK(341)=247THENPCLS(3):GOTO718
145 Q$=INKEY$:IF Q$=""THEN143
146 IFPEEK(339)=253THENGOSUB10
147 IF PEEK(341)=247THENPCLS(3):GOTO718
150 IFQ$="E"THENMS=W+(X*10)+(Y*100):RETURN
155 IFQ$="C"THENT=3:GOTO175
157 IFQ$="A"THENGOTO3000
160 IF T=0 THEN W=VAL(Q$):A=134:B=140:C=162:D=176:IF W=>0 THEN KK=W+1:GOSUB137
165 IF T=1 THEN X=VAL(Q$):A=90:C=118:IF X=>0 THEN KK=X+1:GOSUB 137
170 IF T=2 THEN Y=VAL(Q$):A=46:C=74:IF Y=>0 THEN KK=Y+1:GOSUB 137
175 IF T=3 THEN LINE(46,140)-(162,176),PRESET,BF:W=0:X=0:Y=0:T=0:GOTO145
180 T=T+1:GOTO145
200 FORT=1TO7
205 PLAY"V31;T255;L255;O1CEGBGECEGBGEC"
210 NEXT
215 LINE(46,140)-(162,176),PRESET,BF:RETURN
300 TN=RND(TI):BN=RND(BI)
302 IFM=2ANDCH$="A"THENTN=RND(9):BN=1ELSEIFM=2ANDCH$="B"THENTN=RND(9):BN=RND(9)ELSEIFM=2ANDCH$="C"THENTN=RND(99):BN=RND(9)
305 IF M=1 AND TN<BN THEN 300
306 IF M=2 AND TN<BN THEN 300
307 IF M=0 THEN S=TN+BN
308 IF M=1 THEN S=TN-BN
310 IF M=2 THEN S=TN*BN
315 E=INT(TN/10):F=INT(BN/10)
320 IF E>0 THEN H=(TN-E*10)ELSEH=-1
325 IF F>0 THEN I=(BN-F*10)ELSEI=-1
330 IF E<1 THEN A=134:B=28:C=162:D=64:KK=TN+1:GOSUB 137
335 IF E>0 THENA=90:B=28:C=118:D=64:KK=E+1:GOSUB 137
340 IF E>0 THEN A=134:C=162
345 IF H=>0 THEN KK=H+1:GOSUB 137
350 IF F<1 THEN A=134:B=80:C=162:D=116:KK=BN+1:GOSUB 137
355 IF F>0 THEN A=90:B=80:C=118:D=116:KK=F+1:GOSUB 137
360 IF F>0 THENA=134:C=162
365 IF I=>0 THEN KK=I+1:GOSUB 137
370 DRAW"S4;BM46,123R116D1L116D1R116"
375 IF M=0 THENDRAW"S8;BM64,104"+P$
377 IF M=1 THEN DRAW"S8;BM64,104"+M$
378 IF M=2 THEN DRAW"S8;BM64,104"+MM$
380 GOSUB140
385 IF MS><S THEN GOSUB 200:GOTO380
390 IF MS=S THEN TT=TT+1:P=P+1:GOSUB35:LINE(46,28)-(164,180),PRESET,BF
395 FOR LL=5 TO 75STEP5
400 IF TT=LL THEN GOSUB 50:GOTO406
404 NEXTLL
405 GOTO300
406 IF TT=15 OR TT=30 OR TT=45 OR TT=60 OR TT=75 THEN3100
407 IFPG$<>"AL"THEN:PCLS(3):P=0:GOTO300
410 IF M=0 THEN M=1:PCLS(3):P=0:GOTO300
412 IF M=1 THEN M=2:PCLS(3):P=0:GOTO300
413 IF M=2 THEN M=0:PCLS(3):P=0:GOTO300
415 GOTO300
500 PCLEAR4:PMODE3,1:PCLS(3):CLEAR300:DIMA(27),B(27),C(27),D(27),E(27),F(27),G(27),H(27),I(27),J(27),L(20),M(14)
505 COLOR2,3
515 DRAW"S4;C2;BM40,60BD8D3R1E1R3F4R9E4R3F2R1U5L3D1L11H1U3E1R6H3L13G2D5G1L1BD5BR2E1R1F1D1G1L1H1U1BR20E1R1F1D1G1L1H1U1"
520 PAINT(48,64),2,2:GET(24,60)-(68,76),L,G
525 LINE(76,108)-(74,114),PSET:LINE-(66,114),PSET:LINE-(72,119),PSET:LINE-(70,126),PSET:LINE-(76,121),PSET:LINE-(82,126),PSET:LINE-(80,119),PSET:LINE-(86,114),PSET:LINE-(78,114),PSET:LINE-(76,108),PSET:PAINT(76,118),2,2
530 GET(64,108)-(88,128),M,G
535 PCLS(3):GOSUB1015:SCREEN1,0
635 A=18:B=6:N=1
640 FORX=1TO20
643 IF X>10 THEN C=A+28:D=B+36:KK=N:GOSUB137:GOSUB2600:GOTO655
644 READ B$
645 DRAW"S4;BM"+STR$(A)+","+STR$(B)+B$
650 PAINT(A+14,B+2),2,2:GOSUB2600
653 ON N GOSUB 670,675,680,685,690,695,700,705,710,715
655 N=N+1:IF N>10 THEN N=1
660 A=A+44:IF A=238 THEN A=18:B=B+42
665 NEXTX:FORX=1TO10:SCREEN1,0:GOSUB2600:FORY=1TO50:NEXTY:SCREEN1,1:GOSUB2600:FORY=1TO50:NEXTY:NEXTX:SCREEN1,0:PCLS3:GOSUB10:GOTO720
670 GET(18,6)-(46,42),A,G:RETURN
675 GET(62,6)-(90,42),B,G:RETURN
680 GET(106,6)-(134,42),C,G:RETURN
685 GET(150,6)-(178,42),D,G:RETURN
690 GET(194,6)-(222,42),E,G:RETURN
695 GET(18,48)-(46,84),F,G:RETURN
700 GET(62,48)-(90,84),G,G:RETURN
705 GET(106,48)-(134,84),H,G:RETURN
710 GET(150,48)-(178,84),I,G:RETURN
715 GET(194,48)-(222,84),J,G:RETURN
718 TT=0
720 CLS:PRINT@487,"WHAT GRADE LEVEL?":GOSUB2600:PRINT:PRINT"       a. GRADE  1 THRU 2":GOSUB2600:PRINT:PRINT"       b. GRADE  2 THRU 3":GOSUB2600:PRINT:PRINT"       c. GRADE  3 THRU 5":GOSUB2600:FORD=1TO4:PRINT:GOSUB2600:NEXT
725 INPUT"        ENTER CHOICE NOW";CH$:IFCH$=""THEN725
735 IFCH$="A"THENTI=10:BI=10ELSEIFCH$="B"THENTI=50:BI=50ELSEIFCH$="C"THENTI=99:BI=99ELSEGOTO720
740 CLS:PRINT@483,"DO YOU WISH TO PRACTICE:":GOSUB2600:PRINT:PRINT"       a. ADDITION":GOSUB2600:PRINT:PRINT"       b. SUBTRACTION":GOSUB2600:PRINT:PRINT"       c. MULTIPLICATION":GOSUB2600:PRINT:PRINT"       d. ALL THE ABOVE"
743 FORD=1TO3:PRINT:GOSUB2600:NEXT:G=172:V=192
745 INPUT"        CHOOSE ONE NOW";SL$:IFSL$=""THEN745
755 IFSL$="A"THENM=0:PG$=""ELSEIFSL$="B"THENM=1:PG$="":ELSEIFSL$="C"THENM=2:PG$="":ELSEIFSL$="D"THENM=0:PG$="AL"ELSEGOTO740
760 IFNA$=""THENCLS:PRINT@483,"WHAT IS YOUR FIRST NAME?":FORLC=1TO8:GOSUB2600:PRINT:NEXT:INPUT"          ";NA$:IFNA$=""THEN760
765 SCREEN1,0:GOTO300
1000 DATA "BR12NR4G4D4R4D24L12D4R28U4L12U32BR28","BR4NR20G4D4R4E4R12F4D4G4L16G4D12F4R24U4L20H4U4E4R16E4U12H4BR20","BR4NR20G4D4R4E4R12F4D4G4L12D4R12F4D4G4L12H4L4D4F4R20E4U28H4BR20"
1005 DATA "BR12NR8G12D8R16D16R4U16R8U4L8U16BG4L2G10D2R12U12BU4BR28","NR28D16R20F4D8G4L20D4R24E4U16H4L20U8R24U4BR16","BR4NR20G4D28F4R20E4U12H4L16G4BD5D3F4R12E4U3H4L12G4BU5U12E4R12F4R4U4H4BR20"
1010 DATA "NR28D4R24D32R4U36BR16","BR4NR20G4D8F4D4G4D8F4R20E4U8H4U4E4U8H4BD8H4L12G4D3F4NR12BD6NR12G4D3F4R12E4U3H4BU6E4U3BU8BR20","BR4NR20G4D8F4R20D20R4U32H4BD7D2G4L12H4U2E4R12F4BU7BR20"
1015 DATA "BR4NR20G4D28F4R20E4U28H4BG4L12G4D20F4R12E4U20H4BR24BU4":P$="NU4NL4ND4R4":M$="NL4R4":SC$="D8R16D8NL16BU16NL16BR8NR16D16R16BU16BR8NR16D16R16U16BR8"
1020 SD$="NR16D8ND8R8NF8R8U8BR8NR16D8NR8D8R16":MM$="NE4NF4NG4H4"
1025 RETURN
2500 FORX=1TO1000:NEXT
2505 GOSUB2600
2510 PCLS(3):RETURN
2600 PLAY"V31;T255;L255;O3BF":RETURN
3000 A=0:B=0:C=0:D=0:LL=0:J=INT(S/100):K=(S-J*100)
3005 L=INT(K/10)
3010 IF L>0 THEN LL=(K-L*10)
3015 IF S<10 THEN A=134:B=140:C=162:D=176: KK=S+1:GOSUB 137:GOTO3030
3020 IF S>9 AND S<100 THEN A=90:B=140:C=118:D=176:KK=L+1:GOSUB 137:A=134:C=162:KK=LL+1:GOSUB 137:GOTO3030
3025 IF S>99 THEN A=46:B=140:C=74:D=176:KK=J+1:GOSUB137:A=90:C=118:KK=L+1:GOSUB137:A=134:C=162:KK=LL+1:GOSUB137:GOTO3030
3030 FORT=1TO1500:NEXT:LINE(46,28)-(164,180),PRESET,BF:GOTO300
3100 CLS:PRINT@484,NA$",":FORLC=1TO12:GOSUB2600:PRINT:NEXT:PRINT@129,"IT SURE IS GREAT TO SEE YOU":PRINT@161,"ARE WORKING ON:"
3105 IFSL$="A"THENPRINT@264,"ADDITION"ELSEIFSL$="B"THENPRINT@264,"SUBTRACTION"ELSEIFSL$="C"THENPRINT@264,"MULTIPLICATION"ELSEIFSL$="D"THENPRINT@264,"ALL YOUR MATH"
3110 FORSR=1TO3:GOSUB2500:NEXTSR:FORLC=1TO15:GOSUB2600:PRINT:NEXT
3115 PRINT@482,"WOULD YOU LIKE MORE PRACTICE?":GOSUB2600:PRINT:PRINT"          SELECT (Y/N)":FORLC=1TO4:GOSUB2600:PRINT:NEXT
3120 QE$=INKEY$:IFQE$=""THEN3120
3125 IFQE$="Y"THEN720
3130 END
