
1 '==========================
2 '==NON GRAPHICS ADVENTURE==
3 '===FOR "THE RAINBOW"======
4 '=========CONTEST==========
5 '=====BY GREGORY CLARK=====
6 '=====122 MALE AVENUE======
7 '=SYRACUSE,NEW YORK,13219==
8 '======315-487-8406========
9 '===WRITTEN AUGUST 1982====
10 '=WITH MUCH HELP FROM KEVIN=
11 '**PCLEAR 1 BEFORE LOADING**
12 '*FOR 32K MEMORY-EXT BASIC**
13 '===========================
14 PRINT:CLS:PRINT@228,"SIR RANDOLF OF THE MOORS":FORX=1TO200:PLAY"L200DDA":NEXT
15 CLS:PRINT:PRINT:PRINT:PRINT:PRINT;"-----------SKILL LEVEL---------"
16 PRINT:PRINT;">>>>SELECT DIGIT FROM 0 TO 9<<<<"
17 PRINT:PRINT;"ZERO (0) = EASIEST"
18 PRINT;"NINE (9) = HARDEST"
19 PRINT:PRINT:INPUT"ENTER CHOICE";Q
20 Q1=INT(Q)
21 IF Q1>9 OR Q1<0 THEN CLS:PRINT;"    INPUT MUST BE AN INTEGER           IN RANGE OF 0 TO 9":SOUND100,3:GOTO15
22 CLS:PRINT@232,"JUST A MOMENT"
23 DIM A$(10),B$(30),C$(40),V(12)
24 HV=3:VV=5:LV=3:N=1:S=1:E=1:W=1:U=1:D=1:Y=1:WA=1
25 RB=1:FT=1:SW=1:DA=1:RU=5:DR=1:D3=1:D4=1:ST=1
26 FORX=1TO10:READA$(X):PLAY"L200;D":NEXTX
27 FORX=1TO30:READB$(X):PLAY"L200;C":NEXT
28 FORX=1TO40:READC$(X):PLAY"L200;F":NEXTX:CLS
29 GOTO304
30 PRINT;" %%%%%%%%%%%%%%%%%%%%%%%%%%%%%":PLAY"L150;FP10F":INPUT"WHAT DO YOU WANT TO DO          ";D$
31 IF DI=1 AND WA=1 AND HV=1 AND VV=1 THEN TJ=TIMER:IF TJ-1000 > TI THEN 281
32 IFD$="VIEW"THEN304
33 IFD$="HELP"THEN50
34 IF Y=9 THEN WJ=WJ+1:IF WJ>15-(Q1+1) THEN324
35 IFD$="N"THEN287
36 IFD$="S"THEN287
37 IFD$="E"THEN287
38 IFD$="W"THEN287
39 IFD$="U"THEN287
40 IFD$="D"THEN287
41 IFD$="DIVE"THEN45
42 IFD$="LIST"THEN52
43 PLAY"L150;G;P10;G":INPUT"";E$
44 G$=LEFT$(E$,4)
45 FORA=1TO30
46 F$=LEFT$(D$,4):IF F$=B$(A) THEN49
47 NEXTA
48 PRINT;"I CANNOT ";D$:PLAY"L200;DP10CP10DP6":GOTO30
49 ON A GOTO 67,104,107,109,111,113,122,125,128,136,138,141,168,176,180,187,190,219,221,226,232,240,243,249,260,268,270,275,277,283
50 'START HELP SECTION
51 PRINT;"HELP"
52 'LIST
53 PRINT;"I AM CARRYING"
54 IF SW=1 THENPRINT;"A SWORD"
55 IF DA=1 THENPRINT;"A DAGGER"
56 IF RB=1 THENPRINT;"THE MAGIC RUBY"
57 IF OC=1 THENPRINT"THE OCTAGON OF GOLD"
58 IF HE=1 THENPRINT"THE HEXAGON OF GLASS"
59 IF NE=1 THENPRINT"THE PENTAGON NECKLACE"
60 IF LA=1 THENPRINT"THE LANCE"
61 IF KE=1 THENPRINT"THE KEY OF GOLD
62 IF RI=1 THENPRINT"THE INVISIBILITY RING"
63 IF SK=1 THENPRINT"THE SACK OF MARBLES"
64 IF FT=1 THENPRINT"FLINT AND TINDER"
65 GOTO30:'END OF LIST
66 PRINT;"SORRY!!-NO HELP FOR YOU NOW":GOTO30:'END HELP SECTION
67 'LOOK
68 IF LV=1 AND VV=1 AND HV=1 AND WA=0 AND D1=0 AND SL=0 THEN 69 ELSE 72
69 IFG$=C$(9) THEN PRINT;"IT'S JUST A SLIPPERY,FOUL SMELL-ING,GREENISH SLIME":GOTO30
70 IFG$=C$(10) THEN PRINT;"THERE APPEARS TO BE AN OUTLINE  OF WHAT MAY BE A DOOR UNDER THE SLIME":GOTO30
71 IFG$=C$(11) THENPRINT;"THERE'S TOO MUCH SLIME COVERING IT TO TELL MUCH":GOTO30
72 IF LV=1 AND VV=1 AND HV=1 AND WA=0 AND D1=0 AND SL=1 THEN 73ELSE76
73 IF G$=C$(11) THENPRINT;"IT'S A DOOR WITH A SMALL RING.":GOTO30
74 IFG$=C$(10) THENPRINT;"THE OUTLINE IS MORE DEFINATE":GOTO30
75 IFG$=C$(12) THENPRINT;"IT'S JUST A RING ABOUT 3 INCHES ACROSS":GOTO30
76 IFG$=C$(14) AND VV=4 AND HV=1 AND LV=4 THENPRINT"THE STAIRS THAT ARE THERE ARE   COVERED WITH DUST":GOTO30
77 IF LV=2 AND HV=3 AND VV=3 THEN78ELSE84
78 IFG$=C$(16) THENPRINT"I SEE NOTHING MORE THAN I HAVE  ALREADY TOLD":GOTO30
79 IFG$=C$(17) THENPRINT"THEY ARE COVERED WITH MANY LINESAND SHAPES.":GOTO30
80 IFG$=C$(18) AND DW=0 THENPRINT"JUST DRAWERS-THEY ARE CLOSED":GOTO30
81 IFG$=C$(18) THENPRINT"IT CONTAINS A RING.":GOTO30
82 IFG$=C$(6) AND DW=1 THENPRINT"FROM WHAT THE PICTURES ENGRAVED ON IT SHOW,I THINK THE WEARER OFIT IS NOT ABLE TO BE SEEN,BUT   IT ALSO COULD MEAN THAT IF IT ISPUT ON,THE WEARER IS KILLED. ONETHING FOR SURE-IT ONLY WORKS ONETIME":GOTO30
83 IFG$=C$(19) OR G$=C$(20) THENPRINT"ALL I CAN SAY IS THERE ARE MANY OF THEM AND I WOULD HAVE NO WAY OF FIGURING OUT WHAT MAY BE IN  ANY SINGLE ONE OF THEM.":GOTO30
84 IF LV=4 AND VV=3 AND HV=3 THEN85 ELSE87
85 IFG$=C$(23) AND ST=1 THENPRINT"JUST A STATUE MADE OF QUARTZ":GOTO30
86 IF G$=C$(23) THENPRINT;"I SEE A KEY-IT MUST HAVE BEEN   UNDER THE STATUE":GOTO30
87 IF G$=C$(24) AND ST=0 THENPRINT"IT IS MADE OF GOLD.THE HANDLE ISSHAPED AS A CLOVER LEAF AND THE OTHER END IS FORMED INTO THE    SHAPE OF A CROSS.":GOTO30
88 IFG$=C$(26) AND LV=4 AND VV=3 AND HV=5 THEN PRINT"ONE PLANK SEEMS TO BE LOOSE":GOTO30
89 IF LV=5 AND HV=3 AND VV=1 AND G$=C$(31) THENPRINT"THERE ARE THREE HOLES IN THE    BLOCK.":GOTO30
90 IF G$=C$(32) AND LV=3 AND VV=4 AND HV=2 THEN91ELSE94
91 IF CH=0 THENPRINT"IT'S JUST A SMALL CHEST.":GOTO30
92 IF CH=1 AND HE=0 THENPRINT"THERE IS A HEXAGON OF GLASS IN  THE CHEST.":GOTO30
93 IF CH=1 AND HE=1 THENPRINT"THE CHEST IS EMPTY":GOTO30
94 IF G$=C$(35) AND LA=1 THENPRINT"IT IS MADE OF A WOODEN SHAFT    ABOUT TWO ARM-SPANS LONG WITH A SHARP METAL POINT.":GOTO30
95 IF G$=C$(36) AND LV=5 AND VV=1 AND HV=3 THENPRINT"THERE ARE THREE HOLES-HEXAGON,  OCTAGON AND PENTAGON IN SHAPE.":GOTO30
96 IF LV=5 AND HV=3 AND VV=2 AND G$=C$(37) THENPRINT"THE BODY OF BLANDOR LIES UPON   THE ALTAR.YOUR QUEST IS FINISHED":PRINT:PRINT"CONGRATULATIONS!!":FORX=1TO5000:NEXT:CLS:END
97 IF G$=C$(25) AND LV=4 AND HV=4 AND VV=3 THEN PRINT"IT IS SOLID METAL-THE SURFACE ISFEATURELESS EXCEPT FOR A SMALL  CROSS-SHAPED HOLE IN THE CENTER.":GOTO30
98 IF Y=6 AND G$=C$(38) THENPRINT"THEY ARE ALL SHARP-EXCEPT ONE-  THAT PARTICULAR SPIKE IS BLUNT.":GOTO30
99 IF G$=C$(40) AND SK=1 THENPRINT"JUST A WORN,BROWN LEATHER SACK  WITH A DRAW-STRING TIE.":GOTO30
100 IF LV=3 AND E$="MOAT" THENPRINT"THE CROCODILES LOOK VERY HUNGRY.":GOTO30
101 IF Y=3 AND G$=C$(7) THENPRINT"I DON'T REALLY WANT TO-":GOTO30
102 IF E$="GOBLINS" THENPRINT"EACH ONE HAS A SWORD IN ONE HANDAND THE OTHER HAND IS EXTENDED  OUT-PALM UP.":GOTO30
103 PRINT;"I DON'T SEE ANYTHING SPECIAL":GOTO30
104 'SLAY
105 IF E$="DRAGON" THENPRINT"YOU MUST BE KIDDING!":GOTO30
106 PRINT"I CAN'T":GOTO30
107 'JUMP
108 PRINT;"I CAN'T JUMP OVER THAT!":GOTO30
109 'LISTEN
110 PRINT;"I DON'T HEAR ANYTHING OUT OF THEORDINARY":GOTO30
111 'SAY
112 PRINT;"I SAID '";E$;"'":PRINT;"BUT NOTHING HAPPENED":GOTO30
113 'RUB
114 IF RU<1 THEN PRINT;"IT IS POWERLESS-YOU HAVE USED   ALL FIVE CHARGES":GOTO30
115 IF LV=1 AND Y=3 THEN116ELSE118
116 IF G$=C$(5) THEN PRINT;"THE RUBY IS CHANGING COLORS AND GIVING OFF A HUMMING SOUND.THE  SCORPIANS ARE CHANGING COLORS   ALONG WITH THE GEM."
117 PRINT;"SUDDENLY AS THEY CAME THEY HAVE TURNED INTO HARMLESS ANTS AND   SCURRIED OFF INTO THE CRACKS IN THE WALLS.":Y=1:B3=1:RU=RU-1:GOTO304
118 IF Y=4 AND G$=C$(5) THENPRINT"THE RUBY IS HUMMING AND CHANGINGCOLORS-":FORX=1TO1000:NEXT:PRINT"THE FLAMES HAVE GONE!":Y=1:B4=1:RU=RU-1:GOTO304
119 IFY=7 AND G$=C$(5) THENPRINT"THE RUBY IS GIVING OFF A HUMING SOUND-":FORX=1TO1000:NEXT:PRINT"THERE,I CAN MOVE AGAIN.":Y=1:B7=1:RU=RU-1:GOTO304
120 IF G$=C$(5) THEN RU=RU-1
121 PRINT;"I TRIED - BUT I'M AFRAID NOTHINGWAS ACCOMPLISHED BY IT":GOTO30
122 'TURN
123 IF G$=C$(30) AND MI=0 AND LV=3 AND VV=3 AND HV=4 THENPRINT"IT'S TURNING-":PRINT"THE WALL NEXT TO THE MIRROR IS  OPENING.":T=0:O=5:GOTO447
124 PRINT;"NOTHING HAPPENED":GOTO30
125 'LIFT
126 IF G$=C$(27) AND LV=4 AND VV=3 AND HV=5 AND OC=0 THENPRINT"THERE IS AN OCTAGON SHAPED PIECEOF GOLD UNDER THE PLANK.":GOTO30
127 PRINT;"CAN'T":GOTO30
128 'CUT
129 PLAY"L200;DDA":INPUT"WITH WHAT";H$
130 IF H$="SWORD" OR H$="DAGGER" THEN 131 ELSE PRINT;"NOTHING HAPPENED":GOTO30
131 IF LV=1 AND Y=2 AND G$=C$(1) THEN133ELSE132
132 IF LV=1 AND Y=2 AND G$=C$(2) THEN133ELSE134
133 PRINT;"IT'S NOT WORKING-THE ";H$:PRINT;"IS NOW CAUGHT IN THE WEB":GOTO30
134 '
135 PRINT"NOTHING HAPPENED":GOTO30
136 'STAB
137 PRINT;"I STABBED THE ";E$:PRINT;"BUT NOTHING HAPPENED":GOTO30
138 'CLIMB
139 IF Y=5 AND G$=C$(39) THENPRINT"THE BARS MUST STAY IN PLACE AS  LONG AS THERE IS WEIGHT UPON THEFLOOR! THE BARS HAVE GONE BACK.":B5=1:Y=1:GOTO304
140 PRINT;"I CAN'T CLIMB IT":GOTO30
141 'GET
142 IF VV=1 AND HV=1 AND LV=1 AND WA=1 THEN PRINT;"I CAN'T":GOTO30
143 W1=VV*HV*LV
144 IF G$=C$(3) AND V1=W1 AND SW=3 THEN PRINT;"I'VE GOT IT":SW=1:GOTO30
145 IF G$=C$(4) AND V2=W1 AND DA=3 THEN PRINT;"I'VE GOT IT":DA=1:GOTO30
146 IF G$=C$(5) AND V3=W1 AND RB=3 THEN PRINT;"I'VE GOT IT":RB=1:GOTO30
147 IF G$=C$(12) AND V4=W1 AND FT=3 THEN PRINT;"I'VE GOT IT":FT=1:GOTO30
148 IF G$=C$(6) AND V5=W1 AND RI=3 THEN PRINT;"I'VE GOT IT":RI=1:GOTO30
149 IF G$=C$(13) AND V6=W1 AND NE=3 THEN PRINT;"I'VE GOT IT":NE=1:GOTO30
150 IF G$=C$(40) AND V7=W1 AND SK=3 THENPRINT"I'VE GOT IT":SK=1:GOTO30
151 IF G$=C$(35) AND V8=W1 AND LA=3 THENPRINT"I'VE GOT IT":LA=1:GOTO30
152 IF G$=C$(33) AND V9=W1 AND HE=3 THENPRINT"I'VE GOT IT":HE=1:GOTO30
153 IF G$=C$(21) AND V10=W1 AND NE=3 THENPRINT"I'VE GOT IT":NE=1:GOTO30
154 IF G$=C$(24) AND V11=W1 AND KE=3 THENPRINT"I'VE GOT IT":KE=1:GOTO30
155 IF G$=C$(28) AND V12=W1 AND OC=3 THENPRINT"I'VE GOT IT":OC=1:GOTO30
156 IFG$=C$(15) AND VV=4 AND HV=1 AND LV=4 THENPRINT"I'VE GOT A HANDFULL OF DUST":DU=1:GOTO30
157 IF G$=C$(24) AND KE=0 AND LV=4 AND VV=3 AND HV=3 AND ST=0 THENPRINT"I'VE GOT THE KEY.":KE=1:GOTO30
158 IF G$=C$(22) AND ST=1 AND LV=4 AND VV=3 AND HV=3 THENPRINT"OW! SOMETHING PRICKED ME-":FORX=1TO1500:NEXT:CLS(0):FORX=1TO500:NEXT:CLS:END
159 IF G$=C$(28) AND OC=1 THENPRINT"I'VE ALREADY GOT IT.":GOTO30
160 IF G$=C$(28) AND LV=4 AND VV=3 AND HV=5 THENPRINT"I'VE GOT THE OCTAGON.":OC=1:GOTO30
161 IFG$=C$(6) AND LV=2 AND VV=3 AND HV=3 AND DW=1 THENPRINT"I PUT THE RING ON MY FINGER. I  GUESS THAT MEANS I'M INVISIBLE- BUT FOR HOW LONG?":RI=1:GOTO30
162 IF G$=C$(35) AND LV=3 AND VV=3 AND HV=2 AND LA=0 THENPRINT"I'VE GOT THE LANCE":LA=1:GOTO30
163 IF G$=C$(33) AND HE=0 AND CH=1 AND LV=3 AND HV=2 AND VV=4 THENPRINT"I'VE GOT THE HEXAGON OF GLASS.":HE=1:GOTO30
164 IF LV=2 AND VV=5 AND HV=5 AND G$=C$(40) THEN 165ELSE167
165 IF SK=1 THENPRINT"I'VE ALREADY GOT IT":GOTO30
166 IF SK=0 THENPRINT"I'VE GOT THE SACK":SK=1:GOTO30
167 PRINT;"I CAN'T GET THE ";E$:GOTO30
168 'UNLOCK
169 PRINT;"UNLOCK THE ";E$:LINEINPUT"WITH WHAT? ";M$
170 IF G$=C$(25) AND LV=4 AND HV=4 AND VV=3 THEN171ELSE174
171 IF D3=0 THENPRINT"IT'S ALREADY OPEN.":GOTO30
172 IF M$="KEY" AND KE=0 THENPRINT"I DON'T HAVE THE KEY.":GOTO30
173 IF M$="KEY" AND KE=1 THENPRINT"IT WORKED! THE DOOR SWUNG OPEN.":D3=0:T=0:O=10:GOTO447
174 '
175 PRINT;"THE ";E$;" WON'T UNLOCK":PRINT;"WITH THE ";M$:GOTO30
176 'STAND
177 IF G$=C$(21) AND LV=2 AND VV=5 AND HV=2 AND NE=1 THENPRINT"EVERYTHING IS FUZZY-":FORX=1TO1500:NEXTX:LV=4:HV=1:VV=2:GOTO304
178 IF G$=C$(21) AND LV=4 AND HV=1 AND VV=2 AND NE=1 THENPRINT"EVERYTHING IS FUZZY-":FORX=1TO1500:NEXT:LV=2:HV=2:VV=5:GOTO304
179 PRINT"I CAN'T":GOTO30
180 'PULL
181 IF DI=1 AND G$=C$(8) THEN PRINT;"IT CAME LOOSE FROM THE BOTTOM.  THE WATER IS NOW SWIRLING AROUNDAND AROUND! IT'S ALL GOING OUT  OF A SORT OF DRAIN IN THE FLOOR.THERE! IT'S ALL GONE-I'M ON THE FLOOR OF THE PIT."
182 IF DI=1 AND G$=C$(8) THEN PRINT;"I SEEM TO BE STANDING ON A PILE OF RUBBLE. IT'S TOO DARK TO TELLWHAT IT IS,BUT FROM THE SMELL I DON'T CARE TO KNOW":DI=0:WA=0:T=0:O=0:GOTO447
183 IF VV=1 AND HV=1 AND LV=1 AND WA=0 THEN184ELSE185
184 IF D1=0 AND G$=C$(6) THEN PRINT;"IT'S VERY HEAVY AND THE HINGES  ARE RUSTED,BUT I WAS ABLE TO GETIT OPEN ENOUGH TO GET OUT.":D1=1:T=0:O=2:GOTO447
185 '
186 PRINT;"I CAN'T PULL IT":GOTO30
187 'PUSH
188 IF Y=6 AND G$=C$(38) THENPRINT"THE WALL IS MOVING BACK! AND THEDOORWAYS HAVE RE-APPEARED.":B6=1:Y=1:GOTO304
189 PRINT;"I CAN'T PUSH IT":GOTO30
190 'DROP
191 IF G$=C$(5) THENPRINT"I WON'T DROP THE RUBY-HERMAN    TOLD ME I MAY NEED IT.":GOTO30
192 IF G$=C$(3) AND SW=1 THEN SW=2:GOTO205
193 IF G$=C$(4) AND DA=1 THEN DA=2:GOTO205
194 IF G$=C$(5) AND RB=1 THEN RB=2:GOTO205
195 IF G$=C$(6) AND RI=1 THEN RI=2:GOTO205
196 IF G$=C$(12) AND FT=1 THEN FT=2:GOTO205
197 IF G$=C$(13) AND NE=1 THEN NE=2:GOTO205
198 IF G$=C$(40) AND SK=1 THEN SK=2:GOTO205
199 IF G$=C$(35) AND LA=1 THEN LA=2:GOTO205
200 IF G$=C$(33) AND HE=1 THEN HE=2:GOTO205
201 IF G$=C$(21) AND NE=1 THEN NE=2:GOTO205
202 IF G$=C$(24) AND KE=1 THEN KE=2:GOTO205
203 IF G$=C$(28) AND OC=1 THEN OC=2:GOTO205
204 PRINT;"I DON'T HAVE IT":GOTO30
205 PRINT;"I DROPPED IT-I HOPE YOU REMEMBERWHERE IT IS I DROPPED IT.       THE FLOORS ARE IN SHADOW AND I  MAY NOT BE ABLE TO SEE IT IF I  COME BACK LOOKING FOR IT."
206 IF SW=2 THEN V1=VV*HV*LV:SW=3:GOTO30
207 IF DA=2 THEN V2=VV*HV*LV:DA=3:GOTO30
208 IF RB=2 THEN V3=VV*HV*LV:RB=3:GOTO30
209 IF FT=2 THEN V4=VV*HV*LV:FT=3:GOTO30
210 IF RI=2 THEN V5=VV*HV*LV:RI=3:GOTO30
211 IF NE=2 THEN V6=VV*HV*LV:NE=3:GOTO30
212 IF SK=2 THEN V7=VV*HV*LV:SK=3:GOTO30
213 IF LA=2 THEN V8=VV*HV*LV:LA=3:GOTO30
214 IF HE=2 THEN V9=VV*HV*LV:HE=3:GOTO30
215 IF NE=2 THEN V10=VV*HV*LV:NE=3:GOTO30
216 IF KE=2 THEN V11=VV*HV*LV:KE=3:GOTO30
217 IF OC=2 THEN V12=VV*HV*LV:OC=3:GOTO30
218 PRINT;"I HAVEN'T GOT IT":GOTO30
219 'CARRY
220 PRINT;"I CAN'T CARRY THAT":GOTO30
221 'BRIBE
222 RR$=""
223 IF LV=5 AND Y=9 AND SK=1 AND LEFT$(E$,5)="GUARD" THEN INPUT"WITH WHAT";RR$:IF RR$="MARBLES" THENPRINT"THAT SATISFIED THEM.THEY WENT   BACK DOWN THE HALL ARGUING OVER WHICH ONE WOULD GET THE BLUE    TIGER-EYE.":Y=1:B9=1:GOTO304
224 IFRR$=""THEN INPUT"WITH WHAT";RR$
225 PRINT"I GUESS THEY DON'T WANT THAT!":GOTO30
226 'BREAK
227 IF LV=1 AND Y=2 THEN 228ELSE230
228 IF G$=C$(1) OR G$=C$(2) THEN 229ELSE230
229 PRINT;"IT'S MUCH TOO STRONG TO BREAK":GOTO30
230 '
231 PRINT;"I TRIED TO BREAK THE ";E$:PRINT"BUT NOTHING HAPPENED.":GOTO30
232 'HIT
233 PRINT;"HIT THE ";E$:LINEINPUT"WITH WHAT? ";J$
234 IF G$=C$(22) AND HV=3 AND VV=3 AND LV=4 THEN 235ELSE238
235 IF J$="DAGGER" OR J$="SWORD" THEN 236 ELSE 238
236 IF ST=1 THENPRINT"THE STATUE SMASHED INTO MANY    PIECES,A MECHANISM WITH A NEEDLEATTACHED FELL TO THE FLOOR.":ST=0:GOTO30
237 PRINT"THE STATUE IS ALREADY SMASHED.":GOTO30
238 '
239 PRINT;"I HIT THE ";E$:PRINT;"WITH THE ";J$:PRINT;"BUT IT DIDN'T DO ANYTHING":GOTO30
240 'DIG
241 PRINT"DIG THE ";E$:LINEINPUT"WITH WHAT? ";K$
242 PRINT;"I TRIED TO DIG THE ";E$:PRINT;"WITH THE ";K$:PRINT;"BUT I WAS UNABLE TO DO IT":GOTO30
243 'BURN
244 PLAY"L200;DDA":INPUT"WITH WHAT";I$
245 IFI$="FLINT AND TINDER" THEN246ELSE248
246 IF LV=1 AND Y=2 THEN247ELSE248
247 IF G$=C$(1) OR G$=C$(2) THEN PRINT;" IT ALL BURNED UP IN A FLASH!":B2=1:Y=1:GOTO304
248 PRINT"USING THE ";L$:PRINT;"THE ";E$;" WON'T BURN":GOTO30
249 'OPEN
250 IF G$=C$(18) AND LV=2 AND HV=3 AND VV=3 THEN INPUT"WHICH DRAWER? TOP OR BOTTOM     ";DW$:IFDW$="BOTTOM"THENPRINT"THERE IS A YELLOW GAS COMING OUTFROM THE DRAWER. I CAN'T SEE OR HEAR ANYTHING!":FORX=1TO3000:NEXT:CLS(0):FORX=1TO500:NEXT:CLS:END
251 IF DW$="TOP" THENPRINT;"IT'S OPEN":DW=1:DW$="":GOTO30
252 IF G$=C$(32) AND LV=3 AND HV=2 AND VV=4 THEN253ELSE258
253 INPUT"WITH WHAT";ZX$:IFZX$="LANCE"THEN254ELSE256
254 IF LA=1 THENPRINT"JUST AS THE TOP OF THE CHEST    OPENED A LARGE BLACK SPIDER CAMEOUT AND DISAPPEARED INTO THE    CLUTTER IN THE REAR OF THE ROOM.":CH=1:GOTO30
255 PRINT"I DON'T HAVE A LANCE":GOTO30
256 IF ZX$="SWORD" OR ZX$="DAGGER" OR ZX$="HAND" THENPRINT"A BLACK SPIDER JUMPED FROM THE  CHEST AND BIT ME.IT'S HARD TO   BREATHE":FORX=1TO2000:NEXT:CLS(0):FORX=1TO500:NEXT:CLS:END
257 PRINT"IT WON'T OPEN":GOTO30
258 IF SK=1 AND G$=C$(40) THENPRINT"IT CONTAINS ONLY A FEW WORTHLESSMARBLES.":GOTO30
259 PRINT;"I CAN'T OPEN THE ";E$:GOTO30
260 'PUT
261 INPUT"WHERE";XX$
262 IF XX$="HOLE" AND HE=1 AND OC=1 AND NE=1 THEN 266ELSE263
263 IF XX$="HOLE" AND D4=1 AND LV=5 AND VV=1 AND HV=3 THEN264ELSE267
264 IF NE=1 OR OC=1 OR HE=1 THEN265ELSE267
265 IF G$=C$(33) OR G$=C$(21) OR G$=C$(28) THENPRINT"IT FIT OK,BUT NOTHING HAPPENED":GOTO30
266 IF G$=C$(33) OR G$=C$(21) OR G$=C$(28) THEN PRINT"I PUT ALL THREE IN THE CORRECT  PLACES AND A SECTION OF WALL    SLID OPEN.":T=0:O=8:D4=0:GOTO447
267 PRINT"I CAN'T":GOTO30
268 'SMELL
269 PRINT;"I JUST SMELL A(N) ";E$:GOTO30
270 'SPRINKLE
271 PRINT;"SPRINKLE THE ";E$:LINE INPUT"ON WHAT? ";O$
272 IF TS=0 AND DU=1 AND VV=4 AND HV=1 AND LV=4 AND G$=C$(15) AND O$="STAIRS" THEN PRINT"AS THE DUST HITS THE MISSING    STAIRS IT SPARKLES-OUTLINING THEWHOLE STAIRWAY.IN FACT NOW THE  STAIRWAY IS COMPLETELY VISABLE.":T=1:O=1:TS=1:GOTO447
273 IF O$="STAIRS" AND DU=0 THENPRINT"I DON'T HAVE ANY DUST.":GOTO30
274 PRINT;"I SPRINKLED THE ";E$:PRINT;"ON THE ";O$:PRINT;"NOTHING HAPPENED":GOTO30
275 'KICK
276 PRINT"OUCH!!":GOTO30
277 'DIVE
278 IF LV>1THEN282
279 IF WA=1 AND VV=1 AND HV=1 THEN PRINT;"THE COLD WATER IS ABOUT 15 FEET DEEP. AT THE BOTTOM IS A LOT OF STUFF,BUT IT IS TOO DARK TO SEE WHAT IT IS. I ONLY RECOGNIZE A  PIECE OF CHAIN. PLEASE HURRY-I  CAN'T HOLD MY BREATH TOO LONG!":DI=1
280 IF WA=1 AND VV=1 AND HV=1 THEN TI=TIMER:GOTO30
281 IF WA=1 AND VV=1 AND HV=1 THENPRINT;"YOU TOOK TOO LONG-I COULDN'T    STAY DOWN ANY LONGER-I'M BACK ONTHE SURFACE.":DI=0:GOTO30
282 PRINT;"I CAN'T":GOTO30
283 'WIPE
284 IF VV=1 AND WA=0 AND HV=1 AND D1=0 AND LV=1 AND G$=C$(9) THENPRINT;"IT'S NOT EXACTLY FUN BUT I'VE   GOT MOST OF IT OFF":SL=1:GOTO30
285 PRINT;"I CAN'T":GOTO30
286 PRINT;"I MUST HAVE MIS-UNDERSTOOD--    PLEASE RE-ENTER COMMAND":GOTO30:'TRAP
287 IFD$="N" AND N=1 THEN VV=VV-1
288 IFD$="S" AND S=1 THEN VV=VV+1
289 IFVV>5THENVV=5:GOTO303
290 IFVV<1THENVV=1:GOTO303
291 IF D$="N" AND N=0 OR D$="S" AND S=0 THEN303
292 IFD$="E" AND E=1 THEN HV=HV+1
293 IFD$="W" AND W=1 THEN HV=HV-1
294 IFHV<1THENHV=1:GOTO303
295 IFHV>5THENHV=5:GOTO303
296 IF D$="E" AND E=0 OR D$="W" AND W=0 THEN303
297 IFD$="U" AND U=1 THEN LV=LV+1
298 IFD$="D" AND D=1 THEN LV=LV-1
299 IFLV<1THENLV=1:GOTO303
300 IFLV>5THENLV=5:GOTO303
301 IF D$="U" AND U=0 OR D$="D" AND D=0 THEN303
302 GOTO304
303 PRINT;"I CAN'T MOVE IN THAT DIRECTION":PLAY"L10;AP10":GOTO30
304 GOSUB305:ON LV GOTO 326,358,381,417,431
305 'VARIABLE SCENERIO ROUTINE
306 ON Y GOTO 307,314,316,317,318,319,320,321,322
307 Q6=0
308 IFQ1=0THEN RETURN
309 IFLV=1 AND Q3<4 THEN RETURN
310 Q4=RND(100-(Q1*10))
311 Q5=INT(100-(Q1*10.3))
312 IF Q4<=Q5 THEN RETURN
313 Q6=1
314 'MAZE LEVEL VAR SUBROU
315 IF LV=1 AND VV>1 AND B2=0 THEN PRINT;"A WEB OF STICKY STRING-LIKE     STRANDS FELL FROM THE CEILING   OF THE CORRIDOR. I CAN'T MOVE   VERY WELL-THE MORE I STRUGGLE   AGAINST THEM-THE TIGHTER THEY   BIND ME.":T=0:O=0:Y=2:GOTO447
316 IF LV=1 AND VV>1 AND B3=0THEN PRINT;"SUDDENLY THE AIR IS RESOUNDING  WITH CLICKING.LITERALLY COVERINGTHE FLOOR ARE THOUSANDS OF MAUVESCORPIONS.THEY ARE BLOCKING ALL ESCAPE AND ARE SLOWLY CLOSING IN.":T=0:O=0:Y=3:GOTO447
317 IF LV=2 AND HA=1 AND B4=0 THENPRINT"THERE ARE FLAMES ALL AROUND ME!!A RING OF FIRE COMPLETELY SUR-  ROUNDS ME!":T=0:O=0:Y=4:GOTO447
318 IF LV=4 AND HA=1 AND B5=0 THENPRINT"IRON BARS HAVE COME DOWN FROM   THE CEILING!! ALL THE EXITS ARE BLOCKED.":T=0:O=0:Y=5:GOTO447
319 IF LV=3 AND HA=1 AND B6=0 THENPRINT"SUDDENLY ALL THE EXITS ARE      BLOCKED BY SOLID WALLS.SPIKES   NOW EXTEND FROM THE EAST WALL   AND THE WEST WALL IS MOVING IN. HURRY,BEFORE I'M CRUSHED!":T=0:O=0:Y=6:GOTO447
320 IF LV=4 AND HA=1 AND B7=0 THENPRINT"I CAN'T MOVE MY LEGS! THEY SEEM TO BE FROZEN TO THE FLOOR.":T=0:O=0:Y=7:GOTO447
321 IF B8=0 AND LV=5 AND HA=1 THENPRINT"THE FLOOR IS DROPPING!":FORX=1TO1000:NEXT:PRINT"I'M SLIDING DOWN A CHUTE!":FORX=1TO1000:NEXT:CLS(0):FORX=1TO500:NEXT:LV=1:HV=3:VV=4:B8=1:Y=1:CLS:LA=0:GOTO304
322 IFB9=0 AND LV=5 AND HA=1 THENPRINT"THERE IS A GOBLIN DRESSED IN    GUARD'S ATTIRE BLOCKING THE WAY.HE HAS A SWORD AT LEAST TWICE   THE LENGTH OF MINE AND FROM THE LOOKS OF HIS BUILD HE ISN'T A   PUSHOVER":T=0:O=0:Y=9
323 IF B9=0 AND LV=5 AND HA=1 THEN FORX=1TO1000:NEXT:PRINT:PRINT"OH OH!":PRINT"ANOTHER ONE IS BEHIND ME!":GOTO447
324 IFY=9 AND WJ>15-(Q1+1) THENPRINT"THEY ARE BOTH ATTACKING-":FORX=1TO1500:NEXT:CLS(0):FORX=1TO500:NEXT:CLS:END
325 HA=0:RETURN
326 'PIT AND MAZE
327 IF HV=1 AND VV=1 AND WA=1 AND DI=1 THEN PRINT;"I'M IN THE BOTTOM OF THE PIT,   UNDER WATER,WAITING FOR YOU TO  TELL ME WHAT TO DO!!!":GOTO30
328 IF HV=1 AND VV=1 AND D1=0 AND WA=1 THENPRINT;"I'M TREADING WATER IN A PIT. IT IS ABOUT EIGHT FEET ACROSS IN   EACH DIRECTION.THE WALLS ARE    COVERED WITH A SLIPPERY GREEN   SLIME.I WON'T MENTION WHAT THE  AIR SMELLS LIKE.":T=0:O=0:GOTO447
329 IF HV=1 AND VV=1 AND WA=0 AND D1=1 THENPRINT;"I'M AT THE BOTTOM OF THE PIT.   LAYING IN A HEAP ON THE FLOOR   ARE ENOUGH ASSORTED BONES AND   RUSTED EQUIPMENT TO MAKE UP PER-HAPS TEN TO TWENTY KNIGHTS.":T=0:O=2:GOTO447
330 IF HV=1 AND VV=1 AND WA=0 AND D1=0 THEN PRINT;"THE WATER HAS ALL GONE AND I AM STANDING ON A PILE OF JUNK.IT ISTOO DARK TO REALLY TELL WHAT IS IN THE PILE-NOT THAT I REALLY   WANT TO KNOW FROM THE SMELL.":T=0:O=0:GOTO447
331 IF HV=4 AND VV=1 THENPRINT"I AM AT THE BOTTOM OF A FLIGHT  OF STEPS.":O=3:T=1:GOTO447
332 Q3=Q3+1
333 PRINT;"I AM IN A FEATURELESS MAZE OF   CORRIDORS"
334 IF HV=1 AND VV=2 THEN O=5:T=0:GOTO447
335 IF HV=1 AND VV=3 THEN O=6:T=0:GOTO447
336 IF HV=1 AND VV=4 THEN O=8:T=0:GOTO447
337 IF HV=1 AND VV=5 THEN O=6:T=0:GOTO447
338 IF HV=2 AND VV=1 THEN O=8:T=0:GOTO447
339 IF HV=2 AND VV=2 THEN O=5:T=0:GOTO447
340 IF HV=2 AND VV=3 THEN O=12:T=0:GOTO447
341 IF HV=2 AND VV=4 THEN O=13:T=0:GOTO447
342 IF HV=2 AND VV=5 THEN O=10:T=0:GOTO447
343 IF HV=3 AND VV=1 THEN O=9:T=0:GOTO447
344 IF HV=3 AND VV=2 THEN O=6:T=0:GOTO447
345 IF HV=3 AND VV=3 THEN O=8:T=0:GOTO447
346 IF HV=3 AND VV=4 THEN O=7:T=0:GOTO447
347 IF HV=3 AND VV=5 THEN O=10:T=0:GOTO447
348 IF HV=4 AND VV=5 THEN O=13:T=0:GOTO447
349 IF HV=4 AND VV=4 THEN O=11:T=0:GOTO447
350 IF HV=4 AND VV=3 THEN O=12:T=0:GOTO447
351 IF HV=4 AND VV=2 THEN O=14:T=0:GOTO447
352 IF HV=5 AND VV=1 THEN O=9:T=0:GOTO447
353 IF HV=5 AND VV=2 THEN O=12:T=0:GOTO447
354 IF HV=5 AND VV=3 THEN O=5:T=0:GOTO447
355 IF HV=5 AND VV=4 THEN O=12:T=0:GOTO447
356 IF HV=5 AND VV=5 THEN O=7:T=0:GOTO447
357 PRINT;"LEVEL 1  HV=";HV;"VV=";VV:GOTO30
358 'LEVEL 2
359 IF VV=1 AND HV=4 THENPRINT"I AM AT THE TOP OF A FLIGHT OF  STEPS":T=2:O=4:GOTO447
360 IF VV=1 AND HV=3 THENPRINT;A$(9):O=8:T=0:HA=1:GOTO447
361 IF VV=2 AND HV=3 THENPRINT;A$(9):O=12:T=0:HA=1:GOTO447
362 IF VV=2 AND HV=2 THENPRINT;A$(9):T=0:O=10:HA=1:GOTO447
363 IF VV=2 AND HV=1 THENPRINT"I'M IN A SMALL EMPTY ROOM.":HA=1:T=0:O=3:GOTO447
364 IF VV=3 AND HV=3 THENPRINT;"THIS MUST BE HERMAN'S WORKSHOP! THE WALLS ARE LINED WITH SHELVESCOVERED WITH VARIOUS BOTTLES ANDVESSELS CONTAINING ALL MANNER OFCOLORED AND SHAPED SUBSTANCES.A DESK IS SITTING IN THE CENTER,
365 IF HV=3 AND VV=3 THENPRINT"LITERALLY PILED WITH PAPERS.    THERE ARE WHAT APPEAR TO BE TWO DRAWERS ON THE FRONT OF THE DESK":T=0:O=11:GOTO447
366 IF VV=3 AND HV=4 THENPRINT;A$(9):T=0:O=10:HA=1:GOTO447
367 IF VV=3 AND HV=5 THENPRINT;A$(9):T=0:O=9:HA=1:GOTO447
368 IF VV=4 AND HV=3 AND DR=1 THENPRINT"THERE IS A LARGE DRAGON BLOCKINGTHE HALLWAY.IT LOOKS LIKE A NOR-MAL GREEN,ILL-TEMPERED,WINGED,  FIRE-BREATHING DRAGON.A PENTAGONSHAPED JEWEL IS HANGING AROUND  IT'S NECK.":T=0:O=5:GOTO447
369 IF VV=4 AND HV=3 THENPRINT;A$(9):T=0:O=5:GOTO447
370 IF VV=4 AND HV=5 THENPRINT;A$(9):T=0:O=5:HA=1:GOTO447
371 IF VV=5 AND HV=2 THENPRINT;"I'M IN AN EMPTY ROOM,INLAID IS  A PENTAGON-SHAPED MOSIAC IN THE FLOOR.":O=3:T=0:GOTO447
372 IF RI=0 AND VV=5 AND HV=3 AND DR=1 THENPRINT"I TOLD YOU IT WAS A ILL-TEMPEREDFIRE-BREATHING DRAGON!!         HE'S TURNING ME INTO A WELL-DONESNACK.":FORX=1TO3000:NEXT:CLS(0):FORX=1TO1000:NEXT:CLS:END
373 IF RI=1 AND VV=5 AND HV=3 AND DR=1 THEN374 ELSE 378
374 PRINT"I AM IN FRONT OF THE DRAGON.THE RING MUST BE WORKING-IT DOESN'T SEEM TO NOTICE ME.":PRINT"VISIBLE DIRECTIONS TO MOVE ARE- -NORTH-SOUTH":PRINT" %%%%%%%%%%%%%%%%%%%%%%%%%%%%%":PLAY"L150;FP10F":INPUT"WHAT DO YOU WANT TO DO          ";P$:
375 IF P$="N" THEN T=0:O=5:VV=4:HV=3:GOTO304
376 IF P$="S" THENPRINT"I GOT BY IT! AS I WAS GOING BY IGRABBED THE NECKLACE OFF IT'S   NECK.":DR=0:NE=1:T=0:O=7:VV=5:HV=3:GOTO447 T=0:O=7:VV=5:HV=3:GOTO304
377 PRINT"IT MUST HAVE SENSED SOMETHING!! IT JUST FLASH-FRIED ME.":FORX=1TO3000:NEXT:CLS(0):FORX=1TO500:NEXT:CLS:END
378 IF VV=5 AND HV=3 THENPRINT;A$(9):T=0:O=7:HA=1:GOTO447
379 IF VV=5 AND HV=5 THENPRINT"THE HALLWAY COMES TO A DEAD END.THERE IS A SMALL SACK ON THE    FLOOR.":T=0:O=1:HA=1:GOTO447
380 PRINT;"LEVEL 2  HV=";HV;"VV=";VV:GOTO30
381 'ENTRY LEVEL
382 IF HV=3 AND VV=5 THEN PRINT"I AM ON THE DRAW-BRIDGE AT THE  FRONT OF THE CASTLE.    TO THE  NORTH IS THE ENTRANCE-THE DOOR  IS OPEN. TO THE EAST AND WEST ISA PATH LEADING AROUND THE MOAT."
383 IF HV=3 AND VV=5 THEN PRINT"TO THE SOUTH IS THE GROUP OF    TOWNS-PEOPLE WHO CAME TO WATCH. IT WOULD BE UNTHINKABLE TO GO   BACK-THEY ARE COUNTING ON ME.":T=0:O=13:GOTO447
384 IF HV=3 AND VV=4 THEN PRINT"I'M IN THE GREAT ENTRY HALL.THE VAULTED CEILING IS LOST IN THE  SHADOWS.THERE IS NO SIGN OF ANY-ONE BEING AROUND.":T=0:O=5:GOTO447":T=0:HA=1:O=5:GOTO15000
385 IF HV=4 AND VV=4 THENPRINT"THERE IS A CIRCULAR STAIRWAY    LEADING UPWARDS. A SMALL OPENINGIN THE WALL IS THE ONLY SOURCE  OF LIGHT,THE UPPER PART OF THE  STAIRWAY IS IN SHADOW":T=1:O=1:GOTO447
386 IF HV=3 AND VV=3 AND WA=1 THEN PRINT;"THE FLOOR IS TILTING!!! I CAN'T MOVE FAST ENOUGH TO GET BACK.":LA=0:FORX=1TO1500:NEXTX:FOR X=1TO30:PRINT;"         I'M FALLING!":PRINT:FORY=1TO50:NEXTY:NEXTX:CLS(0)
387 IF HV=3 AND VV=3 AND WA=1 THENFORX=1TO500:NEXTX:CLS:PRINT@233,"SPLASH!!??":FORX=1TO1000:NEXTX:CLS:T=0:O=0:LV=1:HV=1:VV=1:WA=1:GOTO304
388 IF HV=3 AND VV=3 AND WA=0 THEN PRINT"THE FLOOR IS TILTING!!! I CAN'T MOVE FAST ENOUGH TO GET BACK.":LA=0:FORX=1TO1500:NEXTX:FORX=1TO30:PRINT;"         I'M FALLING!":PRINT:FORY=1TO50:NEXTY:NEXTX:CLS(0)
389 IF HV=3 AND VV=3 AND WA=0 THENFORX=1TO500:NEXTX:CLS:PRINT@237,"SPLAT!!":FORX=1TO1000:NEXTX:CLS:END
390 IF HV=3 AND VV=2 THEN PRINT"I AM IN THE REAR ENTRANCE HALL":T=0:O=15:HA=1:GOTO447
391 IF HV=4 AND VV=2 THEN PRINT;A$(9):O=9:T=0:HA=1:GOTO447
392 IF HV=4 AND VV=3 THEN PRINT"I MUST BE IN THE THRONE ROOM. ATTHE REAR OF THIS LARGE ROOM IS ALARGE ORNATE THRONE,ON THE WALL BEHIND IT IS A TRIANGLE-SHAPED  MIRROR. ON EITHER SIDE OF THE   THRONE ARE PLAIN CHAIRS. LARGE  TAPESTRIES DEPICTING STRA
393 IF HV=4 AND VV=3 AND MI=0 THEN PRINT"BEINGS HANG IN TATTERS FROM THE WALLS.":T=0:O=1:GOTO447
394 IF HV=4 AND VV=3 THENPRINT"BEINGS HANG IN TATTERS FROM THE WALLS.THERE IS A DOOORWAY IN THE SOUTH WALL.":T=0:O=5:GOTO447
395 IF HV=2 AND VV=2 THEN PRINT;A$(9):O=8:T=0:HA=1:GOTO447
396 IF HV=2 AND VV=3 AND D2=0 THEN PRINT"THIS IS THE MAIN HALL. SEVERAL  LARGE TABLES AND BENCHES ARE IN THE CENTER. MOUNTED ANIMAL HEADSARE ON THE WALLS,ALONG WITH SOMELANCES.":T=0:O=5:GOTO447
397 IF HV=2 AND VV=3 THEN PRINT"THIS IS THE MAIN HALL. SEVERAL  LARGE TABLES AND BENCHES ARE IN THE CENTER. MOUNTED ANIMAL HEADSARE ON THE WALLS. AT THE REAR ISIS AN OPEN DOOR.":T=0:O=5:GOTO447
398 IF HV=2 AND VV=4 AND CH=0 THEN PRINT"THE ROOM IS FULL OF CHESTS. ALL LOOK AS IF THEY HAVE BEEN BROKENOPEN EXCEPT ONE. IT SITS ON A   SMALL PEDESTAL IN THE CENTER OF THE ROOM.":T=0:O=1:GOTO447
399 IF HV=2 AND VV=4 THENPRINT"ALL THE CHESTS IN THE ROOM ARE  OPEN,INCLUDING THE ONE ON THE   PEDESTAL.":T=0:O=1:GOTO447
400 IF HV=3 AND VV=1 THEN PRINT"I AM ON A FOOT BRIDGE LEADING TOA REAR ENTRANCE TO THE CASTLE.":T=0:O=14:GOTO447
401 PRINT;"I AM ON A PATH OUTSIDE THE CAS- TLE WALLS.THERE IS A SLIME COV- ERED MOAT BETWEEN THE CASTLE ANDTHE PATH."
402 IF HV=1 AND VV=1 THEN O=8:GOTO447
403 IF HV=1 AND VV=2 THEN O=5:GOTO447
404 IF HV=1 AND VV=3 THEN O=5:GOTO447
405 IF HV=1 AND VV=4 THEN O=5:GOTO447
406 IF HV=1 AND VV=5 THEN O=6:GOTO447
407 IF HV=2 AND VV=5 THEN O=10:GOTO447
408 IF HV=4 AND VV=5 THEN O=10:GOTO447
409 IF HV=5 AND VV=5 THEN O=7:GOTO447
410 IF HV=5 AND VV=4 THEN O=5:GOTO447
411 IF HV=5 AND VV=3 THEN O=5:GOTO447
412 IF HV=5 AND VV=2 THEN O=5:GOTO447
413 IF HV=5 AND VV=1 THEN O=9:GOTO447
414 IF HV=4 AND VV=1 THEN O=10:GOTO447
415 IF HV=2 AND VV=1 THEN O=10:GOTO447
416 PRINT;"LEVEL 3  HV=";HV;"VV=";VV:PRINT;"#############ERROR############":GOTO30
417 'LEVEL 4
418 IF VV=2 AND HV=1 THENPRINT"I'M IN AN EMPTY ROOM,INLAID IS  A PENTAGON-SHAPED MOSIAC IN THE FLOOR.":T=0:O=2:GOTO447
419 IF VV=3 AND HV=1 THENPRINT;A$(9):T=0:O=11:HA=1:GOTO447
420 IF VV=3 AND HV=2 THENPRINT;A$(9):O=10:T=0:HA=1:GOTO447
421 IF VV=3 AND HV=3 AND ST=1 THENPRINT;A$(9):PRINT"THERE IS A NICHE WITH A STATUE  OF A GARGOYLE IN THE NORTH WALL.":T=0:O=14:HA=1:GOTO447
422 IF VV=3 AND HV=3 THENPRINT;A$(9):PRINT"THERE IS A NICHE IN THE NORTH   WALL.":T=0:O=14:HA=1:GOTO447
423 IF VV=3 AND HV=4 AND D3=1 THENPRINT;A$(9):PRINT;"THERE IS A MASSIVE DOOR IN THE  EAST WALL.":T=0:O=4:GOTO447
424 IF VV=3 AND HV=4 THENPRINT;A$(9):T=0:O=10:GOTO447
425 IF VV=3 AND HV=5 THENPRINT"THIS TRULY MUST BE BLANDOR'S OWNROOM.THE WALLS ARE COVERED WITH RICH TAPESTRIES,THE FLOOR IS OF WOOD-HIGHLY POLISHED,AND THE    FURNITURE IS GILTED IN GOLD AND COVERED WITH VARIOUS JEWELS.":T=0:O=4:GOTO447
426 IF TS=0 AND VV=4 AND HV=1 THENPRINT"I'M AT THE BOTTOM OF A FLIGHT OFSTAIRS,WELL,ALMOST.THERE ARE    ONLY FIVE STEPS AT THE BOTTOM,  THEN AN EMPTY SECTION OF ABOUT  THIRTY FEET,AND FIVE STEPS AT   THE TOP NEAR THE CEILING.":T=0:O=1:GOTO447
427 IF VV=4 AND HV=1 AND TS=1 THENPRINT"I AM AT THE BOTTOM OF A FLIGHT  OF STEPS.":T=1:O=1:GOTO447
428 IF VV=4 AND HV=3 THENPRINT;A$(9):O=6:T=0:HA=1:GOTO447
429 IF VV=4 AND HV=4 THENPRINT"I AM AT THE HEAD OF A FLIGHT OF STEPS":T=2:O=4:GOTO447
430 PRINT;"LEVEL 4  HV=";HV;"VV=";VV:GOTO30
431 'LEVEL 5
432 IF VV=4 AND HV=1 THENPRINT"I AM AT THE TOP OF A FLIGHT OF  STEPS":T=2:O=2:GOTO447
433 IF VV=5 AND HV=1 THENPRINT;A$(9):T=0:O=6:HA=1:GOTO447
434 IF VV=5 AND HV=2 THENPRINT;A$(9):T=0:O=10:HA=1:GOTO447
435 IF VV=5 AND HV=3 THENPRINT;A$(9):T=0:O=7:HA=1:GOTO447
436 IF VV=4 AND HV=3 THENPRINTA$(9):T=0:O=5:HA=1:GOTO447
437 IF VV=3 AND HV=3 THENPRINT;A$(9):T=0:O=8:HA=1:GOTO447
438 IF VV=3 AND HV=4 THENPRINT;A$(9):T=0:O=10:HA=1:GOTO447
439 IF VV=3 AND HV=5 THENPRINT;A$(9):T=0:O=7:HA=1:GOTO447
440 IF VV=2 AND HV=5 THENPRINT;A$(9):T=0:O=5:HA=1:GOTO447
441 IF VV=1 AND HV=5 THENPRINT;A$(9):T=0:O=9:HA=1:GOTO447
442 IF VV=1 AND HV=4 THENPRINT;A$(9):T=0:O=10:HA=1:GOTO447
443 IF VV=1 AND HV=3 AND D4=1 THENPRINT;A$(9):PRINT"IN THE SOUTH WALL IS ONE BLACK- COLORED BLOCK.:HA=1:T=0:O=3:GOTO447
444 IF VV=1 AND HV=3 THENPRINT;A$(9):T=0:O=8:HA=1:GOTO447
445 IF VV=2 AND HV=3 THENPRINT"IT IS SOME SORT OF SHRINE.THERE ARE SEVERAL LARGE WINDOWS IN THEROOM-THE DAYLIGHT IS A WELCOME  CRANGE FROM THE DARKNESS OF THE CASTLE BELOW.AN ALTAR IS LOCATEDIN THE CENTER OF THE ROOM.":T=0:O=1:GOTO447
446 PRINT;"LEVEL 5  HV=";HV:"VV=";VV:GOTO30
447 D=0:U=0:N=0:S=0:E=0:W=0
448 PRINT;A$(1)
449 IF T=0 AND O=0 THENPRINT;A$(8):GOTO30
450 IF T=1 THEN PRINT;A$(2):U=1:GOTO453
451 IF T=2 THEN PRINT;A$(3):D=1:GOTO453
452 IF T=3 THEN PRINT;A$(2);A$(3):U=1:D=1
453 ON O GOTO454,455,456,457,458,459,460,461,462,463,464,465,466,467,468,469
454 PRINT;A$(4):N=1:GOTO30
455 PRINT;A$(5):S=1:GOTO30
456 PRINT;A$(6):E=1:GOTO30
457 PRINT;A$(7):W=1:GOTO30
458 PRINT;A$(4);A$(5):N=1:S=1:GOTO30
459 PRINT;A$(4);A$(6):N=1:E=1:GOTO30
460 PRINT;A$(4);A$(7):N=1:W=1:GOTO30
461 PRINT;A$(5);A$(6):S=1:E=1:GOTO30
462 PRINT;A$(5);A$(7):S=1:W=1:GOTO30
463 PRINT;A$(6);A$(7):W=1:E=1:GOTO30
464 PRINT;A$(4);A$(5);A$(6):N=1:S=1:E=1:GOTO30
465 PRINT;A$(4);A$(5);A$(7):N=1:S=1:W=1:GOTO30
466 PRINT;A$(4);A$(6);A$(7):N=1:E=1:W=1:GOTO30
467 PRINT;A$(5);A$(6);A$(7):S=1:E=1:W=1:GOTO30
468 PRINT;A$(4);A$(5);A$(6);A$(7):N=1:S=1:E=1:W=1:GOTO30
469 GOTO30
470 'A$(1-10) DATA LINES
471 DATA VISIBLE DIRECTIONS TO MOVE ARE-,-UP,-DOWN,-NORTH,-SOUTH,-EAST,-WEST,---NONE---,I AM IN A HALLWAY. THE WALLS ANDFLOOR ARE SOLID STONE.,10
472 DATA LOOK,SLAY,JUMP,LIST,SAY,RUB,TURN,LIFT,CUT,STAB
473 DATA CLIM,GET,UNLO,STAN,PULL,PUSH,DROP,CARR,BRIB,BREA
474 DATA HIT,DIG,BURN,OPEN,PUT,SMEL,SPRI,KICK,DIVE,WIPE
475 'C$(1-40) DATA LINES
476 DATA WEB,STRA,SWOR,DAGG,RUBY,RING,SCOR,CHAI,SLIM,WALL
477 DATA OUTL,FLIN,NECK,STAI,DUST,DESK,PAPE,DRAW,BOTT,VESS
478 DATA PENT,STAT,NICH,KEY,DOOR,FLOO,PLAN,OCTA,TRIA,MIRR
479 DATA BLOC,CHES,HEXA,SPID,LANC,HOLE,ALTA,SPIK,BARS,SACK
