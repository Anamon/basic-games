10 PCLEAR4:CLEAR100,31000:DIMDP(64),DU(30,30),A(400),B(400):GOSUB560
20 CLS:PRINT"HERE ARE SOME TERMS AND         THEIR MEANINGS:":PRINT:PRINT"body points-AMOUNT OF DAMAGE A  CHARACTER CAN TAKE BEFORE DEATH.";:PRINT"defense points-THE ABILITY TO   WITHSTAND ATTACK WITHOUT TAKING DAMAGE(ARMOR OR DEXTERITY).
30 PRINT"experience level-AFFECTS PLAYERSABILITY TO ATTACK EFFECTIVELY.":PRINT"damage factor-AMOUNT OF DAMAGE  DONE TO CREATURES THE CHARACTER ATTACKED.":PRINT@490,"<enter> TO CONTINUE";
40 GOSUB540
50 CLS:INPUT"DO YOU WANT JUST A NEW DUNGEON   (Y/N)";DE$:IFDE$<>"Y"THEN70ELSEPRINT"PLACE TAPE WITH ADVENTURES IN   THE RECORDER AND PRESS PLAY":INPUT"NAME OF LAST ADVENTURE";L$:IFLEN(L$)>8THENL$=LEFT$(L$,8)
60 LOADML$:GOTO180
70 CLS:PRINT"YOU MAY CHOOSE HOW MANY POINTS  YOUR MAIN CHARACTER WILL HAVE   IN THESE AREAS, EXCEPT FOR THE  EXPERIENCE LEVEL WHICH STARTS   AT 4.  YOU HAVE 35 POINTS TO USEAND NONE OF THE LEVELS CAN BE   BELOW 5.":P=35
80 PRINT@288,"BODY POINTS":PRINT@320,"DAMAGE FACTOR":PRINT@352,"DEFENSE POINTS"
90 GOSUB550:PRINT@304,"";:INPUTBP(0):P=P-BP(0):GOSUB550:PRINT@336,"";:INPUTDF(0):P=P-DF(0):GOSUB550:PRINT@368,"";:INPUTP(0):P=P-P(0):GOSUB550:PRINT@384,"";:IFP<>0THEN70
100 IFBP(0)<5ORDF(0)<5ORP(0)<5THEN70
110 INPUT"WHAT NAME FOR YOUR CHARACTER    (UNDER 9 LETTERS)";NM$(0)
120 CLS:INPUT"DO YOU WANT <H>ENCHMEN          OR <M>ERCENARIES";D$:IFD$="H"THEN140ELSEIFD$<>"M"THEN120
130 CLS:R=RND(4)+1:PRINT"YOU HAVE";R;"MERCENARIES.":FORL=1TO R:BP(L)=8:DF(L)=6:P(L)=6:NM$(L)="MERCENARY":NEXT:FORL=1TO750:NEXT:GOTO180
140 R=RND(3):FORL=1TO R
150 BP(L)=RND(9)+3:DF(L)=RND(7)+2:P(L)=RND(7)+2:NEXTL
160 CLS:PRINT"henchmen names(UNDER 9 LETTERS)":PRINT"BODY PO. DAMAGE FAC. DEFENSE PO.";:FORL=1TO R
170 PRINT"   ";BP(L);"      ";DF(L);"       ";P(L):INPUTNM$(L):NEXT
180 CLS0:FORL=64TO70:PRINT@L,CHR$(128);"dungeon";:NEXT:FORL=224TO234:PRINT@L,CHR$(128);"generation";:NEXTL:FORL=384TO402:PRINT@L,CHR$(128);"period";:NEXTL:POKE65494,0
190 PLAY"V20O4T2L16DP16DP128DP128DP128L4.FP16L16FP128FP128L8FP64L16DP128DP128L8DP64CP64O3L4.B-P128O4L8CP64DP64E-P64FP64L16FP128FP128L1B-P128L8O3B-P64O4CP64L4.DP128L16DP128DP128L8FP64DP64L4.CP64L16CP128CP128L8E-P64CP64O3B-P64O4L16DP128DP128L8DP64L16F
200 PLAY"FP128L8FP48FP48L1F":POKE65495,0
210 FORL=0TO30:FORK=0TO30:DU(L,K)=0:NEXTK,L
220 FORL=1TO64:READDP:DP(L)=DP:NEXTL:DATA1,2,3,4,6,8,10,13,16,18,20,22,23,24,25,27,1,3,4,5,7,10,11,14,17,20,21,22,24,25,26,29,1,2,4,5,6,9,11,15,16,19,21,22,23,25,26,30,1,2,3,5,7,8,9,12,17,18,19,22,23,24,26,28:DU(15,15)=1:DP=1:X=15:Y=15
230 FORL=1TO64:IFDP(L)<>DP THEN270
240 XX=0:YY=0:IFL<17THENAD=32:YY=-1ELSEIFL>16ANDL<33THENAD=48:XX=1ELSEIFL>32ANDL<49THENAD=0:YY=1ELSEIFL>48THENAD=16:XX=-1
250 IFX+XX<0ORX+XX>30ORY+YY<0ORY+YY>30THEN270
260 IFDU(X+XX,Y+YY)=0THEN290
270 NEXT
280 X=A(SS):Y=B(SS):DP=DU(X,Y):SS=SS+1:PRINT@27,401-SS;:IFST<SS ANDST<50THENRESTORE:GOTO210ELSEIFST<SS THEN380ELSE230
290 D1=DP(RND(16)+AD)
300 IFX+XX=0ANDD1<27THEN290ELSEIFX+XX=30ANDD1<27THEN290ELSEIFY+YY=0ANDD1<27THEN290ELSEIFY+YY=30ANDD1<27THEN290
310 DU(X+XX,Y+YY)=D1:FORL=1TO64:IFDP(L)=DP THENP=P+1
320 NEXT
330 IFP>1THENA(ST)=X:B(ST)=Y:ST=ST+1:PRINT@0,400-ST;
340 IFST>399THEN380
350 P=0:FORL=1TO64:IFDP(L)=DP THENP=P+1
360 NEXT
370 IFP>1THENDP=D1:X=X+XX:Y=Y+YY:GOTO230ELSEGOTO230
380 IFSS=401THEN450ELSEX=A(SS):Y=B(SS):DP=DU(X,Y):SS=SS+1
390 FORL=1TO64:IFDP(L)<>DP THEN430
400 XX=0:YY=0:IFL<17THEND1=30:YY=-1ELSEIFL>16ANDL<33THEND1=28:XX=1ELSEIFL>32ANDL<49THEND1=27:YY=1ELSEIFL>48THEND1=29:XX=-1
410 IFDU(X+XX,Y+YY)<>0THEN430
420 DU(X+XX,Y+YY)=D1
430 NEXTL
440 IFSS=401THEN450ELSEX=A(SS):Y=B(SS):DP=DU(X,Y):SS=SS+1:PRINT@27,401-SS;:GOTO390
450 CLS0:FORL=224TO234:PRINT@L,CHR$(128);"completed";:NEXT
460 J=31000:FORL=0TO30:FORK=0TO30:POKEJ,DU(L,K):J=J+1:NEXTK,L:POKE31962,15:POKE31963,15:IFDE$="Y"THEN520
470 J=32000:L=0
480 POKEJ,BP(L):POKEJ+1,DF(L):POKEJ+2,P(L)
490 IFL=0THENPOKEJ+3,&H27:POKEJ+4,&H10 ELSEPOKEJ+3,&H0C:POKEJ+4,&HD0
500 J=J+5:L=L+1:IFNM$(L)<>""THEN480ELSEPOKEJ,42:J=J+1:L=0
510 FORK=1TO LEN(NM$(L)):POKEJ,ASC(MID$(NM$(L),K,1)):J=J+1:NEXTK:L=L+1:POKEJ,42:J=J+1:IFNM$(L)<>""THEN510ELSEPOKEJ,255
520 POKE65494,0:RUN"ADVENTUR"
530 END
540 K$=INKEY$:R=RND(0):IFK$=""THEN540ELSERETURN
550 PRINT@224,"points left";P;:RETURN
560 POKE65495,0:K=1:J=1:G$="by"+CHR$(128)+"greg"+CHR$(128)+"ricketts":L$="dungeon"+CHR$(128)+"adventure":CLS0:FORL=0TO509:PRINT@L,CHR$(128);CHR$(207);:IFL>197ANDL<215THENGOSUB590ELSEIFL>461ANDL<481THENGOSUB600
570 NEXT
580 POKE65494,0:PRINT@510,CHR$(128);:GOTO610
590 PRINT@L,MID$(L$,J,1);:J=J+1:RETURN
600 PRINT@L,MID$(G$,K,1);:K=K+1:RETURN
610 PLAY"O4T5L4DP128L8D+P128L4.EP128L8E-P128L4DP128L8C+P128L4DP128L8D+P128L2EP128L8E-P128L4DP128L8C+P128L4DP128L8D+P128L4EP128L8E-P128L4DP128L8C+P128L4CP128L8O3BP128AP4O4DP2DP128DP128C+P128DP128EP128P4L4.EL8F+P4L4.F+L8AP4L2GP128L8GP128GP128F+P128ED"
620 PLAY"P8D+P128EP8E-P128L4DP128L8DP128DP128DP128DP128DP8D+P128EP8E-P128L4DP128L8DP128DP128C+P128DP128EP4L4.EL8F+P4L4.F+L8AP4L4.GL4GP128L8GP128GP128F+P128GP128L4F+P128L8F+P128F+P128GP128G+P128AP128O3AP128AP128BP128O4C+P128DP8C+P128DP8C+P128L3D"
630 FORL=1TO10:CLS7:CLS8:NEXT:RETURN
