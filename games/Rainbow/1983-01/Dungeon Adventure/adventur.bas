
10 CLS:PCLEAR4:CLEAR1700,31000:DIML$(25),MO$(19),MC(1),MO(1),HM(1),MR(1),CY(1),CA(10,2),CP(11,3),MP(51,2),PO(11,3),N$(11),B(11),BP(11),DF(11),DP(11),EX(11),AL(11):L=RND(-TIMER)
20 DP$(1)="BD18BL5U3HLG2LHLH3U2HEU2HL2BU10R3EU3HUE2R2ERE2REUBR10D2FRERF2RF2D5FDR3BD10L2G2DFDG2L4GL2GD4"
30 DP$(2)="BD18BL5U2HLH3LH2U2HUHL3BU10R2E4UER2ER2FRFERER5F2R2ERFRFDFDFR2BD10L2GLG2DGDG2LG3D2BU36DGL8HU"
40 DP$(3)="BD18BL5U3H2LH5U3HUEU7HU2EUE2UER4EU3BR10D3F2DFRFRFDFD2FD5GDFDGD2GDGDG2L3GD3BR13BU13LHU8ERBL36RFD8GL"
50 DP$(4)="BD18BL5U2HL2HL2HLHUHU4HL2BU10R2E3RERE2R2ERERER2FR2FR2F2DF3DFD2F2D2GD6GLG4LG2D2BR13BU13LHUEU3HU2ERBU13BL13GHL2GHG2LHU"
60 DP$(5)="BD18BL5U3HLH2LHUHU2HU2HUEU2EUE2UE7FER3FR3FR2F3DFD4FD2GD2FDGD2GDG3LGL2D3BR13BU13LHU3EUHUE2BU13BL13GLGLHL2GLHUBL13BD13F2GD3GD3"
70 DP$(6)="BD18BL5U2EUHU2HU3H2L2GL3HL3BU10R3FER2EFRE3U3EU2HU3BR10D4GD2F2D3F2R2E2RFR4BD10L2HL2GLGL3HLGD2GDFD4FD2"
80 DP$(7)="BD18BL5U5EU2HU3HL3GL3H2LGLBU10R3ER7FR7ER2FR3FR2ER6BD10L6HL3GL2GDFD10BU36DG2LHL3HGHU"
90 DP$(8)="BD18BL5U4EU5HUHU6EU6HU2EU6BR10D6FD7GDFD2GD6FD3GD5BR13BU13LHUHU2EU2E2BL36RFGDFG2FDFGL"
100 DP$(9)="BD18BL5U5EUHU2H4L4GL4BU10R7FR3E2R5FR2F3D2FD3GD9GD3BR13BU13LHUEUHE2U3BU13BL13D2GL3GL2HLHU2"
110 DP$(10)="BD18BL5U5EU2HU5HU7E2R4FRERFD8FD6GD7BR13BU13HU2EHU2HE2BU13BL13GHL5GLHBL13BD13F2D2GDFD2GL"
120 INPUT"ARE YOU STARTING A NEW ADVENTURE (Y/N)";L$:IFL$="Y"THEN130ELSECLS:PRINT"PLACE TAPE WITH ADVENTURES      IN THE RECORDER PRESS PLAY":INPUT"WHAT WAS THE NAME GIVEN TO YOUR LAST ADVENTURE";L$:IFLEN(L$)>8THENLOADMLEFT$(L$,8):CLSELSELOADML$
130 J=32000:L=1
140 BP(L)=PEEK(J):DF(L)=PEEK(J+1):DP(L)=PEEK(J+2):EX(L)=(PEEK(J+3)*256)+PEEK(J+4):AL(L)=INT(EX(L)/3280+1):J=J+5:L=L+1:IFPEEK(J)<>42THEN140ELSEJ=J+1:L=1
150 N$(L)=N$(L)+CHR$(PEEK(J)):J=J+1:IFPEEK(J)<>42THEN150ELSEIFPEEK(J+1)=0THENC9=L:J=J+2:L=1:GOTO170ELSEIFPEEK(J+1)=255THENC9=L:J=J+2:L=1ELSEJ=J+1:L=L+1:GOTO150
160 B(L)=BP(L):L=L+1:IFN$(L)<>""THEN160ELSE190
170 FORL=1TO C9:B(L)=PEEK(J):J=J+1:NEXT:J=J+1:CR=(PEEK(J)*256)+PEEK(J+1):J=J+3:FORL=0TO10:CA(L,0)=PEEK(J):CA(L,1)=PEEK(J+1):CA(L,2)=PEEK(J+2):J=J+3:NEXT:IFPEEK(J)=255THEN190ELSEL=1
180 PO$(L)=PO$(L)+CHR$(PEEK(J)):J=J+1:IFPEEK(J+1)=255THEN190ELSEIFPEEK(J)=42THENJ=J+1:L=L+1:GOTO180ELSE180
190 POKE65495,0:FORL=0TO25:READL$:L$(L)=L$:NEXT:FORL=&H7F01 TO &H7FFF:READL$:POKEL,VAL("&H"+L$):NEXT:FORL=0TO19:READL$:MO$(L)=L$:NEXT:PMODE3:PCLS2:PMODE4:GET(0,0)-(7,7),HM,G:PMODE3:PCLS3:PMODE4:GET(0,0)-(7,7),MC,G:PMODE4
200 PCLS0:GET(0,0)-(7,7),MR,G:POKE65494,0:GOSUB810
210 XP=PEEK(31962):YP=PEEK(31963):IFPEEK(31480)=1THENPOKE31480,31
220 PMODE4:COLOR0,1:PCLS:SCREEN1,0:POKE65495,0
230 X=18:Y=18:PCLS:FORJ=YP-2TO YP+2:FORK=XP-2TO XP+2:DP=PEEK(31000+J*31+K):IFDP>30THENDP=DP-30:GOSUB2090
240 GOSUB410:DRAW"BM"+STR$(X)+","+STR$(Y)+"A"+STR$(A)+DP$(L):X=X+37:NEXTK:X=18:Y=Y+37:NEXTJ
250 L$="USE":DRAW"BM192,30":J=3:GOSUB2070:L$="ARROW":DRAW"BM200,40":J=5:GOSUB2070:L$="KEYS":J=4:DRAW"BM224,50":GOSUB2070:L$="CASSETTE":J=8:DRAW"BM192,170":GOSUB2070:L$="STATUS":J=6:DRAW"BM192,180":GOSUB2070
260 POKE65495,0:PUT(88,88)-(95,95),MC,PSET:FORL=1TO100:NEXT:LINE(88,88)-(95,95),PRESET,BF:K$=INKEY$
270 LINE(88,88)-(95,95),PRESET,BF:IFPEEK(342)=247GOSUB920:K$=CHR$(10)ELSEIFPEEK(341)=247GOSUB960:K$="^"ELSEIFPEEK(344)=247GOSUB1000:K$=CHR$(9)ELSEIFPEEK(343)=247GOSUB1030:K$=CHR$(8)ELSEIFK$="S"GOSUB2130:GOTO260ELSEIFK$="C"THEN2180ELSE260
280 POKE65494,0
290 IFPEEK(31000+YP*31+XP)>30ANDRND(100)=69GOSUB2160:GOTO230
300 IFXP=15AND YP=15THENL$="EXIT":J=4:DRAW"BM200,100":GOSUB2070:FORL=1TO5:SOUND240,2:SOUND150,2:NEXT:LINE(200,93)-(230,140),PRESET,BF ELSE320
310 K$=INKEY$:IFK$=""THEN310ELSEIFK$="E"THEN1930ELSE270
320 NU=31000+YP*31+XP:IFPEEK(NU)<31THENPOKENU,PEEK(NU)+30ELSE260
330 FORL=0TO10:IFCA(L,0)=XP ANDCA(L,1)=YP THENR=CA(L,2):GOTO360ELSENEXT
340 IFPEEK(NU)-30>15THEN260ELSEIFRND(6)>2THEN260
350 R=RND(20)-1
360 PLAY"V30O1L255T255":FORL=1TO30:PLAY"CDV-":NEXT:L$="YOU SPOT":J=8:DRAW"BM190,100":GOSUB2070:L=LEN(MO$(R)):L$=RIGHT$(MO$(R),L-14):J=L-14:DRAW"BM180,110":GOSUB2070:L$="FIGHT":J=5:DRAW"BM194,140":GOSUB2070
370 L$="OR RUN":J=6:DRAW"BM192,150":GOSUB2070
380 L$=INKEY$:IFL$=""THEN380ELSEIFL$="R"THEN1060ELSEIFL$="F"GOSUB1120ELSE380
390 GOTO220
400 GOTO400
410 IFDP=0ORDP>30THENL=0ELSEON DP GOSUB430,440,450,460,470,480,490,500,510,520,530,540,550,560,570,580,590,600,610,620,630,640,650,660,670,680,690,700,710,720
420 RETURN
430 L=1:RETURN
440 L=2:A=2:RETURN
450 L=2:A=1:RETURN
460 L=2:A=0:RETURN
470 L=2:A=3:RETURN
480 L=3:A=1:RETURN
490 L=3:A=0:RETURN
500 L=4:A=1:RETURN
510 L=4:A=2:RETURN
520 L=4:A=0:RETURN
530 L=4:A=3:RETURN
540 L=5:A=2:RETURN
550 L=5:A=1:RETURN
560 L=5:A=0:RETURN
570 L=5:A=3:RETURN
580 L=8:A=1:RETURN
590 L=8:A=0:RETURN
600 L=9:A=1:RETURN
610 L=9:A=2:RETURN
620 L=9:A=0:RETURN
630 L=9:A=3:RETURN
640 L=6:RETURN
650 L=7:A=2:RETURN
660 L=7:A=1:RETURN
670 L=7:A=0:RETURN
680 L=7:A=3:RETURN
690 L=10:A=1:RETURN
700 L=10:A=2:RETURN
710 L=10:A=0:RETURN
720 L=10:A=3:RETURN
730 DATAU4E2F2D2L4R4D2,RU6LR3FDGL2R2FDGL3BR4,BR4BUGL2HU4ER2FDBD4,RU6LR3FD4GL2BR3,U3R4L4U3R4BD6L4R4,U3R4L4U3R4BD6,BR2BU3R2D2GL2HU4ER2FBD5,U6D3R4U3D6,BRR2LU6LR2BRBD6,BU2DFREU5BD6,U6BR4G3F3,R4L4U6BR4BD6,U6F2E2D6,U6DF4U5D6,R4L4U6R4D6,U6R3FDGL3BR4BD3
740 DATABUU4ER2FD4GL2HBR2BUF2,U6R3FDGL3RF3,BUFR2EUHL2HUER2FBD5,BU6R4L2D6BR2,NU6R4NU6,BU6D4F2E2U4BD6,NU6E2F2NU6,UE2H2UDF2E2UDG2F2D,BU6DF2E2UDG2D3BR2,BU6R4DG2LR2LG2DR4
750 DATACC,A,A0,FD,7E,FD,CC,A,B8,FD,7E,FF,8E,A,A0,EC,81,ED,89,FB,5E,BC,7E,FF,26,F5,CC,0,20,F3,7E,FF,FD,7E,FF,CC,0,20,F3,7E,FD,FD,7E,FD,8C,1D,58,27,5,BE,7E,FD,20,D9,39,CC,19,40,FD,7E,FD,CC,19,58,FD,7E,FF,8E,19,58,EC,83,ED,89,4,A0,BC,7E,FD,26,F5,CC
760 DATAFF,E0,F3,7E,FF,FD,7E,FF,CC,FF,E0,F3,7E,FD,FD,7E,FD,8C,6,0,27,5,BE,7E,FF,20,D9,39,CC,6,4,FD,7E,FD,CC,6,17,FD,7E,FF,8E,6,4,A6,80,48,48,48,48,48,A7,1B,A6,84,44,44,44,AB,1B,A7,1B,BC,7E,FF,26,E9,CC,0,20,F3,7E,FD,FD,7E,FD,CC,0,20,F3,7E,FF,FD,7E
770 DATAFF,BE,7E,FD,8C,1D,E4,26,CF,39,CC,5,FF,FD,7E,FD,CC,6,12,FD,7E,FF,8E,6,12,A6,84,44,44,48,48,48,48,48,48,48,8B,7F,A7,5,A6,84,44,44,44,44,44,A7,4,A6,82,48,48,48,AB,5,A7,5,BC,7E,FD,26,E9,CC,0,20,F3,7E,FD,FD,7E,FD,CC,0,20,F3,7E,FF,FD,7E,FF,BE,7E
780 DATAFF,8C,1D,F2,26,C0,39
790 DATA15252808039500DRAGONS,15101025034500WIZARDS,10152005043750GIANTS,06150804031650OGRES,06150700021400LG LIZARDS,05050510021250SERPENTS,05090605031250PRIESTS,03050502040750SPIDERS,01020306040600BURGULARS,01020600020450GIANT ANTS
800 DATA01040303030550GOBLINS,01040304030600SKELETONS,01060602010750ZOMBIES,02080606041100WERERATS,04110603051200HARPIES,05140605051500GARGOYLES,08200800041800TROLLS,10301500024125HYDRAS,11104900036300EVIL IDOLS,18302210059999DEMONS
810 CP=1:PMODE4:COLOR0,1:PCLS:SCREEN1,0:FORL=50TO130STEP20:LINE(50,L)-(150,L+20),PSET,B:NEXT:FORL=70TO130STEP20:LINE(L,50)-(L,150),PSET:NEXT:L$="ADVENTURERS SETUP":DRAW"BM10,10":J=17:GOSUB2070:L$="HIT  P  TO PLACE":DRAW"BM18,20":J=16:GOSUB2070
820 L$="CHARACTER UP":DRAW"BM166,40":J=12:GOSUB2070:L$="DO NOT PUT ADVENTURERS IN SAME BOX":DRAW"BM0,181":J=34:GOSUB2070:L$="FACING":DRAW"BM0,60":J=6:GOSUB2070:DRAW"BM25,100U35NG15F15":X=56:Y=56:X1=0:Y1=0:L$="STATUS":J=6:DRAW"BM0,140":GOSUB2070
830 L$=N$(CP):DRAW"BM174,50":J=LEN(N$(CP)):GOSUB2070
840 GET(X,Y)-(X+7,Y+7),CY,G
850 PUT(X,Y)-(X+7,Y+7),MC,PSET:PUT(X,Y)-(X+7,Y+7),MC,NOT
860 L$=INKEY$:IFL$=""THEN850ELSEIFL$=CHR$(94)THENY1=-20ELSEIFL$=CHR$(10)THENY1=20ELSEIFL$=CHR$(8)THENX1=-20ELSEIFL$=CHR$(9)THENX1=20ELSEIFL$="P"THEN880ELSEIFL$="S"THENGOSUB2130:GOTO850ELSE850
870 IFX+X1<56ORX+X1>136ORY+Y1<56ORY+Y1>136THENX1=0:Y1=0:GOTO850ELSEPUT(X,Y)-(X+7,Y+7),CY,PSET:X=X+X1:Y=Y+Y1:X1=0:Y1=0:GOTO840
880 IFCP=1THENPUT(X,Y)-(X+7,Y+7),MC,PSET ELSEIFN$(CP)="MERCENARY"THENPUT(X,Y)-(X+7,Y+7),MR,PSET ELSEPUT(X,Y)-(X+7,Y+7),HM,PSET
890 CP(CP,0)=INT((X-56)*.6+62):CP(CP,2)=CP(CP,0):CP(CP,1)=INT((Y-56)*.6+133):CP(CP,3)=CP(CP,1):CP=CP+1:IFCP=C9+1THEN900ELSELINE(174,44)-(255,50),PRESET,BF:GOTO830
900 L$="REDO YES OR NO":DRAW"BM160,100":J=14:GOSUB2070
910 L$=INKEY$:IFL$="Y"THEN810ELSEIFL$="N"THENRETURNELSE910
920 FORL=104TO118:IFPPOINT(92,L)=0THENRETURNELSENEXT:GOSUB2100
930 YP=YP+1:PUT(88,125)-(95,132),MC,PSET:EXEC&H7F01:LINE(0,148)-(185,191),PRESET,BF:X=18:FORJ=XP-2TO XP+2:DP=PEEK(31000+(YP+2)*31+J):IFDP>30THENDP=DP-30:Y=166:GOSUB2090
940 GOSUB410:DRAW"BM"+STR$(X)+",166A"+STR$(A)+DP$(L):X=X+37:NEXT
950 RETURN
960 FORL=66TO80:IFPPOINT(92,L)=0THENRETURNELSENEXT:GOSUB2100
970 YP=YP-1:PUT(88,51)-(95,58),MC,PSET:EXEC&H7F38:LINE(0,0)-(185,36),PRESET,BF:X=18:FORJ=XP-2TO XP+2:DP=PEEK(31000+(YP-2)*31+J):IFDP>30THENDP=DP-30:Y=18:GOSUB2090
980 GOSUB410:DRAW"BM"+STR$(X)+",18A"+STR$(A)+DP$(L):X=X+37:NEXT
990 RETURN
1000 FORL=104TO118:IFPPOINT(L,92)=0THENRETURNELSENEXT:GOSUB2100
1010 XP=XP+1:PUT(125,88)-(132,95),MC,PSET:EXEC&H7F6F:LINE(148,0)-(185,191),PRESET,BF:Y=18:FORJ=YP-2TO YP+2:DP=PEEK(31002+J*31+XP):IFDP>30THENDP=DP-30:X=166:GOSUB2090
1020 GOSUB410:DRAW"BM166,"+STR$(Y)+"A"+STR$(A)+DP$(L):Y=Y+37:NEXT:RETURN
1030 FORL=66TO80:IFPPOINT(L,92)=0THENRETURNELSENEXT:GOSUB2100
1040 XP=XP-1:PUT(51,88)-(58,95),MC,PSET:EXEC&H7FB0:LINE(0,0)-(36,191),PRESET,BF:Y=18:FORJ=YP-2 TOYP+2:DP=PEEK(30998+J*31+XP):IFDP>30THENDP=DP-30:X=18:GOSUB2090
1050 GOSUB410:DRAW"BM18,"+STR$(Y)+"A"+STR$(A)+DP$(L):Y=Y+37:NEXT:RETURN
1060 FORL=0TO10:IFCA(L,0)<>XP ANDCA(L,1)<>YP THENNEXTELSE1100
1070 FORL=0TO10:IFCA(L,0)<>0THENNEXTELSE1090
1080 GOTO1120
1090 CA(L,0)=XP:CA(L,1)=YP:CA(L,2)=R
1100 NU=31000+YP*31+XP:POKENU,PEEK(NU)-30:IFK$=CHR$(94)THENYP=YP+1ELSEIFK$=CHR$(10)THENYP=YP-1ELSEIFK$=CHR$(8)THENXP=XP+1ELSEXP=XP-1
1110 GOTO220
1120 'FIGHT SEQUENCE
1130 PMODE4:COLOR0,1:SCREEN1,0
1140 PCLS:FORK=23TO179STEP12:FORL=12TO168STEP12:PSET(L,K):NEXTL,K:LINE(0,11)-(180,191),PSET,B:J=LEN(MO$(R))-15:L$=RIGHT$(MO$(R),J+1):DRAW"BM30,10":GOSUB2070:L$=" FIGHT":J=6:GOSUB2070:L$="MOVE":J=4:DRAW"BM184,150":GOSUB2070:L$="FIGHT":J=5
1150 DRAW"BM184,160":GOSUB2070:L$="POTION":J=6:DRAW"BM184,170":GOSUB2070:L$="STATUS":J=6:DRAW"BM184,180":GOSUB2070:CP=1:L$="UP":J=2:DRAW"BM212,60":GOSUB2070
1160 FORL=1TO RND(6)-1
1170 X=INT(RND(143)/12)*12:Y=INT(RND(80)/12)*12+11:LINE(X,Y)-(X+RND(4)*12,Y+RND(4)*12),PSET,BF:NEXT
1180 SOUND200,1:X=CP(CP,0):Y=CP(CP,1):IFCP=1THENPUT(X,Y)-(X+7,Y+7),MC,PSETELSEIFN$(CP)="MERCENARY"THENPUT(X,Y)-(X+7,Y+7),MR,PSETELSEPUT(X,Y)-(X+7,Y+7),HM,PSET
1190 CP=CP+1:IFCP<>C9+1THEN1180ELSEAL=VAL(LEFT$(MO$(R),2)):MP(0,2)=VAL(MID$(MO$(R),3,2)):DF=VAL(MID$(MO$(R),5,2)):DP=VAL(MID$(MO$(R),7,2)):MP=VAL(MID$(MO$(R),9,2)):NU=0:FORL=1TO10:NU=NU+AL(L):NEXT:NU=INT(NU/(RND(0)+.70)/AL)
1200 IFNU>50THENNU=50ELSEIFNU<1THENNU=1
1210 EX=VAL(MID$(MO$(R),11,4))*NU
1220 FORL=1TO NU-1:MP(L,2)=MP(0,2):NEXT
1230 FORL=0TO NU-1
1240 X=INT(RND(160)/12)*12+14:Y=INT((RND(180)+11)/12)*12+1:IFPPOINT(X+4,Y+4)=0THEN1240ELSEIFX>59ANDX<121ANDY>130THEN1240ELSEMP(L,0)=X:MP(L,1)=Y:PLAY"V30O2T255L255":FORK=1TO30:PLAY"CCDV-":NEXT:FORK=1TO5STEP2:CIRCLE(X+4,Y+4),K:NEXTK,L
1250 TT=0:L=RND(100):IFL>50THEN1550ELSECP=1
1260 L$=N$(CP):J=LEN(L$):DRAW"BM184,70":GOSUB2070:X=CP(CP,0):Y=CP(CP,1):GET(X,Y)-(X+7,Y+7),CY,G
1270 DRAW"BM204,140C0NU30NH15NE15":PUT(X,Y)-(X+7,Y+7),CY,PRESET:DRAW"BM204,140C1NU30NH15NE15C0":PUT(X,Y)-(X+7,Y+7),CY,PSET
1280 K$=INKEY$:IFK$=""THEN1270ELSEIFK$="M"GOSUB1320ELSEIFK$="F"GOSUB1380ELSEIFK$="P"GOSUB1500ELSEIFK$="S"GOSUB2130:GOTO1270ELSE1270
1290 IFNU=0THEN1730
1300 CP(CP,0)=X:CP(CP,1)=Y:LINE(184,63)-(255,70),PRESET,BF:CP=CP+1:IFCP=C9+1THENTT=TT+1ELSE1260
1310 IFTT=2THEN1250ELSE1550
1320 PUT(X,Y)-(X+7,Y+7),CY,PSET:PUT(X,Y)-(X+7,Y+7),MO,PRESET
1330 K$=INKEY$:IFK$=""THEN1320ELSEIFK$=CHR$(94)THENL=Y-6:J=Y-1:K=X ELSEIFK$=CHR$(10)THENL=Y+8:J=Y+14:K=X ELSEIFK$=CHR$(8)THENL=X-6:J=X-1:K=Y ELSEIFK$=CHR$(9)THENL=X+8:J=X+14:K=Y ELSEIFK$=CHR$(13)THENCC=0:PUT(X,Y)-(X+7,Y+7),CY,PSET:RETURNELSE1320
1340 IFK=X THEN1350ELSEFORLL=L TO J:IFPPOINT(LL,K)=0THEN1320ELSENEXT:GOTO1360
1350 FORLL=L TO J:IFPPOINT(K,LL)=0 OR PPOINT(K+1,LL)=0THEN1320ELSENEXT
1360 IFK$=CHR$(94)THENY=Y-12ELSEIFK$=CHR$(10)THENY=Y+12ELSEIFK$=CHR$(9)THENX=X+12ELSEIFK$=CHR$(8)THENX=X-12
1370 CC=CC+1:IFCC=3+PO(CP,3)THENCC=0:PUT(X,Y)-(X+7,Y+7),CY,PSET:RETURNELSE1320
1380 L$="E R T":J=5:DRAW"BM190,90":GOSUB2070:L$="D    G":J=6:DRAW"BM190,100":GOSUB2070:L$="C V B":J=5:DRAW"BM190,110":GOSUB2070:GET(X,Y)-(X+7,Y+7),CY,G
1390 PUT(X,Y)-(X+7,Y+7),MO,PRESET:PUT(X,Y)-(X+7,Y+7),CY,PSET:K$=INKEY$:IFK$=""THEN1390
1400 IFK$="E"THENX1=-12:Y1=-12ELSEIFK$="R"THENX1=0:Y1=-12ELSEIFK$="T"THENX1=12:Y1=-12ELSEIFK$="G"THENX1=12:Y1=0ELSEIFK$="B"THENX1=12:Y1=12ELSEIFK$="V"THENX1=0:Y1=12ELSEIFK$="C"THENX1=-12:Y1=12ELSEIFK$="D"THENX1=-12:Y1=0ELSEIFK$="Q"THEN1440ELSE1390
1410 FORL=0TO NU-1:IFMP(L,0)=X+X1 ANDMP(L,1)=Y+Y1 THEN1420ELSENEXT:GOTO1390
1420 L=RND(100):IFL<(AL(CP)+PO(CP,0))*3-DP+40THEN1450
1430 FORL=1TO20:PLAY"T255L255V15O3CO4C":NEXT:DRAW"BM190,130":L=RND(8):IFL=1THENL$="STUPID":J=6:GOSUB2070ELSEIFL=2THENL$="WIMP":J=4:GOSUB2070ELSEIFL=3THENL$="LOSER":J=5:GOSUB2070ELSEIFL=4THENL$="SWISH":J=5:GOSUB2070
1440 FORL=1TO500:NEXT:LINE(185,80)-(255,130),PRESET,BF:RETURN
1450 PLAY"V20T4L4O3GO4CL3EL4CE":DRAW"BM190,130":L=RND(6):IFL=1THENL$="POW":J=3:GOSUB2070ELSEIFL=2THENL$="BAM":J=3:GOSUB2070ELSEIFL=3THENL$="CLOBBER":J=7:GOSUB2070ELSEIFL=4THENL$="SLASH":J=5:GOSUB2070ELSEIFL=5THENL$="SMASH":J=5:GOSUB2070
1460 FORL=1TO500:NEXT
1470 FORL=0TO NU-1:IFMP(L,0)<>X+X1 ORMP(L,1)<>Y+Y1 THENNEXT
1480 MP(L,2)=MP(L,2)-RND(DF(CP)+PO(CP,1)):IFMP(L,2)>0THENLINE(185,80)-(255,130),PRESET,BF:RETURNELSEFORJ=1TO5STEP2:CIRCLE(MP(L,0)+4,MP(L,1)+4),J,1:NEXT:LINE(185,80)-(255,130),PRESET,BF
1490 FORK=L TO NU-1:MP(K,0)=MP(K+1,0):MP(K,1)=MP(K+1,1):MP(K,2)=MP(K+1,2):NEXT:NU=NU-1:RETURN
1500 L=1:CLS:PRINT" 0 NONE"
1510 PRINTL;PO$(L):IFPO$(L+1)<>""THENL=L+1:GOTO1510ELSEINPUT"which one";L:IFPO$(L)=""THENPMODE4:COLOR0,1:SCREEN1,0:RETURN
1520 IFLEN(PO$(L))=15THENPO(CP,0)=PO(CP,0)+RND(4)+4ELSEIFMID$(PO$(L),11,1)="H"THENBP(CP)=B(CP)ELSEIFMID$(PO$(L),11,1)="S"THENPO(CP,1)=PO(CP,1)+RND(10)+8ELSEIFMID$(PO$(L),11,1)="R"THENPO(CP,2)=PO(CP,2)+RND(20)+10ELSEPO(CP,3)=7
1530 FORK=L TO9:PO$(K)=PO$(K+1):NEXT:PMODE4:COLOR0,1:SCREEN1,0:RETURN
1540 GOTO1540
1550 MO=0:J=LEN(MO$(R))-15:L$=MID$(MO$(R),15,J):DRAW"BM184,70":GOSUB2070
1560 CP=1:HY=300
1570 L=INT(SQR(ABS(CP(CP,0)-MP(MO,0))^2+ABS(CP(CP,1)-MP(MO,1))^2)):IFL<HY THENX1=CP(CP,0):Y1=CP(CP,1):HY=L:C1=CP
1580 CP=CP+1:IFCP<>C9+1THEN1570
1590 IFHY<20THEN1670ELSEM1=MP:X2=MP(MO,0):Y2=MP(MO,1)
1600 IFABS(X1-X2)>ABS(Y1-Y2)ANDPPOINT(X2+16,Y2+4)<>0ANDPPOINT(X2+17,Y2+4)<>0ANDSGN(X1-X2)<>-1ANDX2<169THENX2=X2+12:GOTO1640ELSEIFPPOINT(X2+4,Y2+16)<>0ANDPPOINT(X2+5,Y2+16)<>0ANDSGN(Y1-Y2)<>-1THENY2=Y2+12:GOTO1640
1610 IFSGN(X1-X2)<>-1ANDPPOINT(X2+16,Y2+4)<>0ANDPPOINT(X2+17,Y2+4)<>0ANDX2<169THENX2=X2+12:GOTO1640
1620 IFABS(X1-X2)>ABS(Y1-Y2)ANDPPOINT(X2-8,Y2+4)<>0ANDPPOINT(X2-7,Y2+4)<>0THENX2=X2-12:GOTO1640ELSEIFPPOINT(X2+4,Y2-8)<>0ANDPPOINT(X2+5,Y2-8)<>0ANDSGN(Y2-Y1)=1THENY2=Y2-12:GOTO1640
1630 IFPPOINT(X2-8,Y2+4)<>0ANDPPOINT(X2-7,Y2+4)<>0THENX2=X2-12
1640 FORL=1TO5STEP2:CIRCLE(MP(MO,0)+4,MP(MO,1)+4),L,1:NEXT:MP(MO,0)=X2:MP(MO,1)=Y2:SOUND200,1:FORL=1TO5STEP2:CIRCLE(X2+4,Y2+4),L,0:NEXT:M1=M1-1:IFM1=0ANDMO=NU-1THEN1660ELSEIFM1=0THENMO=MO+1:GOTO1560
1650 L=INT(SQR(ABS(X1-X2)^2+ABS(Y1-Y2)^2)):IFL<20ANDMO=NU-1THEN1660ELSEIFL<20THENMO=MO+1:GOTO1560ELSE1600
1660 TT=TT+1:IFTT=1THENCP=1:LINE(184,63)-(255,70),PRESET,BF:GOTO1260ELSELINE(184,63)-(255,70),PRESET,BF:GOTO1250
1670 FORL=1TO5STEP2:CIRCLE(MP(MO,0)+4,MP(MO,1)+4),L,1:CIRCLE(MP(MO,0)+4,MP(MO,1)+4),L,0:NEXT:L=RND(100):IFL<AL*3.75-DP(C1)-PO(C1,2)+25THEN1690
1680 L$="MISSED":J=6:DRAW"BM185,120":GOSUB2070:PLAY"T12L4O4V15BAGFDEC":FORL=1TO500:NEXT:LINE(185,113)-(250,120),PRESET,BF:MO=MO+1:IFMO=NU THEN1660ELSE1560
1690 PLAY"T4V25L2O4DL4O3BO4EL2DO3B":L$="HIT":J=3:DRAW"BM195,120":GOSUB2070:BP(C1)=BP(C1)-RND(DF):GET(X1,Y1)-(X1+7,Y1+7),CY,G:FORL=1TO20:PUT(X1,Y1)-(X1+7,Y1+7),CY,PRESET:PUT(X1,Y1)-(X1+7,Y1+7),CY,PSET:NEXT:LINE(185,113)-(250,120),PRESET,BF
1700 MO=MO+1:IFBP(C1)>0ANDMO=NU THEN1660
1710 IFBP(C1)>0THEN1560ELSELINE(X1,Y1)-(X1+7,Y1+7),PRESET,BF:FORL=C1 TO C9
1715 CP(L,0)=CP(L+1,0):CP(L,1)=CP(L+1,1):B(L)=B(L+1):BP(L)=BP(L+1):DF(L)=DF(L+1):DP(L)=DP(L+1):AL(L)=AL(L+1):EX(L)=EX(L+1):N$(L)=N$(L+1):PO(L,0)=PO(L+1,0):PO(L,1)=PO(L+1,1):PO(L,2)=PO(L+1,2):PO(L,3)=PO(L+1,3):NEXT:LC=1:C9=C9-1:IFC1=1THEN2270
1720 IFMO=NU THEN1660ELSE1560
1730 'BOUTY PHASE
1740 CLS:PRINT"experience collected";EX*(1-((AL(1)-1)*.05)):PRINT"credits collected";:C2=INT(EX/(RND(C9)+1)):CR=CR+C2:PRINTC2:PRINT"credits to date";CR:PRINT"potions collected":L=RND(10):IFL>AL THENPRINT"NONE":GOTO1840
1750 FORL=1TO10:IFPO$(L)=""THEN1770ELSENEXT
1760 PRINT"NONE":GOTO1840
1770 K=RND(5):ON K GOTO1780,1790,1800,1810,1820
1780 PO$(L)="POTION OF SKILL":GOTO1830
1790 PO$(L)="POTION OF HEALING":GOTO1830
1800 PO$(L)="POTION OF STRENGTH":GOTO1830
1810 PO$(L)="POTION OF REFLEXES":GOTO1830
1820 PO$(L)="POTION OF QUICKNESS"
1830 PRINTPO$(L)
1840 PRINT:PRINT:PRINT"<enter>":EX=EX*(1-((AL(1)-1)*.05))
1850 K=C9-1
1860 IFK<>0THENFORL=2TO C9:IFN$(L)<>"MERCENARY"THEN EX(L)=EX(L)+EX/2/K:NEXTELSENEXT
1870 EX(1)=EX(1)+EX/2
1880 L$=INKEY$:IFL$=""THEN1880
1890 FORL=1TO C9:AL=AL(L):AL(L)=INT(EX(L)/3280+1):IFAL<>AL(L)THENB(L)=B(L)+RND(2):NEXTELSENEXT
1900 FORL=1TO10:FORK=0TO3:PO(L,K)=0:NEXTK,L
1910 IFAL(1)=20THEN2280
1920 IFLC=1THENLC=0:GOTO810ELSEFORL=1TO10:CP(L,0)=CP(L,2):CP(L,1)=CP(L,3):NEXT:RETURN
1930 CLS:PRINT"STAYING IN TOWN HAS COST YOU":L=RND(100)+100:PRINTL;"CREDITS":CR=CR-L:FORL=1TO C9:BP(L)=B(L):NEXT:GOSUB2120
1940 GOSUB2110:PRINT"(1)DO YOU WANT TO GAIN LEVELS.":PRINT"(2)HIRE ON HENCHMEN, MERCENARIES";:PRINT"(3)GO BACK TO THE DUNGEON.":INPUTL:IFL=3GOSUB810:GOTO220ELSEIFL=1THEN1950ELSEIFL=2THEN1990ELSE1940
1950 CLS:GOSUB2110:FORL=1TO C9:PRINTL;N$(L):NEXT:INPUT"WHO IS TO GAIN EXPERIENCE";L:IFN$(L)=""THENCLS:GOTO1940
1960 INPUT"HOW MANY CREDITS ARE YOU        SPENDING";K:IFK<0THEN1950ELSEIFK>CR THENK=CR
1970 EX=(RND(0)+.5)*CR:PRINT"YOU HAVE GAINED ";:PRINT USING"#.##";EX/3280;:PRINT" PERCENT    OF A LEVEL":EX(L)=EX(L)+EX:AL=AL(L):AL(L)=INT(EX(L)/3280+1):CR=CR-K:PRINT:PRINT"    <enter>";:IFAL<>AL(L)THENB(L)=B(L)+RND(2)
1980 K$=INKEY$:IFK$=""THEN1980ELSECLS:GOTO1940
1990 CLS:GOSUB2110
2000 IFC9=10THENPRINT"NO ONE WANTS A JOB":ELSE2020
2010 K$=INKEY$:IFK$=""THEN2010ELSECLS:GOTO1940
2020 PRINT"HENCHMEN COST 2000 CREDITS":PRINT"MERCENARIES COST 1500 CREDITS":PRINT"ENTER 0 CREDITS FOR NEITHER":INPUT"AMOUNT";L:IFL=1500THENCR=CR-L:GOSUB2120:GOTO2030ELSEIFL=2000THENCR=CR-L:GOSUB2120:GOTO2040ELSECLS:GOTO1940
2030 C9=C9+1:L=C9:N$(L)="MERCENARY":BP(L)=8:B(L)=8:DF(L)=6:DP(L)=6:EX(L)=3300:AL(L)=2:CLS:GOTO1940
2040 C9=C9+1:L=C9:BP(L)=RND(8)+3:DF(L)=RND(7)+2:DP(L)=RND(7)+2:B(L)=BP(L):EX(L)=3300:AL(L)=2:PRINT"WHAT NAME FOR YOUR HENCHMAN     (UNDER 9 LETTERS)":PRINT"BODY PT/DAMAGE FAC./DEFENSE PTS.";:PRINTTAB(4)B(L);TAB(15)DF(L);TAB(27)DP(L)
2050 INPUTL$:N$(L)=L$:CLS:GOTO1940
2060 GOTO2060
2070 FORL=1TO J:LL=ASC(MID$(L$,L,1))-65:IFLL<0THENDRAW"A0BR4"ELSEDRAW"A0C0"+L$(LL)+"BR4"
2080 NEXT:RETURN
2090 DRAW"BM"+STR$(X)+","+STR$(Y)+"NE2NF2NG2H2":RETURN
2100 DRAW"BM92,92NE2NF2NG2H2":RETURN
2110 PRINT"you have";CR;"credits":RETURN
2120 IFCR<0GOTO2260ELSERETURN
2130 CLS:PRINT"    ATT. LVL./BODY/DAM FAC./DEF/";
2140 FORK=1TO C9:PRINTN$(K);:PRINTTAB(10)AL(K)+PO(K,0);TAB(14)BP(K);TAB(21)DF(K)+PO(K,1);TAB(27)DP(K)+PO(K,2):NEXT:PRINT:PRINT"CREDITS";CR
2150 K$=INKEY$:IFK$=""THEN2150ELSEPMODE4:COLOR0,1:SCREEN1,0:RETURN
2160 PLAY"V31L255T255O4":FORL=1TO30:PLAY"BAGFEDCV-":NEXT:PLAY"O2V31L20T20BAG":FORL=1TO30:PLAY"GV-":NEXT:PCLS:L$="YOUR PARTY HAS BEEN SURPRISED BY":J=32:DRAW"BM10,80":GOSUB2070:DRAW"BRBURULDBU3RULD":R=RND(20)-1:J=LEN(MO$(R))-14
2170 L$=RIGHT$(MO$(R),J):DRAW"BM99,90":GOSUB2070:FORL=1TO1000:NEXT:GOSUB1120:PMODE4:COLOR0,1:PCLS:SCREEN1,0:RETURN
2180 CLS:INPUT"DO YOU WISH TO SAVE DUNGEON     (Y/N)";L$:IFL$="N"THENPMODE4:COLOR0,1:SCREEN1,0:GOTO260
2190 POKE65495,0:J=32000:FORL=1TO C9:POKEJ,BP(L):POKEJ+1,DF(L):POKEJ+2,DP(L):L$=HEX$(EX(L)):POKEJ+3,VAL("&H"+LEFT$(L$,LEN(L$)-2)):POKEJ+4,VAL("&H"+RIGHT$(L$,2)):J=J+5:NEXT:POKEJ,42:J=J+1
2200 FORL=1TO C9:FORK=1TO LEN(N$(L)):POKEJ,ASC(MID$(N$(L),K,1)):J=J+1:NEXT:POKEJ,42:J=J+1:NEXT:POKEJ,0:J=J+1:FORL=1TO C9:POKEJ,B(L):J=J+1:NEXT:POKEJ,42:L$=HEX$(CR):IFLEN(L$)<3THENPOKEJ+1,0:POKEJ+2,VAL("&H"+L$):POKEJ+3,42:GOTO2220
2210 POKEJ+1,VAL("&H"+LEFT$(L$,LEN(L$)-2)):POKEJ+2,VAL("&H"+RIGHT$(L$,2)):POKEJ+3,42
2220 J=J+4:FORL=0TO10:POKEJ,CA(L,0):POKEJ+1,CA(L,1):POKEJ+2,CA(L,2):J=J+3:NEXT:L=1
2230 IFPO$(L)=""THEN2240ELSEK=LEN(PO$(L)):FORLL=1TO K:POKEJ,ASC(MID$(PO$(L),LL,1)):J=J+1:NEXT:POKEJ,42:J=J+1:L=L+1:GOTO2230
2240 POKEJ,255:POKE31962,XP:POKE31963,YP:PRINT"PLACE TAPE OF ADVENTURES        IN RECORDER PRESS PLAY & RECORD":INPUT"NAME OF CURRENT ADVENTURE";L$:IFLEN(L$)>8THENL$=LEFT$(L$,8)
2250 POKE 65494,0:SAVEML$,31000,32767,0:END
2260 CLS:PRINT@100,"THERE ARE NO LOANS MADE         IN THIS GAME BUDDY,             YOU'RE GOING TO JAIL.":PRINT@400,"GAME OVER":FORL=1TO1000:NEXT:END
2270 FORL=1TO10:SCREEN1,1:SCREEN1,0:NEXT:PLAY"V31T2L2O2BP64BP64L8BP64L2BO3P64DP64L8D-P64L4D-P64L4O2BP64BP64B-P64L1B":END
2280 FORL=1TO1000:NEXT:POKE65495,0:CLS0:L$=CHR$(128):CO$=L$+"you"+L$+"have"+L$+"attained"+L$+"jedi"+L$+"knight"+L$:FORL=0TO224:PRINT@L,CO$;:NEXT:POKE65494,0:PLAY"T30L4V30":FORL=1TO30:PLAY"O3GO4CL3EL4CEV-":NEXT
2290 FORK=1TO100:L=RND(15)-1:IFL=7THENNEXTELSEC=143+16*(RND(8)-1):PRINT@L*32,STRING$(32,C);:NEXT
2300 FORL=0TO14:IFL=7THENNEXTELSEPRINT@L*32,STRING$(32,128);:NEXT:FORL=224TO0STEP-1:PRINT@L,CO$;:NEXT:FORL=1TO15:PRINT@L*32,CO$;:NEXT:FORL=0TO14:PRINT@L*32,STRING$(32,128);:NEXT
2310 FORL=1TO30:PRINT@L-1,MID$(CO$,L,1);:PRINT@L+479,CHR$(128);:NEXT:CN$="congratulations"+L$+"champ":K=0
2320 L=RND(21):IFMID$(CN$,L,1)="X"THEN2320ELSEPRINT@228+L,MID$(CN$,L,1);:MID$(CN$,L,1)="X":K=K+1:IFK<>21THEN2320
2330 PRINT@350,"";:END
