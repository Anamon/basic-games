
10 '                          '
20 '       HANGMAN            '
30 '                          '
40 ' DOUG L'HOMMEDIEU    5/82 '
50 '                          '
60 DIM WRD$(10),GWRD$(10),BAD$(9)
70 WRITE=0
80 CLS
90 PRINT @ 11,"HANGMAN": PRINT
100 PRINT "1 - ONE PLAYER(COMPUTER PICKS       THE WORDS)"
110 PRINT "2 - TWO PLAYERS(TAKE TURNS)"
120 PRINT @ 160, "TYPE 1 OR 2"
130 P$=INKEY$: CPICK=RND(122): IF P$="" GOTO 130
140 PLRS=VAL(P$)
150 IF PLRS <>1 AND PLRS <> 2 GOTO 120
160 IF PLRS = 2 GOTO 230
170 PRINT "COMPUTER WILL RANDOMLY PICK A"
180 PRINT "WORD HAVING FROM 2 TO 10 LTRS."
190 PRINT "YOU MUST GUESS THE WORD."
200 PRINT "YOU CAN HAVE NINE WRONG GUESSES"
210 PRINT "BEFORE YOU ARE HUNG!!!"
220 GOTO 320
230 CLS: PRINT @11,"HANGMAN":PRINT
240 PRINT "PLAYER #1 INPUTS A WORD - A"
250 PRINT "LETTER AT A TIME(2 TO 10 LTRS)"
260 PRINT "AFTER THE LAST LETTER SIMPLY"
270 PRINT "PRESS <ENTER>.":PRINT
280 PRINT"PLAYER #2 TAKES OVER AT THAT"
290 PRINT"TIME TO GUESS THE WORD. PLAYER"
300 PRINT"#2 CAN HAVE NINE WRONG GUESSES"
310 PRINT"BEFORE HE IS HUNG !!!"
320 PRINT"-------------------------------"
330 PRINT "1 - EASIER(DUP. LETTERS APPEAR)"
340 PRINT "2 - HARDER(MUST GUESS EVERY LTR)"
350 PRINT @ 448, "TYPE 1 OR 2"
360 E$=INKEY$: IF E$="" GOTO360
370 EASY=VAL(E$)
380 IF EASY <>1 AND EASY <> 2 THEN 350
390 PRINT @ 352, "*******************************"
400 PRINT @ 384, "1 - HELP REMEMBERING WRONG LTRS"
410 PRINT @ 416, "2 - NO HELP REMEMBERING"
420 PRINT @ 448, "TYPE 1 OR 2"
430 H$=INKEY$: IF H$="" GOTO430
440 HELP=VAL(H$)
450 IF HELP <> 1 AND HELP <> 2 GOTO 420
460 CLS
470 FOR I=1 TO 10: WRD$(I)=" ": GWRD$(I)=CHR$(128): NEXT I
480 IF PLRS=2 GOTO 640
490 REM ***COMPUTER PICKS WORD***
500 PRINT @ 172, "COMPUTER"
510 PRINT @ 203, "IS PICKING"
520 PRINT @ 236, "THE WORD"
530 CPICK = RND(122)
540 FOR I=1 TO CPICK
550 READ CWORD$
560 NEXT I
570 RESTORE
580 KNT=LEN(CWORD$)
590 FOR I = 1 TO KNT
600 WRD$(I) = MID$(CWORD$,I,1)
610 NEXT I
620 GOTO 850
630 REM ***PLAYER #1 PICKS WORD***
640 PRINT @ 6, "INPUT THE WORD, ONE              LETTER AT A TIME"
650 PRINT "    TYPE <ENTER> WHEN DONE": PRINT
660 FOR I=1TO 10
670 PRINT "LETTER #";I;"- ";
680 WRD$(I)=INKEY$:IF WRD$(I)="" THEN GOTO 680
690 IF I=1 AND WRD$(I)=CHR$(13) GOTO740
700 IF I=2 AND WRD$(I)=CHR$(13) GOTO740
710 FOR K=65 TO 90
720 IF WRD$(I)=CHR$(K) OR WRD$(I)=CHR$(13) GOTO 750
730 NEXT K
740 PRINT "LETTERS ONLY": GOTO 670
750 PRINT WRD$(I)
760 IF WRD$(I)=CHR$(13) GOTO 780
770 NEXT I
780 KNT=I-1
790 PRINT:PRINT"THE WORD IS  - ";
800 FOR I=1 TO KNT:PRINT WRD$(I);:NEXT I
810 PRINT: PRINT"             - RIGHT ? (Y/N)"
820 YN$=INKEY$: IF YN$="" GOTO820
830 IF YN$="N" GOTO 460
840 IF YN$<>"Y" GOTO 800
850 CLS
860 FOR I=1 TO 9: BAD$(I)=CHR$(128): NEXT I
870 PRINT @ 7,"HANGMAN"
880 FOR I=35 TO 387 STEP 32
890 PRINT @ I, STRING$(16,CHR$(128))
900 NEXT I
910 PRINT@ 449,CHR$(128);"  ";
920 FOR I=2 TO KNT:PRINT CHR$(128);"  ";:NEXT I
930 PRINT:MISS=0
940 REM ***MAIN LOOP***
950 IF HELP=2 GOTO1040   'NO HELP REMEMBERING'
960 INDX=0
970 BAD$(MISS)=GS$
980 PRINT @ 214, "-WRONG-"
990 FOR I=0TO128 STEP 64
1000 FOR K=278TO284 STEP 3
1010 INDX=INDX+1
1020 PRINT @ (K+I), BAD$(INDX)
1030 NEXT K: NEXT I
1040 PRINT @ 480,"GUESS LETTER-";
1050 GS$=INKEY$:IF GS$="" GOTO 1050
1060 FOR N=1 TO KNT
1070 IF WRD$(N)<>GS$ GOTO 1180
1080 FOR IS=1TO3
1090 FOR IK=227TO245 STEP 9: SOUND IK,1: NEXT IK
1100 NEXT IS
1110 IF GWRD$(N)<>CHR$(128) GOTO 1180
1120 GWRD$(N)=WRD$(N)
1130 FOR K=1 TO KNT
1140 PRINT @(449+((K-1)*3)),USING"%%";GWRD$(K)
1150 NEXT K
1160 WRITE=1
1170 IF EASY=2 GOTO 1200  'NOT THE EASY WAY'
1180 NEXT N
1190 REM ***END MAIN LOOP ***
1200 IF WRITE=0 THEN GOTO1320
1210 WRITE=0
1220 FOR I=1 TO KNT
1230 IF GWRD$(I)=CHR$(128) GOTO 1040
1240 NEXT I
1250 PLAY"O4L8CEGL4O5CO4L8GL4O5C"
1260 PRINT @416,"YOU WIN. PLAY AGAIN? (Y/N/C)"
1270 YN$=INKEY$: IF YN$="" GOTO1270
1280 IF YN$="Y" GOTO 460
1290 IF YN$="C" GOTO80
1300 IF YN$<>"N" GOTO 1270
1310 END
1320 MISS=MISS+1
1330 FOR I=50TO2 STEP -8: SOUND I,1:NEXT I
1340 ON MISS GOTO 1360,1390,1520,1570,1610,1670,1710,1750,1810
1350 PRINT "ERROR":END
1360 REM **BASE**
1370 PRINT @ 388,CHR$(223);STRING$(5,CHR$(220));CHR$(223);STRING$(8,CHR$(128))
1380 GOTO 940
1390 REM **UPRIGHT**
1400 PRINT @ 359,CHR$(175); STRING$(11,CHR$(128))
1410 PRINT @ 327,CHR$(175);STRING$(11,CHR$(128))
1420 PRINT @ 295,CHR$(175); STRING$(11,CHR$(128))
1430 PRINT @ 263,CHR$(175); STRING$(11,CHR$(128))
1440 PRINT @ 231,CHR$(175); STRING$(11,CHR$(128))
1450 PRINT @ 199,CHR$(175); STRING$(11,CHR$(128))
1460 PRINT @ 167,CHR$(175); STRING$(11,CHR$(128))
1470 PRINT @ 135,CHR$(175);STRING$(11,CHR$(128))
1480 PRINT @ 103,CHR$(175); STRING$(11,CHR$(128))
1490 PRINT @ 71,CHR$(175);STRING$(11,CHR$(128))
1500 PRINT @ 39,CHR$(175);STRING$(11,CHR$(128))
1510 GOTO 940
1520 REM **TOP/NOOSE**
1530 PRINT @ 40,STRING$(6,CHR$(172))
1540 PRINT @ 46,CHR$(175);STRING$(4,CHR$(128))
1550 PRINT @ 78,CHR$(175);STRING$(4,CHR$(128))
1560 GOTO 940
1570 REM ** HEAD **
1580 PRINT@ 109,CHR$(150);CHR$(156);CHR$(153);STRING$(3,CHR$(128))
1590 PRINT @ 141,CHR$(153);CHR$(147);CHR$(150);STRING$(3,CHR$(128))
1600 GOTO 940
1610 REM **BODY**
1620 PRINT @ 174,CHR$(191);STRING$(4,CHR$(128))
1630 PRINT @ 206,CHR$(191);STRING$(4,CHR$(128))
1640 PRINT @ 238,CHR$(191);STRING$(4,CHR$(128))
1650 PRINT @ 270,CHR$(175);STRING$(4,CHR$(128))
1660 GOTO 940
1670 REM **LEFT LEG**
1680 PRINT @ 301,CHR$(166);STRING$(5,CHR$(128))
1690 PRINT @ 332,CHR$(150);STRING$(6,CHR$(128))
1700 GOTO 940
1710 REM **RIGHT LEG**
1720 PRINT @ 303,CHR$(169);STRING$(3,CHR$(128))
1730 PRINT @ 336,CHR$(153);STRING$(2,CHR$(128))
1740 GOTO 940
1750 REM **LEFT ARM**
1760 PRINT @ 204,CHR$(150)
1770 PRINT @ 205,CHR$(188)
1780 PRINT @ 206,CHR$(191)
1790 PRINT @ 207,STRING$(4,CHR$(128))
1800 GOTO 940
1810 REM **RIGHT ARM - HUNG!**
1820 PRINT @ 207,CHR$(188);STRING$(3,CHR$(128))
1830 PRINT @ 208,CHR$(153);STRING$(2,CHR$(128))
1840 IF HELP=2 GOTO 1870
1850 PRINT @ 212, "-ALL WRONG-"
1860 PRINT @ 412, GS$
1870 PRINT @ 416,"YOU'RE HUNG!";
1880 FOR K=1 TO KNT
1890 PRINT @ (449+((K-1)*3)), USING "%%"; WRD$(K)
1900 NEXT K
1910 PLAY"O1L2GFEDC"
1920 PRINT @ 428, " PLAY AGAIN? (Y/N/C)"
1930 YN$=INKEY$: IF YN$="" GOTO 1930
1940 IF YN$="Y" GOTO 460
1950 IF YN$ = "C" GOTO 80
1960 IF YN$ <> "N" GOTO 1930
1970 END
1980 REM *** 100 WORD LIST ***
1990 DATA NOTEBOOK,NEWSPAPER,HORSE,POOL,SWIMMING,COMPUTER,CRAFTSMAN,BOOK,FLOWER,PIANO,SONG
2000 DATA PICTURE,GLASS,STOVE,MICROWAVE,OVEN,TYPEWRITER,CASSETTE,ELEPHANT,POTS,KITCHEN,VASE
2010 DATA CHURCH,SUIT,TENNIS,FOOTBALL,BASEBALL,TELEVISION,TELEPHONE,BIBLE,CHRISTIAN,TENT,GIRAFFE,TOWEL,PAINT,WATCH,SOAP,REINDEER
2020 DATA ELECTRICITY,CHECK,HOLIDAY,SHUTTLE,CHAIR,COOKIE,PANCAKE,WAFFLE,SODA,LIGHT,LION,DOLLAR,GAME,PILLOW,NUMBER,NAME,SCHOOL
2030 DATA CANDLE,TABLE,SHAMPOO,DOG,CAT,COW,FARMER,COOK,BARN,DOUGHNUT,BICYCLE,UNIVERSITY,JUNIOR,DOCTOR,DENTIST,SPACE,RAIN
2040 DATA SUNSHINE,DANCE,RAINBOW,FLOUR,DRESSES,CLASSROOM,LOCKER,ROCKER,CLOCK,CAMERA,ZEBRA,ZOO,BEAR,TIGER,MOUSE,FISH,AQUARIUM,MAN
2050 DATA FISHERMAN,BEACH,PARK,SHELL,LOCK,ROCK,OVERHEAD,HEAD,MACHINE,BUSH,RESTAURANT,MONKEY,TRUCK,STREET,CLOUD,STORE,SHIP
2060 DATA SAILBOAT,COUCH,BUILDING,COACH,TERMINAL,BOY,GIRL,FRIEND,ENEMY,HAND,FOOT,LEG,ARM,FINGER,SHOE
