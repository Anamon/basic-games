
10 'POKE 65495,1
20 CLS 0:PCLEAR 4:PMODE 1,1:PCLS 3
30 PRINT @64,"WHAT IS YOUR SKILL LEVEL (1-10)";:INPUT SL:CLS 0
40 PRINT @70,"YOU HAVE 5 OUTPOSTS";
50 PRINT @135,"DESTROY INVADERS";
60 PRINT @164,"TO SAVE THE FEDERATION";
70 TR=15-SL
80 A=5
90 DIM S(20,10)
100 SP=5:SX=127:IX=5:IY=5:IZ=1:SZ=5
110 GOSUB 650
120 SCREEN 1,0
130 PCOPY 3 TO 1:PCOPY 4 TO 2
140 GOSUB 560
150 X=JOYSTK(0)*3+33
160 Y=JOYSTK(1)*2+33
170 GOSUB 250:H=0:REM NOT HIT
180 IF INKEY$<>""AND SB>0  THEN S=1:GOSUB 860:SB=SB-1
190 F=PEEK(65280)
200 IF F/2=INT(F/2) THEN GOSUB 310
210 IF H=1 THEN SOUND 100,1:X=SX:Y=SY:GOSUB 840:SY=10:SX=RND(255)+1:IX=(RND(20)+5):PCOPY 3 TO 1:PCOPY 4 TO 2:FORX=1TO900:NEXTX:HS=HS+1:GOSUB 1260:FOR X=1TO2000:NEXTX:SCREEN 1,0
220 RN=RND(2):IF RN=2 THEN IX=IX*-1
230 PCOPY 3 TO 1:PCOPY 4 TO 2
240 GOTO 140
250 REM DRAW CROSS HAIRS
260 C$="C4"
270 R$=C$+"BM"+STR$(X)+","+STR$(Y)
280 DRAW R$
290 DRAW "N;U9NR9ND9NL9"
300 RETURN
310 REM FIRE ROUTINE
320 LC=4
330 COLOR LC,3
340 LINE(120,95)-(X,Y),PSET
350 LINE(134,95)-(X,Y),PSET
360 C$="C1":GOSUB 270
370 LC=1:COLOR LC,3
380 PLAY"V31;T255O5CO4BAG"
390 GOSUB 520:REM CHECK HIT
400 RETURN
410 REM DRAW A SAUCER
420 SZ=SZ+IZ:IF SZ>20 OR SZ<5 THEN IZ=IZ*-1
430 CIRCLE (SX,SY),SZ,4,.3
440 PAINT (SX,SY),4,4
450 RETURN
460 REM CHECK FOR SMARTBOMB HIT
470 FOR J=SX-9 TO SX+9
480 IF PPOINT(J,SY)=LC THEN H=1:RETURN
490 NEXT J
500 H=0
510 RETURN
520 REM CHECK FOR HIT
530 H=0
540 IF ABS(X-SX)<TR AND ABS(Y-SY)<TR THEN H=1
550 RETURN
560 REM MOVE ARRAY
570 IF ABS(SX-127)<20 AND ABS(SY-95)<20 THEN GOSUB 970:SY=30:SX=RND(205)+25
580 SX=SX+IX:SY=SY+IY
590 IF SY<30 THEN IY=RND(15)+5 ELSE IF SY>180 THEN IY=(RND(15)+5)*-1
600 IF SX<25 THEN IX=RND(20)+5:SX=25
610 IF SX>240 THEN IX=(RND(20)+5)*-1:SX=240
620 IF SY<25 THEN SY=25
630 GOSUB 410
640 RETURN
650 REM STARS
660 PMODE 1,3
670 PCLS 3
680 SB=1:REM ONE SMARTBOMB PER OUTPOST
690 FOR X=1 TO 255 STEP 3
700 PSET(RND(255)+1,RND(191)+1,2)
710 NEXT X
720 CIRCLE (127,95),9,2
730 PAINT (127,95),1,2
740 CIRCLE (127,192),100,2,.5
750 PAINT (127,162),2,2
760 FOR X=1 TO 5
770 J=RND(255):K=RND(50):Y=RND(20)
780 CIRCLE(J,K),Y,2
790 PAINT (J,K),2,2
800 NEXT X
810 PMODE 1,1
820 SCREEN 1,0
830 RETURN
840 REM FIRE2 ROUTINE
850 S=2
860 SCREEN 1,1
870 SOUND 1,5:SCREEN 1,0
880 C=1
890 FOR E=1 TO S
900 FOR R=1 TO 100 STEP 7
910 CIRCLE(X,Y),R,C
920 NEXT R
930 C=3
940 NEXT E
950 GOSUB 460:REM CHECK HIT
960 RETURN
970 REM STATION BLOWS UP
980 A=A-1
990 PLAY"T255;O4CCCCCCCCCCCCCCCCCCCCC"
1000 CLS 0
1010 C=4
1020 X=127:Y=95
1030 FOR K=1 TO 2
1040 ST=1.5
1050 J=1
1060 J=J+ST:ST=ST+.05
1070 CIRCLE (X,Y),J,C
1080 IF J<53 THEN 1060
1090 C=3
1100 NEXT K
1110 FOR K=1 TO 500:NEXT K
1120 SCREEN 0,1:CLS 4:GOSUB 1150:GOSUB 1280:GOSUB 650
1130 SCREEN 1,0
1140 RETURN
1150 RM=RND(5)
1160 ON RM GOTO 1170,1190,1210,1230,1240
1170 PRINT @ 229,"TOO BAD SPACE CADET!";
1180 RETURN
1190 PRINT @ 236,"GOTCHA!";
1200 RETURN
1210 PRINT @ 233,"BITE THE DUST!";
1220 RETURN
1230 PRINT @ 235,"YOU BLEW IT!";
1240 PRINT @ 229,"GONE BUT NOT FORGOTTEN!";
1250 RETURN
1260 REM PRINT SCORE
1270 CLS 3
1280 PRINT @ 360,HS;"ALIENS DESTROYED";
1290 PRINT @ 391,A;" OUPOSTS REMAINING";
1300 PRINT @420,SB;"SMART BOMBS IN ARSENAL";
1310 K$=INKEY$:K$=""
1320 IF A=0 THEN PRINT @453,"ALIENS HAVE DESTROYED YOU";:GOSUB 1350
1330 IF HS=15 THEN PRINT @449,"YOU HAVE SAVED THE FEDERATION";: GOSUB 1350
1340 RETURN
1350 SCREEN 0,1:FOR X=1 TO 1000:NEXT X
1360 SCREEN 0,0:FOR X=1 TO 2000:NEXT X
1370 CLS 3
1380 PRINT @ 230,"ANOTHER MISSION (Y/N) ";:INPUT C$
1390 IF LEFT$(C$,1)<>"Y" THEN POKE 65494,0:CLS:END
1400 HS=0:A=5:SB=1
1410 PRINT @64,"CHANGE SKILL LEVEL (1-10)";:INPUT SL:TR=15-SL
1420 RETURN
