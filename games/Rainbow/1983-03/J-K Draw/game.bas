
10 CLS:PRINT"    JOYSTICK/KEYBOARD DRAW":PRINT:INPUT"  DO YOU NEED INSTRUCTIONS";DY$
20 IF DY$="Y"THEN560
30 X=31:Y=15:C=6
40 POKE 65495,0
50 J=1
60 CLS0
70 IFPEEK(341)=247THENY=Y-1
80 IFPEEK(342)=247THENY=Y+1
90 IFPEEK(343)=247THENX=X-1
100 IFPEEK(344)=247THENX=X+1
110 IFINKEY$="S"THEN 470
120 IFX<0THENX=0
130 IFX>63THENX=63
140 IFY<2THENY=2
150 IFY>31THENY=31
160 IFPEEK(339)=239THENC=1
170 IFPEEK(340)=239THENC=2
180 IFPEEK(341)=239THENC=3
190 IFPEEK(342)=239THENC=4
200 IFPEEK(343)=239THENC=5
210 IFPEEK(344)=239THENC=6
220 IFPEEK(345)=239THENC=7
230 IFPEEK(338)=223THENC=8
240 IFPEEK(338)=239THENC=0
250 IFPEEK(340)=253THENJK=1
260 IFINKEY$="L"THEN520
270 IFPEEK(339)=191ORPEEK(341)=254THENCLS0
280 IFC=0THENRESET(A1,S1)
290 SET(X,Y,C)
300 A1=X:S1=Y
310 PRINT@0,"HORIZONTAL:";X," VERTICAL :";Y
320 IFPEEK(340)=251THEN420
330 IFJK=1THEN350
340 GOTO70
350 H=JOYSTK(0):V=JOYSTK(1)
360 IFH<20THENX=X-1
370 IFH>40THENX=X+1
380 IFV<20THENY=Y-1
390 IFV>40THENY=Y+1
400 IFPEEK(341)=253THEN JK=9
410 GOTO110
420 PRINT@0,"                               ";
430 PRINT@0,"RELOCATION OF DOT";
440 RESET(X,Y)
450 INPUT X,Y
460 GOTO70
470 POKE65494,0
480 'MOTORON
490 FORDL=33TO255:PRINT@0,CHR$(DL);:FORD2=0TO5:NEXTD2:NEXTDL
500 SAVEM"DRAWINGS",1056,1535,40999
510 SOUND1,1:GOTO70
520 POKE65494,0
530 PRINT@0,"loading";
540 LOADM"DRAWINGS"
550 SOUND5,1:GOTO70
560 CLS
570 PRINT"    JOYSTICK/KEYBOARD DRAW"
580 PRINT"       BY AARON FRANSEN"
590 PRINT:PRINT" THIS IS A DRAWING PROGRAM"
600 PRINT" WHICH UTILIZES THE KEYBOARD"
610 PRINT" OR THE JOYSTICK TO MOVE A"
620 PRINT" COLORED DOT AROUND THE "
630 PRINT" SCREEN."
640 PRINT:PRINT" \\PRESS ANY KEY TO CONTINUE//"
650 IF INKEY$=""THEN650
660 CLS
670 PRINT" HERE IS A LIST"
680 PRINT" OF THE COMMANDS :"
690 PRINT:PRINT" ARROW KEYS -- MOVE DOT IN                     DIRECTION OF                    ARROW."
700 PRINT" 'S' KEY    -- SAVES DRAWING                   TO TAPE."
710 PRINT" 'L' KEY    -- LOADS DRAWING                   FROM TAPE."
720 PRINT" NO.0 TO 8  -- CHANGES COLOUR                  OF DOT.'0'IS                    'BLACKOUT'DOT."
730 PRINT:PRINT" \\PRESS ANY KEY TO CONTINUE//"
740 IFINKEY$=""THEN740
750 CLS
760 PRINT:PRINT" 'J' KEY    -- CHANGES CONTROL                 OF THE DOT TO                   THE JOYSTICK."
770 PRINT" 'K' KEY    -- CHANGES CONTROL                 OF THE DOT TO                   THE ARROW KEYS."
780 PRINT" clear KEY  -- CLEARS SCREEN."
790 PRINT" FIRE BUTTON-- CLEARS SCREEN."
800 PRINT:PRINT" THE HORIZONTAL AND VERTICAL     LOCATIONS ARE PRINTED AT THE    TOP OF THE SCREEN."
810 PRINT:PRINT" \\PRESS ANY KEY TO CONTINUE//"
820 IFINKEY$=""THEN820
830 CLS
840 PRINT
850 PRINT" YOU WILL REQUIRE A BLANK TAPE   TO SAVE THE DRAWN PICTURE TO    TAPE. WHEN YOU PRESS THE 'S'    KEY, HOLD ON IT UNTIL YOU SEE   CHARACTERS BEING GENERATED      AT THE TOP LEFT OF THE SCREEN."
860 PRINT:PRINT" THE PROGRAM USES 64 BY 32       LOW RESOLUTION GRAPHICS,        ALTHOUGH THE COLOURS CAN        BE QUITE DAZZLING AND REAL-     ISTIC."
870 PRINT" 'R' KEY -- RELOCATES DOT."
880 PRINT"   ++PRESS ANY KEY TO BEGIN++"
890 IFINKEY$=""THEN890
900 GOTO30
