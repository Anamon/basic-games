
1 'MAGIC PUMPKIN
2 'BY GEORGE & ELLEN AFTAMONOW
10 CLEAR1500:DIMH(1,12),P(1,12),W(1,13)
20 C$="L2HU3ER2BD5BR4 HU3ER2FD3GNL2BR4 U4ERFND3ERFD4BR4 U5R2FDGNL2BF2BR3 HU4BR4D4GNL2BR5 U5NL2R2BD5BR4 U3NR2U2R3BD5NL3BR4 U5R2FDGNL2FDBR12 HU4BR3BDD3GNLBR2NHRENU4BDBR3 NU5BR3 U5F2DF2NU5BR4 NHREUHGHUERF"
30 Y$="U3NH2E2BD5BR4 HU3ER2FD3GNL2BR5 HU4BR4D4GNL2BR16 HU4BR3BDD3GNLBR2NHRENU4BDBR4 NU5BR4 U5F2DF2NU5"
40 T$="U5NL2R2BR3 D5BR3 U3NR2U2R3BD5NL3"
50 W1$="L255T255BGACCBO4CGACCBO5C":W2$="T50L50O5G"
60 P1$="O1L10T10CDCF"
70 G1$="O5L15T15ADGF"
80 PMODE3,1:PCLS6:SCREEN1,1
90 DRAW"BM80,170;S16C7U4LE2R3ULER2E2U3LE3 F3LBLLD2RU2BRD3R2F3BL2LNU BL4LURNDBR4RDBR2U4LE2 F3LBLLDRUBRD6 BL2D2LU2BL8D2LU2BL5D2LU2RBR5RBR8RBR2 F2NL2D2L2NU2L8NL2U3L2D3L10":PAINT(92,152),7,7
100 DRAW"BM12,30;S4EUNG4UNG5UNG6UNG6 BUG7BUNE6BLNE6BUNE5BLNE5BUE4R5 E3NE4L4H2NG3HUNG3UEUEUBGNG5 BEE3R2UR2ER HULHL2HLHLHR9FRNG5C8DNGNDC7RDC8NLNG2RFDHLNG2DGLC7 D3GC8F2R3C7E9DG7NG2C8DGLC7BLNG3C8L3H3C7G4F2R5BRNE3C8R5NG8DNL6G8C7 NHGND2U2NLC8E6NEL4C7NE2G6
110 PAINT(16,18),7,7:GET(1,0)-(33,32),W,G:FORW=0TO220STEP2:PUT(W,0)-(W+32,32),W,PSET:NEXTW:FORZ=1TO4:PLAYW1$:NEXTZ:PLAYW2$
120 DRAW"BM14,60;S4C8L4H6U9E5R4DFRF EBD6R3D2RDL6URU2RBL3NUL4UE2F2BR8DR4UH2G2BH5 U4H2LHR3F3D4FE REUR4F5D9G6L12BU4 H2U2FRFR6ERED2G2LGL4HL" 
130 PAINT(20,58),8,8
140 GET(2,34)-(34,64),P,G:FORP=2TO84STEP2:GOSUB320:NEXTP
150 DRAW"BM66,60;S8C5U6E2F2ND2E2F2D6BR6":FORP=86TO112STEP2:GOSUB320:NEXTP:DRAW"BRU4NR6U2E2R2F2D6BR10":FORP=114TO144STEP2:GOSUB320:NEXTP:DRAW"NR3H2U4E2R2FBD3NL2ND4RD3GBR8"
160 FORP=146TO160STEP2:GOSUB320:NEXTP:DRAW"NU8BR8":FORP=162TO182STEP2:GOSUB320:NEXTP:DRAW"NR2H2U4E2R2FBD6G":FORP=184TO220STEP2:GOSUB320:NEXTP:PLAY P1$
170 DRAW"BM4,88;S8C5E9R3FD2NDL2D2RNEBL2U2L2D2R2BR2G6UG2U3G3U3"
180 PAINT(14,80),5,5
190 GET(2,62)-(32,94),H,G:FORG=2TO78STEP2:GOSUB310:NEXTG
200 DRAW"BM60,96;C5U8R6D4L4D4NL2BR8":FORG=80TO96STEP2:GOSUB310:NEXTG:DRAW"NR6U8BR6D8BR4":FORG=98TO116STEP2:GOSUB310:NEXTG
210 DRAW"U8F3ND2E3D8BR5":FORG=118TO136STEP2:GOSUB310:NEXTG:DRAW"U8R6D4L4D4NL2BR8":FORG=138TO156STEP 2:GOSUB310:NEXTG
220 DRAW"U8BR5G4NLF4BR5":FORG=158TO176STEP2:GOSUB310:NEXTG:DRAW"NU8BR4":FORG=178TO196STEP2:GOSUB310:NEXTG
230 DRAW"U8F3D2F3U8":FORG=198TO220STEP2:GOSUB310:NEXTG:PLAY G1$
240 PCLS:PMODE4,1:SCREEN 1,1
250 DRAW"BM70,70;C5S8E2NE2D2R2 E2U2R2D4R2 E2U2L2D2F2R2 U2E2R2D2G2NL2BR4NH2 E2U4BG2R4BG2F2R2 E2U2L2D2F2R2 U2E2NU2D4NL2RBR8 E4U2L2D4NR2D2R2NU2R3NU2R2NU2D4L"
260 DRAW"BM40,100;S16E2U2LD5RNU2BG2FEU2REULDFR2 HEFGREURD2R UNFEFDNLBG2FEU2REULDFBR5"
270 DRAW"NR2UNRURNUNRBD2NDRBR6 NR3HURNRHUEFREBD5 E3U2LD4FE3U2LD4FR EULDFEURND2RD2R"
280 DRAW"BM74,150;S16EUR2HLNDU2EFD5R E2U3LD5ND3R E2BHRNRNU2D3R NRUERDNGFR U2RND2RND2RD2R ENDURNRDBGR2UNLNUR URND2RD2R NR2U2RNRDRNDNUE D2RNURU2R"
290 PLAY"T3O2L8.CL16DL8.CL16DL8.EL16GL8.AL16GL8.AL16BO3L8.DL16CO2L8.BO3L16DL8.CO2L16AL8.GL16GO3L8.CO2L16CL4DGL1C"
300 GOTO330
310 PUT(G,62)-(G+30,94),H,PSET:RETURN
320 PUT(P,34)-(P+32,64),P,PSET:RETURN
330 CLS:INPUT"DO YOU NEED INSTRUCTIONS (Y/N)";I$
340 IFI$="N"THEN380ELSEIFI$="Y" THEN350ELSE330 
350 CLS:PRINT"THE COMPUTER WILL RANDOMLY PICK A GHOST, PUMPKIN OR WITCH.":PRINT:PRINT"THE OBJECT IS FOR YOU TO PICK   ONE OF THE THREE THAT WILL BEAT THE COMPUTER'S CHOICE.":PRINT:PRINT"THE WITCH EATS THE PUMPKIN FOR AWIN."
360 PRINT"THE PUMPKIN WILL EXORCISE THE   GHOST FOR A WIN.":PRINT"THE GHOST WILL SCARE THE WITCH  FOR A WIN.": PRINT
370 INPUT"PRESS 'ENTER' TO START";I$
380 CLSRND(8):INPUT"HOW MANY ROUNDS";N:GOTO410
390 CLSRND(8):NU=NU+1:IFNU=N THENPRINT@44,"FINAL";
400 PRINT@76,"SCORE";:PRINT@160,"YOU=";Y;:PRINT@256,"COMPUTER=";C;:PRINT@352,"TIE=";T;:FORR=1TO1500:NEXTR:IFNU=N THEN660
410 PMODE3,1:PCLS6:SCREEN1,1
420 DRAW"BM40,20S8L2HU3ER2BD5BR3 U5BR4D3NL3D2BR3 HU3ER2FD3GNL2BR4 HU3ER2FD3GNL2BR5 NHREUHGHUERFBD4BR3 U3NR2U2R3BD5NL3BR8 U4ER2FD2NL4D2BR9"
430 DRAW"U5F2DF2NU5BR3 HU4BR4D4GNL2BR4 U4EFND3EFD4BR3 U5R2FDLNL2FD2NL3BR3 U3NR2U2R3BD5NL3BR3 U5R2FDGNL2FD"
440 DRAW"BM12,40;S8U5":PUT(24,36)-(56,66),P,PSET
450 DRAW"BM100,40;S8NR3U2R3U2L3":PUT(112,36)-(142,68),H,PSET
460 DRAW"BM184,40;S8R3UHNLRUHL2":PUT(196,36)-(228,66),W,PSET
470 X=RND(3):A=48:B=100
480 K$=INKEY$:IFK$="1" THEN490ELSE IFK$="2" THEN530ELSE IFK$="3"THEN570ELSE480
490 GOSUB620:GOSUB650:A=176
500 IFX=2 THENGOSUB630:DRAW"BM60,168;"+Y$:Y=Y+1:GOSUB610:GOTO390
510 IFX=3 THENGOSUB640:DRAW"BM20,168;"+C$:C=C+1:GOSUB610:GOTO390
520 IFX=1 THENGOSUB650:DRAW"BM80,168;"+T$:T=T+1:GOSUB610:GOTO390
530 GOSUB620:GOSUB630:A=176
540 IFX=1 THENGOSUB650:DRAW"BM20,168;"+C$:C=C+1:GOSUB610:GOTO390 
550 IFX=2 THENGOSUB630:DRAW"BM80,168;"+T$:T=T+1:GOSUB610:GOTO390
560 IFX=3 THENGOSUB640:DRAW"BM60,168;"+Y$:Y=Y+1:GOSUB610:GOTO390
570 GOSUB620:GOSUB640:A=176
580 IFX=1 THENGOSUB650:DRAW"BM60,168;"+Y$:Y=Y+1:GOSUB610:GOTO390
590 IFX=2 THENGOSUB630:DRAW"BM20,168;"+C$:C=C+1:GOSUB610:GOTO390
600 IFX=3 THENGOSUB640:DRAW"BM80,168;"+T$:T=T+1:GOSUB610:GOTO390
610 FORR=1TO1500:NEXTR:RETURN
620 DRAW"BM52,92;S8U2NH2E2BD4BR4 HU2ER2FD2GNL2BR4 HU3BR4D3GNL2BR":DRAW"BM140,92;S8L2HU2ER2BD4BR4 HU2ER2FD2GNL2BR4 U3ERFND2ERFD3BR3 U4R2FDNL3BD2BR4 HU3BR4D3GBR2BR5 U4NL2R2BD4BR3 NR3U2NR2U2R3BR3 ND4R2FDL2NF2L":RETURN
630 PUT(A,B)-(A+30,B+32),H,PSET:PLAYG1$:RETURN
640 PUT(A,B)-(A+32,B+30),W,PSET:FORZ=1TO4:PLAYW1$:NEXTZ:PLAYW2$:RETURN 
650 PUT(A,B)-(A+32,B+30),P,PSET:PLAYP1$:RETURN
660 CLS0:PRINT@192," DO YOU WANT TO PLAY SOME MORE   (Y/N) ";:INPUTI$
670 IFI$="Y" THEN NU=0:T=0:Y=0:C=0:GOTO380
680 CLS0:PRINT@192," AW C'MON, JUST ONE MORE GAME?    (Y/N)";:INPUTI$:IFI$="Y" THEN NU=0:T=0:Y=0:C=0:GOTO 380
690 IFI$="N" THEN700ELSE680
700 CLS8:PRINT@235,"THE";+CHR$(255);+"DEN";:FORX=1TO700:NEXTX
710 FORX=207TO143STEP-32:PRINT@X,"D";:PRINT@X+32,CHR$(255);:SOUNDX,3:NEXTX
720 FORX=144TO146:PRINT@X,"D";:PRINT@X-1,CHR$(255);:SOUNDX,3:NEXTX
730 FORX=178TO245STEP32:PRINT@X,"D";:PRINT@X-32,CHR$(255);:SOUNDX,3:NEXTX
740 GOTO740
