
1 REM 4K SUB DESTROY BY ROBERT TOSCANI, 4744 WHITAKER AVE. PHILA. PA. 19120
2 CLS:INPUT"WHAT LEVEL 1(EASY)-10(HARD)";U
3 AV=250:B=128:CLS0
5 Z$=CHR$(128):E$=Z$+Z$+Z$
6 A$=Z$+CHR$(199)+CHR$(195)+CHR$(203)+Z$
7 C$(1)=Z$+CHR$(147)+CHR$(159)+CHR$(147):C$(2)=Z$+Z$+CHR$(163)+CHR$(175)+CHR$(163):C$(3)=E$+CHR$(179)+CHR$(191)+CHR$(179)
11 GOSUB25:GOSUB28:GOSUB32
12 B$=INKEY$
13 IF B$=CHR$(8)THENB=B-1
14 IF B$=CHR$(9)THEN B=B+1
15 IF B<128THEN B=128ELSE IF B>155THEN B=155
16 IF B$=CHR$(10)AND D=0THEN D=1:C=B+32
17 PRINT@B,A$;
18 GOSUB36
19 PRINT@E,C$(1);:PRINT@F,C$(2);:PRINT@G,C$(3);
21 IF L=0GOSUB66ELSE GOSUB75
22 IF D=1GOSUB45
23 PRINT@2,"HITS "T;:PRINT@20,"SCORE "S;
24 GOTO12
25 E=RND(15)
26 IF E<7THEN25
27 E=E*32:I=0:RETURN
28 F=RND(15)
29 IF F<7THEN28
30 F=F*32:IF F=E THEN28
31 J=0:RETURN
32 G=RND(15)
33 IF G<7THEN32
34 G=G*32:IF F=G OR G=E THEN 32
35 K=0:RETURN
36 E=E+1:I=I+1
37 IF I>26THEN PRINT@E,E$;:GOSUB25
39 F=F+2:J=J+2
40 IF J>25THEN PRINT@F,E$;:GOSUB28
42 G=G+3:K=K+3
43 IF K>23THEN PRINT@G,E$;:GOSUB32
44 RETURN
45 PRINT@C,CHR$(194);
46 FORX=1TO3
47 IF C=E+X GOSUB54
48 IF C=F+1+X GOSUB58
49 IF C=G+2+X GOSUB62
50 NEXTX
51 PRINT@C,CHR$(128);
52 C=C+32:IF C>510THEN D=0
53 RETURN
54 S=S+1:D=0
55 PRINT@E+1,CHR$(153)+CHR$(150);:SOUND50,3:PRINT@E+1,E$;:GOSUB25:RETURN
58 S=S+2:D=0
59 PRINT@F+2,CHR$(169)+CHR$(166);:SOUND100,3:PRINT@F+2,E$;:GOSUB28:RETURN
62 S=S+3:D=0
63 PRINT@G+3,CHR$(185)+CHR$(182);:SOUND150,2:PRINT@G+3,E$;:GOSUB32:RETURN
66 M=RND(10)
67 FOR N=1TO U
68 READ P(N):IF P(N)=M THEN72
69 NEXTN
70 DATA5,2,10,4,9,1,3,6,8,7
71 RESTORE:RETURN
72 L=RND(3)
73 IF L=1THEN Q(L)=E+2ELSE IF L=2THEN Q(L)=F+3ELSE IF L=3THEN Q(L)=G+4
74 RESTORE
75 PRINT@Q(L),Z$;
76 Q(L)=Q(L)-32:IF Q(L)<128THEN L=0:RETURN
77 PRINT@Q(L),CHR$(245);
78 IF Q(L)<=B OR Q(L)>B+3THEN RETURN
79 SOUND200,4:T=T+1:IF T<3THEN RETURN
81 J$=CHR$(201)+CHR$(195)+CHR$(198)
82 PRINT@B+1,J$;:SOUND AV,4:PRINT@B+1,E$;
84 AV=AV-16:B=B+32:IF B>510THENEND ELSE82
