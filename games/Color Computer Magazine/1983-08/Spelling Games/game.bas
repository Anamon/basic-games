
10 DATAU4E4F4NL8D4BR8,U8R7FD2GNL7FD2GNL7BR9,U8R8D1BD6D1NL8BR8,R2U8L2R6F2D4G2NL6BR11,U8R8L8D4NR6D4R8BR8,U8NR8D4NR6D4BR16,U8R8D2BD3NL3D2GNL7BR9,U8D4R8U4D8BR8,R4U8NL4R4BD8NL4BR8,NU2R8NU8BR8,U8D4R4NE4F4BR8,NU8R8BR8,U8F4E4D8BR8,U8F8NU8BR8
20 DATAU8R8D8L8BR16,U8R8D4L8D4BR16,U8R8D8L8R6H2F4H2R2BR8,U8R8D4L8R4F4BR8,R8U4L8U4R8BD8BR8,BR4U8NL4R4BD8BR8,U8BR8D8NL8BR8,BR4H4U4BR8D4G4BR12,NU8E4F4NU8BR8,E8BL8F8BR8,BR4U4NH4E4BD8BR8,E8L8BD8R8BR8,
30 DATAMONKEY,BILLION,BOUND,ALERT,ASTRONAUT,ADVENTURE,FUDGE,CIRCUIT,FIDDLE,CURIOUS,CREATURE,COZY,COWARD,DAMAGE,EXAMINE,EQUIPMENT,FANCY,FLICK,FURIOUS,GAZE,GRAVITY,GUITAR,HAWK,HUMAN,LEVEE,LIZARD,MAGICIAN,MISSION,MONSTER,MUNCH,ORBIT,PITCH,POUNCE,PRESEN
40 DATA PRICKLY,RAGE,REMARK,SCURRY,SEARCH,SILENTLY,SNARL,STARTLE,SUBMARINE,SWISH,TEMPER,TRAMPLE,TRUE,UNDER,WAIL,WEIGHTLESS
50 CLEAR 400:DIM H$(26),WD$(50),AR$(15),B(26),T$(15),S(1)
60 FOR RD=1 TO 26:READ H$(RD):NEXT RD:FOR RD=0TO50:READWD$(RD):NEXTRD:N=RD-1
70 PMODE3,1:SCREEN1,1:PCLS:DRAW"C4;BM50,80"+H$(23)+H$(8)+H$(9)+H$(3)+H$(8):DRAW"BM172,80"+H$(7)+H$(1)+H$(13)+H$(5):DRAW"C2;BM50,120"+H$(1):DRAW"C3;BM98,120"+H$(21)+H$(14)+H$(19)+H$(3)+H$(18)+H$(1)+H$(13)+H$(2)+H$(12)+H$(5):DRAW"C2;BM50,140"+H$(2)
80 DRAW"C3;BM98,140"+H$(8)+H$(1)+H$(14)+H$(7)+H$(13)+H$(1)+H$(14):DRAW"C2;BM50,160"+H$(3):DRAW"C3;BM98,160"+H$(6)+H$(12)+H$(1)+H$(19)+H$(8)
90 IN$=INKEY$:IF IN$="A" THEN Q=1
100 IF IN$="B" THEN Q=2
110 IF IN$="C" THEN Q=3
120 IF IN$<"A" OR IN$>"C" THEN 90
130 PCLS:DRAW"C4;BM50,80"+H$(6)+H$(21)+H$(19)+H$(5):DRAW"BM172,80"+H$(19)+H$(16)+H$(5)+H$(5)+H$(4)
140 DRAW"C2;BM50,120"+H$(1):DRAW"C3;BM98,120"+H$(19)+H$(12)+H$(15)+H$(23):DRAW"C2;BM50,140"+H$(2):DRAW"C3;BM98,140"+H$(13)+H$(5)+H$(4)+H$(9)+H$(21)+H$(13):DRAW"C2;BM50,160"+H$(3):DRAW"C3;BM98,160"+H$(6)+H$(1)+H$(19)+H$(20):DRAW"C2;BM50,180"+H$(4)
150 DRAW"C3;BM98,180"+H$(19)+H$(16)+H$(5)+H$(5)+H$(4)+H$(25):IF SK=1 THEN 230
160 IN$=INKEY$:IF IN$="A" THEN FS=1
170 IF IN$="B" THEN FS=2
180 IF IN$="C" THEN FS=3
190 IF IN$="D" THEN FS=4
200 IF IN$<"A" OR IN$>"D" THEN 160
210 IF Q=3 THEN 220 ELSE 280
220 PCLS:SK=1:DRAW"C4;BM50,80"+H$(6)+H$(12)+H$(1)+H$(19)+H$(8):DRAW"BM172,80"+H$(19)+H$(16)+H$(5)+H$(5)+H$(4):GOTO 140
230 IN$=INKEY$:IF IN$="A" THEN SP=500
240 IF IN$="B" THEN SP=200
250 IF IN$="C" THEN SP=50
260 IF IN$="D" THEN SP=0
270 IF IN$<"A" OR IN$>"D" THEN 230
280 IF FS=1 THEN FD=.10
290 IF FS=2 THEN FD=.5
300 IF FS=3 THEN FD=1
310 IF FS=4 THEN FD=2
320 PMODE3,1:SCREEN1,1:RT=0:SK=0:HS=0:D=0:EN=0:XP=0:C=0:FOR R=1 TO 26:B(R)=0:NEXT R
325 'choose word
330 PCLS:D=0:L=0:C=INT(RND(N)):IF C=0 THEN 330
340 DRAW "BM60,100"
350 A$=WD$(C):AA=LEN(A$):FOR CK=1 TO AA:AR$(CK)=MID$(A$,CK,1)
360 IF Q<>3 THEN NEXT CK ELSE 370
370 IF Q=3 THEN 380 ELSE 400
380 T=ASC(AR$(CK)):T=T-64:DRAW"C4"+H$(T):NEXT CK:FOR TI=1 TO SP:NEXT TI:PCLS
390 IF Q=3 THEN 440
400 IF Q<>2 THEN 420
405 'set up hangman screen
410 DRAW "C2;BM65,13":FOR BK=1 TO AA:DRAW "BR8R8":NEXT BK:DRAW "C3;BM5,129;R59D10R4D10R4D8L13U25L49D25L13U141R38D13L4U11L30D110":PAINT(6,130),3,3:GOTO 440
420 R=RND(AA):IF B(R)=1 THEN 420
430 B(R)=1:L=ASC(AR$(R)):L=L-64:DRAW"C3"+H$(L):XP=XP+1:IF XP=AA THEN 440 ELSE 420
435 'draw bomb
440 DRAW "C4":LINE(220,130)-(240,180),PSET,BF:CIRCLE(230,130),10,3,.4,3:CIRCLE(230,180),10,4,.4,3:PAINT(230,128),2,3:PAINT(230,182),4:DRAW"C3;BM230,130;U110"
450 DRAW "C4;BM230,20;NH4NU5E4"
460 PLAY "T250O1DA":F=13:X=58:Y=142
470 FOR Z=1 TO AA+3
475 'burn fuse and get input
480 T$=INKEY$:F=F+FD:GET(226,F)-(234,F+7),S,G:LINE(226,F)-(234,F+7),PRESET,BF:PUT(226,F+FD)-(234,F+7+FD),S,PSET
490 IF F>120 THEN 760
500 IF T$="" THEN 480
510 IF Q=2 THEN 770
520 T$(Z)=T$:S=ASC(T$):IF S=8 THEN Z=Z-1
530 IF Z<1 THEN Z=1
535 'erase letter if backspace
540 IF S=8 THEN 550 ELSE 560
550 X=X-16:LINE(X,Y)-(X+12,Y+8),PRESET,BF:DRAW "BL16":GOTO 480
560 IF S=13 THEN 620
570 IF S<64 OR S>90 THEN 480
580 X=X+16:S=S-64
590 DRAW "C2;BM60,150":IF Z=1 THEN 610
600 FOR BK=1 TO Z-1:DRAW"BR16":NEXT BK
610 DRAW H$(S):NEXT Z
615 'check answer
620 FOR CK=1 TO AA:IF T$(CK)=MID$(A$,CK,1) THEN NEXT CK ELSE 690
630 IF Z-1>AA THEN 690
635 'good work response
640 PLAY "V15T6O3GGL2GL4FE":PCLS(4):PLAY "L1E":PCLS:PLAY "DL2E":PCLS(3):PLAY "L1E":CT=CT+1:MS=0
650 IF CT=3 THEN FS=FS+1
660 IF FS>4 THEN FS=4
670 IF CT=3 THEN CT=0
680 T$=INKEY$:GOTO 1160
685 'error in answer
690 CT=0:MS=MS+1:PLAY "O5":FOR LP=1 TO 4:PLAY "T25O-GEC":NEXT LP:DRAW "C3;BM40,40"+H$(20)+H$(8)+H$(5):DRAW "BR16"+H$(23)+H$(15)+H$(18)+H$(4):DRAW "BR16"+H$(23)+H$(1)+H$(19)
700 DRAW "BM60,80"
710 FOR TT=1 TO AA:L$=MID$(A$,TT,1):L=ASC(L$):L=L-64:DRAW H$(L):NEXT TT:FOR TI=1 TO 2000:NEXT TI:T$=INKEY$
720 IF MS=3 THEN FS=FS-1
730 IF FS<1 THEN FS=1
740 IF MS=3 THEN MS=0
750 GOTO 1160
755 'bomb explodes
760 PCLS:PLAY"O1":FOR LP=1 TO 4:PCLS(LP):PLAY "T250O+V30DFDGD":NEXT LP:SCREEN1,1:PCLS:GOTO 690
765 'hangman routine
770 IF T$<"A" OR T$>"Z" THEN 480
780 T=ASC(T$):T=T-64:IF B(T)=1 THEN 480
790 B(T)=1
800 FOR CK=1 TO AA
810 I$=MID$(WD$(C),CK,1):IF I$=T$ THEN 1060 ELSE NEXT CK
820 IF RT=1 THEN 830 ELSE 840
830 RT=0:GOTO 480
840 DRAW "BM30,180"
850 D=D+1:FOR LP=1 TO D:DRAW "BR16":NEXT LP:CL=RND(3):ON CL GOTO 860,870,880
860 DRAW "C2":GOTO 890
870 DRAW "C3":GOTO 890
880 DRAW "C4":GOTO 890
890 DRAW H$(T)
895 'hang him
900 HS=HS+1:ON HS GOTO 910,920,930,940,950,960,970,980
910 CIRCLE(36,44),8,2,2,2:GOTO 1050
920 CIRCLE(34,42),1,3:CIRCLE(38,42),1,3:DRAW "BM36,43;D3":DRAW "BM34,50;R4":DRAW "C4":LINE(35,57)-(39,62),PSET,BF:GOTO 1050
930 DRAW "C3;BM36,62;L8D23F8E8U23L8":CIRCLE(36,70),4:CIRCLE(36,75),4:CIRCLE(36,80),4:PAINT(36,64),3,3:GOTO 1050
940 DRAW "C3;BM30,62;G22F4E17":GOTO 1050
950 DRAW "C3;BM44,62;F22G4H17":GOTO 1050
960 DRAW "C4;BM44,85;BG7F11D25R8U4L2U30H9":GOTO 1050
970 DRAW "C4;BM30,85;BF6D37L12U4R5U39R16":PAINT(36,87),4,4:GOTO 1050
980 LINE(8,31)-(82,128),PRESET,BF:DRAW "C4;BM36,36;U7":CIRCLE(36,44),8,2:CIRCLE(34,42),1,3:CIRCLE(38,42),1,3:DRAW"C2;BM36,43;D3":DRAW"BM34,48;R4":DRAW"C4":LINE(35,51)-(39,56),PSET,BF
990 DRAW"C2;BM36,56;D6L16D40R4U35R4D28BR16U28R4D35R4U40L16":DRAW"BM28,95;D34NR6BD4NR6D11L2D4R8U15BU4U24R4D24NR6BD4NR6D15R8U4L2U11BU4U34":DRAW "C3;BM23,133;D20L4U20":PAINT(21,148),3,3:PAINT(36,63),2,2:PAINT(30,135),2,2:PAINT(42,135),2,2
995 'play taps and show correct word
1000 MS=MS+1:HS=0:CT=0:PLAY "T3V10O2L2GL4GL2O3CP4O2L2GL4O3CL2EP4L2O2GL4O3CL2EP4L2O2GL4O3CL2EP4L2O2GL4O3CL2EP4L2O3CL4EL2GP4L2EL4CL2O2GP4V4GL4GL1O3C"
1010 PCLS:DRAW "C2;BM50,50;"+H$(20)+H$(8)+H$(5):DRAW "C3;BM114,50;"+H$(23)+H$(15)+H$(18)+H$(4):DRAW "C4;BM194,50;"+H$(23)+H$(1)+H$(19):DRAW "C3;BM80,120"
1020 FOR LT=1 TO AA:T$=MID$(WD$(C),LT,1):T=ASC(T$):T=T-64:DRAW H$(T):NEXT LT:FOR TI=1 TO 2000:NEXT TI:PCLS
1030 FOR T=1 TO 26:B(T)=0:NEXT T:D=0:EN=0
1040 IF CT=3 THEN 650 ELSE 720
1050 SOUND 132,2:GOTO 480
1060 RT=1:DRAW "BM40,10":FOR W=0 TO CK:DRAW "BR16":NEXT W
1065 'good letter response
1070 CL=RND(3):ON CL GOTO 1080,1090,1100
1080 DRAW "C2":GOTO 1110
1090 DRAW "C3":GOTO 1110
1100 DRAW "C4":GOTO 1110
1105 'hangman wins
1110 DRAW H$(T):PLAY "T8V15O3CEGO4L2CO3L4GO4L2C":CK=CK+1:EN=EN+1:IF EN=>AA THEN 1120 ELSE 1140
1120 PLAY"T4L2O4AL4GL8GEDEL4GCP8L4GL8O4AO3GDCBL2C":PCLS:DRAW"C2;BM80,40;"+H$(20)+H$(8)+H$(1)+H$(14)+H$(11)+H$(19):DRAW "C4;BM40,80;"+H$(6)+H$(15)+H$(18):DRAW"BM114,80;"+H$(19)+H$(1)+H$(22)+H$(9)+H$(14)+H$(7)
1130 DRAW"C3;BM70,120;"+H$(13)+H$(25):DRAW"BM118,120;"+H$(12)+H$(9)+H$(6)+H$(5):FOR TI=1 TO 2000:NEXT TI:CT=CT+1:MS=0:GOTO 1030
1140 IF CK>AA THEN CK=0 ELSE 810
1150 RT=0:GOTO 480
1160 BB=BB+1:IF BB=8 THEN 1170 ELSE 280
1165 'menue to change games
1170 BB=0:PCLS:DRAW "C3;BM55,44"+H$(23)+H$(8)+H$(1)+H$(20):DRAW "BM185,44"+H$(14)+H$(5)+H$(24)+H$(20):DRAW "C4;BM25,88"+H$(1):DRAW "C2;BM68,88"+H$(3)+H$(8)+H$(1)+H$(14)+H$(7)+H$(5):DRAW "BM180,88"+H$(6)+H$(21)+H$(19)+H$(5)
1180 DRAW "C4;BM25,110"+H$(2):DRAW "C2;BM68,110"+H$(3)+H$(8)+H$(1)+H$(14)+H$(7)+H$(5):DRAW "BM180,110"+H$(7)+H$(1)+H$(13)+H$(5)+H$(19)
1190 DRAW "C4;BM25,132"+H$(3):DRAW "C2;BM68,132"+H$(5)+H$(14)+H$(4):DRAW "BM137,132"+H$(16)+H$(18)+H$(15)+H$(7)+H$(18)+H$(1)+H$(13)
1200 DRAW "C4;BM25,154"+H$(4):DRAW "C2;BM68,154"+H$(19)+H$(1)+H$(13)+H$(5):DRAW "BM148,154"+H$(19)+H$(20)+H$(21)+H$(6)+H$(6)
1210 T$=INKEY$:IF T$="" THEN 1210
1220 IF T$="A" THEN 130
1230 IF T$="B" THEN 70
1240 IF T$="C" THEN END
1250 IF T$="D" THEN 320
