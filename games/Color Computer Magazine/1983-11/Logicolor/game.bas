
120 CLS(0)
130 B=246
140 FOR T=1 TO 500
150 NEXT
160 A$="LOGICOLOR"
170 FOR A=160 TO 223
180 PRINT @ A,CHR$(143+(RND(7)*16));A$;
190 PRINT @ B,A$;CHR$(143+(RND(7)*16));
200 SOUND RND(15)*8,1
210 IF A=B THEN 220 ELSE B=B-1: NEXT
220 FOR T=1 TO 500
230 NEXT
240 PRINT @ 321,"PREPARE TO BREAK COLOR CODE...";
250 FOR T=1 TO 2000
260 NEXT
270 CLS(0)
280 L$=CHR$(8)
290 R$=CHR$(9)
300 D$=STRING$(8,128)
310 Z$=CHR$(128)
320 S$=CHR$(255)
330 FOR A=1 TO 4
340 A$=A$+CHR$(131+(RND(6)*16))
350 B$=B$+RIGHT$(A$,1)+Z$
360 NEXT
370 FOR A=0 TO 448 STEP 32
380 P=0
390 G=0
400 J=0
410 C$=D$
420 B=1
430 PRINT @ A+B,S$;
440 X$=INKEY$
450 IF X$<>"E" AND X$<>L$ AND X$<>R$ AND X$<>"M" AND X$<>"Y" AND X$<>"B" AND X$<>"R" AND X$<>"W" AND X$<>"C" THEN 440
460 IF X$="M" OR X$="Y" OR X$="B" OR X$="R" OR X$="W" OR X$="C" GOSUB 630
470 IF X$="E" AND MID$(C$,1,1)<>Z$ AND MID$(C$,3,1)<>Z$ AND MID$(C$,5,1)<>Z$ AND MID$(C$,7,1)<>Z$ GOSUB 670 ELSE IF X$="E" THEN X$=""
480 IF X$<>"E" THEN PRINT @ A,C$;: PRINT @ A+B,Z$;: QQ=0
490 IF X$=L$ AND B>2 THEN SOUND B*20,1: B=B-2 ELSE IF X$=R$ AND B<6 THEN B=B+2: SOUND B*20,1
500 PRINT @ A+B,S$;
510 IF X$="E" AND J=>4 THEN PRINT @ A+B,Z$;: NEXT A
520 IF PP=15 GOTO 530 ELSE PRINT @ A+B,S$;: GOTO 440
530 PRINT @ 480, "GAME OVER. SEQUENCE:"Z$+B$;
540 FOR T=1 TO 3
550 SOUND 147,3
560 SOUND 176,2
570 FOR G=1 TO 50
580 NEXT G
590 NEXT T
600 SOUND 147,2
610 SOUND 89,3
620 GOTO530
630 J=J+1
640 IF X$="Y" THEN W=16 ELSE IF X$="B" THEN W=32 ELSE IF X$="R" THEN W=48 ELSE IF X$="W" THEN W=64 ELSE IF X$="C" THEN W=80 ELSE IF X$="M" THEN W=96
650 MID$(C$,B,1)=CHR$(131+W)
660 RETURN
670 QQ=1
680 PP=PP+1
690 K$=B$
700 IF C$=B$ GOTO 850 ELSE FOR F=1 TO 7 STEP 2
710 IF MID$(C$,F,1)=MID$(K$,F,1)THEN P=P+1: MID$(C$,F,1)=Z$: MID$(K$,F,1)=Z$
720 NEXT F
730 FOR F=1 TO 7 STEP 2
740 FOR Q=1 TO 7 STEP 2
750 IF MID$(C$,F,1)=MID$(K$,Q,1) AND MID$(K$,Q,1)<>Z$ THEN G=G+1: MID$(K$,Q,1)=Z$: MID$(C$,F,1)=Z$
760 NEXT Q
770 NEXT F
780 PRINT @ A+10,"";
790 IF PP<10 THEN PRINT" ";
800 PRINT PP"- POS:"P"COLOR:"G;
810 FOR T=1 TO 10
820 SOUND RND(10)+150,1
830 NEXT
840 RETURN
850 N=N+1
860 IF N/2=INT(N/2) GOSUB 1000 ELSE GOSUB 1020
870 FOR T=1 TO 4
880 SOUND 176,1
890 SOUND 193,1
900 NEXT
910 FOR T=1 TO 4
920 SOUND 193,1
930 SOUND 204,1
940 NEXT
950 FOR T=1 TO 3
960 SOUND 210,1
970 SOUND 218,1
980 NEXT
990 GOTO 850
1000 PRINT @ 480,"WINNER IN"PP"TURNS.         ";
1010 RETURN
1020 PRINT @ 480,"THAT WAS VERY GOOD!";
1030 RETURN
1040 END
