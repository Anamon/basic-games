
10 PCLEAR1
20 GOSUB 1810
30 REM BY ROBERT TOSCANI,4744 WHITAKER AVE,PHILA, PA. 19120.
40 CLS
50 PRINT@171,"MY STORY"
60 PRINT@206,"BY"
70 INPUT"                                     ";A$
80 FOR A=1 TO 500:NEXT A
90 CLS
100 INPUT"WITH AN ANGRY CLAMOR, THE 1";B1$
110 PRINT"ANSWERING IT, I WAS CONFRONTED"
120 INPUT"BY 2";B2$
130 INPUT"I SAID WHAT ARE 3";B3$
140 INPUT"THE REPLY WAS 4";B4$
150 INPUT"I WAS 5";B5$
160 INPUT"BUT I KEPT 6";B6$
170 INPUT"LEADING THE WAY TO 7";B7$
180 INPUT"I WONDERED WHY 8";B8$
190 INPUT"TO THAT THERE WAS 9";B9$
200 INPUT"IT HAD TO BE 10";B10$
210 INPUT"I TURNED TO 11";Z3$
220 INPUT"AND I SAID 12";Z4$
230 INPUT"THE STUNNING ANSWER WAS 13";Z5$
240 INPUT"THAT 14";Z6$
250 INPUT"AND FORCED ME TO 15";Z7$
260 INPUT"I JUMPED 16";Z9$
270 INPUT"TAKING MY 17";Y1$
280 INPUT"I STARTED 18";Y2$
290 INPUT"I KNEW IF I DIDN'T 19";Y3$
300 INPUT"THAT SETTLED 20";Y5$
310 INPUT"THINGS HAD 21";Y6$
320 INPUT"AND REQUIRED 22";Y7$
330 INPUT "BUT WAS I UP TO 23";Y8$
340 INPUT "I KNEW I HAD TO 24";Y9$
350 INPUT "TAKING 25";XP1$
360 INPUT "I 26";X2$
370 INPUT "THINGS STARTED 27";X3$
380 INPUT"IT WASN'T 28";X4$
390 INPUT"BUT IT HAD 29";X5$
400 INPUT "SURPRISING ME WITH 30";X6$
410 INPUT"I FELT 31";X7$
420 INPUT"THE SITUATION WAS GETTING 32";X8$
430 INPUT"AND ONLY I COULD 33";X9$
440 INPUT"I 34";X10$
450 INPUT"AND IT 35";U1$
460 INPUT" THINGS BEGAN 36";U2$
470 INPUT"AND I WAS ABLE 37";U3$
480 INPUT"I KNEW IT WAS GOING 38";U5$
490 INPUT"AND IT PROVED ONCE AGAIN 39";U6$
500 FOR Z=1 TO 300:NEXT Z
510 CLS
520 PRINT"TO PICK MATCHING MUSIC, YOU"
530 PRINT"HAVE FOUR CHOICES. 1 FOR A"
540 PRINT"WESTERN, 2 FOR SECRET AGENT, 3"
550 PRINT"FOR INSPIRATIONAL AND 4 TO"
560 PRINT"WRITE YOUR OWN. IF YOU PICK 4,"
570 PRINT"YOU WILL GET 18 LINES TO WRITE"
580 PRINT"NOTES ON."
600 INPUT N
610 IFN<1 OR N>4 THEN 600
620 ON N GOTO 1380,1520,1670,1780
630 CLS
640 PRINT@171,"MY STORY"
650 PRINT@206,"BY"
660 PRINT@234,A$
670 PLAY C$
680 CLS
690 PRINT"WITH AN ANGRY CLAMOR THE "B1$"."
700 PRINT "ANSWERING IT, I WAS CONFRONTED"
710 PRINT"BY "B2$"."
720 PLAY D$
730 PRINT "I SAID 'WHAT ARE "B3$"?"
740 PRINT"THE REPLY WAS "B4$"."
750 PLAY E$
760 PRINT"I WAS "B5$","
770 PRINT"BUT I KEPT "B6$"."
780 PLAY F$
790 PRINT"LEADING THE WAY TO "B7$
800 PRINT"I WONDERED WHY "B8$"."
810 PLAY G$
820 PRINT"TO THAT THERE WAS "B9$"!"
830 PRINT"IT HAD TO BE "B10$"."
840 PLAY H$
850 PLAY I$
860 PRINT"I TURNED TO "Z3$
870 PRINT"AND I SAID "Z4$"."
880 PRINT"THE STUNNING ANSWER WAS "Z5$"!"
890 PLAY J$
900 PRINT"THAT "Z6$
910 PRINT"AND FORCED ME TO "Z7$"."
920 PLAYK$
930 PRINT"I JUMPED "Z9$
940 PRINT"TAKING MY "Y1$
950 PRINT"I STARTED "Y2$"."
960 PLAY L$
970 PRINT"I KNEW IF I DIDN'T "Y3$"."
980 PRINT"THAT SETTLED "Y5$"."
990 PLAYM$
1000 PRINT"THINGS HAD "Y6$
1010 PRINT"AND REQUIRED "Y7$
1020 PRINT"BUT WAS I UP TO "Y8$
1030 PLAY N$
1040 PRINT"I KNEW I HAD TO "Y9$"."
1050 PRINT"AND TAKING "XP1$
1060 PRINT"I "X2$"."
1070 PLAY  O$
1080 PRINT"THINGS STARTED "X3$
1090 PRINT "IT WASN'T "X4$
1100 PRINT"BUT IT HAD "X5$"."
1110 PLAY P$
1120 PRINT"SUPRISNG ME WITH "X6$
1130 PRINT"I FELT "X7$"."
1140 PRINT"THE SITUATION WAS GETTING "X8$
1150 PLAY Q$
1160 PRINT"AND ONLY I COULD "X9$"."
1170 PRINT"I "X10$"."
1180 PRINT"AND IT "U1$"!"
1190 PLAY R$
1200 PRINT"THINGS BEGAN "U2$
1210 PRINT"AND I WAS ABLE "U3$"."
1220 PLAY S$
1230 PRINT"I KNEW IT WAS GOING "U5$"."
1240 PRINT"AND IT PROVED ONCE AGAIN "U6$"."
1250 PRINT TAB(12) "THE END"
1260 PLAY T$
1270 FOR Z=1 TO 300:NEXT Z
1280 CLS
1290 PRINT"TO REPEAT THE COMPLETED PIECE,"
1300 PRINT"PRESS 1. TO RUN THE STORY WITH"
1310 PRINT"DIFFERENT MUSIC, PRESS 2. TO"
1320 PRINT"START OVER FROM SCRATCH, PRESS"
1330 PRINT"3. AND TO END IT ALL, PRESS 4."
1340 INPUT WW
1350 IF WW<1 OR WW>4 THEN 1340
1360 ON WW GOTO 630,510,10,1370
1370 END
1380 C$="O3L4T4E-P8L4.GL8GL4FP8L4E-L8E-G"
1390 D$="T3GL4FE-P8L4GL8GL4FP8L8E-E-E-GG"
1400 E$="L4FGL1B-O4L4CO3B-A-O4L1.E-"
1410 F$="O3L4GL1B-O4L4CO3B-A-L1FL4E-FGL2B-"
1420 G$="O4L4CO3B-GL2E-P4O4L4E-E-CO3B-"
1430 H$="O4CO3E-FGL8E-L4E-L8GL4FP8"
1440 I$="L4E-L8E-GGL4FE-P8L4GL8GL4FP8"
1450 J$="L8E-E-E-GGL4F"
1460 K$="XD$;":L$="XE$;":M$="XF$;":N$="XG$;":O$="XH$;":P$="XI$;"
1470 Q$="L4GL1B-O4L4E-L8CO3L4B-A-L1F"
1480 R$="L4.E-L4FGL1B-O4L4CL8O3B-L4GFL1.A-"
1490 S$="L4GL8E-L4E-L8GL4FP8L4E-L8E-GG"
1500 T$="L4FE-P8L4GL8GL4FP8L8E-E-E-GGL4FP8L8E-E-E-GGL4FE-"
1510 GOTO 630
1520 C$="T3O2L2BO3CC#CO2BO3CC#C"
1530 D$="T2O3L8EL16F#F#L8F#L4F#L8EEEEL16GGL8GL4GL8F#F#F#"
1540 E$="XD$;":F$="XD$;"
1550 G$="O4L8D#L2DO3L8BAL1B"
1560 H$="O3L8EL4GO4L8D#L4.DL16O3GL8A#L2.B"
1570 I$="L4GL16GL4.FL8O2BO3E-L4C#P8"
1580 J$="XH$;"
1590 K$="L4O3GL16AGL4.F#L8O2BO3D#L4EP16"
1600 L$="L8O3EL4EL8EL8.F#L16EL4F#L8GL4G"
1610 M$="L8GL8.F#L16EL4F#"
1620 N$="XL$;":O$="XM$;"
1630 P$="O3L8BBP5BBP5BBL8.BL8BL16BL8BBP3"
1640 Q$="XD$;":R$="XD$;":S$="XD$;"
1650 T$="XG$;"
1660 GOTO 630
1670 C$="V15O3L16FFL8.FL16FL8.FL16E-L8.DL16F"
1680 D$="L8.B-L16O4CL8.DL16DL8.DL16CL4O3B-"
1690 E$="L8.B-L16AL8.GL16GL8.GL16A"
1700 F$="L8.B-L16AL8.B-L16GL8.FL16.GL8.FL16DDL4F"
1710 G$="XC$;":H$="XD$;"
1720 I$="L4B-O4CCO3B-AL1V22B-"
1730 J$="V20L4.FL8E-L8.DL16FL8.B-L16O4CL2DO3L4B-P4"
1740 K$="L4.GL8AL8.B-L16AL8.B-L16GL2FD"
1750 L$="L4.FL8E-L8.DL16FL8.B-O4L16CL2DO3L4B-B-O4CCO3B-AL2.B-"
1760 M$="XC$;XD$;":N$="XE$;XF$;":O$="XC$;":P$="XD$;":Q$="XI$;":R$="XJ$;":S$="XK$;":T$="XL$;"
1770 GOTO 630
1780 CLS
1790 INPUT"1";C$:INPUT"2";D$:INPUT"3";E$:INPUT "4";F$:INPUT"5";G$:INPUT"6";H$:INPUT"7";I$:INPUT"8";J$:INPUT"9";K$:INPUT"10";L$:INPUT"11";M$:INPUT"12";N$:INPUT"13";O$:INPUT"14";P$:INPUT"15";Q$:INPUT"16";R$:INPUT"17";S$:INPUT"18-FINAL";T$
1800 GOTO 630
1810 CLS
1820 PRINT@7,"MUSICAL TALES"
1830 PRINT"YOU CAN WRITE YOUR OWN STORY."
1840 PRINT"ALL YOU HAVE TO DO IS FILL IN"
1850 PRINT"NUMBERED BLANKS. THERE ARE 39"
1860 PRINT"SPACES AND YOU CAN PUT WORDS,"
1870 PRINT"PHASES OR WHOLE SENTANCES IN."
1880 PRINT"AFTER DOING THAT, THE COMPUTER"
1890 PRINT"WILL PLAY THE COMPLETE STORY"
1900 PRINT"WITH MUSIC BUT MORE ABOUT THAT"
1910 PRINT"LATER. PRESS ANY KEY TO BEGIN."
1920 MM$=INKEY$:IF MM$="" THEN 1920
1930 RETURN
