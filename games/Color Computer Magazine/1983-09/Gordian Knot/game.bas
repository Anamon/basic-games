
100 REM*GORDIAN KNOT* 4K COLOR
110 REM*SORCERER'S PUZZLES #3 / R. RAMELLA
120 CLS 0
130 CLEAR 400
140 U$=CHR$(94):D$=CHR$(10)
160 L$=CHR$(8):R$=CHR$(9)
180 Z$="H EOSULAYDO O DEVV GANRNKT  IO INAIAGNPANZOERF MENCMRA"
190 X$="YOU HAVE SOLVED GORDIAN KNOT IN AN AMAZING PERFORMANCE"
200 X=1:P=1:N=0
230 FOR A=1 TO 193 STEP96
240 FOR G=A TO A+17
250 PRINT@G,MID$(Z$,X,1);
260 IF MID$(X$,P,1)=MID$(Z$,X,1) THEN N=N+1:PRINT@G+32,CHR$(143+112); ELSEPRINT@G+32,CHR$(128);
270 X=X+1:P=P+1
300 NEXTG,A
310 PRINT@22,"TURN:"H;
320 PRINT@54,"SCORE:"N;
330 IF H=15 OR X$=Z$GOTO740
340 X=1:P=1
360 GOSUB 710
370 V$=INKEY$
380 IF V$="M" GOSUB 570
390 IF W=5 THEN W=0: GOTO200
400 PRINT@X,CHR$(143+64);
410 FOR T=1 TO 20:NEXT
430 PRINT@X,MID$(Z$,P,1);
440 IF V$=L$ THEN X=X-1: P=P-1
450 IF V$=R$ THEN X=X+1: P=P+1
460 IF V$=D$ THEN X=X+96:P=P+18
470 IF V$=U$ THEN X=X-96:P=P-18
480 IF V$=R$ GOSUB530
490 IF V$=L$ GOSUB550
500 IF X<0 THEN X=X+96: P=P+18
510 IF X>210 THEN X=X-96:P=P-18
520 GOTO 370
530 IF X=19 OR X=115 OR X=211 THEN X=X-1:P=P-1:GOTO370
540 RETURN
550 IF X=0 OR X=96 OR X=192 THEN X=X+1:P=P+1:GOTO370
560 RETURN
570 N$=N$+STR$(P)
580 H=H+1
590 IF P/6=INT(P/6) THEN A=6:B=3:GOTO650
600 IF P/5=INT(P/5) THEN A=5:B=4:GOTO650
610 IF P/4=INT(P/4) THEN A=4:B=2:GOTO650
620 IF P/3=INT(P/3) THEN A=3:B=1:GOTO650
630 IF P/2=INT(P/2) THEN A=2:B=1:GOTO650
640 IFP/2<>INT(P/2) THENA=3:B=2
650 FOR G=A TO 54 STEP A
660 F$=MID$(Z$,G-B,1)
670 Z$=LEFT$(Z$,G-B-1)+MID$(Z$,G,1)+RIGHT$(Z$,LEN(Z$)-G+B)
680 Z$=LEFT$(Z$,G-1)+F$+RIGHT$(Z$,LEN(Z$)-G)
690 NEXT G
700 W=5
710 PRINT@320,"S-C-O-R-E S-T-R-I-N-G B-E-L-O-W "N$;:RETURN
740 PRINT@480," THE END ";
750 SOUND RND(13)*10,1
760 FORT=1TO100:NEXTT
780 PRINT@480,"         ";
790 FORT=1TO50:NEXTT:GOTO740
