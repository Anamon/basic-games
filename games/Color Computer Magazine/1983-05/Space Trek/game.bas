10 CLEAR250:SB=PEEK(25)*256+PEEK(26):FORX=1TO5:READZ:POKESB+Z,34:NEXT:DATA78,119,149,193,237
20 A$= FNFNFNFNFNFNFNUSINGUSINGFNUSINGFNUSINGFNUSINGFNUSINGFNUSINGFNUSINGFNUSINGFNDIRFNFNFNFNFNFNFN
30 B$= DIRFORFORFORFORFORFORFORFORFORFORFORFORFORFORFORFORUSING
40 L$(1)= LOADCONTPAINTLOADCONTPAINTLOADCLOADPCOPYLOADCLOADPAINTRSETCONTPCOPYLOADCLOADPAINTRSETCONTPCOPYLOADCLOADPAINTUNLOADCONTPAINTLOADCONTPAINTLOADCONT
50 L$(2)= CONTPAINTLOADCONTPAINTLOADCONTPCOPYRSETCONTPCOPYLOADCLOADPAINTRSETCONTPCOPYLOADCLOADPAINTRSETCONTPCOPYLOADLLISTPAINTLOADCONTPAINTLOADCONTPAINT
60 L$(3)= PAINTLOADCONTPAINTLOADCONTPAINTRSETCLOADPAINTRSETCONTPCOPYLOADCLOADPAINTRSETCONTPCOPYLOADCLOADPAINTRSETCONTRENUMLOADCONTPAINTLOADCONTPAINTLOAD
70 POKE280,TIMER/256:DIMS(8,8),G(8,8),Z(8,8),C(9,2),K(3,3),D$(8),B$(30),C$(3)
80 Z$=STRING$(16," "):Y$=" .............. ":X$=STRING$(7," "):V$="ENTERPRISE":U$="FEDERATION":CO$="## ,##"
90 CLS:PRINT@458,"CC SPACE TREK"TAB(40)"BY JAKE COMMANDER";
100 C$(0)="DOCKED ":C$(1)=CHR$(132)+STRING$(5,140)+CHR$(136):C$(2)=CHR$(148)+STRING$(5,156)+CHR$(152):C$(3)=CHR$(180)+STRING$(5,188)+CHR$(184)
110 A$(0)=STRING$(2,192):A$(1)=CHR$(237)+CHR$(232):A$(2)=STRING$(2,192):A$(3)=CHR$(220)+CHR$(214):A$(4)=CHR$(192)+CHR$(146):A$(5)=CHR$(166)+CHR$(169)
120 FORY=1TO2:FORX=1TO9:READC(X,Y):NEXTX,Y:FORX=1TO8:READD$(X):NEXT:FORX=1TO30:READB$(X):NEXT
130 GOSUB1370:GOSUB1570:T=(RND(20)+19)*100+RND(100):T0=T+1:TT=32:GOSUB1520:L=RND(8):M=RND(8):U=RND(8):V=RND(8):PRINT@320,"AWAIT FURTHER ORDERS."
140 DATA,1,1,1,,-1,-1,-1,,-1,-1,,1,1,1,,-1,-1
150 DATAWARP ENGINES,LOCAL SCANNER,REMOTE SCANNER,PHASER CONTROL,PHOTON TUBES,SHIELD CONTROL,DAMAGE CONTROL,COMPUTER,SET,COURSE,LOCAL,SCANNER
160 DATAREMOTE,SCANNER,FIRE,PHASERS,PHOTON,TORPEDO,SHIELD,CONTROL,DAMAGE,REPORT,ONBOARD,CMPUTER,COMMAND,MODE,CMPUTER,GUIDNCE,STATUS,REPORT,TORPEDO,DATA,COMPUTE,COURSE,GUIDED,TORPEDO,MISSION,RECORD
170 FORJ=1TO8:FORI=1TO8:GOSUB1700:R=RND(100):IFR>96THENK=3ELSEIFR>90THENK=2ELSEIFR>75THENK=1ELSEK=0
180 KT=KT+K:R=RND(100):IFR<98THENB=0ELSEB=1:BT=BT+1
190 S=RND(8):G(I,J)=K*100+B*10+S:Z(I,J)=0:NEXTI,J:K7=KT:IFBT=0THENI=RND(8):J=RND(8):G(I,J)=G(I,J)+10:BT=1
200 IFKT=0THEN170ELSEGOSUB520:PRINT@320,USING"YOU HAVE 32 STARDATES TO DESTROYFLEET OF ## KLINGONS: THERE ARE ## STARBASES IN THIS GALAXY";KT;BT:GOSUB1350
210 CL=0:K=0:B=0:S=0:S(U,V)=3:IFL<1ORL>8ORM<1ORM>8THEN300
220 X=G(L,M):K=INT(X/100):X=X-K*100:B=INT(X/10):S=X-B*10
230 IFK=0THEN270ELSEIFG>200THEN260ELSEIFZ9=1ORZ7=1THEN250
240 PRINT"YOU HAVE ENTERED A COMBAT AREA  WITH YOUR SHIELDS LOW. THIS TIMETHEY DIDN'T SHOOT. BE WARNED!";:Z9=1:GOTO260
250 PRINT"COMBAT AREA  .. CONDITION RED - SHIELDS ARE DANGEROUSLY LOW.
260 GOSUB1330
270 IFB<>0THENFORI=1TOB:GOSUB1310:S(X,Y)=5:NEXT
280 IFS<>0THENFORI=1TOS:GOSUB1310:S(X,Y)=4:NEXT
290 ST=4:GOSUB1420
300 PRINT@128,USINGCO$;L;M;:PRINT@192,USINGCO$;U;V;:IFCL=1THENPRINT@320,"SENSORS DETECT KLINGONS CLOAKING - PHASERS NECESSARY":GOSUB1640
310 IFD(2)<0THENO=0:A=2:GOTO370ELSEO=1:Z=38:FORY=1TO8:FORX=1TO8:PRINT@X*2+Z,A$(S(X,Y));:NEXTX:Z=Z+32:NEXTY
320 IFK<>0THENIFZ7=1THENIFRND(2)>1GOSUB1020ELSEELSEZ7=1
330 A=0:GOSUB380:PRINT@217,"CMD?  ";:M2=0
340 H=0
350 GOSUB390:IFA$<>CHR$(13)THENIFA=0ORA>8THEN350ELSEPRINT@249,B$(A*2-1);:PRINT@281,B$(A*2);:GOTO350
360 PRINT@320,;:IFA<3ORD(A)>=0THENONA+1GOTO330,450,300,710,770,850,980,1000,1100,330
370 PRINT@320,D$(A);" - INOPERATIVE":GOSUB1640:IFA=4ANDCL=1THENPRINT"ILLOGICAL TO REMAIN HERE,":PRINT"KLINGONS CLOAKING":GOTO330ELSE330
380 PRINT@249,X$;:PRINT@281,X$;:RETURN
390 A$=INKEY$:IFA$=""THEN420
400 IFA$=CHR$(13)THENPRINT@320,;:GOTO1640
410 A=VAL(A$):GOSUB380:PRINT@221,A;:RETURN
420 GOSUB1650:H=H+1:IFH<280THEN390
430 H=0:GOSUB1520:PRINT@320,;:GOSUB490:IFK<>0THENIFRND(2)>1THENGOSUB1020:GOTO340ELSE340ELSEIFRND(10)<9THEN340ELSEGOSUB1520:IFL<1ORL>8ORM<1ORM>8THEN340ELSEFORN=1TO8:FORQ=1TO8:X=INT(G(N,Q)/100):IFX=0THENNEXTQ,N
440 G(N,Q)=G(N,Q)-X*100:G(L,M)=G(L,M)+X*100:K=X:PRINT"KLINGONS HAVE JUST ENTERED THIS QUADRANT":GOSUB1640:GOSUB1330:GOSUB1420:GOTO300
450 PRINT@320,;:INPUT"COURSE(1-9) ";C:IFC=0THEN330ELSEIFC<1ORC>9THEN450ELSEPRINT@253,USING"#.#";C;:IFC=9THENC=1
460 PRINT@320,;:INPUT"WARP FACTOR (0-8)";W:IFW=0THEN330ELSEIFW<0ORW>8THEN460
470 IFD(1)<0ANDW>.2THENPRINTD$(1);" DAMAGED, MAX. SPEED IS WARP 0.2":W=.25ELSEIFW<.9THENW=W*1.25
480 GOSUB490:GOTO560
490 IFST=0THENRETURNELSER=0:FORI=1TO8:IFD(I)>=0THENNEXTELSED(I)=D(I)+1:IFD(I)=0THENR=I:I=8:NEXTELSENEXT
500 IFD(7)<0THENRETURNELSEPRINT@320,;:IFR<>0THENGOSUB520:PRINTD$(R);"- NOW FUNCTIONAL.";:GOTO550
510 IFRND(10)<9THENRETURNELSER=RND(8):ONRND(2)GOTO530,540
520 SOUND240,9:SOUND236,7:RETURN
530 D(R)=D(R)-RND(5):IFD(R)<0THENGOSUB520:PRINTD$(7);" REPORT:":PRINTD$(R);" OUT";:GOTO550ELSERETURN
540 D(R)=D(R)+RND(5):GOSUB520:PRINTD$(7);" REPORT:":PRINTD$(R);" - UPGRADED"
550 GOSUB1640:GOSUB1350:GOTO1500
560 GOSUB1020:N=W*8:F=E/(5*(G/100+1)):IFN<=F THENF=0ELSEN=F:F=1
570 X=U:Y=V:S(X,Y)=0
580 GOSUB650:FORH=1TON:GOSUB660:ONZ GOTO590,600,680
590 NEXT:IFF=0THEN640ELSEH=H-1:GOTO620
600 IFM2<>0THENPRINT"SHIP HAS SUSTAINED DAMAGE":FORI=1TO8:D(I)=D(I)-RND(M2+1)+1:NEXT:IFM2=5THEN630
610 PRINT"NAVIGATIONAL ERROR: ";:GOTO630
620 GOSUB1460:PRINT"ENERGY DEPLETED   - ";
630 X=X-X1:Y=Y-Y1:N=H-1:PRINTUSINGD$(1)+"SHUT DOWN AT SECTOR"+CO$;X;Y:GOSUB1640:GOSUB1350
640 U=INT(X+.5):V=INT(Y+.5):S(U,V)=3:GOSUB1390:IFW<1THEN300ELSEGOSUB1520:GOTO300
650 D=INT(C):X1=C(D,1)+(C(D+1,1)-C(D,1))*(C-D):Y1=C(D,2)+(C(D+1,2)-C(D,2))*(C-D):RETURN
660 X=X+X1:Y=Y+Y1:C=INT(X+.5):D=INT(Y+.5):IFC<1ORC>8ORD<1ORD>8THENZ=3ELSEIFS(C,D)<>0THENZ=2:M2=RND(2)-1ELSEZ=1
670 RETURN
680 FORJ=1TO8:FORI=1TO8:S(I,J)=0:NEXTI,J:U=L*8+U+X1*N:V=M*8+V+Y1*N:L=INT(U/8):M=INT(V/8):U=INT(U-L*8+.5):V=INT(V-M*8+.5):IFU=0THENL=L-1:U=8
690 IFV=0THENM=M-1:V=8
700 GOSUB1520:GOSUB1390:GOTO210
710 O=0:A=40:PRINT@A,Y$;:FORJ=M-1TOM+1:FORZ=1TO3:N(Z)=0:NEXT
720 FORI=L-1TOL+1:IFI<1ORI>8ORJ<1ORJ>8THEN750
730 N(I-L+2)=G(I,J):IFD(8)<0THEN750
740 Z(I,J)=G(I,J)
750 NEXT
760 A=A+32:PRINT@A,USING" :###:###:###:  ";N(1);N(2);N(3);:A=A+32:PRINT@A,Y$;:NEXT:A=A+32:PRINT@A,Z$;:GOTO330
770 IFST=0THENPRINTC$(0);" - ";:GOTO370ELSEIFK=0GOSUB1620:GOTO330
780 GOSUB1500:Z7=1:IFD(8)<0THENPRINTD$(8);" FAILURE-MANUALLY ";
790 PRINT"LOCKED ON. ENERGY=";E
800 INPUT"UNITS TO FIRE";F:IFF<=0THEN330ELSEIFF>E THEN780
810 PRINT@320,;:E=E-F:GOSUB1400:IFD(8)<0THENF=RND(F)
820 R=320:FORI=1TO3:IFK(I,3)<0THEN840
830 H=F/K:J=.98:GOSUB1320:K(I,3)=K(I,3)-H:PRINT" KLINGON - ";:GOSUB1640:IFK(I,3)<0THENX=K(I,1):Y=K(I,2):GOSUB1560ELSEPRINTUSING"AT SECTOR"+CO$+" (### LEFT)";K(I,1);K(I,2);K(I,3);:GOSUB1350
840 NEXT:GOSUB1020:GOTO330
850 IFP>0THEN860ELSEPRINT"ALL PHOTON TORPEDOES EXPENDED.":GOTO330
860 IFCL<>0THENGOSUB1630:GOTO330
870 PRINT@320,B$(10);:INPUT" COURSE (1-9)";C:IFC=0THEN330ELSEIFC<1ORC>9THEN870ELSEIFC=9THENC=1
880 Z7=1:GOSUB1530:GOSUB650:X=U-X1:Y=V-Y1:GOSUB1690:GOTO910
890 PRINT@Q,CHR$(214);:GOTO910
900 PRINT@Q,CHR$(128);
910 GOSUB660:IFZ=3THEN960ELSEIFY=INT(Y)ANDO=1 THENGOSUB1600:PRINT@Q,CHR$(159);:ONS(C,D)+1GOTO900,920,900,890,940,950ELSEONS(C,D)+1GOTO910,920,910,910,940,950
920 FORI=1TO3:IFC=K(I,1)ANDD=K(I,2)THENK(I,3)=-1:X=C:Y=D:R=320:GOSUB1560
930 NEXT:GOTO970
940 PRINT@352,USINGB$(10)+" CAPTURED BY STAR GRAVITYAT SECTOR "+CO$;X;Y:GOTO970
950 GOSUB1580:PRINT@320,"WELL DONE! STAR BASE DESTROYED!":B=B-1:BT=BT-1:M2=4:X=C:Y=D:GOSUB1550:PRINT@352,"DAMAGE HAS THROWN YOU OFF COURSE";:C=RND(8):W=RND(8)/2:GOTO560
960 PRINT@352,B$(10);" MISSED."
970 GOSUB1020:GOTO300
980 IFST=0THENPRINTC$(0);" - ";:GOTO370
990 PRINT@320,;:INPUT"TOTAL UNITS ON SHIELDS";X:IFX<0ORX>E+G THEN990ELSEE=E+G-X:G=X:GOSUB1400:GOSUB1410:PRINT@352,"ENERGY CONSUMPTION IS NOW";5*(G/100+1):PRINT"UNITS PER SECTOR":GOSUB1640:GOTO330
1000 R=0:PRINT@320,"NEXT...HIT '^'  ";
1010 A$=INKEY$:IFA$=""THEN1010ELSEIFASC(A$)=13THEN330ELSEIFA$<>"^"THEN1010ELSER=R+1:IFR>8THEN330ELSEGOSUB1510:GOTO1010
1020 IFK=0THENRETURNELSEIFST=0THENZ7=0:PRINT@384,"STAR-BASE SHIELDS PROTECT THE ";V$;:RETURNELSER=320:FORI=1TO3:H=K(I,3):IFH<1THENNEXTELSEJ=1:GOSUB1320:PRINTUSINGV$+" FROM SECTOR"+CO$;K(I,1);K(I,2);:GOSUB1640:G=G-H:GOSUB1360:NEXT
1030 GOTO1410
1040 PRINT"THE "V$;" IS DEAD IN SPACE.YOU ARE AT THE KLINGONS' MERCY":FORZ=1TO6:GOSUB1350:NEXT
1050 IFK=0THEN1080ELSEGOSUB1020:GOTO1050
1060 PRINT"IT IS STARDATE ";T:GOSUB1640:GOTO1080
1070 PRINT@320,"THE "+V$+" WAS DESTROYED":PRINT"THE ";U$;" WILL BE CONQUERED";
1080 PRINT"THERE ARE STILL";KT;"KLINGON":PRINT"BATTLE-CRUISERS SURVIVING.":END
1090 PRINT"LAST BATTLE-CRUISER DESTROYED !":PRINT"THE ";U$;" IS SAVED !!":PRINT"YOUR EFFICIENCY RATING =";K7/(T-T0)*1000:END
1100 CD=0
1110 A=0:GOSUB380:PRINT@217,"OPT?  ";
1120 GOSUB390:IFA>6THEN1120
1130 IFA$=CHR$(13)THENONA+1GOTO330,1290,1150,1160,1170,1280,1140ELSEPRINT@249,B$(A*2+17);:PRINT@281,B$(A*2+18);:GOTO1120ELSE1120
1140 A=0:FORJ=1TO8:FORI=1TO8:CC=INT(Z(I,J)/100):O=INT((Z(I,J)-CC*100)/10):POKE1064+A+(I-1)*2,CC+112:POKE1065+A+(I-1)*2,Z(I,J)-CC*100-O*10+112-64*O:NEXT:A=A+32:NEXT:O=0:GOTO1110
1150 O=0:FORA=40TO266STEP32:PRINT@A,Y$;:NEXT:PRINT@41,"STATUS REPORT:";:PRINT@105,"# ";:PRINTUSING"KLINGONS =##";KT;:PRINT@137,"# ";:PRINTUSING"STARDATES=##";T0+TT-T;:PRINT@169,"# ";:PRINTUSING"STARBASES=##";BT;:GOTO1110
1160 IFK=0THENGOSUB1620:GOTO1110ELSECC=U:A=V:FORF=1TO3:IFK(F,3)<0THEN1270ELSEW=K(F,1):X=K(F,2):GOTO1180
1170 PRINT@384,"ENTER START AND END CO-ORDINATES":PRINT@320,;:INPUT"(X,Y,X,Y)";CC,A,W,X:IFCC-W=0ANDA-X=0THENCC=0:X=0:A=0:GOTO1260
1180 PRINT@352,;:X=X-A:A=CC-W:IFX<0THEN1220ELSEIFA<0THEN1240ELSEIFX>0THEN1190ELSEIFA=0THEN1230
1190 CC=5
1200 IFABS(A)<=ABS(X)THEN1210ELSECC=CC+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO1260
1210 CC=CC+ABS(A)/ABS(X):GOTO1260
1220 IFA>0THENCC=7:GOTO1250ELSEIFX=0THEN1240
1230 CC=1:GOTO1200
1240 CC=3
1250 IFABS(A)>=ABS(X)THENCC=CC+ABS(X)/ABS(A)ELSECC=CC+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
1260 PRINT@352,"COURSE =";CC;:CD=INT(SQR(X^2+A^2)):PRINT", DISTANCE =";CD:GOTO1110
1270 NEXT:GOTO1110
1280 IFD(5)<0THEN370ELSEIFP<1ORCL=1THEN850ELSEIFCD<>0THENC=CC:GOTO880ELSE1300
1290 IFCD<>0THENC=CC:W=CD:GOTO470
1300 PRINT"NO DATA AVAILABLE FOR GUIDANCE  SYSTEM":GOTO1110
1310 X=RND(8):Y=RND(8):IFS(X,Y)<>0THEN1310ELSERETURN
1320 H=(H*2/SQR((K(I,1)-U)^2+(K(I,2)-V)^2))*(J*RND(0)+.2):PRINT@R,USING"### UNIT HIT ON ";H;:GOSUB1640:R=R+32:RETURN
1330 IFK<>3THENIFRND(10)>7THENCL=1
1340 FORI=1TO3:K(I,3)=-1:NEXT:FORI=1TOK:GOSUB1310:S(X,Y)=CL+1:K(I,1)=X:K(I,2)=Y:K(I,3)=200:NEXT:RETURN
1350 FORZZ=1E3 TO1.8E3:NEXT:RETURN
1360 FORZZ=H*.25+1TO1STEP-1:SCREEN0,1:PLAY"T255O1;12":SOUND255,1:SCREEN0,0:NEXT:RETURN
1370 PRINT@0,A$;:FORX=1TO8:PRINTTAB(7)B$:NEXT:PRINTL$(1);
1380 PRINT@33,"DATE";:PRINT@97,"QUAD";:PRINT@90,"FUEL";:PRINT@161,"SECT";:PRINT@154,"SHLD";:PRINT@225,"TORP";:GOTO1500
1390 E=E-((N*5)*(G/100+1))
1400 PRINT@122,USING"####";E;:GOSUB1420
1410 PRINT@186,USING"####";G;:GOSUB1500:IFG<0THEN1070ELSERETURN
1420 FORZ=U-1TOU+1:IFZ<1ORZ>8THENNEXTELSEIFS(Z,V)=5THENZ=U+1:GOSUB1610:NEXT:GOTO1470ELSENEXT
1430 IFK=0THEN1450
1440 IFST<>3THENST=3:GOTO1480ELSE1470
1450 IFE>300THENST=1:GOTO1470ELSEIFE>5*(G/100+1)THENIFST<>2THENST=2:GOTO1480ELSE1470
1460 PRINT@320,;:IFG=0THEN1040ELSEPRINT"YOU HAVE";E;"UNITS OF ENERGY. SHIELDS ARE TAKING";G;"UNITS":GOSUB1350:IFD(6)<0THENPRINT"BUT, ";D$(6);" IS OUT.SITUATION IRRECOVERABLE.":GOTO1040ELSE1440
1470 PRINT@57,C$(ST);:GOTO1500
1480 FORFL=1TO5:PRINT@57,C$(ST);:FORZZ=1TO50:NEXT:PRINT@57,C$(1);:FORZZ=100TO50STEP-4:SOUNDZZ,1:NEXTZZ:IFST=0THENSOUND200,1ELSESOUND1,1
1490 NEXTFL:GOTO1470
1500 PRINT@320,;:RETURN
1510 GOSUB1640::PRINT@320,D$(R);TAB(14);D(R);:GOTO1500
1520 T=T+1:PRINT@64,T;:GOSUB1500:IFT>T0+TT THEN1060ELSERETURN
1530 IFST=0THENP=10ELSEP=P-1
1540 PRINT@257,P;:GOTO1500
1550 GOSUB1580:S(X,Y)=0:G(L,M)=K*100+B*10+S:RETURN
1560 K=K-1:GOSUB1550:GOSUB1420:PRINT@R,USING"KLINGON AT"+CO$+" DESTROYED";X;Y:R=R+32:GOSUB1640:KT=KT-1:IFKT=0THEN1090ELSEGOSUB1350:IFK=0THENCL=0:RETURNELSERETURN
1570 ST=0:FORZ=1TO8:D(Z)=0:NEXT:E=5000:PRINT@121,E;:GOSUB1530:G=0:GOTO1410
1580 IFO<>1THENRETURNELSEGOSUB1600:FORZ=1TO40:ZZ=RND(10):PLAY"T255O1=ZZ;":PRINT@Q+RND(2)-2,CHR$(128+RND(62));:NEXT
1590 PRINT@Q-1,CHR$(128);CHR$(128);:GOTO1500
1600 Q=7.5+X*2+32*Y:RETURN
1610 IFST=0THENRETURNELSEPRINT@320,"SHIELDS DROPPED FOR DOCKING":GOSUB1640:GOSUB1570:GOTO1480
1620 PRINT"SENSORS DETECT NO KLINGONS IN   THIS QUADRANT":RETURN
1630 PRINTD$(5);" WILL BE OF NO USE -USE ";B$(8):GOTO1640
1640 ZZ=PEEK(137):PRINTSTRING$(1535-(PEEK(136)*256+PEEK(137)),32);:POKE137,ZZ:RETURN
1650 SC=SC+1:IFSC>3THENSC=1
1660 PRINT@288,L$(SC);:IFST>1THEN1680ELSEIFSO=0THENIFRND(20)>19THENSO=16ELSERETURN
1670 SO=SO-2:IFSO<2THENSO=0:RETURNELSEPLAY"V=SO;L32T2O5G":RETURN
1680 PLAY"L32T2V8O1A":RETURN
1690 PLAY"V27":FORZO=5TO3STEP-1:FORZN=12TO1STEP-1:PLAY"L255O=ZO;=ZN;":NEXTZN,ZO:RETURN
1700 O=RND(5):N=RND(12):FORX=10TO30STEP10:PLAY"V=X;P255L255O=O;=N;":NEXT:RETURN
