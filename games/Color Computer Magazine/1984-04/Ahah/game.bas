
100 REM * AHAH * TRS-80 COLOR COMPUTERS
110 REM * COLOR BASIC - EXTENDED COLOR BASIC - MC-10 BASIC *
120 REM * SORCERER'S PUZZLES #6 * RICHARD RAMELLA
130 CLEAR 100
140 Q=1294
150 REM * FOR MC-10 MAKE LINE ABOVE 140 Q=16654
160 CLS2
170 Z$="NESSFFTTO"
180 FOR A=65 TO 90
190 IF A>64 AND A<69 OR A=71 OR A=74 OR A>79 AND A<84 OR A=85 THEN B$=B$+CHR$(A): A$=A$+CHR$(32): GOTO 210
200 A$=A$+CHR$(A): B$=B$+CHR$(32)
210 NEXT A
220 PRINT @ 35,A$;
230 FOR Z=67 TO 92
240 PRINT @ Z,CHR$(255);
250 NEXT Z
260 PRINT @ 99,B$;
270 PRINT @ 160,"IT'S NOW TIME TO FIDDLE" 
280 PRINT @ 192,"AROUND WITH A RIDDLE"
290 PRINT @ 224,"IN AN ALPHABETICAL SHOW," 
300 PRINT @ 256,"FOR WHAT'S UP ABOVE"
310 PRINT @ 288,"DOESN'T SEEM TO HAVE LOVE"
320 PRINT @ 320,"FOR WHAT IS SITTING BELOW."
330 PRINT @ 352,"IT'S NEITHER CODE NOR EQUATION."
340 PRINT @ 384,"QUESTION: WHY THE SEPARATION?"
350 PRINT @ 480,"HO! HO! HO!";
360 IF INKEY$="" THEN 360
370 CLS3
380 PRINT@ 64,"WONDER, PONDER,"
390 PRINT@ 96,"PONDER, WONDER..."
400 PRINT @ 128,"HAVE A CASE"
410 PRINT @ 160,"OF VEXING WEARIES."
420 PRINT @ 192,"BLUNDER, BLUNDER,"
430 PRINT @ 224,"MIND ASUNDER..."
440 PRINT @ 256,"WHAT COMES NEXT"
450 PRINT @ 288,"IN THIS NICE SERIES?"
460 PRINT @ 356,"";
470 FOR A=9 TO 1 STEP -1
480 PRINT MID$(Z$,A,1);" ";
490 NEXT A
500 PRINT @ 480,"TYPE THE CHARACTER:";
510 X$=INKEY$
520 IF X$="" THEN 510
530 IF ASC(X$)=PEEK(Q) THEN PRINT @ 480,"CORRECT. BUT WHY?": END
540 PRINT @ 480,"NO, THAT ISN'T IT."
550 END
