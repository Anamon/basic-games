
100 REM * PARTNERS * TRS-80 COLOR COMPUTERS
110 REM * COLOR BASIC - EXTENDED COLOR BASIC - MC-10 BASIC *
120 REM * SORCERER'S PUZZLES #6 * RICHARD RAMELLA
130 CLS
140 CLEAR 100
150 S$="SUNFLOWER"
160 P$="POKE"
170 S=50
180 P=50
190 PRINT S$" HAS"S
200 PRINT P$" HAS"P
210 FOR A=1 TO 2
220 PRINT
230 PRINT S$" WRITES:"A
240 PRINT
250 PRINT "WHAT DOES "P$" WRITE";
260 INPUT B 
270 IF B<>1 AND B<>2 THEN PRINT "ONLY ANSWERS ACCEPTED: 1 OR 2": GOTO 250
280 CLS
290 IF A=2 AND B=1 THEN PRINT S$" +20. "P$" -10.": S=S+20: P=P-10
300 IF A=1 AND B=2 THEN PRINT S$" -10. "P$" +20.": S=S-10: P=P+20
310 IF A=1 AND B=1 THEN PRINT "BOTH LOSE 10.": S=S-10: P=P-10
320 IF A=2 AND B=2 THEN PRINT "BOTH LOSE 5.": S=S-5: P=P-5
330 PRINT
340 PRINT S$" HAS"S
350 PRINT P$" HAS"P
360 IF P>99 AND S>99 THEN PRINT "VERY GOOD. THE PARTNERS WIN.": END
370 IF P<1 THEN PRINT P$" IS BROKE."
380 IF S<1 THEN PRINT S$" IS BROKE."
390 IF P<1 OR S<1 THEN PRINT "BOTH LOSE ALL.": END
400 NEXT A
410 GOTO210
420 END
