
100 REM * THE COUNTERFEIT DOUBLOON * TRS-80 COLOR COMPUTERS
110 REM * COLOR BASIC - EXTENDED COLOR BASIC - MC-10 BASIC *
120 REM *SORCERER'S PUZZLES #6 * RICHARD RAMELLA
130 CLEAR200: CF=RND(9): CLS0: S=104
140 Z$="": AB$="": WL$="": WR$="": WR=0: WL=0: C$="": B$=""
150 P$=CHR$(251): SL=386: SR=411: L$=CHR$(8): R$=CHR$(9)
160 FOR A=1 TO 32: X$=X$+CHR$(128): NEXT A: N$=LEFT$(X$,2)
170 T$=CHR$(128): FOR A=1 TO 9: A$(A)=CHR$(147)+CHR$(147): A(A)=2: NEXT A
180 A(CF)=1: FOR A=1 TO 3: B$=B$+CHR$(175): NEXT A
190 FOR A=1 TO 12: AB$=AB$+CHR$(128): C$=C$+CHR$(207): NEXT A
200 FOR A=366 TO 462 STEP 32: PRINT @ A,B$;: NEXT A: PRINT @ 418,C$;
210 PRINT @ 433,C$;: GOSUB 680: C=1: H=67: PRINT @ H,P$;
220 A$=INKEY$
230 IF A$=L$ OR A$=R$ THEN PRINT @ H,T$;
240 IF A$=R$ THEN H=H+3: C=C+1
250 IF A$=L$ THEN H=H-3: C=C-1
260 IF H<67 THEN H=91: C=9
270 IF H>91 THEN H=67: C=1
280 PRINT @ H,P$;
290 IF A$="W" AND A$(C)<>"" THEN GOSUB 320
300 IF A$="T" THEN PRINT @ H,T$;: GOSUB 450
310 GOTO 220
320 GOSUB 670
330 PRINT @ 330, "WHICH SIDE?";
340 A$=INKEY$
350 IF A$="" THEN 340
360 IF A$<>"L" AND A$<>"R" THEN 320 
370 PRINT @ H-64,N$;
380 IF A$="L" THEN K=SL: GOTO 400
390 K=SR
400 PRINT @ K,A$(C);: A$(C)=""
410 IF A$="L" THEN SL=SL-32: WL=WL+A(C): WL$=WL$+RIGHT$(STR$(C),1): GOTO 430
420 SR=SR-32: WR=WR+A(C): WR$=WR$+RIGHT$(STR$(C),1)
430 GOSUB 670
440 GOTO 220
450 IF WL>WR THEN XZ=326: V$="L"
460 IF WR>WL THEN XZ=341: V$="R"
470 IF WR=WL THEN XZ=303: V$="E"
480 FOR G=1 TO 10
490 PRINT @ XZ,CHR$(255);
500 SOUND 100,1
510 PRINT @ XZ,T$;T$;
520 SOUND 200,1
530 NEXT G
540 S=S+32
550 Q=Q+1
560 PRINT @ S,Q") ";WL$"-"WR$;" ";V$;
570 IF Q=1 THEN FOR M=130 TO 386 STEP 32: PRINT @ M,T$;T$;: PRINT @ M+25,T$;T$;:NEXT: GOSUB 680: GOTO 140
580 IF Q=2 THEN PRINT @ 232,"PHONEY? (1-9)";: G$=INKEY$: IF G$="" THEN 580
590 LL=VAL(G$)
600 PRINT LL;
610 IF Q=1 THEN 140
620 IF A(LL)=1 THEN PRINT @ 264,"RIGHT!";: SOUND RND(50)+200,1: GOTO 620
630 PRINT @ 264,"NO, IT WAS COIN"CF;
640 SOUND 204,2: SOUND 204,2: SOUND193,2
650 SOUND 210,3: SOUND 204,4: SOUND 193,6
660 FOR T=1 TO 20: NEXT: GOTO 640
670 FOR J=330 TO 343: PRINT @ J,T$;: NEXT J: RETURN
680 B=1: FOR A=3 TO 29 STEP 3: PRINT @ A,A$(B);: PRINT @ A+31,B;
690 B=B+1: NEXT: RETURN: END
