100 RANDOMIZE
110 CALL CLEAR
120 FOR I=5 TO 8
130 CALL COLOR(I,16,1)
140 NEXT I
150 CALL CLEAR
160 PRINT "  A S T R O S T O R M !!!"
170 PRINT
180 PRINT
190 PRINT "POWER SHIP WITH < & > KEYS"
200 FOR I=1 TO 9
210 PRINT
220 NEXT I
230 FOR I=14 TO 3 STEP -1
240 FOR J=1 TO 20
250 NEXT J
260 CALL SCREEN(I)
270 NEXT I
280 FOR I=1 TO 400
290 NEXT I
300 S=3
310 PT=0
320 RSHIP=10
330 CSHIP=1
340 SHIP=62
350 COL=2
360 RLSHIP=RSHIP
370 CLSHIP=CSHIP
380 CALL CLEAR
390 PRINT "WHAT ASTEROID DEPTH (1-10)";
400 INPUT X
410 IF (X>10)+(X<1)THEN 380
420 PRINT
430 PRINT "WHAT DIFFICULTY LEVEL (1-10)"
440 INPUT DCULT
450 IF (DCULT>10)+(DCULT<1)THEN 380
460 CALL CLEAR
470 CALL SCREEN(S)
480 CALL COLOR(2,16,1)
490 REM  VARY LIMIT OF LOOP IN THE NEXT LINE IF THE GAME IS TOO HARD OR TOO EASY
500 FOR LOOP=1 TO 1000
510 FOR I=1 TO INT(RND*X)+1
520 COL=INT(RND*30)+2
530 CALL HCHAR(23,COL,42)
540 NEXT I
550 PRINT
560 CALL GCHAR(RSHIP+DCULT,CSHIP,LOC)
570 CALL HCHAR(RLSHIP+DCULT-1,CLSHIP,32)
580 IF LOC=42 THEN 1190
590 CALL SCREEN(S)
600 CALL HCHAR(RSHIP+DCULT,CSHIP,SHIP)
610 CLSHIP=CSHIP
620 RLSHIP=RSHIP
630 CALL KEY(3,A,STATUS)
640 IF A<>ASC(".")THEN 730
650 CALL SOUND(-700,-5,3)
660 PT=PT+5*DCULT*X
670 CSHIP=CSHIP+1
680 IF CSHIP<>32 THEN 720
690 CSHIP=1
700 S=S+1
710 IF S=15 THEN 1030
720 SHIP=62
730 IF A<>ASC(",")THEN 820
740 CALL SOUND(-700,-6,4)
750 SHIP=60
760 PT=PT-8*DCULT*X
770 CSHIP=CSHIP-1
780 IF CSHIP>0 THEN 810
790 CSHIP=32
800 S=S-SGN(S-3)
810 SHIP=60
820 REM
830 NEXT LOOP
840 CALL CLEAR
850 PRINT " YOU DID NOT COMPLETE YOUR"
860 PRINT
870 PRINT "MISSION. THE SOLARIAN"
880 PRINT
890 PRINT "GOVERNMENT HAS FINED YOU"
900 PRINT
910 PRINT PT;"GALACTIC CREDITS."
920 PRINT
930 PRINT
940 PRINT
950 PRINT "  BUT, THEY ALSO HAVE "
960 PRINT
970 PRINT "ANOTHER MISSION FOR YOU!!!"
980 PRINT
990 PRINT
1000 INPUT "ARE YOU GAME (Y OR N)?":G$
1010 IF G$="Y" THEN 300
1020 GOTO 1500
1030 REM  YOU WIN!!
1040 CALL CLEAR
1050 PRINT "  YOU COMPLETED YOUR "
1060 PRINT
1070 PRINT "MISSION. THE SOLARIAN MINI-"
1080 PRINT
1090 PRINT "STER HAS AWARDED YOU ";PT
1100 PRINT
1110 PRINT "SOLARIAN CREDITS FOR YOUR "
1120 PRINT
1130 PRINT "SERVICES AND WISHES YOU"
1140 PRINT
1150 PRINT "TO MAKE ANOTHER DELIVERY!"
1160 PRINT
1170 PRINT
1180 GOTO 980
1190 CALL SCREEN(12)
1200 FOR I=1 TO 50
1210 NEXT I
1220 CALL SCREEN(9)
1230 FOR VOL=24 TO 1 STEP 4
1240 CALL SOUND(200,-7,VOL)
1250 NEXT VOL
1260 FOR VOL=1 TO 24 STEP 4
1270 CALL SOUND(200,-7,VOL)
1280 NEXT VOL
1290 CALL SCREEN(12)
1300 FOR I=1 TO 10
1310 NEXT I
1320 CALL SCREEN(8)
1330 CALL CLEAR
1340 CALL SCREEN(6)
1350 PRINT "    TOO BAD! THE SOLARIAN"
1360 PRINT " GOVERNMENT HAS SENT YOUR"
1370 PRINT " FAMILY ";PT;" GALACTIC"
1380 PRINT " CREDITS."
1390 FOR I=1 TO 5
1400 PRINT
1410 NEXT I
1420 PRINT "   HIT -P- TO PLAY AGAIN"
1430 PRINT " OR -S- TO STOP."
1440 FOR I=1 TO 5
1450 PRINT
1460 NEXT I
1470 CALL KEY(3,K,ST)
1480 IF ST=0 THEN 1470
1490 IF (K=ASC("C"))+(K=ASC("P"))THEN 300
1500 END
