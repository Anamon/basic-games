100 REM TI EXTENDED BASIC
110 CALL CLEAR
120 DISPLAY AT(4,4):"** B A L L O O N S **" :: DISPLAY AT(7,4):"USE THE ARROW KEYS TO" :: DISPLAY AT(9,4):"MOVE YOUR WHITE SPEAR."
130 DISPLAY AT(12,4):"TRY TO POP ALL THE" :: DISPLAY AT(14,4):"BALLOONS IN AS LITTLE" :: DISPLAY AT(16,4):"TIME AS POSSIBLE."
140 CALL CHAR(96,"3C7EFFFFFFFF7E3C8142183C3C184281"):: CALL CHAR(104,"101038FE38101"):: TS=9999
150 DISPLAY AT(23,4):"PRESS <ENTER> TO START."
160 CALL KEY(0,K,S):: IF K<>13 THEN 160
170 CALL CLEAR :: DISPLAY AT(1,1):"TIME:" :: T,B,X,Y=0
180 FOR I=2 TO 26 STEP 4
190 RANDOMIZE :: R=INT(RND*12)+3 :: IF R=8 THEN 190
200 CALL SPRITE(#I,96,R,185,I*8+1,-INT(RND*6+1),0):: NEXT I :: CALL SPRITE(#1,104,16,INT(RND*180+1),233):: CALL SOUND(150,1497,2)
210 CALL KEY(1,K,S):: T=T+1 :: DISPLAY AT(1,7):T :: CALL COINC(ALL,C):: IF C=-1 THEN 290
220 IF (K+1<1)+(K>5)THEN 210
230 ON K+1 GOTO 250,210,260,270,210,240
240 X=-5 :: Y=0 :: GOTO 280
250 X=5 :: Y=0 :: GOTO 280
260 X=0 :: Y=-5 :: GOTO 280
270 X=0 :: Y=5
280 CALL MOTION(#1,X,Y):: CALL COINC(ALL,C):: IF C=0 THEN 210
290 CALL SOUND(50,-5,0)
300 FOR I=26 TO 2 STEP -4 :: CALL DISTANCE(#1,#I,D)::IF D<425 THEN 320
310 NEXT I :: I=2
320 CALL PATTERN(#I,97):: CALL DELSPRITE(#I):: B=B+1 :: IF B<7 THEN 210
330 CALL DELSPRITE(ALL):: IF T<TS THEN TS=T
340 DISPLAY AT(18,4):"FASTEST TIME =";TS :: FOR B=1 TO 15 :: CALL SOUND(100,INT(RND*1000)+500,2):: NEXT B
350 DISPLAY AT(22,4):"TRY AGAIN? (Y/N)"
360 CALL KEY(0,K,S):: IF K=89 THEN 170
370 IF K<>78 THEN 360
380 CALL CLEAR :: END
