10 REM TI EXTENDED BASIC
100 DIM KOLOR(6)
110 RANDOMIZE
120 GOSUB 630
130 REM 108-DEFINE DIAMOND SPRITE CHAR,128-136 ARE THE PADDLES
140 CALL CHAR(108,"10387CFE7C381000000000000000000000000000000000000000000000000000")
150 CALL CHAR(128,"FFFFFFFF0000FFFFFFFF0000FFFFFFFFFFFFFFFF0000FFFFFFFF0000FFFFFFFF")
160 CALL CHAR(132,"000000000000FFFFFFFF0000FFFFFFFF000000000000FFFFFFFF0000FFFFFFFF")
170 SCR=0 :: SK=0 :: CH=10 :: S=0 :: CALL CLEAR :: CALL SCREEN(16):: DISPLAY AT(4,9):"D I A M O N D"
180 FOR ROW=3 TO 6
190 CALL HCHAR(ROW+2,6,32,20)
200 DISPLAY AT(ROW+3,6):"```  ```   ``  ```" :: DISPLAY AT(ROW+4,6):"h  h h  h h  h h  h"
210 DISPLAY AT(ROW+5,6):"p  p p  p p  p p  p" :: DISPLAY AT(ROW+6,6):"x  x xxx  x  x xxx"
220 DISPLAY AT(ROW+7,6):"h  h h  h h  h h" :: DISPLAY AT(ROW+8,6):"```  `  `  ``  `"
230 NEXT ROW
240 DISPLAY AT(18,4):"SKILL LEVEL (1,2) ?" :: ACCEPT AT(18,24)BEEP VALIDATE("12")SIZE(1):SK$ :: SK=VAL(SK$)
250 DROP=25 :: IF SK=2 THEN DROP=40 :: REM CHANGE DROP RATE TO CHANGE DIFFICULTY
260 DISPLAY AT(21,2):"DROP WITH ANGLE (Y/N) ?" :: ACCEPT AT(21,26)BEEP VALIDATE("YN")SIZE(1):ANG$
270 IF ANG$="N" THEN ANG=0 :: GOTO 290
280 ANG=1
290 CALL CLEAR :: SCR=SCR+1
300 DISPLAY AT(1,2):"CHANCES:";CH :: DISPLAY AT(1,15):"SCORE:";S
310 ROW=3 :: FOR I=96 TO 120 STEP 8
320 CALL HCHAR(ROW,3,I,28):: ROW=ROW+1 :: NEXT I
330 CALL HCHAR(24,1,30,32)
340 CALL MAGNIFY(4):: CALL SPRITE(#1,128,5,150,115,0,H)
350 KHAR=108 :: ROW=41 :: FOR J=6 TO 3 STEP-1
360 A$="" :: FOR I=3 TO 30 :: A$=A$&CHR$(I):: NEXT I :: N=28
370 IF N=0 THEN 530
380 R=INT(LEN(A$)*RND+1):: P$=SEG$(A$,R,1):: X=ASC(P$):: N=N-1 :: IF N=0 THEN 400
390 A$=SEG$(A$,1,R-1)&SEG$(A$,R+1,LEN(A$)-R)
400 B=INT(RND*61*ANG)-30*ANG
410 CALL HCHAR(J,X,32):: CALL SPRITE(#2,KHAR,KOLOR(J),ROW,8*(X-1)-2,DROP,B)
420 CALL KEY(0,K,ST):: IF ST=0 THEN CALL JOYST(1,H,V):: H=SGN(H)ELSE H=(K=83)-(K=68)
430 CALL MOTION(#1,0,60*H)
450 CALL COINC(ALL,C):: IF C THEN 510
460 CALL POSITION(#2,DROW,DCOL):: IF DROW<155 THEN 420
470 CALL POSITION(#1,PROW,PCOL)::IF (DCOL-PCOL<16)*(DCOL-PCOL>-8)THEN 510
480 CALL DELSPRITE(#2):: CALL MOTION(#1,0,0):: CH=CH-1 :: CALL SCREEN(11):: FOR F=0 TO 25 STEP 5
490 CALL SOUND(-200,-5,F):: NEXT F :: CALL SCREEN(16):: IF CH=0 THEN GOTO 570
500 GOTO 520
510 CALL DELSPRITE(#2):: CALL MOTION(#1,0,0):: S=S+(60/J)*SK*SCR+(60/J)*SK*SCR*INT(ABS(B)/15)
520 DISPLAY AT(1,2):"CHANCES:";CH :: DISPLAY AT(1,15):"SCORE:";S :: GOTO 370
530 K=K+4 :: ROW=ROW-8 :: M=128 :: IF J<6 THEN M=132
540 FOR F=0 TO 30 STEP 6 :: CALL SOUND(-300,1500,F):: NEXT F
550 CALL SPRITE(#1,M,5,150,115,0,H)
560 NEXT J :: FOR G=600 TO 1400 STEP 100 :: CALL SOUND(100,G,1):: NEXT G :: DROP=DROP+3 :: GOTO 290
570 CALL SCREEN(14):: IF S>HS THEN HS=S
580 CALL DELSPRITE(ALL):: CALL CLEAR :: DISPLAY AT(8,5):"YOUR SCORE: ";S :: DISPLAY AT(11,5):"HIGH SCORE: ";HS
590 DISPLAY AT(16,5):"PLAY AGAIN (Y/N)? " :: ACCEPT AT(16,24)BEEP VALIDATE("NY")SIZE(1):REPLY$
600 IF REPLY$="N" THEN 620
610 GOTO 170
620 STOP
630 REM DEFINE SMALL DIAMONDS AND COLORS
640 FOR I=96 TO 120 STEP 8
650 CALL CHAR(I,"10387CFE7C381000")::NEXT I
660 CALL COLOR(11,11,1)
670 CALL COLOR(9,3,1)
680 CALL COLOR(10,10,1)
690 CALL COLOR(12,14,1)
700 FOR J=3 TO 6 :: READ KOLOR(J):: NEXT J
710 DATA 3,10,11,14
720 RETURN
