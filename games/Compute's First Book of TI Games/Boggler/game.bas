100 REM BOOGLER
110 REM
120 DIM DELTAX(8),DELTAY(8),ROW(1),COL(1),PIECE(1),COUNT(1)
130 GOSUB 980
140 REM MAIN LOOP
150 PLR=1-PLR
160 CALL HCHAR(24,1,128+PLR*8,32)
170 CALL GCHAR(ROW(PLR),COL(PLR),PK)
180 CALL HCHAR(ROW(PLR),COL(PLR),PIECE(PLR))
190 REM
200 CALL KEY(3,WHICH,STATUS)
210 IF (WHICH=13)*(PK=144)THEN 340
220 CALL HCHAR(ROW(PLR),COL(PLR),32)
230 IF STATUS=0 THEN 180
240 CALL HCHAR(ROW(PLR),COL(PLR),PK)
250 TEMPROW=ROW(PLR)
260 TEMPCOL=COL(PLR)
270 ROW(PLR)=ROW(PLR)+(WHICH=69)-(WHICH=88)
280 COL(PLR)=COL(PLR)+(WHICH=83)-(WHICH=68)
290 CALL GCHAR(ROW(PLR),COL(PLR),PK)
300 IF PK<>152 THEN 170
310 ROW(PLR)=TEMPROW
320 COL(PLR)=TEMPCOL
330 GOTO 170
340 CALL HCHAR(ROW(PLR),COL(PLR),PIECE(PLR))
350 REM CHECK FOR FIVE-IN-A-ROW
360 REM
370 REM STEP 1. FIND BOTTOM
380 FOR I=1 TO 8
390 TEMPROW=ROW(PLR)
400 TEMPROW=COL(PLR)
410 DX=DELTAX(I)
420 DY=DELTAY(I)
430 N=0
440 TEMPCOL=TEMPCOL+DX
450 TEMPROW=TEMPROW+DY
460 CALL GCHAR(TEMPROW,TEMPCOL,PK)
470 IF PK<>PIECE(PLR)THEN 500
480 N=N+1
490 GOTO 440
500 N=0
510 TEMPCOL=TEMPCOL-DX
520 TEMPROW=TEMPROW-DY
530 CALL GCHAR(TEMPROW,TEMPCOL,PK)
540 IF PK<>PIECE(PLR)THEN 570
550 N=N+1
560 GOTO 510
570 IF N=5 THEN 800
580 NEXT I
590 REM NO FIVE-IN-A-ROW
600 REM CHECK FOR CAPTURE
610 FOR I=1 TO 8
620 TEMPROW=ROW(PLR)
630 TEMPCOL=COL(PLR)
640 DX=DELTAX(I)
650 DY=DELTAY(I)
660 CALL GCHAR(TEMPROW+DY,TEMPCOL+DX,CK1)
670 CALL GCHAR(TEMPROW+DY*2,TEMPCOL+DX*2,CK2)
680 CALL GCHAR(TEMPROW+DY*3,TEMPCOL+DX*3,CK3)
690 IF (CK1<>PIECE(1-PLR))+(CK2<>PIECE(1-PLR))+(CK3<>PIECE(PLR))THEN 770
700 COUNT(PLR)=COUNT(PLR)+2
710 IF COUNT(PLR)>10 THEN 720 ELSE 730
720 COUNT(PLR)=10
730 CALL VCHAR(5,28*PLR+2,PIECE(1-PLR),COUNT(PLR))
740 CALL HCHAR(TEMPROW+DY,TEMPCOL+DX,144)
750 CALL HCHAR(TEMPROW+DY*2,TEMPCOL+DX*2,144)
760 IF COUNT(PLR)=10 THEN 800
770 NEXT I
780 GOTO 150
790 REM SOMEBODY WON!
800 FOR I=1 TO 20
810 CALL SCREEN(RND*15+1)
820 NEXT I
830 FOR I=1 TO 10
840 CALL COLOR(14-PLR,RND*15+1,RND*15+1)
850 CALL SOUND(-1,I*100+100,I*3)
860 NEXT I
870 CALL COLOR(14-PLR,1,1)
880 CALL CLEAR
890 PRINT "PLAYER ";
900 IF PLR THEN 930
910 PRINT "TWO";
920 GOTO 940
930 PRINT "ONE";
940 PRINT " WINS!"
950 END
960 REM INITIALIZATION
970 REM
980 CALL SCREEN(16)
990 FOR I=1 TO 8
1000 READ A,B
1010 DELTAX(I)=A
1020 DELTAY(I)=B
1030 NEXT I
1040 DATA 0,1,1,0,1,1,-1,-1,-1,0,0,-1,-1,1,1,-1
1050 CALL CLEAR
1060 CALL COLOR(13,9,1)
1070 CALL COLOR(14,6,1)
1080 CALL COLOR(15,14,1)
1090 CALL COLOR(16,12,1)
1100 CALL CHAR(152,"FEFEFEFEFEFEFE00")
1110 CALL CHAR(144,"007E666666667E00")
1120 CALL CHAR(129,"007E7E7E7E7E7E00")
1130 CALL CHAR(137,"007E7E7E7E7E7E00")
1140 CALL CHAR(128,"FFFFFFFFFFFFFFFF")
1150 CALL CHAR(136,"FFFFFFFFFFFFFFFF")
1160 CALL HCHAR(3,3,152,26)
1170 CALL HCHAR(22,3,152,26)
1180 CALL VCHAR(3,3,152,20)
1190 CALL VCHAR(3,29,152,20)
1200 FOR I=4 TO 21
1210 CALL HCHAR(I,4,144,25)
1220 NEXT I
1230 PIECE(0)=129
1240 PIECE(1)=137
1250 COUNT(0)=0
1260 COUNT(1)=0
1270 ROW(0)=12
1280 COL(0)=16
1290 ROW(1)=11
1300 COL(1)=16
1310 CALL HCHAR(ROW(0),COL(0),PIECE(0))
1320 RETURN
