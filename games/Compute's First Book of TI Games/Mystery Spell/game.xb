100 REM TI EXTENDED BASIC
110 REM MYSTERY SPELL
120 DIM A$(26),B$(20)
122 ON ERROR 140
125 CALL INIT :: CALL LOAD(-31878,13)
130 REM **INITIALIZATION AND INTRODUCTION**
140 DISPLAY AT(12,5)ERASE ALL:"ONE MOMENT PLEASE..." :: GOTO 780
150 DISPLAY AT(7,1)ERASE ALL BEEP:"PRESS   FOR": : :"  1  =  INSTRUCTIONS": :"  2  =  MYSTERY SPELL": :"  3  =  FINISH MYSTERY SPELL"
160 DISPLAY AT(23,3):"PLEASE ENGAGE ALPHA LOCK" :: CALL KEY(0,K,S):: IF S=0 OR(K<49 OR K>51)THEN 160 :: ON K-48 GOTO 980,190,170
170 DISPLAY AT(12,5)ERASE ALL BEEP:"THANKS FOR PLAYING," :: DISPLAY AT(14,14-LEN(L$)/2):L$ :: STOP
190 DISPLAY AT(7,1)ERASE ALL BEEP:"CHOOSE A WORD LIST:": : :"  A  =  PRESELECTED WORDS": :"  B  =  CREATE YOUR OWN"
200 CALL KEY(0,K,S):: IF S=0 OR(K<65 OR K>66)THEN 200 :: IF K=66 THEN 220
210 PSW=1 :: GOTO 230
220 PSW=0 :: GOTO 1620
230 CALL CLEAR :: RESTORE 940 :: GOTO 930
240 CALL SPRITE(#2,120,2,78,121,0,0):: CALL MAGNIFY(3):: CALL SPRITE(#4,136,16,8,128,0,1):: CALL SPRITE(#3,140,2,8,128,0,-2)
250 DISPLAY AT(5,9):"MYSTERY SPELL" :: T=200 :: GOSUB 1050 :: IF PSW=1 THEN GOTO 1840
260 DISPLAY AT(19,1)BEEP:" WHAT IS YOUR NAME, PLEASE?" :: DISPLAY AT(23,1):"TYPE NAME, THEN PRESS ENTER"
270 ACCEPT AT(5,9)SIZE(14):L$ :: CALL HCHAR(5,7,32,22)
280 REM **MAIN PROGRAM LOOP**
290 DISPLAY AT(19,1)BEEP:"  CHOOSE THE LEVEL OF PLAY"
300 DISPLAY AT(23,1):"   1) EASY   2) DIFFICULT" :: CALL KEY(0,K,S):: IF S=0 OR K>50 OR K<49 THEN 300 :: IF K=49 THEN ER=7 ELSE ER=5
310 FOR SP=5 TO 13 :: CALL DELSPRITE(#SP):: NEXT SP
320 DISPLAY AT(19,1):" A B C D E F G H I J K L M" :: DISPLAY AT(23,1)BEEP:" N O P Q R S T U V W X Y Z" :: RANDOMIZE
330 CALL HCHAR(5,3,32,28):: W$=B$(INT(20*RND)+1):: F=LEN(W$):: FOR I=1 TO F :: DISPLAY AT(5,2*I+14-F):"_" :: NEXT I :: Y=0 :: SP=13
340 CALL KEY(0,K,S):: IF S=0 OR(K<65 OR K>90)THEN 340 ELSE C=121
350 IF K<78 THEN R=128 ELSE R=160
360 CC=((K-64)*16+16-208*INT((K-64)/14))/8 :: CALL GCHAR((R+24)/8,CC,X):: IF X=32 THEN 370 ELSE 390
370 DISPLAY AT(16,14-(8+LEN(L$))/2)SIZE(8+LEN(L$))BEEP:" OOPS, ";L$;"," :: DISPLAY AT(17,1):" YOU TRIED THAT ONE ALREADY"
380 FOR D=1 TO 500 :: NEXT D :: CALL HCHAR(16,1,33,64):: GOTO 340
390 V=12 :: H=0 :: GOSUB 550
400 IF K<72 OR(K>77 AND K<85)THEN H=-12 ELSE H=12
410 V=0 :: C=(K-64)*16+4-208*INT((K-64)/14):: GOSUB 550
420 X=0 :: CALL HCHAR((R+24)/8,(C+12)/8,32):: FOR I=1 TO F :: IF ASC(SEG$(W$,I,1))<>K THEN 450
430 CALL PATTERN(#2,124):: GOSUB 580
440 CALL PATTERN(#2,120):: DISPLAY AT(5,2*I+14-F)SIZE(-1):CHR$(K):: X=1 :: Y=Y+1
450 NEXT I :: IF X=1 THEN 470
460 CALL PATTERN(#2,128):: GOSUB 620 :: CALL PATTERN(#2,120)
470 H=-H :: C=121 :: GOSUB 550
480 V=-12 :: H=0 :: R=78 :: GOSUB 550
490 IF Y=LEN(W$)THEN GOSUB 740 ELSE 500 :: GOTO 510
500 IF ER=1 THEN GOSUB 710 ELSE 340
510 DISPLAY AT(23,1)BEEP:"     ANOTHER WORD? (Y/N)"
520 CALL KEY(0,K,S):: IF S=0 OR K<>89 AND K<>78 THEN 520 :: IF K=89 THEN 290
530 CALL DELSPRITE(ALL):: GOTO 150
540 REM **SUB TO MOVE HAPPY FACE**
550 CALL MOTION(#2,V,H)
560 CALL COINC(#2,R,C,4,Z):: IF Z=0 THEN 560 ELSE CALL MOTION(#2,0,0):: CALL LOCATE(#2,R,C):: RETURN
570 REM **SUB FOR CORRECT GUESS**
580 B=8*(2*I+14-F):: CALL SPRITE(#1,132,14,R,C,(32-R)/8,(B-C)/8)
590 J=2^(1/12):: FOR A=1 TO 25 :: CALL SOUND(-40,220*J^A,1):: NEXT A
600 CALL COINC(#1,32,B,6,Z):: IF Z=0 THEN 600 ELSE CALL DELSPRITE(#1):: RETURN
610 REM **SUB FOR INCORRECT GUESS**
620 SP=SP-1 :: ER=ER-1 :: IF ER>4 THEN RR=80 ELSE RR=50
630 IF ER=6 OR ER=4 THEN C=52
640 IF ER=5 OR ER=1 THEN C=188
650 IF ER=3 THEN C=110
660 IF ER=2 THEN C=132
670 CALL SPRITE(#SP,140,2,1,120,(RR-1)/8,(C-120)/8)
680 J=2^(1/12):: FOR A=25 TO 1 STEP -1 :: CALL SOUND(-40,440*J^A,1):: NEXT A
690 CALL COINC(#SP,RR,C,6,Z):: IF Z=0 THEN 690 ELSE CALL MOTION(#SP,0,0):: CALL LOCATE(#SP,RR,C):: CALL PATTERN(#SP,100):: RETURN
700 REM **SUB FOR BLACKBIRDS WIN**
710 CALL HCHAR(19,3,32,28):: DISPLAY AT(19,15-(8+LEN(L$))/2):"SORRY, ";L$;","
720 DISPLAY AT(23,1)BEEP:"THE BLACKBIRDS WIN THIS TIME" :: GOSUB 760 :: RETURN
730 REM **SUB FOR PLAYER WINS**
740 CALL HCHAR(19,3,32,28):: DISPLAY AT(19,15-(8+LEN(L$))/2):"GREAT, ";L$;","
750 DISPLAY AT(23,1):"   THAT'S THE SECRET WORD"
760 CALL HCHAR(5,3,32,28):: FOR I=1 TO F :: DISPLAY AT(5,2*I+14-F):SEG$(W$,I,1):: NEXT I :: T=180 :: GOSUB 1050 :: RETURN
770 REM **ASSIGN COLORS AND DEFINE CHARACTERS**
780 FOR I=0 TO 9 :: CALL COLOR(I,2,8):: NEXT I :: CALL COLOR(10,3,8):: CALL COLOR(11,11,8):: CALL COLOR(1,13,8)
800 FOR I=1 TO 25 :: READ C,A$(I):: CALL CHAR(C,A$(I)):: NEXT I :: CALL SCREEN(15):: GOTO 150
810 DATA 112,C0C0C0C0C0C0C0C0,113,0303030303030303,114,FFFFFFFFFFFFFFFF,115,C0C0C0FFFFC0C0C0,116,030303FFFF030303
820 DATA 105,183C3C6E6EFFFFFF,106,FFFFFFFFFFFFFFFF,107,FFFFFF7E7E3C3C18,108,071F7FFFFF7F1F07,109,C0F0FEFFFFFEF0C0
830 DATA 96,00000000030F3FFF,97,FFFFFFFFFFFFFFFF,98,FFFFEFC78383C7EF,99,7F3F1E3C78FCFEFF,33,FFFFFFFFFFFFFFFF
840 DATA 91,1F3F7FFFFFFFFFFF,92,F8FCFEFFFFFFFFFF,93,00000000C0F0FCFF
850 DATA 120,071820404C8880818088844340201807E0180C0232110181011121C2020418E0
860 DATA 124,071820404C888081809F904844231807E0180C023211018101F9091222C418E0
870 DATA 128,071820404C8880818083844840201807E0180C023211018101C12112020418E0
880 DATA 132,030F1F3F3F3F3F1F0F0703010102040880E0F0F8F8F8F8F0E0C080
890 DATA 136,030F3F7F7F3FFFFFFFFF3F7F7F37070100C0CCFEFEFCFFFFFFFFFCFEFEECE080
900 DATA 140,000000000000183D478301000000000000000000000018BCE2C1800000000000
910 DATA 100,00010101000103030707070301000101C0E0F0D0C0E0F0F0F8F8F8F0E0C02020
920 REM **PRINT SCREEN**
930 CALL HCHAR(16,1,33,288):: FOR I=1 TO 21 :: READ R,C,G$ :: DISPLAY AT(R,C)SIZE(-6):G$ :: NEXT I :: GOTO 240
940 DATA 9,12,`[aa\],10,12,qrrrrp,11,12,qrrrrp,12,12,qrrrrp,13,14,st,14,14,st,15,14,st
950 DATA 9,5,iii,10,4,ljjjm,11,4,ljjjm,12,5,kjk,13,6,b,14,6,a,15,6,a
960 DATA 9,22,iii,10,21,ljjjm,11,21,ljjjm,12,22,kjk,13,23,a,14,23,c,15,23,a
970 REM **INSTRUCTIONS**
980 DISPLAY AT(1,8)ERASE ALL:"MYSTERY SPELL": :"   THE OBJECT OF MYSTERY":"SPELL IS TO GUESS THE SECRET":"WORD."
990 DISPLAY AT(6,4):"WHEN YOU PRESS A LETTER,":"THE HAPPY FACE WILL MOVE TO":"THE SELECTED LETTER AND LET":"YOU KNOW WHETHER YOU MADE A"
1000 DISPLAY AT(10,1):"RIGHT OR WRONG CHOICE.":"   A CORRECT CHOICE LAUNCHES":"A BALLOON.  AN INCORRECT ONE":"CAUSES A BLACKBIRD TO LAND."
1010 DISPLAY AT(14,1):"IF TOO MANY BLACKBIRDS LAND,":"YOU WILL LOSE THE GAME.": :"   THREE ARE TWO LEVELS:"
1020 DISPLAY AT(19,1)BEEP:"EASY)  PERMITS 6 INCORRECT":"       GUESSES.": :"HARD)  PERMITS ONLY 4."
1030 DISPLAY AT(24,6):"**PRESS ANY KEY**" :: CALL KEY(0,K,S):: IF S=0 THEN 1030 ELSE 190
1040 REM **SUB FOR BLACKBIRD FLIGHT AND THEME MELODY**
1050 R=8 :: FOR SP=5 TO 13 :: C=INT(RND*240)+1 :: CALL SPRITE(#SP,140,2,R,C,0,12):: R=R+12 :: NEXT SP
1060 CALL SOUND(T,175,0)
1070 CALL SOUND(T,349,0,175,2)
1080 CALL SOUND(T,587,0,175,2)
1090 CALL SOUND(2*T,523,0,440,1,175,2)
1100 CALL SOUND(T,587,0,175,2)
1110 CALL SOUND(2*T,523,0,440,1,185,2)
1120 CALL SOUND(T,196,0)
1130 CALL SOUND(T,330,0,196,2)
1140 CALL SOUND(T,587,0,196,2)
1150 CALL SOUND(2*T,523,0,466,1,196,2)
1160 CALL SOUND(T,587,0,196,2)
1170 CALL SOUND(2*T,523,0,466,1,208,2)
1180 CALL SOUND(T,220,2)
1190 CALL SOUND(T,523,0,440,1,220,2)
1200 CALL SOUND(T,311,2)
1210 CALL SOUND(T,523,0,440,1,311,2)
1220 CALL SOUND(T,294,2)
1230 CALL SOUND(T,494,0,415,1,294,2)
1240 CALL SOUND(T,272,2)
1250 CALL SOUND(T,466,0,392,1,277,2)
1260 CALL SOUND(T,440,0,262,2)
1270 CALL SOUND(T,523,0,262,2)
1280 CALL SOUND(T,587,0,247,2)
1290 CALL SOUND(T,698,0,247,2)
1300 CALL SOUND(2*T,659,0,233,2)
1310 CALL SOUND(2*T,784,0,659,1,131,2)
1320 CALL SOUND(T,880,0,175,2)
1330 CALL SOUND(T,831,0,175,2)
1340 CALL SOUND(T,880,0,262,2,349,2)
1350 CALL SOUND(T,1047,0,262,2,349,2)
1360 CALL SOUND(T,1047,0,220,2)
1370 CALL SOUND(T,880,0,220,2)
1380 CALL SOUND(T,784,0,262,2,349,2)
1390 CALL SOUND(T,698,0,262,2,349,2)
1400 CALL SOUND(T,784,0,233,2)
1410 CALL SOUND(T,698,0,233,2)
1420 CALL SOUND(T,587,0,294,2,349,2)
1430 CALL SOUND(T,698,0,294,2,349,2)
1440 CALL SOUND(T,698,0,220,2)
1450 CALL SOUND(T,587,0,220,2)
1460 CALL SOUND(T,523,0,262,2,349,2)
1470 CALL SOUND(T,440,0,262,2,349,2)
1480 CALL SOUND(T,392,0,247,2)
1490 CALL SOUND(T,784,0,247,2)
1500 CALL SOUND(T,698,0,294,2,349,2)
1510 CALL SOUND(T,659,0,294,2,349,2)
1520 CALL SOUND(T,587,0,196,2)
1530 CALL SOUND(T,440,0,196,2)
1540 CALL SOUND(T,440,0,233,2,349,2)
1550 CALL SOUND(T,494,0,233,2,349,2)
1560 CALL SOUND(T,523,0,175,2,220,2)
1570 CALL SOUND(T,587,0,175,2,220,2)
1580 CALL SOUND(2*T,659,0,262,2)
1590 CALL SOUND(3*T,698,0,262,2,175,0)
1600 FOR I=1 TO 30 STEP 2 :: CALL SOUND(-T,698,I,262,I,175,I):: NEXT I :: RETURN
1610 REM **CREATE A WORD LIST**
1620 DISPLAY AT(1,4)ERASE ALL:"WORD LIST INSTRUCTIONS": : :"  IN THIS SEGMENT YOU MAY":"EITHER CREATE A WORD LIST"
1630 DISPLAY AT(6,1):"OR LOAD AN EXISTING ONE FROM":"A STORAGE DEVICE.": : :"  WHEN CREATING A WORD LIST,":"TYPE EACH WORD, THEN PRESS"
1640 DISPLAY AT(12,1):"ENTER.  MAXIMUM WORD LENGTH":"IS 13 CHARACTERS.  20 WORDS":"MUST BE ENTERED FOR EACH OF":"THE WORD LISTS CREATED."
1650 DISPLAY AT(18,3)BEEP:"AS YOU ENTER EACH LIST,":"YOU MAY SAVE IT TO A STORAGE":"DEVICE FOR FUTURE USE WITH":"MYSTERY SPELL."
1660 DISPLAY AT(24,6):"**PRESS ANY KEY**" :: CALL KEY(0,K,S):: IF S=0 THEN 1660
1670 DISPLAY AT(7,1)ERASE ALL BEEP:"PRESS   TO": : :"  1  =  CREATE A WORD LIST": :"  2  =  LOAD A WORD LIST": :"  3  =  EXIT"
1680 CALL KEY(0,K,S):: IF S=0 OR(K<49 OR K>51)THEN 1680 :: J=0 :: ON K-48 GOTO 1690,1795,190
1690 DISPLAY AT(1,5)ERASE ALL:"ENTER THE WORD LIST:"
1700 I=1 :: C=1 :: FOR A=1 TO 2 :: R=3 :: FOR Z=1 TO 10
1710 ACCEPT AT(R,C)SIZE(-13)BEEP:B$(I):: R=R+2 :: I=I+1 :: NEXT Z :: C=15 :: NEXT A
1720 DISPLAY AT(24,1)BEEP:"CORRECT OR CHANGE ANY? (Y/N)"
1730 CALL KEY(0,K,S):: IF S=0 OR K<>89 AND K<>78 THEN 1730 :: IF K=89 THEN 1700 :: J=1 :: GOTO 1795
1740 FOR I=1 TO 20 :: PRINT #1:B$(I):: NEXT I :: CLOSE #1 :: GOTO 230
1750 FOR I=1 TO 20 :: INPUT #1:B$(I):: NEXT I :: CLOSE #1
1760 DISPLAY AT(11,6)ERASE ALL BEEP:"DO YOU WISH TO SEE": :"    THE WORD LIST? (Y/N)"
1770 CALL KEY(0,K,S):: IF S=0 OR(K<>89 AND K<>78)THEN 1770 :: IF K=89 THEN 1780 ELSE 230
1780 DISPLAY AT(1,10)ERASE ALL BEEP:"WORD LIST" :: R=3 :: FOR I=1 TO 20 STEP 2 :: DISPLAY AT(R,1):B$(I),B$(I+1):: R=R+2 :: NEXT I
1790 DISPLAY AT(24,1):"PRESS ANY KEY WHEN FINISHED" :: CALL KEY(0,K,S):: IF S=0 THEN 1790 ELSE 230
1795 ON ERROR 2000
1800 DISPLAY AT(5,6)ERASE ALL BEEP:"WHAT IS THE NAME": :"  OF YOUR STORAGE DEVICE?": :"(EXAMPLE: CS1 OR DSK1.WORDS)"
1810 DISPLAY AT(23,1):"PLACE TAPE OR DISK IN DEVICE" :: ACCEPT AT(11,3):F$
1815 IF SEG$(F$,1,1)="C" THEN 1842 ELSE OPEN #1:F$,INTERNAL,UPDATE,FIXED 50
1820 IF J=0 THEN 1750 ELSE 1740
1830 REM **PRESELECTED WORD LIST**
1840 FOR I=1 TO 20 :: READ B$(I):: NEXT I :: GOTO 260
1842 IF J=0 THEN 1846
1844 OPEN #1,F$,OUTPUT,INTERNAL,FIXED 50 :: FOR I=1 TO 20 :: PRINT #1:B$(I):: NEXT I :: CLOSE #1 :: GOTO 230
1846 OPEN #1:F$,INPUT ,INTERNAL,FIXED 50 :: FOR I=1 TO 20 :: INPUT #1:B$(I):: NEXT I :: CLOSE #1 :: CALL CLEAR :: GOTO 1760
1850 DATA BANANAS,CARROTS,RHUBARB,CABBAGE,TURNIP,BEANS,CORN,CELERY,WATERMELON,ORANGES,APPLES,PEACHES
1860 DATA MUSHROOMS,ONIONS,POTATOES,TOMATOES,GRAPES,PUMPKIN,SQUASH,LEMONS
2000 PRINT "ERROR...ERROR"
2010 PRINT "CHECK TAPE OR DISK"
2020 PRINT "PROGRAM WILL BEGIN AGAIN"
2030 PRINT "PLEASE WAIT...."
2040 FOR A=1 TO 500
2050 NEXT A
2060 RUN
