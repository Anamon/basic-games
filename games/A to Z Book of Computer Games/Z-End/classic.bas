10 REM  "Z-END"
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 PRINT "SKIP THE RULES (Y OR N)";
50 INPUT Q$
60 IF Q$ = "Y" THEN 150
70 PRINT "I'LL PRINT THE ALPHABET."
80 PRINT "YOU'RE FIRST."
90 PRINT "TYPE THE NUMBER OF LETTERS THAT"
100 PRINT "  I SHOULD OMIT NEXT TIME."
110 PRINT "WE TAKE TURNS AND THE LIMIT"
120 PRINT "  PER TURN IS 5."
130 PRINT "THE ONE THAT GETS THE 'Z' IS"
140 PRINT "  THE LOSER AND THAT'S Z-END."
150 PRINT "GOOD LUCK, CUZ I'M CLEVER..."
155 LET Q$ = " "
160 DATA A,B,C,D,E,F,G,H,I,J,K,L,M
170 DATA N,O,P,Q,R,S,T,U,V,W,X,Y,Z,*
180 LET Z = 1
185 LET Q = 0
190 GOSUB 500
195 IF Q$ = "Y" THEN 155
200 LET P = 2
205 PRINT
210 PRINT "YOUR TURN";
215 LET Q = 0
220 INPUT Q
230 IF Q < 1 THEN 300
240 IF Q > 5 THEN 300
250 GOSUB 500
260 IF Q$ = "Y" THEN 155
270 GOSUB 800
280 GOTO 190
290 PRINT
300 PRINT "ILLEGAL - 1, 2, 3, 4, OR 5!"
310 GOTO 210
500 LET Z = Z + Q
550 FOR I = 1 TO 27
560 READ Z$
570 IF I = Z THEN 590
580 NEXT I
590 LET J = Z
600 FOR I = J TO 26
610 PRINT Z$;
615 READ Z$
620 NEXT I
630 RESTORE
640 PRINT
650 IF 26 - Z = 0 THEN 700
660 RETURN
700 PRINT "Z-END  ";
710 IF P = 1 THEN 780
720 PRINT "OOPS"
730 PRINT
740 PRINT "DO IT AGAIN (Y OR N)";
750 INPUT Q$
760 IF Q$ = "Y" THEN 660
765 PRINT "GOOD BYE"
770 END
780 PRINT "HA HA!"
790 GOTO 730
800 PRINT
810 IF 26 - Z < 6 THEN 900
820 IF 26 - Z > 10 THEN 840
830 GOTO 920
840 LET Q = INT(10 * RND(1))
850 IF Q < 1 THEN 840
860 IF Q > 5 THEN 840
870 PRINT "MY PICK IS"; Q
880 LET P = 1
890 RETURN
900 LET Q = 26 - Z
910 GOTO 870
920 LET Q = 1
930 GOTO 870
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

