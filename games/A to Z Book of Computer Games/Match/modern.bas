5 CLS: COLOR 12
10 REM  "MATCH"
15 PRINT TAB(37); "Match": PRINT: PRINT
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 COLOR 10: PRINT "Do you know the rules (Y/N)";
50 INPUT Q$: COLOR 15
60 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 80
70 GOSUB 1000
80 DIM D(52), M$(9), U(4)
90 COLOR 10: PRINT "How many players (2-4)";
100 INPUT A: COLOR 15
105 LET A = INT(ABS(A))
110 IF A < 5 THEN 140
120 PRINT "Too many players."
130 GOTO 90
140 IF A > 1 THEN 170
150 PRINT "Too few players."
160 GOTO 90
170 GOSUB 2000
180 LET U = 1
190 PRINT
200 FOR I = 1 TO 4
210 LET T(I) = 0
220 NEXT I
400 IF U <= A THEN 420
410 LET U = 1
420 U$ = STR$(U): U$ = RIGHT$(U$, LEN(U$) - 1)
421 P$ = STR$(U(U)): P$ = RIGHT$(P$, LEN(P$) - 1)
425 PRINT "Player #"; U$; " ("; P$; " points)";
430 GOSUB 4000
440 IF U(U) >= 20 THEN 480
450 LET U = U + 1
460 PRINT
470 GOTO 400
480 PRINT "Congratulations!"
490 PRINT
500 COLOR 10: PRINT "Another game (Y/N)";
510 INPUT Q$: COLOR 15
520 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 560
530 PRINT: PRINT "Good -- I'm tired."
540 END
560 FOR I = 1 TO 4
570 LET U(I) = 0
580 NEXT I
590 GOTO 90
999 REM  "INSTRUCTIONS"
1000 COLOR 14: PRINT
1010 PRINT "Two, three, or four players draw cards in turn.  You may draw from two to nine"
1020 PRINT "cards per turn (or you may pass).  Score one point for each card drawn --"
1030 PRINT "unless you get a pair.  Any match ends your turn and you get no score.  The"
1040 PRINT "game is 20 points; first one there wins."
1050 PRINT: COLOR 15
1130 RETURN
1999 REM  "CREATE CARD DECK"
2000 FOR I = 0 TO 3
2010 FOR J = 1 TO 13
2020 LET D(J + I * 13) = J + I * 100
2030 NEXT J
2040 NEXT I
2050 FOR I = 1 TO 52
2060 LET J = INT(100 * RND(1))
2070 IF J < 1 THEN 2060
2080 IF J < 53 THEN 2100
2090 LET J = INT(J / 2) + 1
2100 LET S = D(J)
2110 LET D(J) = D(I)
2120 LET D(I) = S
2130 NEXT I
2140 LET D = 0
2150 RETURN
2999 REM  "DRAW CARDS"
3000 IF D < 52 THEN 3030
3010 PRINT "New deck."
3020 GOSUB 2000
3030 LET D = D + 1
3035 D$ = STR$(D): D$ = RIGHT$(D$, LEN(D$) + 1)
3040 COLOR 11: PRINT "Draw #"; D$; ": ";
3050 LET C = D(D)
3060 DATA Ace,2,3,4,5,6,7,8,9,10
3070 DATA Jack,Queen,King
3080 GOSUB 3500
3090 FOR I = 1 TO C
3100 READ C$
3110 IF I = C THEN 3130
3120 NEXT I
3130 RESTORE
3140 COLOR 13: PRINT C$; S$: COLOR 15
3150 RETURN
3500 S = INT(C / 100)
3510 C = C - S * 100
3520 LET S$ = " of Hearts"
3530 IF S = 1 THEN 3590
3540 LET S$ = " of Clubs"
3550 IF S = 2 THEN 3590
3560 LET S$ = " of Diamonds"
3570 IF S = 3 THEN 3590
3580 LET S$ = " of Spades"
3590 RETURN
3999 REM  "PLAYER INPUT"
4000 COLOR 10: PRINT "  Draw cards (0 or 2-9)";
4010 GOSUB 5500
4020 INPUT Q: COLOR 15
4025 LET Q = INT(ABS(Q))
4030 IF Q = 0 THEN 4130
4040 IF Q > 9 THEN 4140
4050 IF Q < 2 THEN 4140
4060 FOR Q1 = 1 TO Q
4070 GOSUB 3000
4080 FOR Q2 = 1 TO 9
4082 IF M$(Q2) = " " THEN 4090
4084 IF M$(Q2) = C$ THEN 4110
4086 NEXT Q2
4088 GOTO 4094
4090 LET M$(Q2) = C$
4094 NEXT Q1
4100 GOTO 4160
4110 LET Q = 0
4120 PRINT "Match -- ha ha!"
4130 RETURN
4140 PRINT "Illegal entry."
4150 GOTO 4020
4160 U(U) = U(U) + Q
4170 RETURN
5499 REM  "CLEAR TANK"
5500 FOR Q2 = 1 TO 9
5510 LET M$(Q2) = " "
5520 NEXT Q2
5530 LET Q = 0
5540 RETURN
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

