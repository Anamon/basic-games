5 CLS: COLOR 12
10 REM  "QUANTAL"
15 PRINT TAB(36); "Quantal": PRINT: PRINT
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 DIM S(20)
50 COLOR 10: PRINT "Do you want instructions (Y/N)";
60 INPUT Q$: COLOR 15
70 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 100
80 IF LEFT$(Q$, 1) = "N" OR LEFT$(Q$, 1) = "n" THEN 110
90 GOTO 50
100 GOSUB 1000
110 GOSUB 3000
120 GOSUB 2000
130 IF Q = 99 THEN 170
140 GOSUB 4000
150 GOSUB 5000
160 GOTO 130
170 PRINT
180 COLOR 10: PRINT "Another game (Y/N)";
190 INPUT Q$: COLOR 15
200 IF LEFT$(Q$, 1) = "N" OR LEFT$(Q$, 1) = "n" THEN 240
210 COLOR 10: PRINT "Same players (Y/N)";
215 INPUT Q$: COLOR 15
220 IF LEFT$(Q$, 1) = "N" OR LEFT$(Q$, 1) = "n" THEN 110
230 GOTO 120
240 PRINT: PRINT "Bye."
250 END
1000 COLOR 14: PRINT
1010 PRINT "Two players each have 10 switches.  Some are on and some are off.  In a turn"
1020 PRINT "may test one of your switches and leave it is is, or you may change it.  The"
1030 PRINT "first player to have their 10 all on, or all off, wins."
1090 PRINT: COLOR 15
1100 RETURN
1999 REM  "20 RANDOM SWITCHES"
2000 FOR I = 1 TO 20
2010 LET R = INT(10 * RND(1))
2020 LET S(I) = 0
2030 IF R < 5 THEN 2050
2040 LET S(I) = 1
2050 NEXT I
2060 LET P = 1
2070 IF R < 5 THEN 2090
2080 LET P = 11
2090 LET C$ = " (Y/N)"
2100 RETURN
2999 REM  "INTRODUCTIONS"
3000 PRINT "Please tell me your names."
3010 COLOR 10: PRINT "Player #1";
3020 INPUT P1$: COLOR 15
3030 COLOR 10: PRINT "Player #2";
3040 INPUT P2$: COLOR 15
3050 IF P1$ <> P2$ THEN 3080
3060 PRINT "I don't understand."
3070 GOTO 3000
3080 IF P1$ < "AA" THEN 3110
3090 IF P2$ < "AA" THEN 3140
3100 RETURN
3110 COLOR 10: PRINT "No.  Enter player 1's name again";
3120 INPUT P1$: COLOR 15
3130 GOTO 3050
3140 COLOR 10: PRINT "No.  Enter player 2's name again";
3150 GOTO 3040
3999 REM  "PLAYER INPUT
4000 IF P = 1 THEN 4030
4010 LET P = 1
4015 PRINT: PRINT P2$;
4020 GOTO 4040
4030 LET P = 11
4035 PRINT: PRINT P1$;
4040 PRINT ": ";: COLOR 10: PRINT "Switch (1-10)";
4050 INPUT Q: COLOR 15
4060 IF Q > 0 THEN 4090
4070 PRINT "Can't be negative."
4080 GOTO 4040
4090 IF Q <= 10 THEN 4120
4100 PRINT "Too large."
4110 GOTO 4040
4120 IF Q = INT(Q) THEN 4150
4130 PRINT "No decimals!"
4140 GOTO 4040
4150 LET S = P + Q - 1
4160 IF S(S) = 1 THEN 4190
4170 COLOR 13: PRINT "OFF -- ";: COLOR 15
4180 GOTO 4200
4190 COLOR 13: PRINT "ON -- ";: COLOR 15
4200 COLOR 10: PRINT "Change it";
4210 PRINT C$;
4214 LET Q$ = C$
4220 INPUT Q$: COLOR 15
4230 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 4270
4240 IF LEFT$(Q$, 1) = "N" OR LEFT$(Q$, 1) = "n" THEN 4290
4250 LET C$ = " (Y/N)"
4260 GOTO 4200
4270 IF S(S) = 0 THEN 4300
4280 LET S(S) = 0
4290 RETURN
4300 LET S(S) = 1
4310 RETURN
4999 REM  "WINNER?"
5000 LET Q = S(1)
5010 FOR I = 2 TO 10
5020 IF S(I) <> Q THEN 5080
5030 NEXT I
5040 PRINT: COLOR 12: PRINT P2$;
5050 PRINT " just won!": COLOR 15
5060 LET Q = 99
5070 RETURN
5080 LET Q = S(11)
5090 FOR I = 12 TO 20
5100 IF S(I) <> Q THEN 5070
5110 NEXT I
5120 COLOR 12: PRINT P1$;
5130 GOTO 5050
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

