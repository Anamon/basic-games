10 REM  "VERBOTEN"
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 DIM A$(26), X(26), R(2)
50 PRINT "WANT INSTRUCTIONS (Y OR N)";
60 INPUT Q$
70 IF Q$ = "Y" THEN 110
80 IF Q$ = "N" THEN 120
90 PRINT "WAS GIBT ES?"
100 GOTO 50
110 GOSUB 1000
120 GOSUB 2000
130 GOSUB 3000
140 GOSUB 4000
150 GOSUB 5000
160 GOSUB 6000
170 IF X$ <> " " THEN 160
180 LET T1 = T1 + R(1)
190 LET T2 = T2 + R(2)
200 LET R = R + 1
210 PRINT "ROUND #"; R; "     TOTAL"
220 PRINT "# 1 ="; R(1); "       "; T1
230 PRINT "# 2 ="; R(2); "       "; T2
240 PRINT
250 PRINT "GO AGAIN (Y OR N)";
260 LET Q$ = " "
270 INPUT Q$
280 IF Q$ = "Y" THEN 130
290 IF Q$ = "N" THEN 320
300 PRINT "WAS ES LOS?"
310 GOTO 250
320 PRINT "NEW GAME (Y OR N)";
330 LET Q$ = " "
335 INPUT Q$
340 IF Q$ = "N" THEN 400
350 LET T1 = 0
360 LET T2 = 0
370 IF Q$ = "Y" THEN 130
380 PRINT "WAS GIBT ES?"
390 GOTO 320
400 PRINT "AUF WIEDERSEHEN"
410 END
1000 PRINT "YOU AND A FRIEND TAKE TURNS."
1010 PRINT "TYPE ANY LETTER OF THE ALPHABET"
1020 PRINT "THAT HASN'T ALREADY BEEN PICKED."
1030 PRINT "EACH LETTER IS WORTH ONE POINT,"
1040 PRINT "EXCEPT:  (IN A ROUND)"
1050 PRINT "  ONE IS A BONUS, WHICH WILL"
1060 PRINT "  DOUBLE YOUR SCORE, AND, ONE"
1070 PRINT "  IS VERBOTEN, WHICH WILL ERASE"
1080 PRINT "  YOUR SCORE."
1090 PRINT "WHEN THE FORBIDDEN LETTER IS"
1100 PRINT "PICKED THE ROUND ENDS AND YOUR"
1110 PRINT "SCORES ARE UPDATED."
1120 PRINT "THE NEXT ROUND BEGINS WITH A"
1130 PRINT "NEW ALPHABET."
1140 PRINT "                 READY";
1150 INPUT Q$
1160 PRINT
1170 RETURN
1999 REM  "ALPHA TABLE"
2000 DATA A,B,C,D,E,F,G,H,I,J,K,L,M
2010 DATA N,O,P,Q,R,S,T,U,V,W,X,Y,Z
2020 FOR I = 1 TO 26
2030 READ A$(I)
2040 NEXT I
2050 RESTORE
2060 RETURN
2999 REM  "CLEAR TANK"
3000 LET R(1) = 0
3010 LET R(2) = 0
3020 FOR I = 1 TO 26
3030 LET X(I) = 0
3040 NEXT I
3050 RETURN
3999 REM  "BONUS LETTER"
4000 LET B = INT(100 * RND(1)) + 1
4010 IF B > 26 THEN 4000
4020 RETURN
4999 REM  "VERBOTEN LETTER"
5000 LET V = INT(100 * RND(1)) + 1
5010 IF V > 26 THEN 5000
5020 IF V = 8 THEN 5000
5030 RETURN
5999 REM  "PLAYER INPUT"
6000 IF P <> 1 THEN 6030
6010 LET P = 2
6020 GOTO 6040
6030 LET P = 1
6040 PRINT "PLAYER #"; P;
6050 LET X$ = " "
6060 INPUT X$
6070 FOR I = 1 TO 26
6080 IF X$ = A$(I) THEN 6130
6090 NEXT I
6100 PRINT "FRISCH AUF!"
6110 PRINT
6120 GOTO 6040
6130 IF X(I) <> 0 THEN 6260
6140 LET X(I) = P
6150 IF I = V THEN 6220
6160 IF I = B THEN 6190
6170 LET R(P) = R(P) + 1
6180 RETURN
6190 PRINT "*** BONUS ***"
6200 LET R(P) = R(P) * 2
6210 RETURN
6220 PRINT "VERBOTEN!"
6230 LET R(P) = 0
6240 LET X$ = " "
6250 RETURN
6260 IF X(I) = P THEN 6300
6270 PRINT "YOUR FRIEND ";
6280 PRINT "PICKED "; X$; " BEFORE"
6290 GOTO 6050
6300 PRINT "YOU ";
6310 GOTO 6280
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

