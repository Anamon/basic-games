5 CLS: COLOR 12
10 REM "KNIGHTS"
15 PRINT TAB(36); "Knights": PRINT: PRINT
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 COLOR 10: PRINT "Want a description (Y/N)";
50 INPUT Q$: COLOR 15
60 IF LEFT$(Q$, 1) <> "Y" AND LEFT$(Q$, 1) <> "y" THEN 72
70 GOSUB 1000
72 DIM X$(24)
74 DIM C$(9), P$(9)
76 DIM T(9)
80 GOSUB 2000
90 LET C = 0
100 LET C = C + 1
110 COLOR 12: PRINT "Round"; C: COLOR 15
120 GOSUB 4000
130 GOSUB 3000
140 IF W = 0 THEN 300
150 PRINT: PRINT "Good show, old chap!"
160 COLOR 10: PRINT "Want to go again (Y/N)";
170 INPUT Q$: COLOR 15
180 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 80
190 PRINT: PRINT "So long, then..."
200 END
300 GOSUB 5000
310 GOTO 100
1000 REM  "DESCRIPTION"
1010 PRINT
1020 COLOR 15: PRINT , "Knights", "Knaves", "Numskulls"
1030 COLOR 11: PRINT , " G,Q,S", " R,X,P", " L,E,A"
1040 COLOR 13: PRINT , " T F F", " F T F", " T T F"
1045 COLOR 14: PRINT
1050 PRINT "The letters T and F (true/false) tell whether the letters above are in the"
1060 PRINT "right group.  The object is to retype the leters to get all 'true' answers in"
1070 PRINT "as few turns as you can.  Know that:"
1080 COLOR 11: PRINT
1090 PRINT "    * Knights never lie."
1100 PRINT "    * Knaves always lie."
1110 PRINT "    * You can't trust numskulls."
1120 COLOR 14: PRINT
1130 PRINT "Letters are asked for in groups of three with commands (e.g., G,Q,S)."
1140 COLOR 15: PRINT
1180 RETURN
2000 REM  "9-RANDOM LETTERS PICKER"
2010 DATA A,B,C,D,E,G,H,I,J,K,L,M
2020 DATA N,O,P,Q,R,S,U,V,W,X,Y,Z
2030 FOR I = 1 TO 24
2040 READ X$(I)
2050 NEXT I
2060 RESTORE
2070 FOR I = 1 TO 9
2080 LET R = INT(100 * RND(1))
2090 IF R < 1 THEN 2080
2100 IF R < 25 THEN 2130
2110 LET R = INT(R / 2)
2120 GOTO 2090
2130 LET C$(I) = X$(R)
2140 LET X$(R) = " "
2150 IF C$(I) = " " THEN 2080
2160 LET P$(I) = C$(I)
2170 NEXT I
2180 FOR I = 1 TO 9
2190 LET J = INT(10 * RND(1))
2200 IF J < 1 THEN 2190
2210 LET X$ = P$(I)
2220 LET P$(I) = P$(J)
2230 LET P$(J) = X$
2240 NEXT I
2250 RETURN
3000 REM  "PRINT THE PLAYING BOARD"
3020 PRINT , "Knights", "Knaves", "Numskulls"
3030 COLOR 11: PRINT ,: FOR I = 1 TO 9 STEP 3
3040 PRINT " "; P$(I); ","; P$(I + 1); ","; P$(I + 2),
3060 NEXT I
3070 PRINT: COLOR 13: PRINT , " ";
3080 FOR I = 1 TO 9 STEP 3
3090 IF T(I) = 0 THEN PRINT "F ";
3100 IF T(I) <> 0 THEN PRINT "T ";
3110 IF T(I + 1) = 0 THEN PRINT "F ";
3120 IF T(I + 1) <> 0 THEN PRINT "T ";
3130 IF T(I + 2) = 0 THEN PRINT "F ";
3140 IF T(I + 2) <> 0 THEN PRINT "T ";
3150 PRINT , " ";
3160 NEXT I
3170 PRINT: COLOR 15
3180 RETURN
4000 REM  "TRUE/FALSE"
4005 W = 0
4010 FOR I = 1 TO 3
4020 FOR J = 1 TO 3
4030 LET T(I) = 1
4040 IF C$(J) = P$(I) THEN 4070
4050 NEXT J
4060 LET T(I) = 0
4070 NEXT I
4080 FOR I = 4 TO 6
4090 FOR J = 4 TO 6
4100 LET T(I) = 0
4110 IF C$(J) = P$(I) THEN 4140
4120 NEXT J
4130 LET T(I) = -1
4140 NEXT I
4150 FOR I = 1 TO 3
4160 LET T(I + 6) = T(I) + T(I + 3)
4170 NEXT I
4180 FOR I = 1 TO 3
4190 IF T(I) + T(I + 6) <> 2 THEN 4230
4200 NEXT I
4210 LET W = 1
4230 RETURN
5000 REM  "INPUT KNIGHTS"
5010 COLOR 10: PRINT "Knights  ";
5020 INPUT P$(1), P$(2), P$(3): COLOR 15
5022 IF P$(1) >= "a" AND P$(1) <= "z" THEN P$(1) = CHR$(ASC(P$(1)) - 32)
5024 IF P$(2) >= "a" AND P$(2) <= "z" THEN P$(2) = CHR$(ASC(P$(2)) - 32)
5026 IF P$(3) >= "a" AND P$(3) <= "z" THEN P$(3) = CHR$(ASC(P$(3)) - 32)
5030 LET F = 1
5040 LET L = 3
5050 GOSUB 5700
5060 IF F = 0 THEN 5010
5070 GOSUB 5900
5080 IF F = 0 THEN 5010
5200 REM  "INPUT KNAVES"
5210 COLOR 10: PRINT "Knaves   ";
5220 INPUT P$(4), P$(5), P$(6): COLOR 15
5222 IF P$(4) >= "a" AND P$(4) <= "z" THEN P$(4) = CHR$(ASC(P$(4)) - 32)
5224 IF P$(5) >= "a" AND P$(5) <= "z" THEN P$(5) = CHR$(ASC(P$(5)) - 32)
5226 IF P$(6) >= "a" AND P$(6) <= "z" THEN P$(6) = CHR$(ASC(P$(6)) - 32)
5230 LET F = 1
5240 LET L = 6
5250 GOSUB 5700
5260 IF F = 0 THEN 5210
5270 GOSUB 5900
5280 IF F = 0 THEN 5210
5400 REM  "INPUT NUMSKULLS"
5410 COLOR 10: PRINT "Numskulls";
5420 INPUT P$(7), P$(8), P$(9): COLOR 15
5422 IF P$(7) >= "a" AND P$(7) <= "z" THEN P$(7) = CHR$(ASC(P$(7)) - 32)
5424 IF P$(8) >= "a" AND P$(8) <= "z" THEN P$(8) = CHR$(ASC(P$(8)) - 32)
5426 IF P$(9) >= "a" AND P$(9) <= "z" THEN P$(9) = CHR$(ASC(P$(9)) - 32)
5430 LET F = 1
5440 LET L = 9
5450 GOSUB 5700
5460 IF F = 0 THEN 5410
5470 GOSUB 5900
5480 IF F = 0 THEN 5410
5490 PRINT
5500 RETURN
5700 REM  "BAD CHARACTER CHECK"
5710 FOR I = L - 2 TO L
5720 FOR J = 1 TO 9
5730 IF P$(I) = C$(J) THEN 5780
5740 NEXT J
5750 PRINT "Illegal character."
5760 LET F = 0
5770 RETURN
5780 NEXT I
5790 RETURN
5900 REM  "CHECK FOR DUPLICATES"
5910 FOR I = L - 2 TO L
5930 FOR J = 1 TO L
5940 IF P$(I) <> P$(J) THEN 5960
5950 LET F = F + 1
5960 NEXT J
5970 NEXT I
5980 IF F <> 4 THEN 6000
5990 RETURN
6000 PRINT "Duplicates -- no fair!"
6010 LET F = 0
6020 RETURN
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

