5 CLS: COLOR 12
10 REM  "PAR-2"
15 PRINT TAB(37); "Par-2": PRINT: PRINT
20 REM
25 RANDOMIZE TIMER
30 GOSUB 9000
40 DIM P(5)
50 COLOR 10: PRINT "Want the rules (Y/N)";
60 INPUT Q$: COLOR 15
70 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 110
80 IF LEFT$(Q$, 1) = "N" OR LEFT$(Q$, 1) = "n" THEN 120
90 PRINT "Huh?"
100 GOTO 50
110 GOSUB 1000
120 LET P(5) = 1
130 COLOR 10: PRINT "How many players (2-4)";
140 INPUT Q: COLOR 15
150 LET A = INT(Q)
160 IF A < 2 THEN 180
170 IF A < 5 THEN 200
180 PRINT "Huh?"
190 GOTO 130
200 FOR I = 1 TO A
210 LET P(I) = 0
220 NEXT I
230 GOSUB 2000
240 GOSUB 3000
250 GOSUB 4000
260 GOSUB 5000
270 IF D <> 0 THEN 250
280 LET P(P) = P(P) + S
290 IF P <> A THEN 230
300 IF P(5) <> 3 THEN 230
310 PRINT: PRINT: PRINT "Score card"
320 FOR I = 1 TO A
325 I$ = STR$(I): I$ = RIGHT$(I$, LEN(I$) - 1)
330 COLOR 13: PRINT "#"; I$; "="; P(I),
340 NEXT I: PRINT: COLOR 15
350 PRINT
360 COLOR 10: PRINT "Another round (Y/N)";
370 INPUT Q$: COLOR 15
380 IF LEFT$(Q$, 1) = "Y" OR LEFT$(Q$, 1) = "y" THEN 120
390 PRINT: PRINT "So long, gang."
400 END
1000 COLOR 14: PRINT
1010 PRINT "Players take turns.  I will print a ball and hole.  Guess the distance (print"
1020 PRINT "spaces) from the ball to the hole.  Your turn continues until you sink the"
1030 PRINT "ball, or until you have shot six times.  A complete game is 18 holes, and the"
1040 PRINT "lowest score wins."
1080 PRINT: COLOR 15
1120 RETURN
1999 REM  "PLAYER NUMBER"
2000 IF P < A THEN 2030
2010 LET P = 0
2020 LET P(5) = P(5) + 1
2030 LET P = P + 1
2040 COLOR 12: PRINT: PRINT: PRINT "Hole"; P(5),
2050 PRINT "Player"; P,
2060 PRINT "Score ="; P(P)
2070 PRINT: COLOR 15
2080 RETURN
2999 REM  "BALL & HOLE"
3000 LET B = 1
3010 LET H = INT(100 * RND(1)) + 4
3020 IF H > 75 THEN 3010
3030 LET D = H - B
3040 COLOR 13: PRINT TAB(B); "*";: COLOR 15
3050 COLOR 11: PRINT TAB(H); "O": COLOR 15
3060 LET S = 0
3070 RETURN
3999 REM  "STROKE"
4000 COLOR 10: PRINT "Stroke";
4010 INPUT Q: COLOR 15
4020 LET Q = INT(ABS(Q))
4030 IF B > H THEN 4110
4040 IF B + Q < 80 THEN 4080
4050 PRINT "Off the course -- try again."
4060 LET S = S + 1
4070 GOTO 4000
4080 LET B = B + Q
4090 LET S = S + 1
4100 RETURN
4110 IF B - Q < 0 THEN 4050
4120 LET Q = 0 - Q
4130 GOTO 4080
4999 REM  "MOVE THE BALL"
5000 IF B <> H THEN 5150
5010 COLOR 13: PRINT TAB(H); CHR$(2): COLOR 15
5020 LET M$ = "Ace"
5030 IF S > 1 THEN 5070
5040 PRINT M$; "!"
5050 LET D = H - B
5060 RETURN
5070 LET M$ = "Par"
5080 IF S > 2 THEN 5100
5090 GOTO 5040
5100 LET M$ = "Bogey"
5110 IF S > 3 THEN 5130
5120 GOTO 5040
5130 LET M$ = "Duffer"
5140 GOTO 5040
5150 IF S < 6 THEN 5180
5160 LET H = B
5170 GOTO 5130
5180 IF B > H THEN 5220
5190 COLOR 13: PRINT TAB(B); "*";: COLOR 15
5200 COLOR 11: PRINT TAB(H); "O": COLOR 15
5210 GOTO 5050
5220 COLOR 11: PRINT TAB(H); "O";: COLOR 15
5230 COLOR 13: PRINT TAB(B); "*": COLOR 15
5240 GOTO 5050
9000 REM  "RANDOM NUMBER ROUTINE"
9010 LET Z = RND(1)
9020 RETURN

