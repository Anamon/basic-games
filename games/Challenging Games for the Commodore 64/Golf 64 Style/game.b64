10 REM GOLF 64 STYLE
20 POKE 53280,13:POKE 53281,5
30 REM "{DOWN}" IS DOWN CURSOR
50 Q=0:T=0
70 FOR H=1 TO 18
80 E=0
90 D=INT(400*RND(1))+100
100 Q=Q+INT(D/70+.5)
110 PRINT "{CLR}":REM CLEAR SCREEN
120 PRINT TAB(10) "{RED}{DOWN}THIS IS HOLE:";H:REM RED
130 PRINT TAB(5) "{BLU}{2 DOWN}NUMBER OF STROKES SO FAR:";T:REM BLUE
140 PRINT TAB(8) "{C4}{DOWN}PAR AFTER THIS HOLE:";Q:REM GRAY 1
160 IF E>0 THEN PRINT TAB(10) "{WHT}{DOWN}STROKES TAKEN:";E:REM WHITE
170 PRINT TAB(12) "{YEL}{DOWN}DISTANCE:";D:REM YELLOW
190 IF D=0 THEN GOTO 390
200 IF D<20 THEN GOTO 320
210 PRINT TAB(3) "{BLK}{DOWN}WHICH CLUB WILL YOU USE (1-8)";:REM BLACK
220 INPUT C
230 IF C<1 OR C>8 THEN GOTO 210
240 R=INT(C*20*(RND(1)+.5))
250 GOSUB 500
260 PRINT TAB(11) "{RED}{DOWN}YOU HIT";R;"YARDS":REM RED
280 D=ABS(D-R)
290 E=E+1
295 T=T+1
300 FOR DD=1 TO 3000:NEXT DD
310 GOTO 110
320 PRINT TAB(8) "{BLU}{DOWN}YOU ARE ON THE GREEN":REM BLUE
340 FOR DD=1 TO 3000:NEXT DD
350 Z=INT(D/6)+1
360 PRINT TAB(10) "{BLK}{DOWN}YOU TOOK";Z;"PUTTS":REM BLACK
380 E=E+Z
390 T=T+Z
400 PRINT TAB(9) "{WHT}{DOWN}NUMBER OF STROKES";E:REM WHITE
410 FOR DD=1 TO 2500:NEXT DD
420 NEXT H
425 PRINT "{CLR}":POKE 53280,5:POKE 53281,13
430 PRINT TAB(10) "{BLK}{6 DOWN}YOU SCORE IS";T:REM BLACK
440 PRINT TAB(4) "{2 DOWN}THE PAR FOR THIS COURSE WAS";Q
450 PRINT TAB(3) "{2 DOWN}DO YOU WANT ANOTHER ROUND (Y/N)";
460 INPUT A$
470 IF A$="N" THEN END
480 RUN
500 POKE 54296,15
510 POKE 54277,17:POKE 54278,17
520 POKE 54276,17
540 POKE 54273,12:POKE 54272,216
545 FOR DD=1 TO 50:NEXT DD
550 POKE 54277,0:POKE 54278,0:POKE 54276,0
560 RETURN
