10 REM WARLOCK'S CASTLE
15 REM "{DOWN}" IS DOWN CURSOR
20 GOSUB 960
30 PRINT "{CLR}":REM CLEAR SCREEN
40 DIM A(30)
50 DIM B(30)
60 G=3
70 FOR Z=0 TO 30
80 A(Z)=0
90 NEXT Z
100 FOR Z=0 TO 10
110 A=INT(30*RND(1))+1
120 IF A(A)=1 THEN 110
130 A(A)=1
140 B(Z)=A+11
150 NEXT Z
155 IF B(10)=0 THEN B(10)=INT(25*RND(1))+1
160 PRINT "{4 DOWN}" TAB(10) "{BLU}THERE IS MAGIC HERE":REM BLUE
170 FOR DD=1 TO 1000:NEXT DD
180 X=(INT(16*RND(1))+1)*2+10
190 PRINT "{DOWN}" TAB(5) "YOU ARE BEING SENT TO ROOM";X:REM CURSOR DOWN
200 FOR DD=1 TO 1000:NEXT DD
210 Y=7
220 PRINT "{CLR}":REM CLEAR SCREEN
230 PRINT "{DOWN}" TAB(8) "{BLU}YOU ARE NOW IN ROOM #";X:REM BLUE
240 A=-1
250 FOR Z=0 TO 15
260 IF B(Z)=X THEN A=INT(Z/5)
270 NEXT Z
280 IF A=0 THEN 160
295 IF A<>1 THEN GOTO 350
300 FOR DD=1 TO 1000:NEXT DD
310 PRINT "{2 DOWN}{BLK}" TAB(9) "THERE ARE TROLLS HERE":REM BLACK
320 FOR DD=1 TO 1000:NEXT DD
330 PRINT "{2 DOWN}" TAB(9) "THEY HAVE KILLED YOU"
340 GOTO 770
350 IF A=2 AND RND(1)>.4 THEN PRINT "{3 DOWN}YOU HAVE FOUND THE WARLOCK":GOTO 690
375 F=X-1:Q=X+1:H=X+Y
376 IF F<1 THEN F=1
377 IF Q<1 THEN Q=1
378 IF H<1 THEN H=1
380 PRINT "{DOWN}" TAB(4) "YOU CAN MOVE TO: #";F;"#";Q;"#";H
390 FOR Z=0 TO 2
400 A(Z)=0
410 NEXT Z
420 FOR Z=0 TO 10
430 D=B(Z)-X
440 IF ABS(D)=1 OR D=Y THEN A(INT(Z/5))=1
450 NEXT Z
460 D=ABS(D)
470 IF D=2 OR D=6 OR D=8 THEN A(2)=1
480 IF A(0)=1 THEN PRINT "{DOWN}{YEL}" TAB(5) "*** THERE IS MAGIC NEARBY ***":REM YELLOW
490 IF A(1)=1 THEN PRINT "{DOWN}{BLK}" TAB(4) "*** THERE ARE TROLLS NEARBY ***"
495 REM BLACK
500 IF A(2)=1 THEN PRINT "{DOWN}{C4}" TAB(5) "*** THE WARLOCK IS NEARBY ***":REM GRAY 1
510 A=2:IF RND(1)>.8 THEN PRINT "{DOWN}{RED}" TAB(8) "YOU HAVE FOUND AN ARROW":G=G+1
515 REM RED
520 PRINT "{DOWN}{BLU}" TAB(3) "WHICH ROOM DO YOU WISH TO ENTER";:REM BLUE
530 INPUT M
540 PRINT "{CLR}":REM CLEAR SCREEN
550 FOR DD=1 TO 1000:NEXT DD
560 IF M<0 THEN 610
570 IF M<1 OR M>45 THEN PRINT "{DOWN}" TAB(10) "CANNOT MOVE THERE":GOTO 530
580 X=M
590 Y=-Y
600 GOTO 220
610 IF RND(1)>.6 THEN 630
620 IF M=-B(10) THEN 860
630 G=G-1
640 PRINT "{3 DOWN}{WHT}" TAB(14) "YOU MISSED":REM WHITE
650 PRINT "{DOWN}" TAB(9) "YOU HAVE";G;"ARROWS LEFT"
660 FOR DD=1 TO 1000:NEXT DD
670 PRINT "{CLR}":REM CLEAR SCREEN
680 GOTO 230
690 FOR DD=1 TO 1500:NEXT:PRINT "{CLR}":REM CLEAR SCREN
700 X=INT(4*RND(1))+1
710 IF X=1 THEN Y$="TOAD"
720 IF X=2 THEN Y$="TREE"
730 IF X=3 THEN Y$="RABBIT"
740 IF X=4 THEN Y$="WORM"
745 FOR DD=1 TO 1000:NEXT DD
750 PRINT "{2 DOWN}{BLK}" TAB(3) "YOU HAVE BEEN TURNED INTO A ";Y$
770 GOSUB 2000
780 GOTO 780
860 IF A=2 AND RND(1)>.6 THEN G=G-1:GOTO 640
870 PRINT "{CLR}":REM CLEAR SCREEN
880 FOR DD=1 TO 1000:NEXT DD
890 PRINT TAB(6) "{5 DOWN}{BLK}YOU HAVE KILLED THE WIZARD":REM BLACK
900 GOSUB 3000
905 GOTO 905
910 FOR DD=1 TO 1000:NEXT DD
960 PRINT "{CLR}":REM CLEAR SCREEN
970 POKE 53280,2:POKE 53281,10
980 PRINT "{5 DOWN}":REM DOWN CURSOR
990 FOR X=1 TO 40:PRINT "{YEL}" CHR$(113);:NEXT:REM YELLOW
1000 PRINT "{2 DOWN}{BLK}" TAB(8) "### WARLOCK'S CASTLE ###":REM BLACK
1010 PRINT "{2 DOWN}"
1020 FOR X=1 TO 40:PRINT "{YEL}" CHR$(113);:NEXT:REM YELLOW
1030 GOSUB 4000
1040 RETURN
2000 POKE 54296,15
2010 POKE 54277,17:POKE 54278,130
2020 POKE 54276,33
2030 POKE 54273,4:POKE 54272,73
2040 FOR DD=1 TO 1000:NEXT
2050 POKE 54273,5:POKE 54272,185
2060 FOR DD=1 TO 750:NEXT
2070 POKE 54273,3:POKE 54272,155
2080 FOR DD=1 TO 1500:NEXT
2090 POKE 54276,0:POKE 54277,0:POKE 54278,0
2100 RETURN
3000 POKE 54296,15:POKE 54277,130:POKE 54278,130
3010 POKE 54276,17
3020 X=2:Y=100:XX=1:YY=10
3030 POKE 54273,X:POKE 54272,Y
3040 FOR DD=1 TO 175:NEXT
3045 T=T+1
3050 X=X+XX:Y=Y+YY
3060 IF T=9 THEN YY=-YY:XX=-XX
3065 IF T>16THEN POKE 54276,0:POKE 54277,0:POKE 54278,0:END
3070 GOTO 3030
4000 POKE 54296,15
4010 POKE 54277,20:POKE 54278,130
4020 POKE 54276,33
4040 POKE 54273,3:POKE 54272,54
4045 FOR DD=1 TO 500:NEXT
4050 POKE 54273,3:POKE 54272,155
4055 FOR DD=1 TO 500:NEXT
4060 POKE 54273,2:POKE 54272,220
4070 FOR DD=1 TO 750:NEXT
4080 POKE 54273,3:POKE 54272,54
4085 FOR DD=1 TO 1000:NEXT
4090 POKE 54277,0:POKE54278,0:POKE54276,0
4100 RETURN
