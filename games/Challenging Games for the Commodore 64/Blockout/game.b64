5 REM BLOCKOUT, BY G. RYAN
10 GOTO 205
15 PRINT BR$:FOR Z=1 TO 21:PRINT " {C+}";TAB(38);"{C+}":NEXT:PRINT BR$;"{HOME}":RETURN
16 REM "{C+}" IS ON + KEY "{HOME}" IS HOME CURSOR
20 GET K$:D%=ASC(K$+Z$):P%=1:B%=B%+1:PRINT "{HOME}{C3}{DOWN}{11 RIGHT}";B%
21 REM HOME LT RED DOWN CURSOR RIGHT CURSOR
22 IF K%(D%) THEN LK%=K%(D%)
25 X1%=X1%+LK%:TA%=SM%+X1%:LC%=PR%-(PEEK(TA%)=32):POKE TA%,C1%
27 POKE CM+X1%,L1%:ON LC% GOTO90,20,85,30
30 D%=PEEK(J) AND15:P%=2:B%=B%+1:PRINT "{HOME}{C3}{DOWN}{11 RIGHT}";B%:REM SAME AS LINE 20
32 IF J%(D%) THEN LJ%=J%(D%)
35 X2%=X2%+LJ%:TA%=SM%+X2%:LC%=PR%-(PEEK(TA%)=32):POKE TA%,C2%
37 POKE CM+X2%,L2%:ON LC% GOTO90,30,85,20
40 GET K$:X%=ASC(K$+CHR$(0)):G%=1-((X%=134)+2*(X%=135)+3*(X%=133)+4*(X%=136))
43 ON G% GOTO 40,45,50,55,60
45 PL%=PL%+1:PL%=-(PL%<3)*PL%:T%=1809:P$=PL$(PL%):C%=13:GOSUB 65:GOTO 40
50 R%=R%+1:R%=-(R%<5)*R%:T%=1894:RM%=RN%(R%):P$=STR$(RM%)+"  "
52 C%=13:GOSUB 65:GOTO 40
55 HS%=0:T%=1081:P$="  00  ":C%=10:GOSUB65:GOTO 40
60 RETURN
65 FOR Q=1 TO LEN(P$):A%=ASC(MID$(P$,Q,1)):POKE T%+Q-1,A%+64*(A%>63)
67 POKE CM-1024+T%+Q-1,C%:NEXT :RETURN
70 GET K$:IF ASC(K$+CHR$(0))=136 THEN 160
75 IF VAL(TI$)<>T THEN 70
80 RETURN
85 PR%=1:ON P% GOTO 30,20
90 S%(P%)=S%(P%)+B%:IF S%(1)>HS% THEN HS%=S%(1)
95 IF S%(2)>HS% THEN HS%=S%(2)
100 TI$="000000":T=3:GOSUB 70:GOTO 120
105 PRINT "{CLR}{C6}PLR <1>{YEL}  BONUS{C1}  HI SCR{BLU}  ROUND{C6}  PLR <2>"
106 REM CLEAR SCREEN LT GREEN YELLOW ORANGE BLUE LT GREEN
107 L%=4:D%=S%(1):GOSUB 115
110 L%=20:D%=HS%:GOSUB 115:L%=27:D%=RN%:GOSUB 115
112 L%=35:D%=S%(2):GOSUB 115:RETURN
115 PRINT "{HOME}{C3}{DOWN}"TAB(L%-INT(LEN(STR$(D%))/2))D%:RETURN
120 GOSUB 105:PRINT "{HOME}{5 DOWN}";TAB(11);"{C7}BONUS FOR ROUND";RN%;"{DOWN}"
121 REM HOME LT RED DOWN CURSOR LT BLUE
122 PRINT TAB(12);"GOES TO PLAYER";P%
125 PRINT "{C6}{2 DOWN}":PRINT TAB(15)B%" POINTS":TI$="000000":T=4:GOSUB 70
126 REM LT GREEN DOWN CURSOR
130 B%=100:RN%=RN%+1:LK%=1:LJ%=-1:IF RN%> RM% THEN 200
135 X1%=528:X2%=551:GOSUB 105:GOSUB 15:ON PL% GOTO 140,145,150
140 PR%=3:GOTO 20
145 PR%=1:GOTO 30
150 PR%=1:GOTO 20
155 S%(1)=0:S%(2)=0:RN%=0:PL%=PL%+1:GOTO 130
160 PL%=0:R%=0:RN%=0:GOSUB 105
161 PRINT "{HOME}{5 DOWN}{PUR} {38 C+}":REM HOME DOWN PURPLE
162 PRINT " {C+}";TAB(38);"{C+}"
165 PRINT " {C+}  {3 C+}  {C+}         {C+}            {C+}  {C+}  {C+}"
167 PRINT " {C+}  {C+}  {C+} {C+}         {C+}            {C+}  {C+}  {C+}"
170 PRINT " {C+}  {3 C+}  {C+}         {C+}            {C+}  {C+}  {C+}"
172 PRINT " {C+}  {C+}  {C+} {C+}  {2 C+}  {2 C+} {C+} {C+}  {2 C+} {C+} {C+} {3 C+} {C+}  {C+}"
175 PRINT " {C+}  {C+}  {C+} {C+} {C+} {C+} {C+}   {2 C+}  {C+} {C+} {C+} {C+}  {C+}     {C+}"
180 PRINT " {C+}  {3 C+}  {C+} {3 C+} {3 C+} {C+} {C+} {3 C+} {2 C+}   {C+}  {C+}  {C+}"
182 PRINT " {C+}"TAB(38);"{C+}"
185 PRINT " {20 C+}BY G.RYAN, 1983{3 C+}"
187 PRINT "{2 DOWN}     {C1}{RVS}F1{OFF}  {C7}CLEAR HIGH SCORE"
188 REM DOWN CURSOR ORANGE REV ON LT BLUE REV OFF
190 PRINT "{DOWN}     {C1}{RVS}F3{OFF}  {C7}PLAYER OPTIONS: 2 PLAYERS"
192 PRINT "{DOWN}     {C1}{RVS}F5{OFF}  {C7}SELECT NO. OF ROUNDS: 5"
195 PRINT "{DOWN}     {C1}{RVS}F7{OFF}  {C7}START GAME/ABORT GAME":GOSUB 40:GOTO 240
200 PRINT "{HOME}{15 DOWN}"TAB(26)"{YEL}GAME OVER":TI$="000000":T$="09":GOSUB 70
201 REM HOME CURSOR DOWN YELLOW
202 PL%=0:R%=0:RN%=0:GOTO 160
205 DIM K%(255),J%(15),RN%(4),S%(2),PL%(2):J%(14)=-40:J%(13)=40:J%(5)=1
207 J%(6)=1:J%(7)=1:J%(9)=-1:J%(10)=-1:J%(11)=-1
210 K%(65)=-40:K%(97)=-40:K%(83)=-40:K%(115)=-40:K%(62)=1:K%(46)=1
212 K%(88)=40:K%(120)=40:K%(67)=40:K%(99)=40:K%(44)=-1:K%(60)=-1
215 RN%(0)=5:RN%(1)=10:RN%(2)=20:RN%(3)=50:RN%(4)=100
220 SM%=1024:CM=55296:J=56230:C1%=160:C2%=160:L1%=2:L2%=6:RM%=5:Z$=CHR$(0)
225 BR$="{PUR} {38 C+}":PL$(0)="2 PLAYERS"
226 REM PURPLE
227 PL$(1)="1 J'STICK":PL$(2)="1 K'BOARD"
230 POKE 53274,0:POKE 53265,PEEK(53265) AND 159:POKE 53269,0
232 POKE 53270,PEEK(53270) AND 15:POKE 53272,21
235 POKE 53280,0:POKE 53281,0:PRINT CHR$(9);CHR$(142);CHR$(8):GOTO 160
240 GOTO 155
