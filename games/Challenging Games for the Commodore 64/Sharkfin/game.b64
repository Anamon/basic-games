10 REM USS SHARKFIN
20 GOSUB 1000
30 PRINT "{CLR}":REM CLEAR SCREEN
35 POKE 53280,2:POKE 53281,6
40 GOSUB 870
50 GOSUB 670
60 IF L<0 THEN GOTO 430
70 PRINT:PRINT "{DOWN}" TAB(2) "WHAT IS YOUR ORDER, SIR"
80 L=L-1
90 PRINT "{DOWN}" TAB(2) "N:S:E:W:T(ORPEDO):F(ORWARD):B(ACK)";
100 INPUT Z$
110 IF Z$="T" THEN GOSUB 280
120 IF Z$="N" THEN X=X-1
130 IF Z$="S" THEN X=X+1
140 IF Z$="E" THEN Y=Y+1
150 IF Z$="W" THEN Y=Y-1
160 IF Z$="F" THEN Z=Z-1
170 IF Z$="B" THEN Z=Z+1
180 GOSUB 510
190 A=A+INT(3*RND(1))-INT(3*RND(1))
200 IF A<1 OR A>10 THEN GOTO 190
210 B=B+INT(3*RND(1))-INT(3*RND(1))
220 IF B<1 OR B>10 THEN GOTO 210
230 C=C+INT(3*RND(1))-INT(3*RND(1))
240 IF C<1 OR C>10 THEN GOTO 230
260 GOTO 50
270 L=L-2
280 IF ABS(A-X)>3 OR ABS(B-Y)>3 OR ABS(C-Z)>3 THEN GOSUB 970
290 FOR DD=1 TO 1500:NEXT DD
310 IF ABS(A-X)>3 OR ABS(B-Y)>3 OR ABS(C-Z)>3 THEN RETURN
320 PRINT "{DOWN}" TAB(4) "{WHT}ORDER TO FIRE UNDERSTOOD, SIR":REM WHITE
330 FOR DD=1 TO 1000:NEXT DD
340 PRINT "{DOWN}" TAB(10) "TORPEDO RUNNING"
350 IF RND(1)>0.7 THEN GOTO 400
355 PRINT "{DOWN}" TAB(5):FOR TT=1 TO 6
360 PRINT ">";
365 FOR DD=1 TO 500:NEXT
370 NEXT TT
380 PRINT "{BLK}MISSED":FOR DD=1 TO 500:NEXT DD:REM BLACK
390 GOTO 420
400 PRINT "{DOWN}" TAB(5):FOR TT=1 TO 6
405 PRINT ">";
410 FOR DD=1 TO 500:NEXT DD
412 NEXT TT
415 PRINT "ENEMY SUBMARINE DESTROYED":GOTO 3000
420 RETURN
430 FOR DD=1 TO 2000:NEXT DD
432 POKE 53280,0:POKE 53281,7
435 PRINT "{CLR}{10 DOWN}" TAB(10) "{BLK}THE GAME HAS ENDED":REM CLR SCRN DOWN CURSOR
436 REM BLACK
440 IF TY=0 THEN PRINT "{DOWN}" TAB(5) "YOU HAVE STAYED UNDER TOO LONG"
460 IF L<=0 THEN PRINT "{DOWN}" TAB(7) "ALL YOUR FUEL HAS BEEN USED"
470 GOTO 2000
480 PRINT "{DOWN}" TAB(6) "YOU HAVE COLLIDED WITH THE ENEMY SUBMARINE"
500 GOTO 2000
510 IF ABS(A-X)>3 OR ABS(B-Y)>3 OR ABS(C-Z)>3 THEN RETURN
520 IF RND(1)>0.75 THEN RETURN
530 PRINT "{CLR}":PRINT "{DOWN}" TAB(15) "{C1}WARNING":REM CLEAR SCREEN ORANGE DOWN CURSOR
535 PRINT "{DOWN}" TAB(6) "THE ENEMY IS FIRING AT US"
538 GOSUB 2500
540 FOR DD=1 TO 1000:NEXT DD
560 IF RND(1)>0.7 THEN GOTO 630
570 PRINT "{DOWN}" TAB(6) "{WHT}THE ENEMY HAS HIT US SIR":REM WHITE
580 DP=DP-15
590 FOR DD=1 TO 1500:NEXT DD
600 IF L<0 THEN GOTO 470
605 IF DP<0 THEN GOTO 3500
620 RETURN
630 PRINT "{DOWN}" TAB(6) "{BLK}THE ENEMY MISSED US, SIR":REM BLACK
640 FOR DD=1 TO 2000:NEXT DD
660 RETURN
670 PRINT "{CLR}":REM CLEAR SCREEN
680 PRINT "{DOWN}" TAB(6) "{YEL}FUEL REMAINING:";L:REM YELLOW
690 TY=TY-1
700 IF TY=0 THEN GOTO 430
710 PRINT "{DOWN}" TAB(6) "TIME:";TY
720 IF L<3 THEN PRINT "{DOWN}" TAB(4) "FUEL IS RUNNING LOW"
740 PRINT "{DOWN}" TAB(6) "YOU ARE AT";X;" ";Y;" ";Z
750 IF A=X AND B=Y AND C=Z THEN GOTO 480
760 PRINT TAB(6) "{DOWN}THE ENEMY IS ";
770 IF A<>X OR B<>Y THEN PRINT TAB(6) "TO THE "
775 PRINT
778 PRINT TAB(6)
780 IF A<X THEN PRINT "NORTH";
790 IF A>X THEN PRINT "SOUTH";
800 IF B>Y THEN PRINT "EAST";
810 IF B<Y THEN PRINT "WEST";
830 IF C>Z THEN PRINT " BEHIND YOU"
840 IF C<Z THEN PRINT " IN FRONT OF YOU"
850 RETURN
870 L=15+INT(20*RND(1))+1
875 DP=25+INT(30*RND(1))+1
880 T=0
890 TY=35
900 A=INT(10*RND(1))+1
910 B=INT(10*RND(1))+1
920 C=INT(10*RND(1))+1
930 X=INT(10*RND(1))+1
940 Y=INT(10*RND(1))+1
950 Z=INT(10*RND(1))+1
960 RETURN
970 FOR DD=1 TO 1000:NEXT DD
980 PRINT "{DOWN}" TAB(10) "{C3}### OUT OF RANGE ###":REM LT RED
990 RETURN
1000 POKE 53280,6:POKE 53281,14
1005 PRINT "{CLR}":REM CLEAR SCREEN
1010 PRINT "{10 DOWN}":REM DOWN CURSOR
1020 PRINT TAB(10) "{BLK}### {YEL}USS SHARKFIN {BLK}###":REM BLACK YELLOW BLACK
1030 POKE 54296,15:POKE 54277,17:POKE 54278,17
1040 POKE 54276,33:TT=0
1050 POKE 54273,2:POKE 54272,220
1060 FOR DD=1 TO 350:NEXT DD
1070 POKE 54273,3:POKE 54272,103
1080 FOR DD=1 TO 350:NEXT DD
1090 IF TT=4 THEN POKE 54276,0:POKE 54277,0:POKE 54278,0:TT=0:RETURN
1100 TT=TT+1:GOTO 1050
2000 GOSUB 1030
2010 GOTO 2010
2500 POKE 54296,15:TT=0
2505 POKE 54277,17:POKE 54278,17:POKE 54276,65
2510 POKE 54275,0:POKE 54274,255
2520 POKE 54273,14:POKE 54272,107
2530 FOR DD=1 TO 100:NEXT
2540 POKE 54276,0:POKE 54277,0:POKE 54278,0
2550 FOR DD=1 TO 50:NEXT
2560 IF TT>6 THEN RETURN
2570 TT=TT+1:GOTO 2505
3000 FOR DD=1 TO 2000:NEXT DD:PRINT "{CLR}"
3006 PRINT "{5 DOWN}"
3007 FOR GG=1 TO 40:PRINT "{YEL}" CHR$(113);:NEXT GG:REM YELLOW
3010 PRINT:PRINT "{2 DOWN}" TAB(12) "{CYN}YOU HAVE WON!!":REM CYAN
3012 PRINT "{2 DOWN}":FOR GG=1 TO 40:PRINT "{YEL}" CHR$(113);:NEXT GG:REM YELLOW
3020 POKE 54296,15
3030 POKE 54277,34:POKE 54278,130
3040 POKE 54276,17
3050 HF=54273:LF=54272
3060 POKE HF,57:POKE LF,172
3070 FOR DD=1 TO 125:NEXT DD
3080 POKE HF,64:POKE LF,118
3090 FOR DD=1 TO 125:NEXT DD
3100 POKE HF,51:POKE LF,97
3110 FOR DD=1 TO 125:NEXT DD
3120 POKE HF,57:POKE LF,172
3130 FOR DD=1 TO 500:NEXT DD
3140 POKE 54276,0:POKE 54277,0:POKE 54278,0
3200 GOTO 3200
3500 FOR DD=1 TO 1000:NEXT
3510 PRINT "{CLR}":REM CLEAR SCREEN
3520 POKE 53280,0:POKE 53281,2
3530 PRINT "{10 DOWN}"
3540 PRINT TAB(8) "{YEL}### YOU HAVE BEEN SUNK ###":REM YELLOW
3550 GOSUB 1030
3560 GOTO 3560
