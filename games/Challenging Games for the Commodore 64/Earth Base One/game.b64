5 REM EARTH BASE ONE
7 PRINT "{CLR}":REM CLEAR SCREEN
9 POKE 53280,0:POKE 53281,11
10 PRINT "{8 DOWN}" TAB(8) "{C1}#*#  EARTH BASE ONE  #*#":REM ORANGE
20 PRINT "{5 DOWN}" TAB(9) "PRESS ANY KEY TO BEGIN"
30 GET A$:IF A$="" THEN 30
40 PRINT "{CLR}":REM CLEAR SCREEN
50 DIM A(20),A%(20):N=1:LI=5
60 PRINT "{CLR}{HOME}";TAB(5) "{RVS}{PUR}SCORE:";S;TAB(25) "LIVES:";LI:IF N>20 THEN N=20
61 REM CLEAR SCREEN HOME REV ON PURPLE
63 FOR I=1 TO 10:A(I)=1070+(I*2):A%(I)=1:NEXT:H=0:NL=N:B=0
65 FOR I=11 TO 20:A(I)=1186+(I*2):A%(I)=1:NEXT
70 X=1954:CO=54272
80 FOR I=1 TO N:POKE A(I),88:POKE A(I)+CO,1:NEXT
90 FOR SH=1 TO N:IF A%(SH)=0 THEN 250
95 FOR U=1 TO 100/NL:NEXT
100 K=PEEK(197)
105 IF K<>64 THEN X1=X:X=X+(K=42ANDX>1944)-(K=50ANDX<1983):POKE X1,32
110 POKE X,65:POKE X+CO,0
120 IF PEEK(197)=10 AND B=0 THEN Y=X-40:POKE Y,93:POKE Y+CO,0:B=1
130 POKE A(SH),32:A(SH)=A(SH)+40:IF PEEK(A(SH))=93 THEN 500
132 IF A(SH)>1944 THEN 1000
135 POKE A(SH),88:POKE A(SH)+CO,1
140 IF B=1 THEN Y1=Y:Y=Y-40:IF PEEK(Y)=88 THEN 500
150 IF B=1 THEN POKE Y,93:POKE Y+CO,1:POKE Y1,32
160 IF B=1 THEN IF Y<1103 THEN B=0:POKE Y,32
250 NEXT:FOR DD=1 TO 15:NEXT:GOTO 90
500 FOR G=1 TO 20:IF A(G)=Y THEN A%(G)=0
505 NEXT
510 C=Y+CO:POKE Y,160:POKE C,1:POKE Y-1,160:POKE C-1,1:POKE Y+1,160:POKE C+1,1
520 POKE Y+40,160:POKE C+40,1:POKE Y-40,160:POKE C-40,1
540 S=S+50
550 POKE C,0:POKE C-1,0:POKE C+1,0:POKE C+40,0:POKE C-40,0
570 NL=NL-1:B=0:H=H+1:IF H=N THEN N=N+1:GOTO 60
580 GOTO 250
1000 PRINT "{HOME}{8 DOWN}":REM HOME DOWN CURSOR
1010 PRINT TAB(10) "{RVS}{YEL}AN ALIEN HAS LANDED{OFF}":REM REV ON YELLOW REV OFF
1020 LI=LI-1:IF LI=0 THEN 1100
1030 FOR DD=1 TO 2000:NEXT DD:GOTO 60
1100 FOR DD=1 TO 1500:NEXT DD
1110 PRINT "{HOME}{8 DOWN}"
1120 PRINT TAB(10) "{RVS}{GRN}  THE GAME IS OVER  {OFF}":REM REV ON GATNEEN REV OFF
1130 FOR DD=1 TO 1500:NEXT DD
1140 PRINT TAB(11) "{DOWN}{C1}ANOTHER GAME (Y/N)":REM ORANGE
1150 POKE 198,0
1160 GET A$:IF A$="" THEN GOTO 1160
1170 IF A$="Y" THEN RUN
1180 PRINT "{CLR}":END:REM CLEAR SCREEN
