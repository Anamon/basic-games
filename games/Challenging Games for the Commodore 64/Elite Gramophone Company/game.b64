10 REM ELITE GRAMOPHONE COMPANY
20 PRINT "{CLR}":REM CLEAR SCREEN "{DOWN} IS DOWN CURSOR
30 GOSUB 170
40 GOSUB 1500
50 WE=WE+1
60 GOSUB 950
70 GOSUB 1220
80 PRINT "{CLR}"
90 GOSUB 950
100 GOSUB 1100
110 GOSUB 950
120 GOSUB 790
130 GOSUB 360
140 CA=CA-INT(WG*WF)
150 GOTO 50
170 PRINT "{CLR}"
180 POKE 53280,8:POKE 53281,14
190 PRINT "{8 DOWN}"
200 PRINT "{RED}{RIGHT}*****  ELITE GRAMOPHONE COMPANY *****{C7}":REM RED RIGHTCRSR LT BLUE
210 FOR DD=1 TO 500:NEXT DD
220 POKE 53280,0:POKE 53281,3
230 GOSUB 2200
240 FOR DD=0 TO 15:POKE 53280,DD
250 FOR TT=1 TO 200:NEXT TT
260 NEXT DD
270 PRINT "{CLR}"
280 POKE 53280,6:POKE 53281,2
290 RETURN
360 REM UNPREDICTALES
370 PRINT "{CLR}"
380 IF RND(1)>0.45 THEN GOTO 460
390 A=INT(10*RND(1))+1
395 GOSUB 2000
400 PRINT "{5 DOWN}" TAB(2) "UNIONS DEMAND A";A;"% PAY INCREASE"
410 WG=INT(100*(WG+(A*WG/100)))/100
420 GOSUB 1620
430 PRINT "{DOWN}" TAB(2) "PAY PER EMPLOYEE IS NOW $";WG
440 GOSUB 1620
450 PRINT "{CLR}"
460 IF RND(1)<0.80 GOTO 560
470 GOSUB 2000
475 PRINT "{5 DOWN}" TAB(4) "FIRE DESTROYS SOME OF YOUR STOCK"
480 PRINT "{DOWN}" TAB(6) "STANDBY FOR DAMAGE REPORT..."
490 GOSUB 1620
500 A=INT(SK/2*RND(1)*2)+1
505 IF A>SK THEN GOTO 500
510 SK=SK-A
520 PRINT "{CLR}":REM CLEAR SCREEN
525 PRINT "{5 DOWN}" TAB(4) "THERE WERE";A;"GRAMOPHONES DESTROYED"
527 PRINT "{DOWN}" TAB(4) "VALUE OF STOCK DESTROYED WAS $";A*SP
530 GOSUB 1620
540 PRINT "{DOWN}" TAB(4) "STOCK ON HAND IS NOW";SK
550 GOSUB 1620
560 IF RND(1)>0.3 THEN GOTO 670
565 PRINT "{CLR}":GOSUB 2000
570 PRINT "{5 DOWN}" TAB(4) "SUPPLIER ANNOUNCES PRICE RISE"
585 GOSUB 1620
590 A=INT(100*CT/7*RND(1))/100
595 IF A<.01 THEN GOTO 590
610 PRINT "{DOWN}" TAB(2) "THE COST OF MAKING GRAMOPHONES HAS"
620 PRINT "{DOWN}" TAB(2) "GONE UP BY $";A;"EACH"
630 FOR DD=1 TO 1000:NEXT DD
640 CT=CT+A
650 PRINT "{DOWN}" TAB(2) "IT NOW COSTS $";CT;"TO MAKE EACH ONE"
660 GOSUB 1620
670 IF RND(1)<0.65 AND MP<SP THEN RETURN
680 PRINT "{CLR}"
685 GOSUB 2000
690 PRINT "{5 DOWN}" TAB(2) "YOU HAVE AN OPPORTUNITY TO RAISE YOUR"
692 PRINT "{DOWN}" TAB(16) "PRICE"
695 PRINT "{DOWN}" TAB(2) "GRAMOPHONES NOW SELL FOR $";SP
700 GOSUB 1620
710 PRINT "{DOWN}" TAB(2) "STATE THE PERCENTAGE OF YOUR PRICE"
715 PRINT "{DOWN}" TAB(14) "INCREASE";
720 INPUT A
730 IF A>0 THEN Z=Z+A
740 SP=INT(100*(SP+A*SP/100))/100
750 GOSUB 1620
760 PRINT "{CLR}":PRINT "{5 DOWN}" TAB(4) "GRAMOPHONES NOW SELL FOR $";SP
770 GOSUB 1630
780 RETURN
790 REM SALE
800 PRINT "{DOWN}" TAB(4) "STOCK ON HAND IS:-";SK
810 GOSUB 1620
820 PRINT "{CLR}"
825 GOSUB 2000
830 PRINT "{5 DOWN}" TAB(8) "STANDBY FOR SALES REPORT"
840 GOSUB 1620
850 PRINT "{CLR}":FOR DD=1 TO 1000:NEXT DD
860 A=INT(RND(1)*SK/(Z/100))+1
870 IF A>SK THEN GOTO 860
880 GOSUB 2000
890 PRINT "{2 DOWN}" TAB(4) "TOTAL GRAMOPHONES SOLD..";:FOR DD=1 TO 1000:NEXT:PRINT A
900 SK=SK-A
910 PRINT "{2 DOWN}" TAB(4) "INCOME FROM SALES: $";A*SP
920 CA=CA+INT(A*SP)
930 GOSUB 1620
940 RETURN
950 REM PRINTOUT
960 PRINT "{CLR}"
970 IF CA+SK<1 THEN GOTO 1370
980 IF CA+SK>999999 THEN GOTO 1640
985 GOSUB 2000
990 PRINT TAB(8) "{C7}FACTORY REPORT: WEEK";WE:REM LIGHT BLUE
1000 PRINT "{DOWN}CAPITAL ON HAND IS $";CA
1010 PRINT "{DOWN}STOCK ON HAND";SK;"GRAMOPHONES"
1015 PRINT "{DOWN}WORTH $";SK*SP
1020 PRINT "{DOWN}THEY SELL FOR $";SP;"EACH"
1030 PRINT "{DOWN}AND COST $";CT;"EACH TO MAKE"
1040 PRINT "{DOWN}WORKFORCE IS";WF;"PEOPLE"
1050 PRINT "{DOWN}THEIR WAGES ARE $";WG;"EACH"
1060 PRINT "{DOWN}THIS WEEKS WAGES BILL IS $";WG*WF
1070 PRINT "{DOWN}EACH PERSON CAN MAKE";PD;"GRAMOPHONES"
1080 PRINT "{DOWN}A TOTAL OUTPUT OF";PD*WF;"GRAMOPHONES
1090 RETURN
1100 PRINT "{DOWN}HOW MANY GRAMOPHONES DO YOU WISH TO MAKE"
1105 INPUT MK
1110 IF MK=0 THEN RETURN
1120 IF MK*CT>CA THEN PRINT "{DOWN}NOT ENOUGH CAPITAL TO MAKE THAT MANY":GOTO 1100
1130 IF MK>PD*WF THEN PRINT "{DOWN}NOT ENOUGH WORKERS TO MAKE THAT MANY":GOTO 1100
1140 PRINT "{CLR}":PRINT "{5 DOWN}" TAB(3) "PRODUCTION TARGET FOR WEEK";WE;"IS";MK;
1150 MK=ABS(MK-(INT(RND(1)*2+1))*INT(RND(1)*10))
1160 GOSUB 1620
1170 PRINT:PRINT "{DOWN}" TAB(6) "TOTAL MADE IN WEEK";WE;"WAS";MK
1180 SK=SK+MK
1190 CA=INT(CA-CT*MK)
1200 GOSUB 1620
1210 RETURN
1220 REM PEOPLE
1230 PRINT "{DOWN}HOW MANY PEOPLE DO YOU WISH TO HIRE":
1235 INPUT A
1240 WF=WF+A
1250 PRINT "{CLR}":PRINT "{5 DOWN}" TAB(6) "TOTAL WORKFORCE IS NOW";WF
1260 GOSUB 1630
1270 IF A<>0 THEN GOTO 1350
1280 PRINT "{DOWN}HOW MANY PEOPLE DO YOU WISH TO FIRE";
1285 INPUT A
1290 IF A=0 THEN GOTO 1350
1300 IF A>WF THEN GOTO 1280
1310 A=INT(RND(1)*A+1)
1320 GOSUB 1630
1330 PRINT "{DOWN}THE UNION WILL ONLY ALLOW YOU TO FIRE";A
1340 WF=WF-A
1350 GOSUB 1630
1360 RETURN
1370 REM BANKRUPTCY
1380 L=0
1385 GOSUB 2000
1390 PRINT "{CLR}":PRINT "{HOME}{8 DOWN}":REM CLEAR SCREEN HOME DOWN CURSOR
1392 POKE 53280,2:POKE 53281,0
1395 PRINT TAB(10) "{YEL}#####  BANKRUPT  #####":FOR DD=1 TO 750:NEXT DD:REM YELLOW
1400 L=L+1:IF L=10 THEN GOTO 1430
1405 POKE 53281,2:POKE 53280,0:FOR DD=1 TO 500:NEXT
1410 GOTO 1390
1430 PRINT "{CLR}":GOSUB 1620
1440 PRINT "{3 DOWN}YOU KEPT THE ELITE GRAMOPHONE COMPANY"
1445 PRINT "{DOWN}RUNNING FOR";WE;"WEEKS":FOR DD=1 TO 5000:NEXT DD
1450 PRINT "{CLR}":PRINT "{DOWN}" TAB(4) "DO YOU WANT TO TRY AGAIN (Y/N)"
1460 GET A$:IF A$="" THEN 1460
1470 IF A$="Y" THEN RUN
1490 PRINT "{CLR}":PRINT "{8 DOWN}" TAB(16) "GOODBYE"
1495 GOTO 1495
1500 REM VARIABLES
1510 CA=500+INT(500*RND(1))+1
1520 SK=100+INT(500*RND(1))+1
1530 SP=10+INT(5*RND(1))+1
1540 CT=2+INT(5*RND(1))+1
1550 IF CT>SP THEN GOTO 1530
1560 WF=7+INT(10*RND(1))+1
1570 WG=12+INT(RND(1)*(SP*5))
1580 PD=5+INT(5*RND(1))+1
1590 WK=1
1600 Z=1
1610 RETURN
1620 FOR DD=1 TO 2500:NEXT DD:RETURN
1630 FOR DD=1 TO 1500:NEXT DD:RETURN
1640 C=0
1650 PRINT "{CLR}":PRINT "{HOME}{8 DOWN}":REM CLEAR SCREEN HOME DOWN CURSOR
1653 POKE 53280,2:POKE 53281,4
1655 PRINT TAB(3) "{WHT}$$$$$  YOU'VE MADE A MILLION  $$$$$":REM WHITE
1660 C=C+1
1670 GOSUB 2000:FOR DD=1 TO 200:NEXT DD
1680 PRINT "{HOME}{8 DOWN}"
1690 PRINT TAB(3) "$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$":FOR DD=1 TO 250:NEXT DD
1700 IF C=10 GOTO 1450
1710 GOTO 1650
2000 R1=INT(50*RND(1))+4
2010 R2=INT(100*RND(1))+100
2020 POKE 54296,15
2030 POKE 54277,64:POKE 54278,130
2040 POKE 54276,17
2050 POKE 54273,R1:POKE 54272,R2
2060 FOR DD=1 TO 250:NEXT DD
2070 POKE 54276,0:POKE 54277,0:POKE 54278,0
2080 RETURN
2200 POKE 54296,15:A=54277:S=54278:W=54276
2210 POKE A,130:POKE S,130
2220 POKE W,128
2230 HF=54273:LF=54272
2240 POKE HF,14:POKE LF,107
2250 FOR DD=1 TO 350:NEXT DD
2260 POKE W,0:POKE S,0:POKE A,0
2265 FOR DD=1 TO 20:NEXT DD
2270 POKE A,15:POKE S,20:POKE W,128
2280 POKE HF,12:POKE LF,216
2290 FOR DD=1 TO 75:NEXT DD
2300 POKE W,0:POKE A,0:POKE S,0
2305 T=T+1
2310 FOR DD=1 TO 200:NEXT DD
2320 IF T=4 THEN T=0:RETURN
2330 GOTO 2200
