1 REM ASTEROID
2 REM "{CLR} IS CLER SCREEN "{DOWN}" IS CURSOR DOWN "{RVS}" IS REV ON "{OFF}" IS REV OFF
5 GOSUB 9000
10 DEF FNR(X)=INT(RND(1)*X)+1:GOSUB 3000
15 POKE 53280,6:POKE 53281,4
20 YR=YR+1:FK=FK+INT(FK/(2+FNR(18))-FK/(FNR(15)+3)):GOTO 710
30 PRINT "{DOWN}COMPUTER REPORT:"
40 IF O<OJ*FK THEN 8000
50 IF F<FR*FK THEN 8010
60 IF CA<1 THEN 8020
70 IF FK<2 THEN 8030
80 IF FK<13 THEN PRINT "{BLU}{DOWN}{RVS}WARNING: POPULATION IS NEARING CRITICAL":GOSUB 8700
83 REM BLUE
90 IF O<2*OJ*FK THEN PRINT "{BLU}{DOWN}{RVS}WARNING: OXYGEN SUPPLY LOW":GOSUB 8700:REM BLUE
100 IF F<2*FR*FK THEN PRINT "{BLU}{DOWN}{RVS}WARNING: FOOD STOCKS LOW":GOSUB 8700:REM BLUE
110 IF CA<2000 THEN PRINT "{BLU}{DOWN}{RVS}WARNING: CASH ON HAND RUNNING LOW":GOSUB 8700
112 REM BLUE
120 PRINT "{RED}{DOWN}THERE ARE";FK;"PEOPLE ON THE ASTEROID":REM RED
125 PRINT "{DOWN}IN THE YEAR";YR
130 PRINT:PRINT "{DOWN}CASH: $";CA:PRINT "{DOWN}MAINTENANCE COSTS: $";RE
140 PRINT "{DOWN}OXYGEN COSTS: $";OC;"PER UNIT"
150 PRINT "{DOWN}OXYGEN NEED PER PERSON:"OJ:PRINT "{DOWN}FOOD:";F;"UNITS"
160 PRINT "{BLK}{DOWN}{RVS}PRESS ANY KEY TO CONTINUE{RED}":REM BLACK RED
170 GET Q$:IF Q$="" THEN GOTO 170
180 RETURN
700 GOSUB 30
710 PRINT "{CLR}"
720 PRINT "{RED}{DOWN}HOW MANY TONS OF ORE WILL YOU TRADE?":REM RED
730 PRINT "{DOWN}OXYGEN USAGE WILL BE";AC;"PER TON"
735 PRINT "{DOWN}EACH TON SELLS FOR";AP;:INPUT B
740 IF B*AC>O THEN PRINT "{DOWN}NOT ENOUGH OXYGEN":FOR DD=1 TO 1500:NEXT:GOSUB 8700
745 IF B*AC>O THEN GOTO 720
750 CA=CA+B*AP:O=O-B*AC:PRINT "{CLR}":GOSUB 30
760 PRINT "{CLR}{DOWN}FOOD COSTS $";FC;"PER UNIT"
765 PRINT "{DOWN}EACH PERSON NEEDS";FR;"UNITS ($";FC*FR;"EACH)"
770 PRINT "{DOWN}$";FK*FC*FR;"FOR THE ASTEROID"
780 PRINT "{DOWN}THIS WILL LAST";INT(F/(FR*FK));"YEARS AT THE"
785 PRINT "{DOWN}PRESENT POPULATION"
790 INPUT "{DOWN}HOW MUCH FOOD WILL YOU BUY";C
795 IF C*FC>CA THEN PRINT "{DOWN}NOT ENOUGH MONEY":GOTO 790
800 F=F+C*FC:CA=CA-C*FC:PRINT "{CLR}":GOSUB 30
850 PRINT "{CLR}{DOWN}HOW MUCH OXYGEN WILL YOU BUY"
855 PRINT "{DOWN}PRESENT STOCKS WILL LAST FOR";INT(O/(OJ*FK))"YEARS"
860 PRINT "{DOWN}AT THE PRESENT LEVEL OF POPULATION";:INPUT D
865 IF D*OC>CA THEN FL=1
870 IF D*OC>CA THEN PRINT "{DOWN}NOT ENOUGH MONEY":FOR DD=1 TO 1500:NEXT:GOTO 850
880 PRINT "{CLR}":IF FNR(5)=2 THEN GOSUB 7000
2000 F=F-FK*FR:CA=CA-RE-D*OC:O=O+D-FK*OJ:GOTO 20
3000 Y=FNR(5):A$="{RVS}THE ASTEROID IS DEAD":FK=FNR(40)+80
3005 CA=INT(7*(RND(800)+700)/FNR(3))
3010 FC=FNR(7):AC=FNR(3)+1:F=FNR(500)+2000:G=2000+FNR(1500):AP=30*FNR(AC)
3020 RE=FNR(400)+200:FR=FNR(5)+1:OJ=FNR(3)+2:OC=FNR(7):O=2000-FNR(1500)
3030 RETURN
7000 J=FNR(6):PRINT "{DOWN}{RVS}THE STATION HAS BEEN ATTACKED BY"
7010 ON J GOTO 7020,7030,7040,7050,7060,7070
7020 PRINT "{DOWN}A FLEET OF SIRIAN SHIPS":GOTO 7080
7030 PRINT "{DOWN}RENEGADE SPACEMINERS":GOTO 7080
7040 PRINT "{DOWN}MARTIAN SPACE PIRATES":GOTO 7080
7050 PRINT "{DOWN}CALLISTAN CLAIM JUMPERS":GOTO 7080
7060 PRINT "{DOWN}A ROBOT CONTROLLED SURVEY SHIP":GOTO 7080
7070 PRINT "{DOWN}A VENUSIAN BATTLE CRUISER"
7080 FOR DD=1 TO 1500:NEXT DD
7083 Z=1+INT(FK/(FNR(15)+1)):PRINT "{DOWN}THERE WERE";Z;"PEOPLE KILLED"
7085 GOSUB 8600
7090 Z1=FNR(250)+250:PRINT "{DOWN}DAMAGE WAS $";Z1
7100 Z2=FNR(300):Z3=FNR(300):PRINT "{DOWN}FOOD STOCKS HAVE FALLEN BY";Z2
7110 F=F-Z2:FK=FK-Z:CA=CA-Z1
7120 PRINT "{DOWN}"Z3;"UNITS OF OYXGEN HAVE BEEN LOST":O=O-Z3
7130 PRINT "{DOWN}PRESS ANY KEY TO CONTINUE"
7140 GET P$:IF P$="" THEN GOTO 7140
7150 RETURN
8000 PRINT A$:PRINT "{DOWN}YOU RAN OUT OF OXYGEN IN YEAR";YR:GOTO 8500
8010 PRINT A$:PRINT "{DOWN}YOU RAN OUT OF FOOD IN YEAR";YR:GOTO 8500
8020 PRINT A$:PRINT "{DOWN}YOU WENT BANKRUPT IN YEAR";YR:GOTO 8500
8030 PRINT "{DOWN}YOUR POPULATION HAS FALLEN TO";FNR(26):CA=FNR(300)
8040 PRINT "{DOWN}DO YOU WISH TO GIVE UP NOW (1)
8050 PRINT "{DOWN}OR DO YOU WISH TO ATTEMPT TO TRADE"
8060 PRINT "{DOWN}OUT OF TROUBLE (2)"
8070 INPUT B:IF B=1 THEN 8500
8080 PRINT "{DOWN}I HOPE YOU HAVE MADE THE RIGHT DECISION":GOTO 20
8500 FOR DD=1 TO 3000:NEXT:PRINT "{CLR}"
8510 POKE 53280,0:POKE 53281,11
8520 PRINT "{8 DOWN}" TAB(15) "{BLK}FAREWELL":REM BLACK
8530 POKE 53296,15
8540 POKE 54277,17:POKE 54278,130
8550 POKE 54276,33
8560 POKE 54273,1:POKE 54272,205
8565 FOR DD=1 TO 500:NEXT DD
8570 POKE 54276,0:POKE54277,0:POKE 54278,0
8575 FOR DD=1 TO 20:NEXT DD
8580 T=T+1:IF T<6 THEN GOTO 8540
8590 T=0:GOTO 8590
8600 POKE 54296,15
8610 POKE 54277,24:POKE 54278,136
8620 POKE 54276,65:POKE 54275,4:POKE 54274,200
8630 POKE 54273,22:POKE 54272,227
8640 FOR DD=1 TO 250:NEXT DD
8650 POKE 54276,0:POKE 54277,0:POKE 54278,0
8660 RETURN
8700 POKE 54296,15
8710 POKE 54277,17:POKE 54278,17
8720 POKE 54276,33
8730 POKE 54273,19:POKE 54272,63
8740 FOR DD=1 TO 150:NEXT DD
8750 POKE 54276,0:POKE 54277,0:POKE 54278,0
8760 FOR DD=1 TO 5:NEXT DD
8770 T=T+1:IF T<6 THEN GOTO 8710
8780 T=0:RETURN
9000 PRINT "{CLR}":POKE 53280,0:POKE 53281,2
9005 PRINT "{BLK}{7 DOWN}":FOR DD=1 TO 40:PRINT CHR$(113);:NEXT
9010 A$="{BLK} ASTEROID qqqqqqqqq ASTEROID qqqqqqqqq":REM BLACK
9020 L=40
9030 FOR T=1 TO L
9040 PRINT "{HOME}{8 DOWN}"
9050 IF (L-T)<1 THEN 9090
9060 PRINT TAB(L-T)LEFT$(A$,T);
9070 FOR DD=1 TO 100:NEXT
9080 NEXT
9090 FOR T=1 TO 39
9100 PRINT "{HOME}{8 DOWN}"
9110 PRINT MID$(A$,T,L-T);
9120 FOR DD=1 TO 100:NEXT
9130 NEXT
9150 RETURN
