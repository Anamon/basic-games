10 REM CHECKERS
15 REM "{DOWN}" IS DOWN CURSOR
20 POKE 53280,0:POKE 53281,7
30 PRINT "{CLR}":REM CLEAR SCREEN
40 PRINT "{8 DOWN}" TAB(12) "{RED}PLEASE STAND BY":REM RED
50 GOSUB 950
60 Z=24
70 Q=0
90 FOR G=69 TO 72:IF A(G)=-1 THEN A(G)=-2
100 NEXT
105 POKE 53280,2:POKE 53281,6
110 GOSUB 740
130 IF A(Z)=9 OR A(Z)<1 THEN GOTO 220
140 IF Z<28 AND A(Z)=1 THEN A(Z)=2
150 Y=1
160 IF A(Z+X(Y))<0 AND A(Z+2*(X(Y)))=0 THEN Q=X(Y)
170 IF A(Z)=2 AND A(Z-X(Y))<0 AND A(Z-2*X(Y))=0 THEN Q=-X(Y)
180 IF Q<>0 AND Z+2*Q>23 THEN GOTO 250
190 Q=0
200 IF Y=2 THEN GOTO 220
210 Y=2:GOTO 160
220 Z=Z+1
230 IF Z<73 THEN 130
240 IF Q=0 THEN 340
250 A(Z+Q)=0:A(Z+2*Q)=A(Z):A(Z)=0
260 Z=Z+2*Q:CO=CO+1:GOSUB 740
270 Q=0:Y=1
280 IF A(Z+X(Y))<0 AND A(Z+2*(X(Y)))=0 THEN Q=X(Y)
290 IF Q<>0 AND Z+2*Q>23 THEN 250
310 IF Y=1 THEN Y=2:GOTO 280
320 GOTO 480
340 U=0:Q=0
350 Z=24+INT(RND(1)*49):U=U+1
360 IF(A(Z)=9 OR A(Z)=-1 OR A(Z)=-2 OR A(Z)=0) AND U<1000 THEN GOTO 350
380 Y=1
385 IF A(Z+X(Y))<>0 THEN 420
390 IF A(Z+X(Y))=0 AND A(Z+2*X(Y))>-1 AND A(Z+2*X(Y)+1)>-1 THEN Q=X(Y)
395 IF A(Z+X(Y))=0 AND U>150 THEN Q=X(Y)
400 IF A(Z)=2 AND A(Z-X(Y))=0 AND A(Z-2*X(Y)+1)>-1 THEN Q=-X(Y)
405 IF U>600 AND A(Z)=2 AND A(Z-X(Y))=0 THEN Q=-X(Y)
410 IF Q<>0 THEN GOTO 450
420 IF Y=1 THEN Y=2:GOTO 390
430 IF U<1000 THEN GOTO 350
440 PRINT "{DOWN}" TAB(10) "I CONCEDE THE GAME":END
450 A(Z+Q)=A(Z):A(Z)=0
470 GOSUB 740
480 PRINT:PRINT TAB(8) "{PUR}MOVE FROM";:REM PURPLE
482 INPUT A$
485 PRINT "{DOWN}" TAB(8) "MOVE TO";
487 INPUT B$
490 FOR GG=1743 TO 1863:POKE GG,32:NEXT GG
510 FOR W=1 TO 2:Z=0
520 IF W=1 THEN C$=A$
530 IF W=2 THEN C$=B$
540 Z=-24*(C$="G9")-25*(C$="E9")-26*(C$="C9")
542 IF Z<>0 THEN GOTO 580
544 Z=-27*(C$="A9")-30*(C$="H8")-31*(C$="F8")
546 IF Z<>0 THEN GOTO 580
548 Z=-32*(C$="D8")-33*(C$="B8")-37*(C$="G7")
550 IF Z<>0 THEN GOTO 580
552 Z=-38*(C$="E7")-39*(C$="C7")-40*(C$="A7")
554 IF Z<>0 THEN GOTO 580
556 Z=-43*(C$="H6")-44*(C$="F6")-45*(C$="D6")
558 IF Z<>0 THEN GOTO 580
560 Z=-46*(C$="B6")-50*(C$="G4")-51*(C$="E4")
562 IF Z<>0 THEN GOTO 580
564 Z=-52*(C$="C4")-53*(C$="A4")-56*(C$="H3")-57*(C$="F3")
566 IF Z<>0 THEN GOTO 580
568 Z=-58*(C$="D3")-59*(C$="B3")-63*(C$="G2")
570 IF Z<>0 THEN GOTO 580
572 Z=-64*(C$="E2")-65*(C$="C2")-66*(C$="A2")-69*(C$="H1")-70*(C$="F1")
574 IF Z<>0 THEN GOTO 580
576 Z=-71*(C$="D1")-72*(C$="B1")
580 IF W=1 THEN D=Z
590 IF W=2 THEN E=Z
600 NEXT:A$="":B$="":C$=""
620 A(E)=A(D):A(D)=0
650 IF ABS(D-E)>7 THEN A((D+E)/2)=0:HU=HU+1
670 GOSUB 740
680 IF ABS(D-E)>7 THEN PRINT "{DOWN}" TAB(4) "JUMP AGAIN (Y/N)";
685 IF ABS(D-E)>7 THEN INPUT U$:IF U$<>"N" THEN GOTO 480
687 FOR GG= 1744 TO 1783:POKE GG,32:NEXT GG
690 IF HU<12 AND CO<12 THEN GOTO 60
700 IF HU=12 THEN PRINT "{DOWN}" TAB(4) "THE HUMAN WINS":END
710 IF CO=12 THEN PRINT "{DOWN}" TAB(6) "I WIN I WIN!!!"
740 GOSUB 1500
770 PRINT "{HOME}":PRINT "{DOWN}" TAB(8) "{WHT}COMPUTER:";CO;"HUMAN:";HU:REM HOME WHITE
780 PRINT:PRINT:PRINT
785 PRINT TAB(14) "{RVS}{GRN}ABCDEFGH":REM REV ON GREEN
790 T=-2:FOR K=0 TO 3:PRINT TAB(14):FOR J=0 TO 3
800 PRINT "{BLK}{C+}{CYN}"; CHR$(A(72-J-13*K));:NEXT:T=T+1:REM BLACK (COMMKEY +) CYAN
820 PRINT "{YEL}" INT((J+K)/2)+T:REM YELLOW
830 FOR J=0 TO 3:PRINT TAB(14) "{CYN}" CHR$(A(66-J-13*K));"{BLK}{C+}";:REM CYAN BLACK
850 NEXT:T=T+1
860 PRINT "{YEL}" INT((J+K)/2)+T:REM YELLOW
870 NEXT
880 PRINT TAB(14)"{RVS}{GRN}ABCDEFGH":PRINT:REM REV ON GREEN
890 GOSUB 2000
920 RETURN
950 DIM A(99):X(1)=-6:X(2)=-7
960 FOR Z=1 TO 99:A(Z)=9:NEXT
970 FOR Z=1 TO 32:READ B:READ C:A(B)=C:NEXT
980 DATA 72,1,71,1,70,1,69,1,66,1,65,1,64,1,63,1,59,1,58,1,57,1,56,1
990 DATA 53,0,52,0,51,0,50,0,46,0,45,0,44,0,43,0
1000 DATA 40,-1,39,-1,38,-1,37,-1,33,-1,32,-1,31,-1,30,-1,27,-1,26,-1,25,-1
1005 DATA 24,-1
1010 CO=0:HU=0
1020 PRINT "{CLR}":REM CLEAR SCREEN
1030 RETURN
1500 FOR M=24 TO 72
1510 IF A(M)=1 THEN A(M)=209
1520 IF A(M)=2 THEN A(M)=113
1530 IF A(M)=0 THEN A(M)=32
1540 IF A(M)=-1 THEN A(M)=120
1550 IF A(M)=-2 THEN A(M)=75
1560 NEXT
1570 RETURN
2000 FOR M= 24 TO 72
2010 IF A(M)=209 THEN A(M)=1
2020 IF A(M)=113 THEN A(M)=2
2030 IF A(M)=32 THEN A(M)=0
2040 IF A(M)=120 THEN A(M)=-1
2050 IF A(M)=75 THEN A(M)=-2
2060 NEXT
2070 RETURN
