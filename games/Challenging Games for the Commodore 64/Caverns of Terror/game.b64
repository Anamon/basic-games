10 REM CAVERNS OF TERROR
15 REM "{CLR}"=CLEAR SCREEN "{DOWN}"=DOWN CURSOR
20 PRINT "{CLR}":REM CLEAR SCREEN
25 POKE 53280,0:POKE 53281,11
30 PRINT TAB(7) "{PUR}{8 DOWN}### CAVERNS OF TERROR ###":REM PURPLE CURSOR DOWN
35 GOSUB 2000
40 GOSUB 1250
50 X=0:G=30:W=1
60 PRINT TAB(1)"{C3}{DOWN}YOU ARE AT THE ENTRANCE TO A SYSTEM OF":REM LT RED
62 PRINT TAB(8)"{DOWN}DARK TUNNELS AND CAVERNS"
65 PRINT TAB(5)"{DOWN}YOU HAVE THIRTY PIECES OF GOLD"
70 PRINT "{RIGHT}{2 DOWN}WHEN YOU REACH THE END OF THE CAVERNS"
72 PRINT "{RIGHT}{DOWN}YOU MUST HAVE AT LEAST TWENTY PIECES"
75 PRINT TAB(4)"{DOWN}OF GOLD TO PAY THE GATE KEEPER"
80 PRINT TAB(14) "{BLK}{3 DOWN}PRESS RETURN":REM BLACK
90 GET A$
100 IF A$="" THEN GOTO 90
110 GOSUB 910
120 IF W<1 THEN W=INT(8*RND(1))+1
130 PRINT TAB(7)"{GRN}{4 DOWN}THIS IS A MAZE OF CAVERNS":REM GREEN
150 IF W=>10 THEN GOTO 970
160 PRINT TAB(8)"{3 DOWN}THIS IS CAVERN NUMBER";W
180 PRINT TAB(6)"{2 DOWN}CAVERN NUMBER TEN IS THE EXIT"
200 X=X+1
210 PRINT TAB(7)"{2 DOWN}THIS IS CHALLENGE NUMBER";X
220 IF G<1 THEN G=3
240 PRINT TAB(8)"{2 DOWN}YOU HAVE";G;"GOLD PIECES"
250 GOSUB 1240
260 GOSUB 910
280 K=INT(4*RND(1))+2
290 PRINT TAB(6)"{C7}{4 DOWN}YOU ARE NOW FACING";K;"TUNNELS":REM LT BLUE
300 PRINT TAB(7)"{2 DOWN}WHICH ONE WILL YOU TAKE";
310 INPUT A
320 GOSUB 910
330 IF RND(1)<.2 THEN GOTO 630
350 IF A<>K THEN 360
360 K=INT(4*RND(1))+1
370 IF K=1 THEN E$="GHOUL"
380 IF K=2 THEN E$="WEREWOLF"
390 IF K=3 THEN E$="TROLL"
400 IF K=4 THEN E$="VAMPIRE"
410 PRINT TAB(3)"{C1}{2 DOWN}THE TUNNEL IS BLOCKED BY A ";E$:REM ORnge
420 E=INT(4*RND(1))+1
430 IF E=1 THEN F$="POISONED SPEAR"
440 IF E=2 THEN F$="CURSED SWORD"
450 IF E=3 THEN F$="FIREBRAND"
460 IF E=4 THEN F$="CROSSBOW"
470 PRINT TAB(7)"{DOWN}ARMED WITH A ";F$
500 PRINT TAB(7)"{YEL}{2 DOWN}WHICH WEAPON DO YOU CHOOSE:-":REM YELLOW
510 PRINT TAB(7)"{DOWN}A POINTED STICK (1)"
520 PRINT TAB(7)"{DOWN}A SMALL ROCK    (2)"
530 PRINT TAB(7)"{DOWN}BARE HANDS      (3)"
570 INPUT B
580 C=INT(3*RND(1))+1
590 GOSUB 910
600 IF B=C THEN GOSUB 1090
610 IF B<>C THEN GOSUB 1160
620 GOTO 80
630 K=INT(4*RND(1))+1
640 ON K GOSUB 670,720,770,820
650 GOTO 80
670 PRINT TAB(2)"{5 DOWN}YOU HAVE FALLEN THROUGH A TRAPDOOR"
690 W=W-1
700 G=G-INT(2*RND(1))-1
710 RETURN
720 PRINT TAB(5)"{4 DOWN}A FLASHFLOOD WASHES YOU DOWN"
730 PRINT TAB(13) "{DOWN}A SIDE TUNNEL"
740 W=W+1
750 G=G-INT(2*RND(1))+1
760 RETURN
770 PRINT TAB(10)"{5 DOWN}YOU HAVE BEEN HELPED"
775 PRINT TAB(12)"{DOWN}BY A PASSING GNOME"
790 G=G+INT(5*RND(1))+1
800 W=W+INT(3*RND(1))+1
810 RETURN
820 PRINT TAB(6)"{2 DOWN}WHOOPEEE!!! A HOARD OF GOLD"
830 PRINT TAB(4)"{DOWN}YOU MAY TAKE UP TO 5 PIECES ONLY"
840 PRINT TAB(5)"{DOWN}BE CAREFUL, THE MORE YOU TAKE"
850 PRINT TAB(8)"{DOWN}THE MORE IT WILL COST YOU"
860 PRINT TAB(6)"{2 DOWN}HOW MANY DO YOU WISH TO TAKE";
870 INPUT D
880 IF D>5 THEN GOTO 870
890 G=G+D
900 RETURN
910 PRINT "{CLR}":REM CLEAR SCREEN
920 FOR DD=1 TO 250:NEXT
940 RETURN
960 IF W<>10 THEN RETURN
970 PRINT TAB(5)"{2 DOWN}YOU ARE STANDING AT THE EXIT"
980 PRINT TAB(7)"{DOWN}DO YOU HAVE ENOUGH GOLD?"
990 PRINT TAB(8)"{2 DOWN}PRESS RETURN TO FIND OUT"
1000 INPUT C$:PRINT "{CLR}"
1010 IF G<20 THEN PRINT TAB(6)"{BLK}{4 DOWN}YOU DO NOT HAVE ENOUGH GOLD":REM BLACK
1015 FOR DD=1 TO 1000:NEXT
1016 IF G<20 THEN PRINT TAB(6)"{2 DOWN}THE GUARDIAN HAS KILLED YOU"
1017 IF G>=20 THEN GOTO 1040
1020 PRINT TAB(7)"{2 DOWN}YOU COULD NOT PAY THE TOLL"
1030 GOSUB 2000:END
1040 PRINT TAB(6)"{WHT}{5 DOWN}YES, YOU CAN PAY THE TOLL":REM WHITE
1050 PRINT TAB(8)"{DOWN}YOU HAVE WON THE GAME"
1060 PRINT "{RIGHT}{DOWN}YOU MAY KEEP THE REMAINING GOLD PIECES":REM RIGHT CURSOR
1075 GOSUB 2000
1080 GOTO 1080
1090 PRINT TAB(4)"{4 DOWN}YOU HAVE FOUGHT YOUR WAY PAST"
1095 PRINT TAB(13)"{DOWN}THE ";E$
1100 G=G+INT(3*RND(1))+1
1110 PRINT TAB(8)"{DOWN}AND HAVE";G;"GOLD PIECES"
1120 W=W+INT(3*RND(1))+1
1140 PRINT TAB(7)"{DOWN}YOU ARE APPROACHING CAVE";W
1150 RETURN
1160 PRINT TAB(3)"{4 DOWN}THE ";E$;" BEAT YOU AND LEFT YOU"
1170 G=G-INT(4*RND(1))-1
1180 IF G<0 THEN G=0
1190 PRINT TAB(8)"{DOWN}WITH";G;"PIECES OF GOLD"
1200 W=INT(8*RND(1))+1
1220 PRINT TAB(5)"{DOWN}AND CHASED YOU INTO CAVERN";W
1230 RETURN
1240 FOR DD=1 TO 3000:NEXT:PRINT "{CLR}":RETURN
1250 FOR DD=1 TO 1500:NEXT:PRINT "{CLR}":RETURN
2000 POKE 54296,15
2010 POKE 54277,136:POKE 54278,130
2020 POKE 54276,33
2030 POKE 54273,3:POKE 54272,155
2040 FOR DD=1 TO 500:NEXT
2060 POKE 54273,4:POKE 54272,73
2070 FOR DD=1 TO 750:NEXT
2090 POKE 54273,3:POKE 54272,54
2100 FOR DD=1 TO 1250:NEXT
2110 POKE 54276,0:POKE 54277,0:POKE 54278,09
2120 RETURN
