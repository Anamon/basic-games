1 REM GET THE TREASURE
100 VC=53248:POKEVC+32,6:POKEVC+33,1:PRINTCHR$(147):LS=40
105 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
110 S1=((PEEK(53272)AND240)/16)*1024+160
120 C1=55296+160:S2=S1+770:C2=C1+770
130 DIMT(9),CL(5),FT(9,1),NT%(11,1)
140 FORX=0TO9:READY:T(X)=Y:NEXTX
150 FORX=0TO5:READY:CL(X)=Y:NEXTX
160 FORX=0TO11:READY,Z:NT%(X,0)=Y:NT%(X,1)=Z:NEXTX
170 GOSUB1000
200 FORX=0TO9
210 Y=INT(RND(1)*10):FT(X,0)=T(Y)
220 Y=INT(RND(1)*6):FT(X,1)=CL(Y):NEXTX:ER=0
240 PRINT"{CLR}{RVS}{BLU}*********** GET THE TREASURE ***********{OFF}{BLK}";
250 PRINT"{2 DOWN}cccc{19 S*}ccccccccccccccccc";
260 PRINT"{17 DOWN}{12 S*} {RED}TREASURE CHEST{BLK} {11 S*}c";
300 FORY=0TO9:GOSUB2000:POKES1+X,FT(Y,0):POKEC1+X,FT(Y,1):NEXTY
310 GOSUB2000:POKES1+X,42:POKEC1+X,0:J1=INT(X/LS):K1=X-(J1*LS)
320 TI$="000000"
400 FORX=0TO9
410 PRINT"{HOME}{2 DOWN}{2 SPACE}FIND --> ":POKES1-69,FT(X,0):POKEC1-69,FT(X,1)
420 J2=J1:K2=K1:Y=PEEK(56321)
425 IF(YAND1)=0THENJ2=J2-1
430 IF(YAND2)=0THENJ2=J2+1
440 IF(YAND4)THENK2=K2+1
450 IF(YAND8)THENK2=K2-1
460 IFJ2<0THENJ2=0
470 IFJ2>16THENJ2=16
480 IFK2<0THENK2=0
490 IFK2>39THENK2=39
500 IFJ1=J2ANDK1=K2THEN420
510 Y=PEEK(S1+(J2*40)+K2):IFY<>32THEN600
520 POKES1+(J1*LS)+K1,32:POKEC1+(J1*LS)+K1,1
525 POKES1+(J2*LS)+K2,42:POKEC1+(J2*LS)+K2,0
530 J1=J2:K1=K2:GOTO420
600 Z=PEEK(C1+(J2*LS)+K2)
605 IFZ=FT(X,1)ANDY=FT(X,0)THEN700
610 ER=ER+1:POKEMC+0,0:POKEMC+1,5:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15
620 POKEMC+24,10
630 FORZ=1TO150:NEXTZ:POKEMC+4,32
640 POKEMC+24,0
650 GOTO420
700 POKES1+(J2*LS)+K2,42:POKEC1+(J2*LS)+K2,0
705 POKES1+(J1*LS)+K1,32:POKEC1+(J1*LS)+K1,1
710 POKES2+(X*2),Y:POKEC2+(X*2),Z
720 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,8
730 FORY=20TO50STEP5
740 POKEMC+1,Y:POKEMC+4,17:FORZ=1TO80:NEXTZ
750 POKEMC+4,16:FORZ=1TO20:NEXTZ,Y
760 POKEMC+24,0
770 J1=J2:K1=K2
780 NEXTX
800 PRINT"{CLR}{DOWN}{RVS}{11 SPACE}G A M E{4 SPACE}O V E R{11 SPACE}{OFF}";
810 PRINT"{2 DOWN}{11 SPACE}TOTAL ERRORS{2 SPACE}= ";ER
820 PRINT"{2 DOWN}{11 SPACE}TOTAL SECONDS =";INT(TI/60)
830 PRINT"{4 DOWN}{11 SPACE}WANT TO PLAY AGAIN?"
835 PRINT"{DOWN}{13 SPACE}(REPLY {RVS}Y{OFF} OR {RVS}N{OFF})"
840 GETX$:IFX$=""THEN840
850 IFX$="Y"THEN200
860 IFX$<>"N"THEN840
870 GOSUB3000:PRINTCHR$(147)
880 END
1000 PRINT "{CLR}{BLU}{DOWN}{RVS} ********** GET THE TREASURE ********** {OFF}";
1010 PRINT"{2 DOWN}{BLK} TEN TREASURES WILL BE PLACED ON THE{5 SPACE}COMPUTER SCREEN."
1020 PRINT"{2 DOWN} THE TOP OF THE SCREEN WILL SHOW YOU"
1025 PRINT "THE TREASURE YOU MUST FIND"
1030 PRINT"{2 DOWN} USE THE JOYSTICK TO MOVE YOURSELF (*){3 SPACE}TO THE CORRECT TREASURE."
1040 PRINT"{2 DOWN} AS YOU GET EACH ITEM, IT WILL BE MOVED{2 SPACE}TO YOUR TREASURE CHEST."
1050 PRINT"{2 DOWN} GET ALL TEN ITEMS IN THE CHEST AND{6 SPACE}YOU WIN!"
1060 GOSUB3000:PRINT"{DOWN}{8 SPACE}(PRESS ANY KEY TO START)";
1070 GETX$:IFX$=""THEN1070
1080 RETURN
2000 X=INT(RND(1)*680):Z=PEEK(S1+X)
2010 IFZ<>32THEN2000
2020 RETURN
3000 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
3010 FORX=0TO11
3020 Y=INT(NT%(X,0)/256)
3030 POKEMC+0,NT%(X,0)-(Y*256)
3040 POKEMC+1,Y:POKEMC+4,17
3050 FORY=1TONT%(X,1)*80:NEXTY
3060 POKEMC+4,16:FORY=1TO30:NEXTY
3070 NEXTX:POKEMC+24,0
3080 RETURN
10000 DATA81,83,160,90,65,88,81,95,105,102
10010 DATA0,2,4,5,6,12
10020 DATA6430,2,6430,2,6430,2,4817,2,5407,2,5407,2
10030 DATA4817,4,8101,2,8101,2,7217,2,7217,2,6430,4
