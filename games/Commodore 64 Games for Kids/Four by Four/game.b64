1 REM FOUR BY FOUR
10 VC=53248:POKEVC+32,14:POKEVC+33,1:PRINTCHR$(147)
20 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
50 DIMC$(34),L$(27),A$(3,3),C%(3),L%(3),NT%(29):X=RND(-TI)
60 M=((PEEK(53272)AND240)/16)*1024
70 C=55296-M
80 FORX=0TO29:READZ:NT%(X)=Z:NEXTX
100 PRINT"{BLU}{14 SPACE}{RVS}FOUR BY FOUR{OFF}" 
110 PRINT"{2 DOWN} THE COMPUTER DISPLAYS FOUR CATEGORIES{3 SPACE} AND FOUR LETTERS."
120 PRINT"{DOWN} ENTER AN ANSWER THAT MATCHES THE"
130 PRINT" CATEGORY AND STARTS WITH THE REQUESTED{2 SPACE}LETTER ('*' = ANY LETTER)."
140 PRINT"{DOWN} PRESS {RVS}RETURN{OFF} TO SKIP A QUESTION AND{6 SPACE}GO BACK TO IT LATER."
150 PRINT"{DOWN} TYPE {RVS}QUIT{OFF} TO STOP THE GAME BEFORE THE{3 SPACE}FOUR MINUTE TIME LIMIT."
160 PRINT"{DOWN} SCORING IS BASED ON CORRECT ANSWERS{5 SPACE}AND THE TOTAL TIME USED."
165 GOSUB7500
170 PRINT"{3 DOWN}{12 SPACE}(PRESS ANY KEY){BLK}"
180 FORX=0TO34:READX$:C$(X)=X$:NEXTX:FORX=0TO27:READX$:L$(X)=X$:NEXTX:HS=0
190 GETX$:IFX$=""THEN190
200 FORX=0TO3
210 Y=INT(RND(1)*35):IFX=0THENGOTO235
220 QQ=0:FORZ=0TOX-1:IFC%(Z)=YTHENQQ=1
230 NEXTZ:IFQQ=1THEN210
235 C%(X)=Y
240 NEXTX
250 FORX=0TO3
260 Y=INT(RND(1)*28):IFX=0THEN285
270 QQ=0:FORZ=0TOX-1:IFL%(Z)=YTHENQQ=1
280 NEXTZ:IFQQ=1THEN260
285 L%(X)=Y
290 NEXTX
300 FORX=0TO3:FORY=0TO3:A$(X,Y)="":NEXTY,X:TI$="000000"
400 HC=0:X=0
405 Y=0
410 FORZ=0TO3:IFA$(X,Z)=""THENY=1
420 NEXTZ:IFY=0THEN700
430 HC=1:PRINT"{CLR}{2 DOWN}{RED}{2 SPACE}";C$(C%(X));"{BLK}"
440 FORY=0TO3:PRINT"{3 DOWN}{2 SPACE}{RED}";L$(L%(Y));"{BLK}":NEXTY
450 X$="{BLK}{HOME}{6 DOWN}":FORY=0TO3:PRINTX$;"{5 RIGHT}";A$(X,Y):X$=X$+"{4 DOWN}":NEXTY
500 Y=0
505 IFA$(X,Y)<>""THEN600
507 QQ=0
510 GOSUB7000
515 IFQQ=1THEN800
520 IFI$="QUIT"THEN800
530 IFL$(L%(Y))="*"ANDLEN(I$)>0THEN550
540 IFLEN(I$)=0ORLEFT$(I$,1)<>L$(L%(Y))THEN600
550 A$(X,Y)=I$
600 Y=Y+1:IFY<=3THEN505
700 X=X+1:IFX<=3THEN405
710 IFHC=1THEN400
800 S=0:FORX=0TO3:W=0:FORY=0TO3
810 IFA$(X,Y)<>""THENW=W+1
820 NEXTY:S=S+(W*W):NEXTX
830 FORX=0TO3:W=0:FORY=0TO3
840 IFA$(Y,X)<>""THENW=W+1
850 NEXTY:S=S+(W*W):NEXTX:Y=0:IFS<16THEN870
860 Y=INT((240-(TI/60))/4):IFY<0THENY=0
870 S=S+Y:Z=0:IFS>HSTHENHS=S:Z=1
900 PRINT"{CLR}{BLK}{DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}{DOWN}"
910 PRINT"{DOWN}{5 SPACE}YOUR SCORE =";S
920 PRINT"{DOWN}{5 SPACE}HIGH SCORE =";HS
930 IFZ=1THENPRINT"{DOWN}{5 SPACE}{RVS}YOU SET A NEW HIGH!{OFF}"
940 PRINT"{3 DOWN}{5 SPACE}ENTER REPLAY OPTION:"
950 PRINT"{2 DOWN}{7 SPACE}{RVS}E{OFF} END THE GAME"
960 PRINT"{DOWN}{7 SPACE}{RVS}S{OFF} REPLAY WITH THE SAME SETUP"
970 PRINT"{DOWN}{7 SPACE}{RVS}N{OFF} REPLAY WITH A NEW SETUP"
980 GETX$:IFX$=""THEN980
990 IFX$="S"THEN300
1000 IFX$="N"THEN200
1010 IFX$<>"E"THEN980
1020 GOSUB7500
1030 PRINTCHR$(147)
1040 END
7000 I$="":J=245+(Y*160)+M:POKEJ,160:POKEJ+C,0
7010 GETX$:IFTI$>"000400"THENQQ=1:RETURN
7020 IFX$<>""THEN7030
7022 TL$=MID$(TI$,3,2)+"."+MID$(TI$,5,2)
7024 PRINT"{HOME}";SPC(17);"{RVS}{BLU}";TL$;"{BLK}{OFF}";
7026 GOTO7010
7030 L=ASC(X$):IFL<>20THEN7100
7040 IFLEN(I$)=0THEN7010
7050 X$=LEFT$(I$,LEN(I$)-1):I$=X$:POKEJ+LEN(I$),160:POKEJ+LEN(I$)+1,32
7060 POKEJ+C+LEN(I$),0:POKEJ+C+LEN(I$)+1,1:GOTO7010
7100 IFL=13THENPOKEJ+LEN(I$),32:POKEJ+C+LEN(I$),1:RETURN
7150 IFL>63THENL=L-64
7160 POKEJ+LEN(I$),L:POKEJ+C+LEN(I$),0:I$=I$+X$
7170 POKEJ+LEN(I$),160:POKEJ+C+LEN(I$),0:GOTO7010 
7500 REM *** PLAY THE SONG ***
7510 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
7520 FORX=0TO29
7530 Y=INT(NT%(X)/256)
7540 POKEMC+0,NT%(X)-(256*Y)
7550 POKEMC+1,Y:POKEMC+4,17
7560 FORY=1TO200:NEXTY:POKEMC+4,16
7570 FORY=1TO20:NEXTY,X:POKEMC+24,0
7580 RETURN
8000 DATA7217,5728,4291,4291,5728,7217,7217,7217,6430,5407,4291,4291
8010 DATA5407,6430,6430,6430,7217,5728,4291,4291,5728
8020 DATA7217,7217,7217,8583,8583,7217,6430,5728,5728
9000 DATA"NAMES OF U.S. CITIES","NAMES OF U.S. STATES","NAMES OF FRUITS"
9010 DATA"NAMES OF VEGETABLES","FAMOUS SINGERS","CHEMICAL ELEMENTS"
9020 DATA"U.S. PRESIDENTS","NAMES OF BIRDS"
9030 DATA"NAMES OF FLOWERS","FAMOUS WOMEN","FAMOUS MEN"
9040 DATA"MUSICAL INSTRUMENTS","ORGANS OF THE BODY"
9050 DATA"NAMES OF ANIMALS","NATIONS OF THE WORLD","NAMES OF OCCUPATIONS"
9060 DATA"FAMOUS SONG TITLES","NAMES OF CARS","NAMES OF SPICES"
9070 DATA"NAMES OF INSECTS","FAMOUS BOOK TITLES","BOOKS OF THE BIBLE"
9080 DATA"FAMOUS AUTHORS","WEIGHTS AND MEASURES"
9090 DATA"WORLD LEADERS","NAMES OF MAMMALS","NAMES OF COLORS"
9100 DATA"NAMES OF FISH","BREEDS OF DOG","MAGAZINE NAMES"
9110 DATA"CORPORATION NAMES"
9120 DATA"MOVIE ACTORS","MOVIE ACTRESSES","NAMES OF MOVIES"
9130 DATA"NAMES OF T.V. SHOWS"
9500 DATAA,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,R,S,T,U,V,W,Y,Z,*,*,*,*
