1 REM SKELETON
100 CB=828:NW%=43
105 VC=53248:POKEVC+32,6:POKEVC+33,13:PRINTCHR$(147)
110 DIM NT%(19),WT$(NW%-1),WI$(19),CC$(NW%-1)
115 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
120 S1=((PEEK(53272)AND240)/16)*1024:S2=55296
130 FORX=0TO19:READZ:NT%(X)=Z:NEXTX:FORX=1TONW%:READX$,XX$
135 WT$(X-1)=X$:CC$(X-1)=XX$:NEXT X
140 PRINT"{CLR}{BLK}{2 SPACE}{RVS}{9 SPACE}S K E L E T O N{12 SPACE}{OFF}{DOWN}"
142 GOSUB8000
146 PRINT"THIS GAME TEACHES YOU THE NAMES OF THE{2 SPACE}BONES IN THE HUMAN SKELETON:"
150 PRINT"{6 DOWN} THE COMPUTER WILL SELECT THE NAMES OF"
160 PRINT" THE BONES, AND PRESENT THEM TO YOU."
170 PRINT"{DOWN} YOU MUST DETERMINE THE BONE BY"
175 PRINT" GUESSING ONE LETTER AT A TIME."
180 PRINT"{DOWN} IF YOU PICK MORE THAN 11 INCORRECT"
190 PRINT" LETTERS, A SKELETON WILL HANG IN YOUR{3 SPACE}CLOSET!!"
200 PRINT"{2 DOWN}{7 SPACE}(PRESS ANY KEY TO START)":GOSUB2500
210 PRINT"{CLR}{6 DOWN}{9 SPACE}DO YOU WISH TO PLAY"
214 PRINT"{DOWN}{8 SPACE}{RVS}B{OFF}EGINNERS OR {RVS}A{OFF}DVANCED?"
215 GET Q$:IF Q$<>"B" AND Q$<>"A" THEN 215
216 IF Q$="A" THEN NW%=43
217 IF Q$="B" THEN NW%=25
219 PRINT"{9 DOWN}{9 SPACE}(TEN SECOND DELAY)";
220 FORX=1TONW%:POKECB+X,0:NEXTX
230 FORX=1TONW%:GOSUB800:POKECB+Z,X:NEXTX:POKEVC+33,15:PRINTCHR$(147)
300 FORLC=1TONW%
310 X=PEEK(CB+LC):WO$=WT$(X-1):SY$=CC$(X-1)
320 LW%=LEN(WO$)
330 SC=0:FORX=0TOLW%-1
340 WI$(X)=MID$(WO$,X+1,1):IFASC(WI$(X))=32THENSC=SC+1
345 NEXTX
350 LC%=0:OF%=881:SO=640:BC%=0:GOSUB1000:LW%=LW%-1
400 GETX$:IFX$=""THEN400
410 X=ASC(X$):IFX<65ORX>90THEN400
500 HI=0:FORY=0TOLW%:IFWI$(Y)<>X$THEN530
510 HI=1:WI$(Y)="$":POKES1+SO+CC+Y*2,X-64
520 LC%=LC%+1
530 NEXTY:IFHI=1THEN560
540 GOSUB3000:BC%=BC%+1
550 IFBC%>10THENGOSUB2000:LO%=LO%+1:FORX=1TO5000:NEXTX:GOTO700
560 IF(LC%+SC)<=LW%THEN400
570 GOSUB4000:WI%=WI%+1
700 PRINT"{CLR}{BLU}{2 DOWN}{13 SPACE}{RVS}ANOTHER BONE ?{OFF}":PRINT"{2 DOWN}{5 SPACE}ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
720 GETX$:IFX$=""THEN720
730 IFX$="Y"THENNEXTLC:GOTO220
740 IFX$<>"N"THEN720
750 PRINT"{BLK}{3 DOWN}{5 SPACE}NUMBER CORRECT =";WI%
760 PRINT"{2 DOWN}{5 SPACE}NUMBER MISSED{2 SPACE}=";LO%
770 PRINT"{2 DOWN}{5 SPACE}FINAL SCORE{4 SPACE}=";INT((WI%*100)/(WI%+LO%));"{LEFT}%"
780 END
800 Y=0
810 Z=INT(RND(1)*NW%)+1:IFPEEK(CB+Z)=0THENRETURN
820 Y=Y+1:IFY<10THEN810
830 Z=1
840 IFPEEK(CB+Z)=0THENRETURN
850 Z=Z+1:GOTO840
1000 PRINT"{CLR}{BLK}{DOWN}{12 SPACE}S K E L E T O N"
1110 PRINT"{12 DOWN}"
1500 PRINT"{2 DOWN}{40 S*}";
1502 QX=LEN(SY$)/2:QQ=INT(QX):IFQQ<>QX THEN QQ=QQ+1
1504 QQ=20-QQ
1505 PRINT SPC(QQ);SY$
1510 PRINT"{DOWN} ENTER A LETTER:";
1515 CC=INT((40-(LW%*2-1))/2)
1520 Z=0:FORY=0TO((LW%-1)*2)STEP2:X=70:IFASC(WI$(Z))=32THENX=32
1540 POKES1+SO+CC+Y,X:IFX=70THENPOKES2+SO+CC+Y,6
1550 Z=Z+1:NEXTY
1560 Z=WI%+LO%:IFZ=0THENZ=.1
1570 PRINT "{HOME}{24 DOWN}";SPC(13);"SCORE =";
1580 PRINTINT((WI%*100)/Z);"{LEFT}%";
1590 RETURN
2000 FORX=0TOLW%:IFWI$(X)="$"THEN2020
2010 Y=ASC(WI$(X)):IFY=32THEN2020
2015 POKES1+SO+CC+X*2,Y-64
2020 NEXTX
2030 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,8
2040 FORY=80TO20STEP-10
2050 POKEMC+1,Y:POKEMC+4,17:FORX=1TO60:NEXTX
2060 POKEMC+4,16:FORX=1TO30:NEXTX,Y
2070 POKEMC+24,0
2080 RETURN
2500 REM *** SPRITE AND SONG ***
2505 X=5:Y=0:POKEMC+24,15:POKEVC+21,1
2510 Z=INT(NT%(Y)/256)
2520 POKEMC+0,NT%(Y)-(Z*256)
2530 POKEMC+1,Z:POKEMC+4,17
2540 Y=Y+1:IFY>19THENY=0
2550 POKEVC+0,X:X=X+10
2560 IFX>60AND(PEEK(VC+16)AND1)=1THENX=5:POKEVC+16,0:POKEVC+0,X
2570 IFX>255THENX=5:POKEVC+16,1:POKEVC+0,X
2580 IFPEEK(2040)=13THENPOKE2040,14:GOTO2600
2590 POKE2040,13
2600 FORZ=1TO120:NEXTZ:POKEMC+4,16
2610 FORZ=1TO20:NEXTZ
2620 GETX$:IFX$=""THEN2510
2630 POKEMC+24,0:POKEVC+21,0
2640 RETURN
3000 PRINT"{BLK}";
3005 ON(BC%+1)GOTO3010,3020,3030,3040,3050,3060,3070,3080,3090,3100,3110,3120
3010 PRINT"{HOME}{3 DOWN}";SPC(18);"{RVS}{S#} {C*}{OFF}":GOTO3500
3020 PRINTSPC(18);"{RVS}q q{OFF}":GOTO3500
3030 PRINTSPC(18);"{RVS} {S-} {OFF}":GOTO3500
3040 PRINTSPC(18);"{C*}{RVS} {OFF}{S#}":GOTO3500
3050 PRINTSPC(14);"{RVS}{CB}{OFF}{4 SPACE}{RVS}-{OFF}{4 SPACE}{CB}":GOTO3500
3060 PRINTSPC(15);"m{3 SPACE}{S-}{3 SPACE}n":GOTO3500
3070 PRINTSPC(16);"m{2 CP}{CE}{2 CP}n":GOTO3500
3080 PRINTSPC(18);"{S*}{S+}{S*}":GOTO3500
3090 PRINTSPC(18);"{S*}{S+}{S*}":GOTO3500
3100 PRINTSPC(15);"nm{2 SPACE}{S+}{2 SPACE}nm":GOTO3500
3110 PRINTSPC(13);"mn{2 SPACE}m{RVS}q q{OFF}n{2 SPACE}mn"
3500 POKEMC+0,0:POKEMC+1,5:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15:POKEMC+24,8
3505 POKEVC+33,0
3510 FORY=1TO100:NEXTY:POKEMC+4,32
3520 FORY=1TO30:NEXTY:POKEMC+24,0:POKEVC+33,15
3530 POKES1+OF%,X-64:POKES2+OF%,0:OF%=OF%+2
3540 RETURN
4000 REM *** FLASH ON A WIN ***
4010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,12
4020 FORX=8TO15
4030 POKEVC+33,X
4040 POKEMC+1,X*4:POKEMC+4,17
4050 FORY=1TO50:NEXTY
4060 POKEMC+4,16:FORY=1TO30:NEXTY,X
4070 POKEMC+24,0
4080 RETURN
5000 DATA1072,1607,1804,1804,1607,1432,1204,1432,1204,1432,1072,1072
5010 DATA1432,1804,2145,1804,2145,1607,1204,1072
6000 REM *** LOAD THE SPRITES ***
6020 .
7000 DATA"CLAVICLE","COLLARBONE","HUMERUS","UPPER ARM"
7005 DATA"ULNA","FOREARM","RADIUS","FOREARM","TIBIA","SHIN"
7010 DATA"FIBULA","CALF","FEMUR","THIGH"
7015 DATA"METATARSAL BONES","FOOT (SOLE)","TARSAL BONES","FOOT (ANKLE)"
7020 DATA"PATELLA","KNEECAP","CARPAL BONES","HAND (WRIST)","METACARPAL BONES"
7025 DATA"HAND (KNUCKLE)","PHALANGES","FINGERS","PHALANGES","TOES","COCCYX"
7030 DATA"TAILBONE","ILIUM","PELVIS (HIP)","PUBIS","PELVIS (GROIN)","ISCHIUM"
7035 DATA"PELVIS (SEAT)","SACRUM","BASE OF SPINE","VERTEBRAE","SPINE","RIBS"
7040 DATA"CHEST","STERNUM","BREASTBONE","SCAPULA","SHOULDER BLADE","MANDIBLE"
7045 DATA"JAW","CRANIUM","BRAIN CASE","PARIETAL BONE","TOP OF YOUR HEAD"
7050 DATA"SPHENOID","THE BASE OF THE SKULL","TEMPORAL BONE"
7055 DATA "ALONGSIDE YOUR HEAD","OCCIPITAL BONE","THE BACK OF THE HEAD"
7060 DATA"MASTOID PROCESS","TUCKED BEHIND THE EAR","PUBIC SYMPHYSIS"
7065 DATA"PELVIC CONNECTION (GROIN)","LUMBAR VERTEBRAE","THE LOWER SPINE"
7070 DATA"THORACIC VERTEBRAE","THE MIDDLE OF THE BACK","CERVICAL VERTEBRAE"
7075 DATA"A PAIN IN THE NECK","MAXILLA","STIFF UPPER LIP","ZYGOMATIC BONE"
7077 DATA"CHEEK","NASAL BONE","NOSE","FRONTAL BONE","FOREHEAD"
7080 DATA"SACROILIAC JOINT","THE PELVIC CONNECTION (HIP)","AXIS"
7085 DATA"TURN YOUR HEAD","ATLAS","HOLD UP YOUR HEAD"
7090 DATA"AXIS","SECOND NECK BONE","ATLAS","TOP NECK BONE"
8000 REM *** LOAD THE SPRITES ***
8010 FORX=832TO832+127:READZ:POKEX,Z:NEXTX
8020 POKEVC+0,0:POKEVC+1,93:POKEVC+16,0:POKEVC+21,0:POKEVC+23,1
8030 POKEVC+27,0:POKEVC+28,0:POKEVC+29,1:POKEVC+39,0:POKE2040,13
8040 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129
8050 RETURN
8900 DATA0,56,0,0,84,0,0,56,0,0,16,0,48,124,0,8,146,0,5,125,0,2,16,128
8910 DATA0,124,64,0,16,32,0,124,192,0,186,0,0,68,0,0,130,0,1,1,0,2,0,128
8920 DATA1,1,0,0,130,0,1,131,0,0,0,0,0,0,0,0
8930 DATA0,56,0,0,84,0,0,56,0,0,16,0,0,124,0,1,147,0,2,124,128,4,16,64,2,124
8940 DATA128,4,16,64,2,124,128,0,186,0,0,68,0,0,68,0,0,68,0,0,0,0,0,68
8950 DATA0,0,68,0,0,68,0,0,130,0,0,0,0,0
