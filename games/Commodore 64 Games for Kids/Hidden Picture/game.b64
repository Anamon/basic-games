1 REM HIDDEN PICTURE
100 DIMCC%(9,1),NT%(22,1),JT%(10,1) 
110 VC=53248:POKEVC+32,6:POKEVC+33,12:PRINTCHR$(147)
114 CT$="{BLK}{WHT}{RED}{CYN}{PUR}{GRN}{BLU}{YEL}":LS=40
118 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
120 FORX=0TO9:FORY=0TO1:READZ:CC%(X,Y)=Z:NEXTY,X
125 FORX=0TO22:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
130 PRINT"{WHT}{DOWN} {RVS}{12 SPACE}HIDDEN PICTURE{12 SPACE}{OFF}"
140 PRINT"{2 DOWN} USE THE JOYSTICK TO ERASE THE SCREEN"
150 PRINT" AND UNCOVER THE HIDDEN PICTURE."
160 PRINT"{2 DOWN} STOP DOING THE CURRENT PICTURE BY"
165 PRINT" PRESSING THE FIRE BUTTON ON THE " 
168 PRINT" JOYSTICK, OR BY UNCOVERING THE WHOLE{4 SPACE}PICTURE."
170 GOSUB2000
175 PRINT"{2 DOWN}{7 SPACE}(PRESS ANY KEY TO START)"
180 GETX$:IFX$=""THEN180
190 M=((PEEK(53272)AND240)/16)*1024:C=55296
195 FORX=0TO10:FORY=0TO1:READZ:JT%(X,Y)=Z:NEXTY,X
200 SC=INT(RND(1)*10):DC=0:JF=0
210 POKEVC+32,1:POKEVC+33,CC%(SC,0)
220 X$=MID$(CT$,CC%(SC,0)+1,1):PRINTX$;:PC=CC%(SC,1)
230 GOTO4000
250 C1=INT(RND(1)*25):R1=INT(RND(1)*LS)
260 CV=PEEK(M+(C1*LS)+R1):POKEM+(C1*LS)+R1,160:POKEC+(C1*LS)+R1,15
270 IFCV<128THENCV=CV+128
300 C2=C1:R2=R1:GOSUB8000
350 IFJFTHEN1000
360 IFC2<0THENC2=0
370 IFC2>24THENC2=24
380 IFR2<0THENR2=0
390 IFR2>39THENR2=39
400 IFC1=C2ANDR1=R2THEN300
405 POKEM+(C1*LS)+R1,CV:POKEC+(C1*LS)+R1,PC
410 C1=C2:R1=R2:CV=PEEK(M+(C1*LS)+R1):IFCV<128THENCV=CV+128:DC=DC+1
420 POKEM+(C1*LS)+R1,160:POKEC+(C1*LS)+R1,15
430 IFDC>998THEN1000
440 GOTO300
1000 REM *** PICTURE DONE ***
1005 GOSUB2000:POKEVC+32,6:POKEVC+33,15
1010 PRINT"{CLR}{BLK}{3 DOWN}{4 SPACE}ANOTHER PICTURE?"
1020 PRINT"{2 DOWN}{4 SPACE}PRESS {RVS}Y{OFF} FOR YES":PRINT"{2 DOWN}{4 SPACE} PRESS {RVS}N{OFF} FOR NO"
1030 GETX$:IFX$=""THEN1030
1040 IFX$="Y"THEN200
1050 IFX$<>"N"THEN1030
1060 PRINTCHR$(147)
1070 END
2000 REM *** PLAY A SONG ***
2010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
2020 FORX=0TO22
2030 Y=INT(NT%(X,0)/256)
2040 POKEMC+0,NT%(X,0)-(Y*256)
2050 POKEMC+1,Y:POKEMC+4,17
2060 FORY=1TONT%(X,1)*90:NEXTY
2070 POKEMC+4,16:FORY=1TO30:NEXTY,X
2080 POKEMC+24,0
2090 RETURN
4000 K=INT(RND(1)*4)+1
4010 ONKGOTO4100,4300,4500,4700
4100 PRINT"{CLR}{DOWN}{10 SPACE}TULIPS AND WINDMILLS{2 DOWN}"
4110 PRINTSPC(19);"{C+}"
4120 PRINTSPC(19);"{C+}"
4130 PRINTSPC(19);"{C+}"
4140 PRINTSPC(18);"u{C+}i"
4150 PRINTSPC(12);"{7 C+}{S+}{7 C+}"
4160 PRINTSPC(18);"g{C+}h"
4170 PRINTSPC(18);"g{C+}h"
4180 PRINTSPC(18);"g{C+}h"
4190 PRINTSPC(18);"g{C+}h"
4200 PRINTSPC(18);"g h"
4205 PRINTSPC(18);"g h"
4207 PRINTSPC(18);"g h"
4210 PRINT"{2 SPACE}x{3 SPACE}x{3 SPACE}x{3 SPACE}x{3 SPACE}g h{2 SPACE}x{3 SPACE}x{3 SPACE}x{3 SPACE}x "
4220 PRINT" m{S-}n m{S-}n m{S-}n m{S-}n{2 SSPACE}g h{SSPACE}m{S-}n{SSPACE}m{S-}n m{S-}n m{S-}n"
4230 PRINT"{40 CU}";
4240 PRINT"{3 DOWN}{12 SPACE}DECORATE HOLLAND"
4250 GOTO250
4300 PRINT"{CLR}{DOWN}{5 SPACE}*{8 SPACE}*{8 SPACE}*{5 SPACE}*{8 SPACE}*"
4310 PRINT"{2 SPACE}*{6 SPACE}*{9 SPACE}*{2 SPACE}*{4 SPACE}*{7 SPACE}*{3 SPACE}*"
4320 PRINT"{7 SPACE}*{8 SPACE}*{9 SPACE}*{4 SPACE}*{5 SPACE}*"
4330 PRINT"{2 SPACE}*{11 SPACE}*{6 SPACE}*{8 SPACE}*{3 SPACE}*"
4340 PRINT"{7 SPACE}*{3 SPACE}*{3 SPACE}ui{8 SPACE}*{10 SPACE}* "
4350 PRINT" *{13 SPACE}jk{2 SPACE}*{7 SPACE}*{3 SPACE}*{6 SPACE}*"
4360 PRINT"{3 SPACE}*{9 SPACE}*{7 SPACE}*{12 SPACE}*"
4370 PRINT"{13 SPACE}v{9 SPACE}"
4380 PRINT"{13 SPACE}{C+}{9 SPACE}"
4390 PRINT"{6 SPACE}{2 C+}{5 SPACE}{C+}{2 SPACE}{S+}{6 SPACE}{S+}{7 SPACE}<>"
4400 PRINT"{6 SPACE}{2 C+}{5 SPACE}{C+}{2 SPACE}{C+}{2 SPACE}x{3 SPACE}{C+}{7 SPACE}{2 C+}"
4410 PRINT"{3 SPACE}{2 S+} {2 C+}{5 SPACE}{C+}{2 SPACE}{C+}{2 SPACE}{C+}{3 SPACE}{C+}{4 SPACE}v{2 SPACE}{2 C+}{3 SPACE}v{2 SPACE}"
4420 PRINT"{3 SPACE}{2 C+} {2 C+}{2 SPACE}a{2 SPACE}{C+}{2 SPACE}{C+}{2 SPACE}{C+}{3 SPACE}{C+}{4 SPACE}{C+}{2 SPACE}{2 C+}{3 SPACE}{C+} "
4430 PRINT"{2 SPACE}{3 C+} {2 C+}{2 SPACE}{C+} {2 C+}{2 SPACE}{C+}{2 SPACE}{2 C+}{2 SSPACE}{2 C+}{2 SPACE}{2 C+}q{2 C+}{2 SSPACE} {C+} "
4440 PRINT"{2 SPACE}{3 C+}{SSPACE}{2 C+}{C-} {5 C+} {2 C+} {6 C+} {4 C+} {3 C+} {4 C+}"
4450 PRINT" {38 C+}"
4460 PRINT"{40 CU}";
4470 PRINT"{2 DOWN}";SPC(11);"MOON OVER MANHATTAN"
4480 GOTO250
4500 PRINT"{CLR}{DOWN} .{2 SPACE}.{3 SPACE}.{2 SPACE}.{4 SPACE}. .{3 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}.{3 SPACE}."
4510 PRINT"{2 SPACE}. .{3 SPACE}.{2 SPACE}.{4 SPACE}. .{2 SPACE}.{2 SPACE}.{3 SPACE}. .{2 SPACE}. .{2 SPACE}."
4520 PRINT".{2 SPACE}.{3 SPACE}.{4 SPACE}.{2 SPACE}.{2 SPACE}. .{2 SPACE}. .{2 SPACE}.{2 SPACE}.{2 SPACE}. . ."
4530 PRINT" . .{2 SPACE}.{2 SPACE}.{3 SPACE}.{2 SPACE}. .{C+} . .{2 SPACE}. . .{3 SPACE}.{2 SPACE}. "
4540 PRINT".{3 SPACE}.{3 SPACE}.{3 SPACE}.{4 SPACE}{5 C+}{3 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}.{4 SPACE}"
4550 PRINT"{2 SPACE}.{2 SPACE}.{4 SPACE}.{3 SPACE}. {7 C+} .{2 SPACE}. .{2 SPACE}.{3 SPACE}. ."
4560 PRINT".{3 SPACE}.{2 SPACE}.{4 SPACE}.{3 SPACE}{S-}{5 SPACE}{S-}{2 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}.{4 SPACE}"
4570 PRINT"{2 SPACE}.{3 SPACE}.{3 SPACE}.{2 SPACE}. s{CW} q q {CQ}s .{2 SPACE}. .{2 SPACE}.{2 SPACE}. ."
4580 PRINT".{3 SPACE}.{3 SPACE}.{3 SPACE}.{3 SPACE}{S-}{2 SPACE}z{2 SPACE}{S-} . . .{3 SPACE}.{6 SPACE}"
4590 PRINT" .{3 SPACE}. .{3 SPACE}.{2 SPACE}. {S-} j{S*}k {S-}{2 SPACE}. .{2 SPACE}.{4 SPACE}.{3 SPACE}"
4600 PRINT"{2 SPACE}. .{4 SPACE}.{3 SPACE}.{2 SPACE}j{5 S*}k.{2 SPACE}. .{2 SPACE}.{2 SPACE}.{4 SPACE}"
4610 PRINT".{2 SPACE}.{2 SPACE}.{4 SPACE}.{3 SPACE}.{2 SPACE}{S-} {S-} . .{2 SPACE}.{2 SPACE}.{5 SPACE}.{2 SPACE}"
4620 PRINT" .{2 SPACE}.{3 SPACE}.{4 SPACE}.{2 SPACE}u{S*}k j{S*}i. .{3 SPACE}.{3 SPACE}.{2 SPACE}.{2 SPACE}"
4630 PRINT"{2 SPACE}.{2 SPACE}.{3 SPACE}.{2 SPACE}.{3 SPACE}{S-}{5 SPACE}{S-} . . .{2 SPACE}.{4 SPACE}.{2 SPACE}"
4632 PRINT"{3 SPACE}.{3 SPACE}.{3 SPACE}q{4 S*}{CW}{5 SPACE}{CQ}{4 S*}q.{4 SPACE}.{5 SPACE}"
4634 PRINT"{2 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}.{2 SPACE}. {S-}{5 SPACE}{S-} . . .{2 SPACE}.{3 SPACE}.{2 SPACE}."
4636 PRINT"{2 SPACE}.{2 SPACE}.{3 SPACE}.{3 SPACE}.{2 SPACE}{S-}{5 SPACE}{S-} . . . .{2 SPACE}.{3 SPACE}. "
4640 PRINT"{40 CU}";
4650 PRINT"{DOWN}";SPC(10);"SOME PEOPLE CAN MAKE"
4660 PRINT"{DOWN}";SPC(9);"SNOWMEN IN THE WINTER.";
4670 GOTO250
4700 PRINT"{CLR}{3 DOWN}{12 SPACE}{16 C@}"
4710 PRINT"{11 SPACE}nnnnnnnnnnnnnnnnnm"
4720 PRINT"{10 SPACE}nnnnnnnnnnnnnnnnn{2 SSPACE}m"
4730 PRINT"{10 SPACE}{CJ}{15 SPACE}{CJ}{2 SPACE}{CL}"
4740 PRINT"{10 SPACE}{CJ}{15 SPACE}{CJ}{2 SPACE}{CL}"
4750 PRINT"{10 SPACE}{CJ}{15 SPACE}{CJ}{2 SPACE}{CL}"
4760 PRINT"{10 SPACE}{CJ}{2 SPACE}{3 C+}{5 SPACE}{3 C+}{2 SPACE}{CJ}{2 SPACE}{CL}"
4770 PRINT"{10 SPACE}{CJ}{2 SPACE}{3 C+}{5 SPACE}{3 C+}{2 SPACE}{CJ}{2 SPACE}{CL}"
4780 PRINT"{10 SPACE}{CJ}{10 SPACE}{3 C+}{2 SPACE}{CJ}{2 SPACE}{CL}"
4790 PRINT"{10 SPACE}{CJ}{10 SPACE}{3 C+}{2 SPACE}{CJ}{2 SPACE}{CL}"
4800 PRINT"{10 SPACE}{CJ}{10 SPACE}{3 C+}{2 SPACE}{CJ}{2 SPACE}{CL}"
4810 PRINT"{3 SPACE}{34 CY}"
4820 PRINT"{2 DOWN}{3 SPACE}DO YOU LIVE IN A HOUSE LIKE THIS?"
4830 GOTO250
8000 REM *** READ JOYSTICK ***
8010 Z=15-(PEEK(56321)AND15)
8020 C2=C2+JT%(Z,0)
8030 R2=R2+JT%(Z,1)
8040 IF(PEEK(56321)AND16)=0THENJF=1
8050 RETURN
9000 REM *** SCREEN COLOR TABLE ***
9010 DATA2,1,3,6,3,0,4,1,2,1,5,1,6,1,6,3,7,0,7,2
9100 REM *** SONG DATA ***
9110 DATA8583,1,9634,2,8583,2,8101,2,6430,2,5728,2,7217,2,7217,1
9120 DATA6430,2,8101,2,8101,1,7217,2,8583,2,8583,1,9634,2
9130 DATA8583,2,8101,2,6430,2,5728,2,7217,2,8583,2,8583,2,5728,2
9200 REM *** JOYSTICK TABLE ***
9210 DATA0,0,-1,0,1,0,0,0,0,-1,-1,-1,1,-1,0,0,0,1,-1,1,1,1
