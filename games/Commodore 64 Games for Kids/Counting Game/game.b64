1 REM COUNTING GAME
100 DIMNT%(4,10,1),W1$(18),W2$(7)
110 FORX=0TO4:FORY=0TO10:FORZ=0TO1
120 READW:NT%(X,Y,Z)=W:NEXTZ,Y,X
130 FORX=0TO18:READX$:W1$(X)=X$:NEXTX
140 FORX=0TO7:READX$:W2$(X)=X$:NEXTX:G$="q*aszw":C$="{BLK}{RED}{PUR}{GRN}{BLU}"
150 VC=53248:POKEVC+32,14:POKEVC+33,1:PRINTCHR$(147):X=RND(-TI)
160 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
200 PRINT"{BLK}{DOWN}{13 SPACE}{RVS}COUNTING{2 SPACE}GAME{OFF}"
210 PRINT"{3 DOWN} THIS GAME TEACHES YOU TO RECOGNIZE THE{2 SPACE}NUMBERS FROM 1 TO 99."
220 PRINT"{2 DOWN} EACH NUMBER PICKED IS SHOWN"
230 PRINT" REPRESENTED AS A WORD AND AS A NUMBER{3 SPACE}OF OBJECTS."
235 PRINT"{2 DOWN} YOU MUST ENTER THE NUMBER REPRESENTED."
240 PRINT"{2 DOWN} A GAME CAN CONSIST OF FROM 1 TO 500{5 SPACE}QUESTIONS."
250 X=INT(RND(1)*5):GOSUB8000
260 PRINT"{3 DOWN}{7 SPACE}(PRESS ANY KEY TO START)"
270 GETX$:IFX$=""THEN270
300 PRINTCHR$(147);"{3 DOWN} WHAT IS THE MAXIMUM NUMBER TO BE USED?"
304 PRINT" (ENTER A NUMBER FROM 1 TO 99)"
310 X$="":INPUT"{3 DOWN} ";X$
320 RG=VAL(X$):IFRG<1 OR RG>99THEN300
400 PRINT"{CLR}{3 DOWN} HOW MANY QUESTIONS SHOULD BE ASKED IN{3 SPACE}THIS GAME?"
410 PRINT" (ENTER A VALUE FROM 1 TO 500)"
420 X$="":INPUT"{3 DOWN} ";X$
430 NQ=VAL(X$):IFNQ<1ORNQ>500THEN400
440 NC=0
500 FORLC=1TONQ:PN=INT(RND(1)*RG)+1
510 IFPN<20THENX$=W1$(PN-1):GOTO550
520 X=INT(PN/10):Y=PN-(X*10):X$=W2$(X-2)
530 IFY=0THEN550
540 X$=X$+"-"+W1$(Y-1)
550 X=INT(RND(1)*5)+1:Y=INT(RND(1)*6)+1
560 Y$=MID$(C$,X,1)+MID$(G$,Y,1)+"{SSPACE}"
600 PRINT"{CLR}{DOWN}{4 SPACE}QUESTION #";LC
610 PRINT"{4 DOWN}{4 SPACE}";X$;"{2 DOWN}"
620 FORX=1TOPN:PRINTY$;:NEXTX:PRINT"{BLK} "
630 INPUT"{2 DOWN}{4 SPACE}HOW MANY OBJECTS";X$
640 X=VAL(X$):IFX=PNTHEN700
650 POKEVC+33,0:POKEMC+0,0:POKEMC+1,20:POKEMC+4,33
655 POKEMC+5,15:POKEMC+6,15:POKEMC+24,8
660 FORX=1TO300:NEXTX:POKEMC+4,32
670 FORX=1TO30:NEXTX:POKEMC+24,0
680 PRINTCHR$(147):POKEVC+33,1
690 GOTO800
700 X=INT(RND(1)*5):GOSUB8000
710 NC=NC+1
800 NEXTLC
900 PRINT"{CLR}{2 DOWN}{14 SPACE}{RVS}GAME{2 SPACE}OVER{OFF}"
910 PRINT"{3 DOWN}{3 SPACE}NUMBER OF QUESTIONS =";NQ
920 PRINT"{DOWN}{3 SPACE}NUMBER CORRECT{6 SPACE}=";NC
930 X=INT((NC*100)/NQ)
940 PRINT"{DOWN}{3 SPACE}PERCENT CORRECT{5 SPACE}=";X;"{LEFT}%"
950 PRINT"{4 DOWN}{11 SPACE}PLAY AGAIN? ({RVS}Y{OFF}/{RVS}N{OFF})"
960 GETX$:IFX$=""THEN960
970 IFX$="Y"THEN300
980 IFX$<>"N"THEN960
990 PRINTCHR$(147)
1000 END
8000 REM *** PLAY SONGS ***
8010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
8020 FORY=0TO10
8030 Z=INT(NT%(X,Y,0)/256)
8040 POKEMC+0,NT%(X,Y,0)-(Z*256)
8050 POKEMC+1,Z:POKEMC+4,17
8060 FORZ=1TONT%(X,Y,1)*70:NEXTZ
8070 POKEMC+4,16:FORZ=1TO20:NEXTZ,Y
8080 POKEMC+24,0
8090 RETURN
9000 DATA4050,2,5407,2,5407,2,5407,2,5728,1,6430,2
9010 DATA7217,2,8583,1,8101,2,0,1,0,1
9020 DATA4817,2,6430,2,7217,2,8101,2,6430,2,6430,1
9030 DATA8101,2,9634,2,9634,2,7217,4,0,1
9040 DATA6430,2,7217,2,8101,2,6430,2,6430,2,7217,2
9050 DATA8101,2,6430,2,8101,2,8583,2,9634,4
9060 DATA5407,2,5728,2,6430,2,5407,2,5728,2,6430,2,5407,2,5728,2
9070 DATA6430,2,7217,2,4817,2
9080 DATA4291,2,4291,2,4291,2,4817,1,5407,2,5407,2
9090 DATA4817,1,5407,2,5728,1,6430,4,0,1
9200 DATAONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN,EIGHT,NINE,TEN
9210 DATAELEVEN,TWELVE,THIRTEEN,FOURTEEN,FIFTEEN,SIXTEEN
9215 DATASEVENTEEN,EIGHTEEN,NINETEEN
9220 DATATWENTY,THIRTY,FORTY,FIFTY,SIXTY,SEVENTY,EIGHTY,NINETY
