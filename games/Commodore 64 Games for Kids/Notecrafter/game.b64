1 REM NOTECRAFTER
100 VC=53248:POKEVC+32,6:POKEVC+33,0:PRINTCHR$(147)
105 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
110 DIM NT%(37,1):GOSUB1000
115 PRINT"{WHT}{CLR}{DOWN}{RVS}{10 SPACE}N O T E C R A F T E R{9 SPACE}{OFF}";
120 PRINT"{2 DOWN} THIS GAME TESTS BOTH YOUR MEMORY AND{4 SPACE}YOUR EAR FOR MUSIC."
130 PRINT"{2 DOWN} THE COMPUTER WILL PICK A NOTE AND PLAY"
135 PRINT" IT.{2 SPACE}THE COMPUTER WILL THEN PICK A"
140 PRINT" NOTE FOR YOU AND PLAY IT."
150 PRINT"{2 DOWN} YOU MUST RAISE OR LOWER THE PITCH OF"
155 PRINT" YOUR NOTE UNTIL IT MATCHES THE NOTE"
160 PRINT" THE COMPUTER PICKED."
170 PRINT"{2 DOWN}{9 SPACE}(HIT ANY KEY TO START)"
180 GOSUB1500
190 DIMRT$(5):FORX=0TO5:READX$:RT$(X)=X$:NEXTX
200 REM *** SELECT THE NOTES
210 CN=INT(RND(1)*61)+10
220 PN=INT(RND(1)*61)+10:IFCN=PNTHEN220
230 POKEVC+33,15:PRINT"{CLR}{BLK}{3 DOWN}{12 SPACE}COMPUTER'S NOTE:"
240 X=CN:GOSUB2000:FORX=1TO1000:NEXTX
250 PRINT"{3 DOWN}{13 SPACE}PLAYER'S NOTE:"
260 X=PN:GOSUB2000
300 PRINT"{CLR}{2 DOWN} ENTER OPTION ---{2 DOWN}"
310 PRINT"{DOWN}{3 SPACE}{RVS}1{OFF} LOWER PITCH A SMALL AMOUNT"
320 PRINT"{DOWN}{3 SPACE}{RVS}2{OFF} LOWER PITCH A MEDIUM AMOUNT"
330 PRINT"{DOWN}{3 SPACE}{RVS}3{OFF} LOWER PITCH A LARGE AMOUNT"
340 PRINT"{2 DOWN}{3 SPACE}{RVS}5{OFF} *** NOTE MATCHES ***"
350 PRINT"{2 DOWN}{3 SPACE}{RVS}7{OFF} RAISE PITCH A SMALL AMOUNT"
360 PRINT"{DOWN}{3 SPACE}{RVS}8{OFF} RAISE PITCH A MEDIUM AMOUNT"
370 PRINT"{DOWN}{3 SPACE}{RVS}9{OFF} RAISE PITCH A LARGE AMOUNT"
400 X=PEEK(197):IFX=64THEN400
410 IFX=56THENPN=PN-1:GOTO500
420 IFX=59THENPN=PN-5:GOTO500
430 IFX=8THENPN=PN-10:GOTO500
440 IFX=16THEN700
450 IFX=24THENPN=PN+1:GOTO500
460 IFX=27THENPN=PN+5:GOTO500
470 IFX=32THENPN=PN+10:GOTO500
480 GOTO400
500 IFPN<10THENPN=10
610 IFPN>70THENPN=70
620 X=PN:GOSUB2000:GOTO400
700 IFPN<>CNTHEN800
710 PRINT"{CLR}{2 DOWN}{12 SPACE}CONGRATULATIONS!"
720 PRINT"{3 DOWN}{2 SPACE}YOU PICKED THE VERY SAME NOTE AS"
725 PRINTSPC(11);"{DOWN}THE COMPUTER DID!!"
730 PRINT"{3 DOWN}{3 SPACE}YOU MUST BE A VERY GOOD MUSICIAN!!"
740 PRINT"{3 DOWN}{8 SPACE}PRESS ANY KEY TO CONTINUE"
750 POKE 198,0
760 GOSUB 1500
770 GOTO 900
800 PRINT"{CLR}{3 DOWN}{12 SPACE}COMPUTER'S NOTE:"
805 X=CN:GOSUB2000:FORX=1TO1000:NEXTX
810 PRINT"{3 DOWN}{13 SPACE}PLAYER'S NOTE:"
815 X=PN:GOSUB2000
820 IFPN<CNTHENX=CN-PN:GOTO840
830 X=PN-CN
840 Y=100-INT((X*100)/60):IFY<1THENY=1
850 PRINT"{3 DOWN}{11 SPACE}YOUR SCORE IS";Y;"{LEFT}%"
860 X=INT((100-Y)/10):IFX<0THENX=0
870 IFX>5THEN900
880 PRINT"{3 DOWN}{9 SPACE}YOUR EAR FOR MUSIC IS":X$=RT$(X)
885 IFLEN(X$)<38THENX$=" "+X$+" ":GOTO885
890 PRINTX$;
900 PRINT"{4 DOWN}{11 SPACE}PLAY AGAIN ({RVS}Y{OFF} OR {RVS}N{OFF})"
910 GETX$:IFX$=""THEN910
920 IFX$="Y"THEN200
930 IFX$<>"N"THEN910
940 PRINTCHR$(147)
950 END
1000 FORX=832TO832+63:READZ:POKEX,Z:NEXTX
1010 POKEVC+0,40:POKEVC+2,120:POKEVC+4,200:POKEVC+6,22
1020 POKEVC+1,40:POKEVC+3,30:POKEVC+5,20:POKEVC+7,10
1030 POKEVC+16,8:POKEVC+23,15:POKEVC+27,15:POKEVC+28,0:POKEVC+29,15
1040 FORX=0TO3:POKEVC+39+X,X+2:NEXTX
1050 FORX=0TO3:POKE2040+X,13:NEXTX
1100 FORX=0TO37:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
1110 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129
1120 RETURN
1500 POKEVC+21,15:POKEMC+24,15:X=0
1510 Y=INT(NT%(X,0)/256)
1520 POKEMC+0,NT%(X,0)-(Y*256)
1530 POKEMC+1,Y:POKEMC+4,17
1540 FORZ=1TONT%(X,1)*100:NEXTZ
1550 X=X+1:IFX>37THENX=0
1560 POKEMC+4,16
1570 FORZ=1TO7STEP2
1580 Y=PEEK(VC+Z)+8:IFY>250THENY=10
1590 POKEVC+Z,Y
1595 NEXTZ
1600 GETX$:IFX$=""THEN1510
1610 POKEMC+24,0:POKEVC+21,0
1620 PRINTCHR$(147)
1630 FORX=1TO1000:NEXTX
1640 RETURN
1700 DATA1,255,248,1,255,248,1,255,248,1,192,56,1,192,56,1,255,248,1,192
1710 DATA56,1,192,56,1,192,56,1,192,56,1,192,56,1,192,56,1,192,56,1,192,56
1720 DATA15,193,248,31,195,248,63,199,248,127,143,240,255,31,224,254
1730 DATA31,192,124,15,128,0
1800 DATA8101,2,8583,2,10814,2,5728,1,5728,2,5728,2,5728,2,6430,2,5728,1
1810 DATA8101,2,10814,1,8583,2,11457,1,6430,2,7217,1,8101,2,8583,1
1820 DATA10814,2,10814,1,10814,2,5728,1,5728,2,5728,1,6430,2,5728,1,8101,2
1830 DATA10814,2,10814,2,8583,2,11457,1,6430,2,7217,1
1840 DATA8101,2,8101,1,7217,1,7217,2,8101,3,8101,2
1900 DATAEXCELLENT,"VERY GOOD",GOOD,"ABOVE AVERAGE",NORMAL,"NOT BAD"
2000 POKEMC+0,0:POKEMC+1,X:POKEMC+4,17:POKEMC+5,15:POKEMC+6,15
2010 POKEMC+24,5
2020 FORX=1TO800:NEXTX:POKEMC+4,16
2030 FORX=1TO50:NEXTX
2040 POKEMC+24,0
2050 RETURN
