1 REM WORD SCRAMBLE
100 TS=50
110 DIMWT$(TS-1),R%(20),NT%(30,1)
120 VC=53248:POKEVC+32,14:POKEVC+33,12:PRINTCHR$(147)
130 Z=((PEEK(53272)AND240)/16)*1024:M=Z+603:C=55296+603
140 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
150 READX$:IFX$<>"$SONG"THEN150
160 FORW=0TO30:FORY=0TO1:READX:NT%(W,Y)=X:NEXTY,W
200 PRINT"{2 SPACE}{WHT}*********{2 SPACE}{BLK}DROW{2 SPACE}ELBMARCS{2 SPACE}{WHT}*********"
210 PRINT"{2 DOWN} THE COMPUTER WILL SHOW YOU A SCRAMBLED{2 SPACE}WORD."
220 PRINT"{DOWN} YOU MUST UNSCRAMBLE IT AND ENTER THE{4 SPACE}CORRECT WORD."
230 PRINT"{2 DOWN}{4 SPACE}+5 POINTS FOR EACH CORRECT WORD."
240 PRINT"{DOWN}{4 SPACE}-1 POINT FOR AN INCORRECT GUESS."
250 PRINT"{2 DOWN} EACH GAME LASTS TWO MINUTES."
255 PRINT"{DOWN} PRESS THE {RVS}F1{OFF} KEY TO SKIP A WORD."
260 GOSUB6000:FORX=1TO1000:NEXTX
270 GOSUB6200
280 PRINT"{DOWN}{7 SPACE}(PRESS ANY KEY TO START)"
290 GETX$:IFX$=""THEN290
300 PRINT"{CLR}{3 DOWN}{2 SPACE}ENTER SKILL LEVEL:"
310 PRINT"{2 DOWN}{6 SPACE}{RVS}1{OFF}{SSPACE}- BEGINNER":PRINT"{10 SPACE}(4 LETTERS)"
320 PRINT"{2 DOWN}{6 SPACE}{RVS}2{OFF} - INTERMEDIATE":PRINT"{10 SPACE}(5-6 LETTERS)"
330 PRINT"{2 DOWN}{6 SPACE}{RVS}3{OFF}{SSPACE}- ADVANCED":PRINT"{10 SPACE}(7-9 LETTERS)"
335 PRINT"{2 DOWN}{2 SPACE}(ENTER 1, 2 OR 3)"
340 GETX$:IFX$=""THEN340
350 IFX$<"1"ORX$>"3"THEN340
360 SK=VAL(X$):RESTORE:IFSK=1THEN380
370 X=(SK-1)*TS:FORY=1TOX:READX$:NEXTY
380 FORX=1TOTS:READX$:WT$(X-1)=X$:NEXTX
390 SC=0:WC=1:TL$="000200":TI$="000000"
500 X=INT(RND(1)*TS):UW$=WT$(X):IFUW$="$"THEN500
510 WT$(X)="$"
520 X=LEN(UW$):FORY=1TOX:R%(Y)=0:NEXTY:FORY=1TOX
530 Z=INT(RND(1)*X)+1:IFR%(Z)<>0THEN530
540 R%(Z)=Y:NEXTY:SW$=" ":FORY=1TOX:SW$=SW$+MID$(UW$,R%(Y),1):NEXTY
550 IFSW$=UW$THEN520
560 X$=MID$(TI$,3,2)+"."+MID$(TI$,5,2)
565 IFTL$<TI$THEN700
570 PRINT"{CLR}{2 DOWN}{3 SPACE}WORD{2 SPACE}#";WC:PRINT"{DOWN}{3 SPACE}TIME{2 SPACE}= ";X$
580 PRINT"{DOWN}{3 SPACE}SCORE =";SC
590 PRINT"{4 DOWN}{3 SPACE}--->{4 SPACE}{BLK}";SW$;"{WHT}";
600 GOSUB8000
605 IFSW$="q"THEN680
610 IFSW$<>UW$THENSC=SC-1:GOSUB7000:GOTO600
620 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,5
630 FORX=3TO12
640 POKEMC+1,X*3:POKEMC+4,17:POKEVC+33,X
650 FORY=1TO40:NEXTY:POKEMC+4,16
660 FORY=1TO20:NEXTY,X:POKEMC+24,0
670 SC=SC+5
680 WC=WC+1:IFWC<(TS+1)THEN500
700 IFSC<0THENSC=0
710 PRINT"{CLR}{2 DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}":PRINT"{3 DOWN}{6 SPACE}SCORE =";SC
720 PRINT"{3 DOWN}{6 SPACE}PLAY AGAIN? ({RVS}Y{OFF} OR {RVS}N{OFF})"
730 GETX$:IFX$=""THEN730
740 IFX$="Y"THEN300
750 IFX$<>"N"THEN730
760 GOSUB6000:PRINTCHR$(147)
770 END
6000 REM *** PLAY THE SONG ***
6010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,39:POKEMC+6,129:POKEMC+24,8
6020 FORX=0TO30
6030 Y=INT(NT%(X,0)/256)
6040 POKEMC+0,NT%(X,0)-(Y*256)
6050 POKEMC+1,Y:POKEMC+4,33
6060 FORY=1TONT%(X,1)*60:NEXTY
6070 POKEMC+4,32:FORY=1TO20:NEXTY,X
6080 POKEMC+24,0
6090 RETURN
6200 REM *** FIX THE TITLE ***
6210 Z=Z+52:X$="WORD{2 SPACE}SCRAMBLE"
6220 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,5
6230 FORX=1TO14
6240 POKEMC+1,X*3:POKEMC+4,33
6250 Y=ASC(MID$(X$,X,1))
6260 IFY<>32THENY=Y-64
6270 POKEZ+X,Y
6280 FORY=1TO40:NEXTY:POKEMC+4,32
6290 FORY=1TO10:NEXTY,X:POKEMC+24,0
6295 RETURN
7000 REM *** BAD ANSWER - BEEP IT ***
7010 POKEMC+0,0:POKEMC+1,10:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15:POKEMC+24,5
7020 FORY=1TO500:NEXTY
7030 POKEMC+4,32
7040 FORY=1TO100:NEXTY:POKEMC+24,0
7050 PRINT"{HOME}{6 DOWN}{3 RIGHT}SCORE =";SC;"{5 SPACE}";
7060 RETURN
8000 SW$="":FORX=0TO19:POKEM+X,32:POKEC+X,0:NEXTX:POKEM,160
8010 GETX$:IFTL$<TI$THEN700
8020 Y$=MID$(TI$,3,2)+"."+MID$(TI$,5,2):PRINT"{HOME}{4 DOWN}{11 RIGHT}";Y$
8025 IFX$=""THEN8010
8030 X=ASC(X$)
8040 IFX<>133THEN8045
8042 PRINT"{6 DOWN}{11 RIGHT}{BLK}";UW$:FORX=1TO1500:NEXTX:PRINT"{WHT}{CLR}":SW$="q":RETURN
8045 IFX=13THENRETURN
8050 IFX=20THEN8100
8055 IFX<65ORX>90THEN8010
8060 Z=LEN(SW$):POKEM+Z,X-64:POKEM+Z+1,160:SW$=SW$+X$:GOTO8010
8100 Z=LEN(SW$):IFZ=0THEN8010
8110 POKEM+Z,32:POKEM+Z-1,160:X$=LEFT$(SW$,Z-1):SW$=X$:GOTO8010
9000 DATATALK,FOUR,BEST,BLUE,WARM,SOAP,FOOT,PILL,WORK,YELL
9010 DATAEVEN,GATE,HELP,HOUR,JOKE,WORD,GI RL,MOON,NICE,DISH
9020 DATAJULY,JUNE,BOOK,HIDE,BOYS,TREE,CORN,GONE,QUIT,EASY
9030 DATAGOLD,GOAL,WHIP,PILE,PIGS,PLAY,TEST,BOIL,TOYS,LION
9040 DATAROAR,THEM,ZOOM,MATH,FIVE,NINE,FARM,JUNK,HEAD,IRON
9100 DATAAPPLE,BUTTER,CHILD,DRIVE,ENOUGH,GIVEN,HOLLOW,IGLOO,LAMBS,MAKER
9110 DATANOISE,PLAYER,QUIET,RUBBER,SHAVE,TELLER,VIOLET,WATER,YOUNG,ZEBRA
9120 DATAPLEASE,CAMEL,STORY,MOUSE,RABBIT,PENCIL,DESKS,CHAIR,WATCH,SHELL
9130 DATASLEEP,ORANGE,BOTTLE,HOTEL,CANDLE,MONDAY,FORGET,LIGHT,PILLOW,SHELF
9140 DATAGIANT,WHALE,SHIRT,THREE,EVERY,PHONE,LETTER,GRASS,BUBBLE,ROAST
9200 DATAALPHABET,BOTTLED,GEOGRAPHY,DANGEROUS,ELEMENTS
9210 DATAFEBRUARY,GREATER,HELIPORT,ISOLATE,LAMINATE
9230 DATAMESSAGE,NOVEMBER,OPTIMIST,PASSAGE,REMEMBER
9240 DATASALIVATE,TELEPHONE,UNKNOWN,VACCINATE,WITNESS
9250 DATAMUSHROOM,EVAPORATE,PHYSICIAN,REGULATE,REPORTER
9260 DATACOMPUTER,TERMINAL,GRANDSON,APRICOT,QUESTION
9270 DATAWEATHER,GIRAFFE,SHARPEN,HYDROGEN,JOURNAL 
9280 DATABEDROOM,TROUSERS,NECKTIE,RELIGION,SCIENCE 
9290 DATAGUMBALL,LUBRICATE,BATHROOM,CLOTHES,WASHING
9300 DATALAUNDRY,DRAWER,ASPARAGUS,OATMEAL,PENGUIN 
20000 REM *** SONG DATA ***
20005 DATA"$SONG"
20010 DATA2408,1,2864,2,3215,2,3608,2,3215,1,2864,2,2703,2,2408,2,2703,1
20020 DATA2864,2,3608,2,4817,2,4817,2,4291,1,3608,2,4291,1,4817,2,4050,2
20030 DATA3215,2,3608,2,4050,2,3608,2,2864,2,2408,2,2703,1,2864,2
20040 DATA3608,2,4050,2,3608,2,2864,1,2408,2
