1 REM SPELLING BEE
100 NW%=40:VC=53248:MC=54272
105 POKEVC+32,15:POKEVC+33,1:PRINTCHR$(147)
106 FORX=54272TO54296:POKEX,0:NEXTX
110 DIM NT%(11),WT$(NW%-1),WI$(19)
120 S1=((PEEK(53272)AND240)/16)*1024:S2=55296
130 FORX=0TO11:READZ:NT%(X)=Z:NEXTX
140 PRINT"{BLK}{DOWN}{RVS}{14 SPACE}SPELLING BEE{14 SPACE}{OFF}";
150 PRINT"{2 DOWN}THE COMPUTER WILL SELECT A WORD AND SHOW";
160 PRINT"YOU THE NUMBER OF LETTERS IT CONTAINS."
170 PRINT"{2 DOWN}YOU MUST GUESS THE WORD BY SELECTING ONELETTER AT A TIME."
180 PRINT"{2 DOWN}IF YOU PICK MORE THAN 11 INCORRECT"
190 PRINT "LETTERS, THE SPELLING BEE WILL DISAPPEAR AND YOU WILL LOSE THAT ROUND."
200 GOSUB2500:PRINT"{2 DOWN}{8 SPACE}(PRESS ANY KEY TO START)"
210 GETX$:IFX$=""THEN210
220 PRINTCHR$(147);"{3 DOWN}{2 SPACE}ENTER SKILL LEVEL:"
230 PRINT"{3 DOWN}{4 SPACE}{RVS}1{OFF} BEGINNER":PRINT"{3 DOWN}{4 SPACE}{RVS}2{OFF} INTERMEDIATE"
240 PRINT"{3 DOWN}{4 SPACE}{RVS}3{OFF} ADVANCED"
250 GETP$:IFP$=""THEN250
260 IFP$<"1"ORP$>"3"THEN250
270 X=(VAL(P$)-1)*NW%:IFX=0THEN290
280 FORY=1TOX:READX$:NEXTY
290 FORX=1TONW%:READX$:WT$(X-1)=X$:NEXTX:WU=0
300 WU=WU+1
310 X=INT(RND(1)*NW%):WO$=WT$(X):IFWO$="$"THEN310
320 WT$(X)="$":LW%=LEN(WO$)
330 FORX=0TOLW%-1
340 WI$(X)=MID$(WO$,X+1,1):NEXTX
350 LC%=0:OF%=921:BC%=0:GOSUB1000:LW%=LW%-1
400 GET X$:IFX$=""THEN400
410 X=ASC(X$):IFX<65ORX>90THEN400
500 HI=0:FORY=0TOLW%:IFWI$(Y)<>X$THEN530
510 HI=1:WI$(Y)="$":POKES1+720+UL+(Y*2),X-64
520 LC%=LC%+1
530 NEXTY:IFHI=1THEN560
540 GOSUB3000:BC%=BC%+1
550 IFBC%>10THENGOSUB2000:LO%=LO%+1:FORX=1TO2000:NEXTX:GOTO700
560 IFLC%<=LW%THEN400
570 GOSUB4000:WI%=WI%+1:GOTO700
700 IFWU=NW%THENPRINT"{CLR}{RED}{2 DOWN}":GOTO750
710 PRINTCHR$(147);"{BLK}{3 DOWN}{12 SPACE}{RVS}ANOTHER WORD ?{OFF}"
715 PRINT"{2 DOWN}{13 SPACE}ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
720 GETX$:IFX$=""THEN720
730 IFX$="Y"THEN300
740 IFX$<>"N"THEN720
750 PRINT"{3 DOWN}{7 SPACE}# CORRECT =";WI%
760 PRINT"{2 DOWN}{7 SPACE}# MISSED{2 SPACE}=";LO%
770 GOSUB2500:FORX=1TO1000:NEXTX
780 PRINTCHR$(147)
790 END
1000 POKEVC+32,6:POKEVC+33,12
1005 PRINTCHR$(147);"{BLK}{DOWN}{8 SPACE}S P E L L I N G{4 SPACE}B E E{2 DOWN}"
1010 PRINTSPC(20);"{BLK}{C+}{C-}"
1020 PRINTSPC(20);"{2 C+}"
1030 PRINTSPC(16);"m{C@}{2 SPACE}{2 C+}"
1040 PRINTSPC(18);"m {C+}{C-}{CM}"
1050 PRINTSPC(18);"{RVS}{S#}{YEL} {BLK} {YEL}{C*}{OFF}{BLK}n n"
1060 PRINTSPC(16);"c{RVS}{YEL} {BLK} {YEL} {BLK} {YEL} {BLK} {OFF}q"
1070 PRINTSPC(18);"{C*}{RVS}{YEL} {BLK} {OFF}{YEL}{S#}{BLK}m m"
1080 PRINTSPC(18);"n {C+}{C-}{CM}"
1090 PRINTSPC(16);"n{CT}{2 SPACE}{2 C+}"
1100 PRINTSPC(20);"{2 C+}"
1110 PRINTSPC(20);"{C+}{C-}"
1500 PRINT"{5 DOWN}{BLK}{40 S*}";
1510 PRINT" ENTER A LETTER:"
1520 UL=INT((40-((LW%*2)-1))/2)
1525 X=(LW%-1)*2
1530 FORY=0TOXSTEP2
1540 POKES1+720+UL+Y,121:POKES2+720+UL+Y,1:NEXTY
1550 RETURN
2000 REM **** WORD MISSED / SHOW IT
2005 FORX=0TOLW%:IFWI$(X)="$"THEN2020
2010 Y=ASC(WI$(X)):POKES1+720+UL+(X*2),Y-64
2020 NEXTX
2030 POKEMC+0,0:POKEMC+1,1:POKEMC+5,15:POKEMC+6,15
2040 POKEMC+24,15:FORY=80TO10STEP-5
2050 POKEMC+1,Y:POKEMC+4,17:FORX=1TO50:NEXTX
2060 POKEMC+4,16:NEXTY:POKEMC+24,0
2070 RETURN
2500 REM *** PLAY A LITTLE SONG
2510 POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
2520 FORX=0TO11:Y=NT%(X):Z=INT(Y/256)
2530 Y=Y-(Z*256):POKEMC+0,Y:POKEMC+1,Z
2540 POKEMC+4,17:FORY=1TO100:NEXTY
2550 POKEMC+4,16:FORY=1TO40:NEXTY
2560 NEXTX:POKEMC+24,0
2570 RETURN
3000 REM *** BLANK OUT PART OF THE BEE
3002 PRINT"{HOME}{2 DOWN}":FORY=0TOBC%:PRINT" ":NEXTY
3005 PRINT"{32 SPACE}"
3010 POKEVC+33,0:POKEMC+0,49:POKEMC+1,10
3020 POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
3030 POKEMC+4,17:FORY=1TO250:NEXTY:POKEMC+4,16
3040 POKEMC+24,0:POKEVC+33,12
3050 POKES1+OF%,X-64:POKES2+OF%,1:OF%=OF%+2
3060 RETURN
4000 REM *** WORD GUESSED - FLASH SCREEN
4010 POKEMC+0,0:POKEMC+5,15:POKEMC+6,15:POKEMC+1,0:Y=30:POKEMC+24,15
4020 FORX=2TO12:POKEVC+33,X
4030 POKEMC+1,Y:POKEMC+4,17:FORZ=1TO100:NEXTZ
4040 POKEMC+4,16:Y=Y+4:NEXTX:POKEMC+24,0
4050 RETURN
5000 DATA5728,5728,7217,8583,7217,5728,6430,8101,5728,6430,5728,5728
6000 DATABOY,GIRL,FIRST,HER,WERE
6010 DATATAKE,NAME,DAY,ASK,SUN
6020 DATAWITH,WHEN,THEM,AND,WENT
6030 DATASTOP,CAN,LOOK,READ,BOOK
6040 DATACAN,BEAT,TIME,RIDE,ADD
6050 DATABALL,HARD,BIG,MAKE,CAME
6060 DATAGAME,GREEN,GOOD,SAY,MEAN
6070 DATATREE,HAD,TOP,MAN,GAVE
7000 DATAAFTERNOON,BECAUSE,THIRD,ALWAYS,ANIMAL
7010 DATABASEBALL,ALIKE,CLOCK,BROTHER,CATCH
7020 DATAMORNING,SEVEN,FRIEND,GROUND,HOUSE
7030 DATALEARN,LARGE,LUNCH,PURPLE,HORSE
7040 DATALAST,BEST,SCHOOL,LITTLE,UNDER
7050 DATAWATER,LETTER,AROUND,DRINK,THANK
7060 DATAPRINT,AGAIN,ABOVE,SISTER,LIGHT
7070 DATACOLOR,AIRPLANE,ABOUT,EARTH,ROUND
8000 DATASHELF,DINNER,WINDOW,MACHINE,FINGERNAIL 
8010 DATANEIGHBOR,BALLOON,DIRECTION,SANDWICH,PRESIDENT
8020 DATANOTEBOOK,ENGINE,ASLEEP,LANGUAGE,VEGETABLE
8030 DATACOMPANY,TOMORROW,SCREAM,MINUTE,KNIVES
8040 DATAIMPOSSIBLE,BACON,FROZEN,NEEDLE,CAMERA
8050 DATACARDBOARD,BAREFOOT,BEAUTY,IMPORTANT,ANNOUNCE
8060 DATAGARBAGE,ALARM,FARMER,SNEEZE,GENERAL
8070 DATASPOON,WEIGHT,CONTAIN,FAMILY,MYSELF
