1 REM STATE..UNION
100 NS=50:CB=828
110 VC=53248:POKEVC+32,2:POKEVC+33,6:PRINTCHR$(147)
120 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
130 DIM QT$(5),NT%(22,1)
140 READX$:IFX$<>"$SONG"THEN140
150 FORX=0TO22:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
160 SM=((PEEK(53272)AND240)/16)*1024
170 CM=55296
200 GOSUB700
205 S%=0:FORX=1TONS:POKECB+X,0:NEXTX:POKEVC+32,6:POKEVC+33,15:PRINTCHR$(147)
210 FORX=1TONS:GOSUB800:POKECB+Z,X:NEXTX
300 X=1
305 Y=PEEK(CB+X):Y=(Y-1)*6
310 RESTORE:IFY=0THEN330
320 FORW=1TOY:READX$:NEXTW
330 FORW=1TO6:READX$:QT$(W-1)=X$:NEXTW
400 W=0
405 PRINTCHR$(147);"{BLK}{23 DOWN}";SPC(15);"SCORE =";S%;
410 PRINT"{HOME}{2 DOWN}{3 SPACE} QUESTION #";W+1
420 PRINT"{3 DOWN}";QT$(W);".{3 DOWN}"
430 X$="":POKE198,0:INPUTX$:IFX$=QT$(4)THEN500
435 IFX$="END"THEN600
440 POKEVC+33,0:POKEMC+0,0:POKEMC+1,20:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15
450 POKEMC+24,8:FORV=1TO200:NEXTV
452 POKEMC+4,32:FORV=1TO30:NEXTV
454 POKEMC+24,0:PRINTCHR$(147):POKEVC+33,15
455 IFW=4THEN465
456 W=W+1:IFW<4THEN405
457 GOSUB4000
458 IFX$=QT$(4)THEN500
459 IFX$="END"THEN600
460 W=4:GOTO440
465 PRINT"{BLU}{2 DOWN}{3 SPACE}STATE=";QT$(4);"{BLK}":S%=S%-5
470 FORW=1TO1500:NEXTW
480 GOTO590
500 S%=S%+(5-W)
510 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,9
520 FORV=4TO15
530 POKEVC+33,V:POKEMC+1,V*3:POKEMC+4,33
540 FORW=1TO30:NEXTW:POKEMC+4,32
550 FORW=1TO10:NEXTW
560 NEXTV
570 POKEMC+24,0
590 X=X+1:IFX<=NSTHEN305
600 PRINTCHR$(147);"{DOWN}{15 SPACE}{RVS}TEST OVER!{OFF}"
610 PRINT"{3 DOWN}{4 SPACE}YOUR SCORE = ";S%
620 IFS%<0THENS%=0
625 X=X-1:IFX<1THENX=1
630 X=INT((S%*100)/(X*5))
640 PRINT"{DOWN}{4 SPACE}YOUR GRADE = ";X;"{LEFT}%"
650 PRINT"{3 DOWN}{4 SPACE}PLAY AGAIN?":PRINT"{DOWN}{4 SPACE}ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
660 GETX$:IFX$=""THEN660
670 IFX$="Y"THEN205
680 IFX$<>"N"THEN660
690 GOSUB900:PRINTCHR$(147)
695 END
700 PRINT"{WHT}{2 SPACE}{RVS}{9 SPACE}STATE OF THE UNION{9 SPACE}{OFF}"
705 PRINT"{DOWN} YOU WILL BE ASKED UP TO 5 QUESTIONS{5 SPACE}ABOUT EACH U.S. STATE."
710 PRINT"{DOWN} POINTS WILL BE GIVEN DEPENDING UP ON"
712 PRINT" HOW MANY QUESTIONS YOU NEED:"
715 PRINT"{2 DOWN}{2 SPACE}1 QUESTION{2 SPACE}={2 SPACE}5.":PRINT"{2 SPACE}2 QUESTIONS ={2 SPACE}4."
720 PRINT"{2 SPACE}3 QUESTIONS ={2 SPACE}3.":PRINT"{2 SPACE}4 QUESTIONS ={2 SPACE}2."
722 PRINT"{2 SPACE}5 QUESTIONS ={2 SPACE}1."
725 PRINT"{2 SPACE}ALL MISSED{2 SPACE}= -5."
730 PRINT"{2 DOWN} TYPE {RVS}END{OFF} TO QUIT EARLY."
785 PRINT"{3 DOWN}{7 SPACE}(PRESS ANY KEY TO START)"
786 GOSUB900
787 PRINT"{DOWN}{10 SPACE}(TEN-SECOND DELAY)";
790 GETX$:IFX$=""THEN790
795 RETURN
800 W=0
810 Z=INT((RND(1)*NS)+1):IFPEEK(CB+Z)=0THENRETURN
820 W=W+1:IFW<9THEN810
830 Z=1
840 IFPEEK(CB+Z)=0THENRETURN
850 Z=Z+1:GOTO840
900 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
910 FORX=0TO22
920 Y=INT(NT%(X,0)/256)
930 POKEMC+0,NT%(X,0)-(Y*256)
940 POKEMC+1,Y:POKEMC+4,17
950 FORY=1TONT%(X,1)*40:NEXTY
960 POKEMC+4,16:FORY=1TO20:NEXTY,X:POKEMC+24,0
970 RETURN
1000 DATA"JEFFERSON DAVIS BECAME PRESIDENT OF THE CONFEDERACY HERE"
1010 DATA"THE SOUTH'S LARGEST STEEL PRODUCING{5 SPACE}STATE"
1020 DATA"IT'S FAMOUS FOR MAGNOLIAS, COTTON, AND{2 SPACE}EARLY AEROSPACE RESEARCH"
1030 DATA"THE STATE CAPITAL IS MONTGOMERY",ALABAMA,627
1040 DATA"THE HIGHEST PEAK IN NORTH AMERICA IS{4 SPACE}HERE"
1050 DATA"IT WAS ONCE KNOWN AS SEWARD'S ICE BOX"
1060 DATA"IT HAS THE LARGEST AREA AND THE SMALLEST POPULATION"
1070 DATA"THE STATE CAPITAL IS JUNEAU",ALASKA,1
1080 DATA"THE LAST MAINLAND STATE TO JOIN THE{5 SPACE}UNION"
1090 DATA"ASTRONOMERS LIKE ITS DRY, CLEAR AIR"
1100 DATA"THE HOME OF THE GRAND CANYON"
1110 DATA"THE STATE CAPITAL IS PHOENIX",ARIZONA,569
1120 DATA"IT PRODUCES 90% OF AMERICA'S ALUMINUM"
1130 DATA"IT WAS THE HOME OF HANGING JUDGE PARKER"
1140 DATA"IT CONTAINS AMERICA'S ONLY DIAMOND MINE"
1150 DATA"THE STATE CAPITAL IS LITTLE ROCK",ARKANSAS,583
1160 DATA"THE MOST POPULOUS OF THE STATES"
1170 DATA"IT CONTAINS THE LOWEST ELEVATION IN{5 SPACE}NORTH AMERICA"
1180 DATA"THERE WAS 'GOLD IN THEM THAR HILLS'"
1190 DATA"THE STATE CAPITAL IS SACRAMENTO",CALIFORNIA,444
1200 DATA"THE STATE WITH THE HIGHEST OVERALL{6 SPACE}ELEVATION"
1210 DATA"IT PRODUCES 72% OF THE WORLD'S{10 SPACE}MOLYBDENUM"
1220 DATA"ITS NAME MEANS 'COLORED RED' IN SPANISH"
1230 DATA"THE STATE CAPITAL IS DENVER",COLORADO,414
1240 DATA"IT WAS THE HOME OF NATHAN HALE"
1250 DATA"IT RECEIVED STATEHOOD IN 1788"
1260 DATA"IT IS FAMOUS FOR YALE UNIVERSITY"
1270 DATA"THE STATE CAPITAL IS HARTFORD",CONNECTICUT,275
1280 DATA"THE FIRST STATE TO RATIFY THE{11 SPACE}CONSTITUTION"
1290 DATA"IT IS SOMETIMES CALLED THE 'CHEMICAL{4 SPACE}CAPITAL OF THE WORLD'"
1300 DATA"AMERICA'S SECOND SMALLEST STATE"
1310 DATA"THE STATE CAPITAL IS DOVER",DELAWARE,394
1320 DATA"ITS COASTLINE MEASURES 1,350 MILES"
1330 DATA"THE SOUTHERNMOST OF THE CONTINENTAL U.S"
1340 DATA"THE HOME OF CAPE CANAVERAL"
1350 DATA"THE STATE CAPITAL IS TALLAHASSEE",FLORIDA,832
1360 DATA"THE LAST OF THE 13 COLONIES TO BE{7 SPACE}SETTLED"
1370 DATA"HOME OF STONE MOUNTAIN PARK"
1380 DATA"BIRTHPLACE OF UNCLE REMUS, SCARLETT{5 SPACE}O'HARA AND COCA-COLA"
1390 DATA"THE STATE CAPITAL IS ATLANTA",GEORGIA,670
1400 DATA"THIS STATE WAS FORMED BY ERUPTING{7 SPACE}VOLCANOS"
1410 DATA"THE 50TH STATE TO JOIN THE UNION"
1420 DATA"WORLD WAR II STARTED FOR THE U.S. WITH{2 SPACE}AN ATTACK HERE"
1430 DATA"THE STATE CAPITAL IS HONOLULU",HAWAII,760
1440 DATA"IT LEADS IN PRODUCTION OF SILVER AND{4 SPACE}COBALT"
1450 DATA"HOME OF THE NATION'S DEEPEST CANYON --{2 SPACE}HELLS CANYON"
1460 DATA"THE STATE CAPITAL IS BOISE"
1465 DATA"FAMOUS FOR ITS POTATOES",IDAHO,249
1470 DATA"THE LINCOLN-DOUGLAS DEBATES WERE HELD{3 SPACE}HERE"
1480 DATA"IT IS HOME OF THE WORLD'S TALLEST{7 SPACE}BUILDING"
1490 DATA"THE STATE CAPITAL IS SPRINGFIELD"
1500 DATA"THE WINDY CITY IS PART OF THIS GREAT{4 SPACE}LAKE STATE",ILLINOIS,385
1510 DATA"THE SMALLEST OF THE 'LOWER 48' STATES{3 SPACE}WEST OF THE ALLEGHENIES"
1520 DATA"ITS LAKE MICHIGAN WATERFRONT IS LINED{3 SPACE}WITH STEEL MILLS"
1530 DATA"IT IS THE HOME OF PURDUE UNIVERSITY"
1540 DATA"THE STATE CAPITAL IS INDIANAPOLIS",INDIANA,427
1550 DATA"THIS FARMING STATE PRODUCES 10% OF{6 SPACE}AMERICA'S FOOD"
1560 DATA"IT IS THE LEADING HOG STATE IN THE U.S"
1570 DATA"IT IS BORDERED BY SIX FARMING AND TWO{3 SPACE}GREAT LAKE STATES"
1580 DATA"THE STATE CAPITAL IS DES MOINES ",IOWA,342
1590 DATA"IT PRODUCES MORE WHEAT THAN ANY OTHER{3 SPACE}STATE"
1600 DATA"IT HAD ITS OWN CIVIL WAR OVER SLAVERY IN 1851"
1610 DATA "LEAVENWORTH FEDERAL PENITENTIARY IS{5 SPACE} HERE"
1620 DATA"THE STATE CAPITAL IS TOPEKA",KANSAS,459
1630 DATA"PRESIDENTS OF BOTH CIVIL WAR FACTIONS{3 SPACE}CAME FROM THIS STATE"
1640 DATA"THE STATE CAPITAL IS FRANKFORT"
1650 DATA"IT IS THE SITE OF THE WORLD'S MOST{6 SPACE}FAMOUS HORSE RACE"
1660 DATA"MAMMOTH CAVE AND FORT KNOX ARE HERE",KENTUCKY,468
1670 DATA"IT CONTAINS THE NATION'S SECOND BUSIEST SEAPORT"
1680 DATA"IT IS DIVIDED INTO PARISHES, AND NOT{4 SPACE}COUNTIES"
1690 DATA"THE STATE CAPITAL IS BATON ROUGE"
1700 DATA"THE HOME OF CAJUNS, CRAWFISH AND MARDI{2 SPACE}GRAS",LOUISIANA,704
1710 DATA "THIS STATE HAD A 'TEA PARTY' BUT NOT IN BOSTON"
1720 DATA"IT WAS A PART OF MASSACHUSETTS UNTIL{4 SPACE}1820"
1730 DATA"THE STATE CAPITAL IS AUGUSTA"
1740 DATA"IT IS FAMOUS FOR ITS LOBSTER",MAINE,116
1750 DATA"THE ONLY STATE WITH A STATE SPORT --{4 SPACE}JOUSTING"
1760 DATA"THE FIRST STATE TO RATIFY THE{11 SPACE}CONSTITUTION"
1770 DATA"IT STRADDLES CHESAPEAKE BAY"
1780 DATA"THE STATE CAPITAL IS ANNAPOLIS",MARYLAND,393
1790 DATA"THE PILGRIMS FIRST LANDED HERE"
1800 DATA"HOME OF THE INFAMOUS SALEM WITCH TRIALS"
1810 DATA"THE HOME OF HARVARD UNIVERSITY"
1820 DATA"THE STATE CAPITAL IS BOSTON",MASSACHUSETTS,235
1830 DATA"IT IS COMPOSED OF TWO PENINSULAS,{7 SPACE}DIVIDED BY THE GREAT LAKES"
1840 DATA"THE STATE CAPITAL IS LANSING"
1845 DATA"IT IS THE AUTOMOBILE CAPITAL OF THE{5 SPACE}WORLD"
1850 DATA"A GREAT LAKE SHARES ITS NAME",MICHIGAN,267
1860 DATA"IT CONTAINS MORE THAN 15,000 LAKES"
1870 DATA"THE HOME OF THE MAYO CLINIC"
1880 DATA"THE MISSISSIPPI RIVER HAS ITS ORIGIN IN THIS STATE"
1890 DATA"THE STATE CAPITAL IS ST. PAUL",MINNESOTA,221
1900 DATA"ONLY VIRGINIA HAD MORE CIVIL WAR{8 SPACE}BATTLES THAN THIS STATE"
1910 DATA"DEVASTATED IN 1969 BY AMERICA'S MOST{4 SPACE}POWERFUL HURRICANE"
1920 DATA"ITS NAME IS AN INDIAN WORD MEANING{6 SPACE}'LARGE RIVER'"
1930 DATA"THE STATE CAPITAL IS JACKSON",MISSISSIPPI,625
1940 DATA"IT RANKS FIRST IN THE NATION IN LEAD{4 SPACE}MINING"
1950 DATA"IT WAS ADMITTED AS A SLAVE STATE WHEN{3 SPACE}MAINE JOINED AS A FREE STATE"
1960 DATA"IT IS THE 'GATEWAY TO THE WEST'"
1970 DATA"THE STATE CAPITAL IS JEFFERSON CITY",MISSOURI,463
1980 DATA"IT IS THE MUSTARD SEED CAPITAL OF THE{3 SPACE}U.S"
1990 DATA"GLACIER NATIONAL PARK IS HERE"
2000 DATA"THE SITE OF CUSTER'S LAST STAND"
2010 DATA"THE STATE CAPITAL IS HELENA",MONTANA,173
2020 DATA"THE BIRTHPLACE OF PRESIDENT GERALD FORD"
2030 DATA"THE PLATTE RIVER RUNS THE WIDTH OF THE{2 SPACE}STATE"
2040 DATA"THE TRANSCONTINENTAL RAILROAD STARTED{3 SPACE}HERE IN 1865"
2050 DATA"THE STATE CAPITAL IS OMAHA",NEBRASKA,378
2060 DATA"THE DRIEST STATE, WITH LESS THAN 3{6 SPACE}INCHES OF RAIN PER YEAR"
2070 DATA"THE SITE OF UNDERGROUND NUCLEAR TESTS" 
2080 DATA"THE STATE CAPITAL IS CARSON CITY"
2090 DATA"IT IS THE GAMBLING CAPITAL OF THE U.S",NEVADA,407
2100 DATA"IT SHARED A GOVERNOR WITH MASSACHUSETTS FROM 1699-1741"
2110 DATA"AS THE 9TH STATE TO RATIFY, IT MADE THE CONSTITUTION LEGAL"
2120 DATA"PUBLIC PARK LANDS COMPRISE ONE-FIFTH OF{2 SPACE}THIS STATE"
2130 DATA"THE STATE CAPITAL IS CONCORD","NEW HAMPSHIRE",195
2140 DATA"ONE OF ITS CITIES WAS USED AS A PATTERN FOR THE MONOPOLY BOARD"
2150 DATA"THE STATE CAPITAL IS TRENTON"
2160 DATA"THE HOME OF PRINCETON AND RUTGERS{7 SPACE}UNIVERSITIES"
2170 DATA"THE SITE OF 'AMERICA'S OTHER LAS VEGAS'","NEW JERSEY",314
2180 DATA"IT LEADS THE NATION IN URANIUM{10 SPACE}PRODUCTION"
2190 DATA"THE FIRST ATOMIC BOMB WAS PRODUCED AND{2 SPACE}EXPLODED HERE"
2200 DATA"THE STATE CAPITAL IS SANTA FE"
2210 DATA"CARLSBAD CAVERNS IS ITS MAJOR TOURIST{3 SPACE}ATTRACTION","NEW MEXICO",614
2220 DATA"ITS LARGEST CITY WAS THE FIRST U.S.{5 SPACE}CAPITOL"
2230 DATA"IT LEADS THE NATION IN MANUFACTURING"
2240 DATA"IT CONTAINS THE NATION'S LARGEST CITY{3 SPACE}AND PORT"
2250 DATA"THE STATE CAPITAL IS ALBANY","NEW YORK",233
2260 DATA"THE WRIGHT BROTHERS' FIRST PLANE FLEW{3 SPACE}HERE"
2270 DATA"HOME OF THE NEW WORLD'S 'LOST COLONY'"
2280 DATA"IT LEADS THE NATION IN TOBACCO{10 SPACE}PRODUCTION"
2290 DATA"THE STATE CAPITAL IS RALEIGH","NORTH CAROLINA",551
2300 DATA"THE BIRTHPLACE OF ULYSSES GRANT AND SIX OTHER PRESIDENTS"
2310 DATA"THE ABOLITIONIST MOVEMENT BEGAN HERE"
2320 DATA"IT LEADS THE NATION IN LIME PRODUCTION"
2330 DATA"THE STATE CAPITAL IS COLUMBUS",OHIO,389
2340 DATA"90% OF THIS RURAL STATE IS DEVOTED TO{3 SPACE}FARM LAND"
2350 DATA"HOME OF THE BADLANDS"
2360 DATA"SITE OF THE RED RIVER VALLEY"
2370 DATA"THE STATE CAPITAL IS BISMARCK","NORTH DAKOTA",178
2380 DATA"IT WAS THE BIRTHPLACE OF WILL ROGERS"
2390 DATA"IT WAS PART OF INDIAN TERRITORY DURING{2 SPACE}THE 19TH CENTURY"
2400 DATA"INDIANS WANTED PART OF ITS LAND TO{6 SPACE}BECOME THE STATE OF SEQUOYAH"
2410 DATA"THE STATE CAPITAL IS OKLAHOMA CITY",OKLAHOMA,620
2420 DATA"IN 1811 JOHN JACOB ASTOR FOUNDED A FUR{2 SPACE}DEPOT HERE"
2430 DATA"IT BOASTS THE NATION'S DEEPEST LAKE,{4 SPACE}CRATER LAKE"
2440 DATA"IT LEADS THE NATION IN LUMBER{11 SPACE}PRODUCTION"
2450 DATA"THE STATE CAPITAL IS SALEM",OREGON,205
2460 DATA"BENJAMIN FRANKLIN IS ITS MOST FAMOUS{4 SPACE}SON"
2470 DATA"IT IS THE HOME OF THE 1ST RADIO STATION -- KDKA"
2480 DATA"VALLEY FORGE AND GETTYSBURG ARE BOTH IN THIS STATE"
2490 DATA"THE STATE CAPITAL IS HARRISBURG",PENNSYLVANIA,312
2500 DATA"ROGER WILLIAMS STARTED THE STATE'S FIRST SETTLEMENT IN 1636"
2510 DATA"IT WAS THE LAST OF THE ORIGINAL COLONIES TO RATIFY THE CONSTITUTION"
2520 DATA"IT IS THE NATION'S SMALLEST STATE, AND{2 SPACE}IT IS NOT AN ISLAND"
2530 DATA"THE STATE CAPITAL IS PROVIDENCE","RHODE ISLAND",276
2540 DATA"THE CIVIL WAR STARTED HERE"
2550 DATA"IT WAS THE 1ST STATE TO SECEDE FROM THE UNION"
2560 DATA"IT IS THE BIGGEST PEACH-PRODUCING STATE EAST OF CALIFORNIA"
2570 DATA"THE STATE CAPITAL IS COLUMBIA","SOUTH CAROLINA",551
2580 DATA"IT CONTAINS THE GEOGRAPHICAL CENTER OF{2 SPACE}THE U.S"
2590 DATA"IT LEADS THE NATION IN GOLD PRODUCTION"
2600 DATA"IT IS FAMOUS FOR MOUNT RUSHMORE"
2610 DATA"THE STATE CAPITAL IS PIERRE","SOUTH DAKOTA",258
2620 DATA"IT LEADS THE NATION IN ZINC PRODUCTION"
2630 DATA"ANDREW JACKSON MADE HIS HOME HERE"
2640 DATA"IT IS THE MUSIC CENTER OF AMERICA"
2650 DATA"THE STATE CAPITAL IS NASHVILLE",TENNESSEE,547
2660 DATA"IT WAS AN INDEPENDENT COUNTRY FOR ALMOST A DECADE"
2670 DATA"IT LEADS THE NATION IN OIL, CATTLE AND{2 SPACE}COTTON PRODUCTION"
2680 DATA"THE BATTLE OF THE ALAMO WAS FOUGHT HERE"
2690 DATA"THE STATE CAPITAL IS AUSTIN",TEXAS,738
2700 DATA"IT CONTAINS THE NATION'S LARGEST OPEN{3 SPACE}PIT COPPER MINE"
2710 DATA"ITS ORIGINAL NAME WAS DESERET"
2720 DATA"BRIGHAM YOUNG LED THE FIRST MORMON{6 SPACE}SETTLERS HERE"
2730 DATA"THE STATE CAPITAL IS SALT LAKE CITY",UTAH,410
2740 DATA"IT CLAIMS TWO U.S. PRESIDENTS, BUT ONE{2 SPACE}WAS PROBABLY BORN IN CANADA"
2750 DATA"IT IS THE ONLY NEW ENGLAND STATE WITHOUT AN OCEAN COASTLINE"
2760 DATA"IT'S FAMOUS FOR THE PRODUCTION OF MAPLE SYRUP"
2770 DATA"THE STATE CAPITAL IS MONTPELIER",VERMONT,194
2780 DATA"IT IS FAMOUS FOR COAL PRODUCTION"
2790 DATA"HARPERS FERRY IS LOCATED HERE"
2800 DATA"STONEWALL JACKSON WAS A NATIVE OF THIS{2 SPACE}STATE"
2810 DATA"THE STATE CAPITAL IS CHARLESTON","WEST VIRGINIA",431
2820 DATA"IT HAS THE MILDEST CLIMATE OF ALL THE{3 SPACE}NORTHERN STATES"
2830 DATA"IT LEADS THE NATION IN APPLE PRODUCTION"
2840 DATA"IT CONTAINS THE ONLY ACTIVE VOLCANO IN{2 SPACE}THE CONTINENTAL U.S"
2850 DATA"THE STATE CAPITAL IS OLYMPIA",WASHINGTON,86
2860 DATA"IT WAS THE HOME OF THE MAIN CAPITAL OF{2 SPACE}THE CONFEDERACY"
2870 DATA"THE FIRST PERMANENT ENGLISH SETTLEMENT{2 SPACE}IN AMERICA WAS HERE"
2880 DATA"IT IS THE BIRTHPLACE OF EIGHT{11 SPACE}PRESIDENTS"
2890 DATA"THE STATE CAPITAL IS RICHMOND",VIRGINIA,472
2900 DATA"IT WAS THE BOYHOOD HOME OF HARRY{8 SPACE}HOUDINI"
2910 DATA"IT LEADS THE NATION IN BEER PRODUCTION"
2920 DATA"IT IS THE NATION'S LEADING DAIRY STATE"
2930 DATA"THE STATE CAPITAL IS MADISON",WISCONSIN,264
2940 DATA"WOMEN WERE FIRST GIVEN THE RIGHT TO VOTE HERE"
2950 DATA"ITS DEVIL'S TOWER WAS THE FIRST U.S.{4 SPACE}NATIONAL MONUMENT"
2960 DATA"IT IS THE HOME OF YELLOWSTONE NATIONAL{2 SPACE}PARK"
2970 DATA"THE STATE CAPITAL IS CHEYENNE",WYOMING,294
3900 DATA"$SONG"
3910 DATA6430,2,5407,2,6430,4,6430,2,5407,2,6430,4,6430,2,5407,2,6430,2
3920 DATA7217,2,6430,2,5407,2,6430,4,0,1,5407,2,5728,2,6430,2,5728,4
3930 DATA4817,2,6430,2,5728,4,4817,2,4291,8
4000 V=VAL(QT$(5))
4002 POKEVC+33,6
4008 PRINTCHR$(147);"{WHT}{DOWN}{4 SPACE}{CD}{RVS}{CU}{CY}{CU}{OFF}{CI}{CO}";SPC(25);"{RVS} {OFF}{CK}"
4010 PRINT"{4 SPACE}{CL}{RVS}{7 SPACE}{2 CT}{2 CY}{2 CU}{OFF}{CO}{2 CP}{CO}{13 SPACE}{RVS}{2 SPACE}{OFF}{CK}"
4020 PRINT"{4 SPACE}{RVS}{S#}{17 SPACE}{CC}{OFF}{11 SPACE}{RVS}{S#}{2 SPACE}{OFF}"
4030 PRINT"{3 SPACE}{RVS}{S#}{19 SPACE}{C*}{S#}{CU} {CU}{C*}{OFF}{4 SPACE}{RVS}{S#}{3 SPACE}{OFF}{CK}"
4040 PRINT"{2 SPACE}{RVS}{S#}{22 SPACE}{OFF}{7 SPACE}{RVS}{S#}{4 SPACE}{CC}{OFF}"
4050 PRINT"{2 SPACE}{RVS}{24 SPACE}{OFF} {RVS}{C*}{OFF}{2 SPACE}{RVS}{CV}{4 SPACE}{OFF}{CU}"
4060 PRINT"{2 SPACE}{RVS}{24 SPACE}{OFF} {RVS}{2 SPACE}{OFF} {RVS}{S#}{3 SPACE}{OFF}{CK}"
4070 PRINT"{2 SPACE}{RVS}{CH}{23 SPACE}{CU}{8 SPACE}{OFF}"
4080 PRINT"{2 SPACE}{CL}{RVS}{31 SPACE}{OFF}{S#}"
4090 PRINT"{2 SPACE}{CN}{RVS}{31 SPACE}{OFF}{CV}"
4100 PRINT"{3 SPACE}{C*}{RVS}{30 SPACE}{OFF}{CF}"
4120 PRINT"{4 SPACE}{C*}{RVS}{28 SPACE}{OFF}{S#}{CV}"
4130 PRINT"{5 SPACE}{C*}{RVS}{27 SPACE}{OFF}{CV}"
4140 PRINT"{6 SPACE}{RVS}{CF}{25 SPACE}{OFF}{S#}"
4150 PRINT"{7 SPACE}{CY}{C*}{RVS}{22 SPACE}{CN}{OFF}"
4160 PRINT"{10 SPACE}{CC}{2 CU}{RVS}{19 SPACE}{OFF}"
4170 PRINT"{13 SPACE}{C*}{RVS}{11 SPACE}{CU}{CY}{OFF} {C*}{RVS}{3 SPACE}{OFF}"
4180 PRINT"{14 SPACE}{CY}{C*}{RVS}{6 SPACE}{OFF}{2 CU}{6 SPACE}{C*}{RVS} {C*}"
4190 PRINT"{16 SPACE}{C*}{RVS}{2 SPACE}{OFF}{S#}{10 SPACE}{CL}{RVS}{2 SPACE}{C*}{OFF}"
4200 PRINT"{17 SPACE}{C*}{RVS} {C*}{OFF}{11 SPACE}{C*}{RVS} {CL}{OFF}"
4210 PRINT"{32 SPACE}{CC}"
4220 POKESM+V,160:POKECM+V,0
4230 INPUT"{DOWN} WHICH STATE";X$
4240 PRINT"{BLK}";
4250 POKEVC+33,15
4260 RETURN
