1 REM UNICORN
100 NS=37:CB=900:VC=53248
110 PRINTCHR$(147):POKEVC+32,6:POKEVC+33,12
120 DIM QT$(7),NT%(16,1)
130 READX$:IFX$<>"$"THEN130
140 FORX=0TO16:READZ,Y:NT%(X,0)=Z:NT%(X,1)=Y:NEXTX
150 FORX=54272TO54295:POKEX,0:NEXTX
160 SX=54272:POKESX+5,17
170 FL$="{RVS}{40 SPACE}{OFF}"
180 POKESX+6,129
200 GOSUB700
205 S%=0:FORX=1TONS:POKECB+X,0:NEXTX
210 FORX=1TONS:GOSUB800:POKECB+Z,X:NEXTX
300 X=1
305 Y=PEEK(CB+X):Y=(Y-1)*8
310 RESTORE:IFY=0THEN330
320 FORW=1TOY:READX$:NEXTW
330 FORW=1TO8:READX$:QT$(W-1)=X$:NEXTW
340 W=VAL(QT$(7))+3:AN$=QT$(W):FORW=0TO3:RS(W)=0:NEXTW
350 FORW=1TO4
360 V=INT(RND(1)*4):IFRS(V)<>0THEN360
370 RS(V)=W:NEXTW
400 W=0
405 GOSUB4300
410 PRINT"{3 SPACE}QUESTION #";W+1
420 X$=QT$(RS(W)-1)
430 PRINT"{2 DOWN}";X$;".{2 DOWN}":PRINT" ";
440 GOSUB900:IFX$="q"THEN500
442 IFX$="END"THEN600
445 W=W+1:IFW<=3THEN405
448 GOSUB4300
450 PRINT"{3 SPACE}QUESTION # 5":PRINT"{2 DOWN}{3 SPACE}CHOOSE ONE OF THE FOLLOWING:{DOWN}"
460 PRINT"{3 SPACE}";QT$(4):PRINT"{3 SPACE}";QT$(5):PRINT"{3 SPACE}";QT$(6);"{DOWN}":PRINT" ";:W=4
470 GOSUB900:IFX$="q"THEN500
475 IFX$="END"THEN600
480 PRINT"{DOWN}{3 SPACE}ANSWER=";AN$:S%=S%-5
490 FORW=1TO2500:NEXTW:GOTO540
500 S%=S%+(5-W):POKE54296,15:POKEVC+0,65
510 POKEVC+16,0:POKEVC+21,1:J=8:POKESX+0,65
520 FORV=65TO255STEP10:GOSUB550:NEXTV
530 POKEVC+21,0:POKE54296,0
540 X=X+1:IFX<=NSTHEN305
545 GOTO600
550 POKEVC+29,0:POKEVC+0,V:POKESX+1,J:POKESX+4,17:FORK=1TO20:NEXTK
560 POKEVC+29,1:POKESX+4,16:FORK=1TO5:NEXTK:J=J+3
570 RETURN
600 PRINT"{BLK}{CLR}{DOWN}{15 SPACE}{RVS}TEST OVER!{OFF}"
610 PRINT"{3 DOWN} YOUR SCORE = ";S%
620 IFS%<0THENS%=0
625 X=X-1:IFX<1THENX=1
630 X=INT((S%*100)/(X*5))
640 PRINT"{2 DOWN} YOUR GRADE = " ;X; "{LEFT}%"
650 PRINT"{3 DOWN} PLAY AGAIN?":PRINT"{DOWN} ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
660 GETX$:IFX$=""THEN660
670 IFX$="Y"THEN205
680 IFX$<>"N"THEN660
690 GOSUB4000:PRINTCHR$(147):END
700 PRINT"{BLK}";FL$;
702 PRINT"{DOWN}* * * * *{4 SPACE}U N I C O R N{5 SPACE}* * * * *";
704 PRINT"{DOWN}";FL$;
705 PRINT"{DOWN}YOU WILL BE ASKED UP TO FIVE QUESTIONS"
706 PRINT"ABOUT SELECTED MYTHOLOGICAL CHARACTERS."
710 PRINT"{2 DOWN}POINTS WILL BE AWARDED DEPENDING ON HOW MANY QUESTIONS YOU NEED:{DOWN}"
715 PRINT"{4 SPACE}1 QUESTION{2 SPACE}={2 SPACE}5.":PRINT"{4 SPACE}2 QUESTIONS ={2 SPACE}4."
720 PRINT"{4 SPACE}3 QUESTIONS ={2 SPACE}3.":PRINT"{4 SPACE}4 QUESTIONS ={2 SPACE}2."
722 PRINT"{4 SPACE}5 QUESTIONS ={2 SPACE}1."
725 PRINT"{4 SPACE}ALL MISSED{2 SPACE}= -5."
728 PRINT"{DOWN}TYPE {RVS}END{OFF} TO STOP THE GAME EARLY."
730 GOSUB4000
740 FORX=832TO832+63:READZ:POKEX,Z:NEXTX
785 PRINT"{DOWN}{8 SPACE}(PRESS ANY KEY TO START)";
787 POKEVC+39,11:POKEVC+23,0:POKEVC+28,0
788 POKE2040,13:POKEVC+1,80
790 GETX$:IFX$=""THEN790
795 RETURN
800 W=0
810 Z=INT((RND(1)*NS)+1):IFPEEK(CB+Z)=0THENRETURN
820 W=W+1:IFW<9THEN810
830 Z=1
840 IFPEEK(CB+Z)=0THENRETURN
850 Z=Z+1:GOTO840
900 X$="":POKE198,0:INPUTX$:IFX$=AN$THENX$="q":RETURN
903 IFX$="END"THENRETURN
905 POKESX+5,15
910 POKE54296,15:POKEVC+33,0:POKESX+0,31:POKESX+1,17
920 POKESX+4,17:FORJ=1TO400:NEXTJ
930 POKESX+4,16:FORJ=1TO050:NEXTJ:POKESX+5,17
935 IFW<>4THENPRINTCHR$(147)
940 POKEVC+33,12:POKE54296,0:RETURN
1000 DATA"SHE WAS THE GREEK GODDESS OF WITCHCRAFT"
1010 DATA"THIS TITAN KEPT HER POWERS WHEN ZEUS{4 SPACE}RULED THE UNIVERSE"
1020 DATA"SHE IS USUALLY SHOWN AS HAVING 3 HEADS"
1030 DATA"HOWLING DOGS WERE ASSOCIATED WITH HER{3 SPACE}SPELL"
1040 DATAHECATE,ARTEMIS,MEDUSA,1
1050 DATA"SHE WAS THE WORLD'S MOST BEAUTIFUL{6 SPACE}WOMAN"
1060 DATA"PARIS SEDUCED HER AWAY FROM MENELAUS,{3 SPACE}HER HUSBAND"
1070 DATA"THE TROJAN WAR WAS FOUGHT TO RECLAIM{4 SPACE}HER"
1080 DATA"SHE RETURNED TO HER HUSBAND AFTER PARIS WAS SLAIN"
1090 DATAMEDEA,HELEN,CIRCE,2
1100 DATA"HE WAS KNOWN TO THE ROMANS AS MERCURY,{2 SPACE}THE MESSENGER GOD"
1110 DATA"HE GUARDED TRAVELERS, SHEPHERDS AND{5 SPACE}TRADERS"
1120 DATA"A TRICKSTER WHO HELPED ROBBERS COMMIT{3 SPACE}CRIMES"
1130 DATA"HE USED A WINGED CAP AND WINGED ANKLES{2 SPACE}FOR TRAVELING"
1140 DATAMARS,ACHILLES,HERMES,3
1150 DATA"HIS SONS WERE ROMULUS AND REMUS, TWIN{3 SPACE}FOUNDERS OF ROME"
1160 DATA"HE WAS THE ROMAN GOD OF WAR"
1170 DATA"THE WARRIOR GODDESS BELLONE TRAVELED AT HIS SIDE"
1180 DATA"HE WAS THE ROMAN GOD OF AGRICULTURE AND SPRING"
1190 DATAMARS,ZEUS,JASON,1
1200 DATA"HE INVENTED THE TROJAN HORSE"
1210 DATA"HIS TRIP HOME FROM THE TROJAN WAR TOOK{2 SPACE}TWENTY YEARS"
1220 DATA"HE RECLAIMED HIS WIFE PENELOPE BY{7 SPACE}KILLING HER 100 SUITORS"
1230 DATA"THE ROMANS CALLED HIM ULYSSES"
1240 DATAHERMES,ODYSSEUS,ZEUS,2
1250 DATA"THE GREEK GOD OF THE SEA, KNOWN TO THE{2 SPACE}ROMANS AS NEPTUNE"
1260 DATA"HIS FATHER SWALLOWED HIM, BUT ZEUS FREED HIM"
1270 DATA"HE CREATED THE HORSE"
1280 DATA"HE WON HIS KINGDOM OF THE SEA BY{8 SPACE}CHOOSING LOTS"
1290 DATAAGAMEMNON,HERMES,POSEIDON,3
1300 DATA"HE WAS GOD OF THE WIND, THE SON OF{6 SPACE}AEOLUS"
1310 DATA"IN LIFE, HE WAS THE KING OF CORINTH"
1320 DATA"HE WAS A CRAFTY AND EVIL MAN"
1330 DATA"HIS ETERNAL PUNISHMENT WAS PUSHING A{4 SPACE}BOULDER UPHILL"
1340 DATASISYPHUS,DIONYSUS,ARTEMIS,1
1350 DATA"HE WAS THE SON OF ZEUS AND THE FATHER OF PELOPS"
1360 DATA"HE SINNED BY REVEALING THE SECRETS OF{3 SPACE}ZEUS"
1370 DATA"HE KILLED HIS SON PELOPS AND SERVED HIM AT A BANQUET"
1380 DATA"HE WAS PUNISHED BY ETERNAL HUNGER AND{3 SPACE}THIRST"
1390 DATAHERACLES,TANTALUS,SISYPHUS,2
1400 DATA"AS THE GOD OF GODS, HE RULED OLYMPUS"
1410 DATA"HE USED THE CYCLOPES TO HELP DEFEAT THE TITANS"
1420 DATA"HE GAVE AWAY THE SEA AND THE UNDERWORLD, BUT KEPT THE HEAVENS"
1430 DATA"HIS ROMAN NAME IS JUPITER"
1440 DATAPOSEIDON,APOLLO,ZEUS,3
1450 DATA"HIS FATHER SERVED HIM AS A MAIN COURSE{2 SPACE}AT A BANQUET"
1460 DATA"HIS SHOULDER WAS EATEN BY A GODDESS, BUT WAS REPLACED WITH IVORY"
1470 DATA"HE WAS RESTORED TO LIFE AND WON HIS{5 SPACE}BRIDE IN A CHARIOT RACE"
1480 DATA"HE WAS CURSED FOR CHEATING IN A CHARIOT RACE"
1490 DATAPELOPS,DAEDALUS,HERMES,1
1500 DATA"A TITAN, HIS NAME MEANS 'FORETHOUGHT'"
1510 DATA"HE CREATED MANKIND"
1520 DATA"HE STOLE FIRE FROM HEAVEN TO GIVE TO{4 SPACE}MAN"
1530 DATA"HE WAS CHAINED TO A MOUNTAIN FOR THEFT, AND RESCUED BY HERCULES
1540 DATAPHAETON,PROMETHEUS,PELOPS,2
1550 DATA"SHE WAS CREATED BY ZEUS TO THWART MAN"
1560 DATA"HER NAME MEANS 'GIFT OF ALL' BECAUSE SHE GOT GIFTS FROM ALL GODS"
1570 DATA"SHE MARRIED EPIMETHEUS -- CREATOR OF{4 SPACE}ANIMALS"
1580 DATA"BY OPENING A JAR, SHE UNLEASHED EVIL ON THE WORLD"
1590 DATAATHENA,ECHO,PANDORA,3
1600 DATA"HIS PRIDE ALMOST DESTROYED THE EARTH"
1610 DATA"HIS DREAM WAS TO BE RECOGNIZED AS{7 SPACE}APOLLO'S SON"
1620 DATA"HE DROVE THE CHARIOT OF THE SUN TOO NEAR THE EARTH"
1630 DATA"ZEUS KILLED HIM TO SAVE EARTH FROM THE{2 SPACE}BURNING SUN"
1640 DATAPHAETON,AENEAS,MARS,1
1650 DATA"KNOWN TO THE ROMANS AS PLUTO, HE WAS GOD OF THE UNDERWORLD"
1660 DATA"APHRODITE TOLD EROS TO SHOOT HIM WITH{3 SPACE}ARROWS SO HE WOULD KNOW LOVE"
1670 DATA"HE FELL IN LOVE WITH PERSEPHONE AND{5 SPACE}KIDNAPPED HER"
1680 DATA"AS LONG AS HE KEEPS PERSEPHONE, THE{5 SPACE}EARTH IS DARK AND COLD"
1690 DATAZEUS,HADES,MARS,2
1700 DATA"SHE WAS CAPTURED BY HADES TO BE HIS{5 SPACE}BRIDE"
1710 DATA"EATING POMEGRANATE SEEDS FORCED HER TO{2 SPACE}LIVE WITH HADES"
1720 DATA"HER MOTHER MADE EARTH BARREN UNTIL SHE{2 SPACE}WAS RECLAIMED"
1730 DATA"THE FOUR MONTHS SHE LIVES WITH HADES ARE CALLED WINTER"
1740 DATACASSANDRA,ECHO,PERSEPHONE,3
1750 DATA"SHE ALWAYS HAD TO HAVE THE LAST WORD"
1760 DATA"SHE PROTECTED ZEUS FROM HERA BY TRAPPING HER IN CONVERSATION"
1770 DATA"SHE WAS CURSED BY NEVER BEING ABLE TO{3 SPACE}SPEAK FIRST"
1780 DATA"SHE WASTED AWAY TO NOTHING BUT A VOICE{2 SPACE}FOR LOVE OF NARCISSUS"
1790 DATAECHO,ATHENA,HELEN,1
1800 DATA"HE LOVED NO MAN OR WOMAN"
1810 DATA"A WHITE FLOWER WITH A PURPLE HEART BEARS HIS NAME"
1820 DATA"HE BROKE THE HEART OF ECHO AND CAUSED{3 SPACE}HER END"
1830 DATA"HE FELL IN LOVE WITH HIS OWN REFLECTION"
1840 DATASISYPHUS,NARCISSUS,JASON,2
1850 DATA"HE WAS A POET AND A SINGER"
1860 DATA"HE LOVED ONLY EURYDICE AND SHE DIED"
1870 DATA"HE BEGGED HADES AND PERSEPHONE TO GIVE{2 SPACE}HIS BRIDE LIFE"
1880 DATA"HE LOST EURYDICE AGAIN BECAUSE HE TURNED TO LOOK BACK"
1890 DATAAGAMEMNON,DIONYSUS,ORPHEUS,3
1900 DATA"AN ORACLE PROPHESIED HE WOULD KILL HIS{2 SPACE}GRANDFATHER"
1910 DATA"HIS GOAL WAS TO GET THE HEAD OF MEDUSA"
1920 DATA"HE CUT OFF THE HEAD OF MEDUSA AND{7 SPACE}ESCAPED UNHARMED"
1930 DATA"HERMES GAVE HIM MAGIC GIFTS TO HELP HIM GET MEDUSA"
1940 DATAPERSEUS,PROMETHEUS,HERACLES,1
1950 DATA"SHE WAS ONE OF THREE UGLY SISTERS"
1960 DATA"SHE WAS THE ONLY ONE OF THREE SISTERS{3 SPACE}WHO COULD BE KILLED"
1970 DATA"PEOPLE WHO LOOKED UPON HER WERE TURNED{2 SPACE}TO STONE"
1980 DATA"HER BLOOD DROPLETS ARE NOW POISONOUS{4 SPACE}DESERT SNAKES"
1990 DATAMEDEA,MEDUSA,ATHENA,2
2000 DATA"SHE WAS THE DAUGHTER OF CASSIOPEIA"
2010 DATA"SHE WAS CHAINED TO A ROCK BECAUSE HER{3 SPACE}MOTHER WAS BEAUTIFUL AND VAIN"
2020 DATA"SHE WAS OFFERED AS A SACRIFICE TO THE{3 SPACE}SEA GOD NEREUS"
2030 DATA"PERSEUS SAVED HER, AND SHE BECAME HIS{3 SPACE}BRIDE"
2040 DATAATHENA,ARTEMIS,ANDROMEDA,3
2050 DATA"HIS NAME MEANT 'GLORY OF HERA,' BUT HERA WANTED HIM KILLED"
2060 DATA"HE DID 12 IMPOSSIBLE TASKS TO PLEASE{4 SPACE}KING EURYSTHEUS"
2070 DATA"CALLED HERCULES BY THE ROMANS, HE WENT{2 SPACE}TO MOUNT OLYMPUS AFTER DEATH"
2080 DATA"HE WAS KILLED BY THE BLOOD OF A CENTAUR"
2090 DATAHERACLES,HADES,HERMES,1
3000 DATA"HE WAS RAISED BY A CENTAUR, CHIRON"
3010 DATA"HE ORGANIZED A QUEST TO FIND THE GOLDEN FLEECE"
3020 DATA"HIS SHIP WAS THE ARGO AND HIS WARRIORS{2 SPACE}WERE ARGONAUTS"
3030 DATA"HE WON THE FLEECE, BUT NOT THE THRONE OF HIS KINGDOM"
3040 DATAAENEAS,JASON,APOLLO,2
3050 DATA"HER POTION GOT JASON THE GOLDEN FLEECE"
3060 DATA"SHE DELIVERED HER OWN BROTHER TO JASON{2 SPACE}TO BE KILLED"
3070 DATA"SHE KILLED KING PELIAS TO HELP JASON{4 SPACE}BECOME KING"
3080 DATA"SHE KILLED JASON'S WIFE TO AVENGE HIS{3 SPACE}BETRAYAL"
3090 DATAMEDUSA,ARTEMIS,MEDEA,3
3100 DATA"HE WAS EARTH'S BEST ARCHITECT AND{7 SPACE}SCULPTOR"
3110 DATA"HE KILLED HIS NEPHEW BECAUSE TALUS HAD{2 SPACE}MORE TALENT THAN HE DID"
3120 DATA"THE MINOTAUR NOR ITS VICTIMS COULD{6 SPACE}ESCAPE THE LABYRINTH HE MADE"
3130 DATA"HE MADE HUMAN WINGS, BUT HIS SON ICARUS FELL TO EARTH AND DIED"
3140 DATADAEDALUS,THESEUS,PHAETON,1
3150 DATA"BECAUSE HIS FATHER TRIED TO KILL HIM,{3 SPACE}HIS NAME MEANS 'SWOLLEN FOOT'"
3160 DATA"HE UNKNOWINGLY KILLED HIS FATHER, AS AN ORACLE PREDICTED"
3170 DATA"HE SOLVED THE RIDDLE OF THE SPHINX AND{2 SPACE}MARRIED JOCASTA"
3180 DATA"HE BLINDED HIMSELF AFTER LEARNING{8 SPACE}JOCASTA WAS HIS MOTHER"
3190 DATATHESEUS,OEDIPUS,APOLLO,2
3200 DATA"HE WANTED TO BE A HERO LIKE COUSIN{6 SPACE}HERACLES"
3210 DATA"MEDEA TRIED TO KILL HIM BEFORE AEGUS{4 SPACE}NAMED HIM AS SON AND HEIR"
3220 DATA"HE WAS CHOSEN AS A SACRIFICE TO THE{5 SPACE}MINOTAUR"
3230 DATA"HE SLEW THE MINOTAUR AND ESCAPED THE{4 SPACE}LABYRINTH"
3240 DATASISYPHUS,PROMETHEUS,THESEUS,3
3250 DATA"HE WAS THE MOST FAMOUS GREEK WHO FOUGHT IN THE TROJAN WAR"
3260 DATA"HE COULD NOT BE HURT BECAUSE HIS MOTHER DIPPED HIM IN THE RIVER STYX"
3270 DATA"HE KILLED THE TROJAN LEADER HECTOR AT{3 SPACE}THE SCAEAN GATE"
3280 DATA"HE WAS KILLED BY AN ARROW THAT PIERCED{2 SPACE}HIS VULNERABLE HEEL"
3290 DATAACHILLES,AGAMEMNON,AENEAS,1
3300 DATA"HE WAS A TROJAN PRINCE AND SON OF{7 SPACE}APHRODITE"
3310 DATA"HE KILLED TURNUS OVER THE LOVE OF{7 SPACE}LAVINIA"
3320 DATA"THE CAESARS TRACED THEIR ORIGIN TO HIM"
3330 DATA"HE WAS THE ANCESTRAL HERO OF ROME"
3340 DATAMARS,AENEAS,HERMES,2
3350 DATA"HE WAS THE KING WHO LED THE ACHAEANS IN THE TROJAN WAR"
3360 DATA"CLYTEMNESTRA, PRINCESS OF SPARTA, WAS{3 SPACE}HIS FAITHLESS WIFE"
3370 DATA"HE SACRIFICED A WOMAN TO SAVE HIS FLEET FROM A WINDSTORM"
3380 DATA"HE WAS KILLED BY THE LOVER OF{11 SPACE}CLYTEMNESTRA"
3390 DATAAPOLLO,ACHILLES,AGAMEMNON,3
3400 DATA"SHE WAS THE GODDESS OF LOVE, CALLED{5 SPACE}VENUS BY THE ROMANS"
3410 DATA"SHE WAS FATHERED BY OURANOS AFTER HIS{3 SPACE}CASTRATION"
3420 DATA"SHE WAS THE GODDESS OF INTELLECTUALISM{2 SPACE}AND ALSO OF THE SKY"
3430 DATA"SHE WAS HONORED AS THE MOTHER OF AENEAS"
3440 DATAAPHRODITE,ARTEMIS,ATHENA,1
3450 DATA"HE WAS THE GOD OF PROPHECY AND LIGHT"
3460 DATA"ARCHERS, PHYSICIANS, AND MUSICIANS{6 SPACE}CALLED HIM THEIR PATRON"
3470 DATA"AESCULAPIUS, GOD OF MEDICINE, WAS HIS{3 SPACE}SON"
3480 DATA"HIS JOB WAS TO DRIVE THE CHARIOT OF THE SUN ACROSS THE SKY"
3490 DATAPERSEUS,APOLLO,DIONYSUS,2
3500 DATA"APOLLO'S TWIN, SHE WAS GODDESS OF THE{3 SPACE}MOON AND OF BIRTH"
3510 DATA"HER ROMAN COUNTERPART WAS DIANA"
3520 DATA"SHE WAS GUARDIAN OF FORESTS AS WELL AS{2 SPACE}OF WILD ANIMALS"
3530 DATA"BECAUSE HE CHALLENGED HER TO A DISCUS{3 SPACE}-THROWING CONTEST, ORION DIED"
3540 DATAATHENA,ANDROMEDA, ARTEMIS,3
3550 DATA"SHE WAS KNOWN TO ROME AS MINERVA,{7 SPACE}GODDESS OF WAR AND WISDOM"
3560 DATA"SHE WAS FULLY ARMED FOR WAR AT BIRTH"
3570 DATA"SHE WON ATHENS IN A CONTEST WITH{3 SPACE}POSEIDON"
3580 DATA"HER TEMPLE IS THE PARTHENON"
3590 DATAATHENA,APHRODITE,CASSANDRA,1
3600 DATA"APOLLO GAVE HER THE GIFT OF PROPHECY"
3610 DATA"SHE WAS CURSED THAT NO ONE WOULD BELIEVE HER PREDICTIONS"
3620 DATA"AGAMEMNON CAPTURED HER AFTER TROY FELL"
3630 DATA"SHE WAS MURDERED BY CLYTEMNESTRA"
3640 DATACIRCE,CASSANDRA,HECATE,2
3650 DATA"A FAMOUS SEER, SHE WAS A DAUGHTER OF THE SUN"
3660 DATA"SHE WAS BANISHED TO THE ISLE OF AEAEA{3 SPACE}FOR KILLING HER HUSBAND"
3670 DATA"SHE RESTORED FRIENDS OF ODYSSEUS WHO HAD BEEN TURNED INTO PIGS"
3680 DATA"LIVING WITH ODYSSEUS FOR A YEAR, SHE HAD HIS SON"
3690 DATAHECATE,CASSANDRA,CIRCE,3
3700 DATA"THE GOD OF WINE, HE WAS CALLED BACCHUS{2 SPACE}BY THE ROMANS"
3710 DATA"A PARTY ANIMAL, HIS ORGIES WERE ATTENDED BY SATYRS AND NYMPHS"
3720 DATA"HIS SACRED ANIMALS WERE THE BULL, GOAT{2 SPACE}AND SERPENT"
3730 DATA"HE WAS WORSHIPPED AS A GOD OF DRUNKEN-{2 SPACE}NESS ALL OVER THE KNOWN WORLD"
3740 DATADIONYSUS,ZEUS,HADES,1
4000 POKE54296,15:FORX=0TO16
4010 POKESX+0,NT%(X,1):POKESX+1,NT%(X,0)
4020 POKESX+4,33:FORY=1TO150:NEXTY
4030 POKESX+4,32:FORY=1TO25:NEXTY,X
4040 POKE54296,0:RETURN
4100 DATA"$",67,15,63,75,56,99,63,75,56,99,50,60,56,99,50,60,44,193,50,60
4110 DATA56,99,42,62,44,193,50,60,56,99,63,75,67,15
4200 DATA0,0,2,0,1,4,0,3,248,0,1,208,0,3,248,0,7,156,0,15,0,0,30,0,31,254
4210 DATA0,127,252,0,95,252,0,79,252,0,74,10,0,20,5,0,20,2,0,40,5,0,40,10
4220 DATA0,80,4,0,80,8,0,160,0,0,160,0,0,0
4300 PRINTCHR$(147);"{23 DOWN}";SPC(15);"SCORE =";S%;
4310 PRINT"{HOME}";SPC(13);"{DOWN}U N I C O R N{8 DOWN}"
4320 RETURN
