1 REM MR PRESIDENT
100 NP=39:CB=828:VC=53248:MC=54272
105 POKEVC+32,6:POKEVC+33,12:PRINTCHR$(147)
110 FORX=54272TO54296:POKEX,0:NEXTX
120 F1$="{BLU}{RVS} * * * * * * {RED}{27 SPACE}{OFF}":F2$="{BLU}{RVS}{2 SPACE}* * * * *{2 SPACE}{OFF}"
130 F3$="{RED}{RVS}{40 SPACE}{OFF}"
140 DIM NT%(31)
150 READX$:IFX$<>"$SONG"THEN150
160 FORX=0TO31:READZ:NT%(X)=Z:NEXTX
170 SM=((PEEK(53272)AND240)/16)*1024:CM=55296
180 GOSUB700
200 S%=0:FORX=1TONP:POKECB+X,0:NEXTX:PRINTCHR$(147);:POKEVC+32,12:POKEVC+33,1
210 FORX=1TONP:GOSUB800:POKECB+Z,X:NEXTX
300 X=1
305 Y=PEEK(CB+X):Y=(Y-1)*5
310 RESTORE:IFY=0THEN330
320 FORW=1TOY:READX$:NEXTW
330 FORW=1TO5:READX$:QT$(W-1)=X$:NEXTW
400 W=0
405 GOSUB3000
407 POKEVC+33,1
408 PRINT"{BLK}{HOME}{23 DOWN}";SPC(15);"SCORE =";S%;
410 PRINT"{HOME}{10 DOWN}{2 SPACE}QUESTION #";W+1
420 PRINT"{2 DOWN}HE ";QT$(W);".{2 DOWN}"
430 X$="":POKE198,0:INPUTX$:IFX$=QT$(4)THEN500
435 IFX$="END"THEN600
440 POKEMC+0,0:POKEMC+1,10:POKEMC+4,17:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
445 POKEVC+33,0:FORV=1TO100:NEXTV:POKEMC+4,16
450 POKEMC+24,0:W=W+1:IFW<=3THEN405
455 GOSUB3000
460 POKEVC+33,1:PRINT"{RED}{HOME}{13 DOWN} ANSWER = ";QT$(4):S%=S%-5
465 PRINT"{BLK}{HOME}{23 DOWN}";SPC(15);"SCORE =";S%;
470 FORW=1TO2500:NEXTW:GOTO560
480 GOTO600
500 S%=S%+4-W
510 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:W=20:POKEMC+24,8
520 FORV=12TO1STEP-1:POKEVC+33,V:POKEMC+4,17
530 POKEMC+1,W:W=W+5:FORX1=1TO50:NEXTX1
540 POKEMC+4,16:NEXTV
550 POKEMC+24,0
560 X=X+1:IFX<=NPTHEN305
600 PRINTCHR$(147);"{BLK}{DOWN}{15 SPACE}{RVS}TEST OVER!{OFF}"
610 PRINT"{3 DOWN}{2 SPACE}YOUR SCORE = ";S%
620 IFS%<0THENS%=0
625 X=X-1:IFX<1THENX=1
630 X=INT((S%*100)/(X*4))
640 PRINT"{2 DOWN}{2 SPACE}YOUR GRADE = ";X;"{LEFT}%"
650 PRINT"{3 DOWN}{2 SPACE}PLAY AGAIN?":PRINT"{DOWN}{2 SPACE}ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
660 GETX$:IFX$=""THEN660
670 IFX$="Y"THEN200
680 IFX$<>"N"THEN660
690 GOSUB3500:PRINTCHR$(147)
695 END
700 PRINT"{BLK}{DOWN}{8 SPACE}*** MISTER PRESIDENT ***"
705 PRINT"{WHT}{2 DOWN}YOU WILL BE GIVEN UP TO FOUR QUESTIONS{2 SPACE}ABOUT EACH U.S. PRESIDENT."
710 PRINT"{2 DOWN}POINTS WILL BE GIVEN DEPENDING UPON HOW MANY QUESTIONS YOU NEED:"
715 PRINT"{2 DOWN}{3 SPACE}1 QUESTION{2 SPACE}={2 SPACE}4.":PRINT"{3 SPACE}2 QUESTIONS ={2 SPACE}3."
720 PRINT"{3 SPACE}3 QUESTIONS ={2 SPACE}2.":PRINT"{3 SPACE}4 QUESTIONS ={2 SPACE}1."
725 PRINT"{DOWN}{3 SPACE}ALL MISSED{2 SPACE}= -5."
728 PRINT"{2 DOWN} TYPE {RVS}END{OFF} TO END THE GAME EARLY."
730 GOSUB3500
785 PRINT"{2 DOWN}{12 SPACE}(PRESS ANY KEY)";
790 GETX$:IFX$=""THEN790
795 RETURN
800 W=0
810 Z=INT((RND(1)*NP)+1):IFPEEK(CB+Z)=0THENRETURN
820 W=W+1:IFW<10THEN810
830 Z=1
840 IFPEEK(CB+Z)=0THENRETURN
850 Z=Z+1:GOTO840
1000 DATA"SERVED FROM 1789 TO 1797"
1005 DATA"WAS THE ONLY PRESIDENT ELECTED{7 SPACE}UNANIMOUSLY"
1010 DATA"MAY HAVE DIED FROM BEING BLED TO{5 SPACE}DEATH BY LEECHES"
1020 DATA"HAD JOHN ADAMS FOR A VICE-PRESIDENT","G WASHINGTON"
1030 DATA"WAS THE FIRST TO BE DEFEATED FOR{5 SPACE}REELECTION"
1040 DATA"WAS THE FIRST TO LIVE IN THE WHITE{3 SPACE}HOUSE"
1050 DATA"ERRONEOUSLY SAID ON HIS DEATHBED,{4 SPACE}'JEFFERSON STILL LIVES'"
1055 DATA"SERVED FROM 1797 TO 1801"
1060 DATA"J ADAMS","WAS THE SECOND PRESIDENT BORN IN{5 SPACE}VIRGINIA"
1070 DATA"BEAT CHARLES PINCKNEY IN A{11 SPACE}REELECTION"
1075 DATA"DIED ON AMERICA'S 50TH ANNIVERSARY"
1080 DATA"SERVED FROM 1801 TO 1809","T JEFFERSON"
1090 DATA"WAS CALLED THE 'FATHER OF THE{8 SPACE}CONSTITUTION'"
1100 DATA"HAD BOTH OF HIS VICE-PRESIDENTS DIE{2 SPACE}IN OFFICE"
1110 DATA"DECIDED TO ENTER THE WAR OF 1812","SERVED FROM 1809 TO 1817"
1115 DATA"J MADISON"
1120 DATA"WAS A STUDENT OF THOMAS JEFFERSON"
1130 DATA"WAS BOTH SECRETARY OF STATE AND{6 SPACE}SECRETARY OF WAR"
1140 DATA"WAS THE FOURTH PRESIDENT BORN IN{5 SPACE}VIRGINIA"
1150 DATA"SERVED FROM 1817 TO 1825","J MONROE"
1160 DATA"WAS SECRETARY TO RUSSIA'S U.S.{7 SPACE}MINISTER AT AGE 14"
1170 DATA"WON HIS OFFICE THANKS TO HENRY CLAY"
1180 DATA"PUBLISHED POETRY","SERVED FROM 1825 TO 1829","Q ADAMS"
1190 DATA"WAS THE 1ST TO EXPERIENCE AN{9 SPACE}ASSASSINATION ATTEMPT"
1200 DATA"WAS THE HERO OF THE BATTLE OF NEW{4 SPACE}ORLEANS"
1210 DATA"WAS OUR 7TH PRESIDENT","WAS NICKNAMED 'OLD HICKORY'","A JACKSON"
1220 DATA"RAN TWICE AS A DEMOCRAT, AND ONCE AS A 'FREE SOIL' CANDIDATE"
1230 DATA"LOST POPULARITY DUE TO THE DEPRESSION{3 SPACE}OF 1837"
1240 DATA"WAS VICE-PRESIDENT UNDER ANDREW{6 SPACE}JACKSON"
1250 DATA"WAS OUR 8TH PRESIDENT, AND SERVED{4 SPACE}FROM 1837 TO 1841","M VAN BUREN"
1260 DATA"WAS THE OLDEST MAN EVER TO BE MADE{3 SPACE}PRESIDENT, EXCEPT FOR REAGAN"
1270 DATA"DIED FROM A COLD CAUGHT DURING HIS{3 SPACE}INAUGURAL ADDRESS"
1280 DATA"WAS THE FIRST TO DIE IN OFFICE"
1290 DATA"WAS OUR 9TH PRESIDENT, AND SERVED{4 SPACE}FOR ONLY 31 DAYS","W HARRISON"
1300 DATA"HAD THE NICKNAME IN OFFICE OF 'OLD{3 SPACE}VETO'"
1310 DATA"WAS THE 1ST PRESIDENT IN DANGER OF{3 SPACE}IMPEACHMENT"
1320 DATA"WAS THE 1ST PRESIDENT TO TAKE{8 SPACE}OFFICE AFTER A PRESIDENT'S DEATH"
1330 DATA"WAS OUR 10TH PRESIDENT, AND SERVED{3 SPACE}FROM 1841 TO 1845","J TYLER"
1340 DATA"BEAT HENRY CLAY BY 38,000 VOTES"
1350 DATA"ANNEXED TEXAS AND OREGON TO THE U.S"
1360 DATA"HAD GEORGE DALLAS AS HIS{13 SPACE}VICE-PRESIDENT"
1370 DATA"WAS OUR 11TH PRESIDENT, AND SERVED{3 SPACE}FROM 1845 TO 1849","J POLK"
1380 DATA"HAD THE NICKNAME OF 'OLD ROUGH AND{3 SPACE} READY'"
1390 DATA"WAS A 2ND COUSIN OF JAMES MADISON"
1400 DATA"DIED FROM EATING TOO MANY CHERRIES"
1410 DATA"WAS OUR 12TH PRESIDENT, AND SERVED{3 SPACE}FROM 1849 TO 1850","Z TAYLOR"
1420 DATA"BEGAN HIS CAREER AS AN APPRENTICE{4 SPACE}WOOLCARDER"
1430 DATA"SERVED AS MEDIATOR IN A GUANO{8 SPACE}DISPUTE"
1440 DATA"SERVED AS VICE-PRESIDENT TO ZACHARY{2 SPACE}TAYLOR"
1450 DATA"WAS OUR 13TH PRESIDENT, AND SERVED{3 SPACE}FROM 1850 TO 1853","M FILLMORE"
1460 DATA"WAS A BRIGADIER GENERAL IN THE{7 SPACE}MEXICAN WAR"
1470 DATA"WAS THE YOUNGEST U.S. SENATOR AT AGE 33"
1480 DATA"HAD WILLIAM R. KING AS HIS{11 SPACE}VICE-PRESIDENT"
1490 DATA"WAS OUR 14TH PRESIDENT, AND SERVED{3 SPACE}FROM 1853 TO 1857","F PIERCE"
1500 DATA"OPPOSED FREEING THE SLAVES BECAUSE{3 SPACE}THEY WOULD TAKE OVER THE SOUTH"
1510 DATA"TRIED 3 TIMES BEFORE FINALLY BEING{3 SPACE}NOMINATED AS PRESIDENT"
1520 DATA"WAS OUR 15TH PRESIDENT, AND SERVED{3 SPACE}FROM 1857 TO 1861"
1530 DATA"WAS OUR ONLY BACHELOR PRESIDENT","J BUCHANAN"
1540 DATA"BEAT STEPHEN DOUGLAS IN A FAMOUS{5 SPACE}SENATORIAL RACE"
1550 DATA"WAS THE 1ST PRESIDENT TO BE{10 SPACE}ASSASSINATED"
1560 DATA"WAS OUR 16TH PRESIDENT, AND SERVED{3 SPACE}FROM 1861 TO 1865"
1570 DATA"WAS KNOWN AS THE 'GREAT EMANCIPATOR'","A LINCOLN"
1580 DATA"WAS A SOUTHERN SENATOR WITH NORTHERN LOYALTIES DURING THE CIVIL WAR"
1590 DATA"WAS OUR 17TH PRESIDENT, AND SERVED{3 SPACE}FROM 1865 TO 1869"
1600 DATA"WAS VICE-PRESIDENT UNDER LINCOLN"
1610 DATA"WAS IMPEACHED AND ACQUITTED","A JOHNSON"
1620 DATA"WAS A TEETOTALER, BUT SMOKED 20{6 SPACE}CIGARS EACH DAY"
1630 DATA"WAS HELPED TO ELECTION BY THE NEWLY{2 SPACE}FREED SLAVES"
1640 DATA"WAS KNOWN AS AMERICA'S GREATEST WAR{2 SPACE}HERO"
1650 DATA"WAS OUR 18TH PRESIDENT, AND SERVED{3 SPACE}FROM 1869 TO 1877","U GRANT"
1660 DATA"HAD A WIFE WITH THE NICKNAME OF{6 SPACE}'LEMONADE LUCY'"
1670 DATA"WON THE PRESIDENCY WITH 250,000 VOTES{3 SPACE}LESS THAN HIS OPPONENT"
1680 DATA"WON THE PRESIDENCY BY 1 ELECTORAL{4 SPACE}VOTE"
1690 DATA"WAS OUR 19TH PRESIDENT, AND SERVED{3 SPACE}FROM 1877 TO 1881","R HAYES"
1700 DATA"WAS A FORMER LAY MINISTER AND{8 SPACE}EVANGELIST"
1710 DATA"WAS OPPOSED TO WAR OR SLAVERY BECAUSE{3 SPACE}OF HIS RELIGION"
1720 DATA"WAS THE 2ND PRESIDENT TO BE{10 SPACE}ASSASSINATED"
1730 DATA"WAS OUR 20TH PRESIDENT, AND SERVED{3 SPACE}DURING 1881","J GARFIELD"
1740 DATA"WAS FROM VERMONT, BUT MAY HAVE BEEN{2 SPACE}BORN IN CANADA"
1750 DATA"HAD NO VICE-PRESIDENT"
1760 DATA"WAS VICE-PRESIDENT TO JOHN GARFIELD"
1770 DATA"WAS OUR 21ST PRESIDENT, AND SERVED{3 SPACE}FROM 1881 TO 1885","C ARTHUR"
1780 DATA"WAS A BACHELOR WHEN ELECTED, BUT{5 SPACE}MARRIED WHILE IN OFFICE"
1790 DATA"SAID 'I HAVE TRIED SO HARD TO DO{5 SPACE}RIGHT'"
1800 DATA"SERVED FROM 1885 TO 1889 AND FROM{4 SPACE}1893 TO 1897"
1810 DATA"WAS BOTH OUR 22ND AND 24TH PRESIDENT","G CLEVELAND"
1820 DATA"HAD LEVI P. MORTON AS HIS{12 SPACE}VICE-PRESIDENT"
1830 DATA"HAD A GRANDFATHER WHO HAD BEEN{7 SPACE}PRESIDENT BEFORE HIM"
1840 DATA"DEFEATED GROVER CLEVELAND AND WAS{4 SPACE}DEFEATED BY HIM"
1850 DATA"WAS OUR 23RD PRESIDENT, AND SERVED{3 SPACE}FROM 1889 TO 1893","B HARRISON"
1860 DATA"TWICE DEFEATED WILLIAM JENNINGS BRYAN{3 SPACE}FOR THE PRESIDENCY"
1870 DATA"SERVED 6 TERMS IN THE HOUSE OF{7 SPACE}REPRESENTATIVES"
1880 DATA"WAS OUR 25TH PRESIDENT, AND SERVED{3 SPACE}FROM 1897 TO 1901"
1890 DATA"WAS ASSASSINATED BY ANARCHIST LEON{3 SPACE}CZOLGOSZ","W MCKINLEY"
1900 DATA"BECAME PRESIDENT AFTER MCKINLEY'S{4 SPACE}ASSASSINATION"
1910 DATA"WAS OUR 26TH PRESIDENT, AND SERVED{3 SPACE}FROM 1901 TO 1909"
1920 DATA"WAS THE FOUNDER OF THE NATIONAL PARK SYSTEM"
1930 DATA"ORGANIZED THE 'ROUGH RIDERS'","T ROOSEVELT"
1940 DATA"WAS SECRETARY OF WAR UNDER TEDDY{5 SPACE}ROOSEVELT"
1950 DATA"BECAME CHIEF JUSTICE OF THE SUPREME{2 SPACE}COURT AFTER BEING PRESIDENT"
1960 DATA"WAS OUR 27TH PRESIDENT, AND SERVED{3 SPACE}FROM 1909 TO 1913"
1970 DATA"WAS OUR FATTEST PRESIDENT, WEIGHING{2 SPACE}325 POUNDS WHEN ELECTED"
1980 DATA"W TAFT"
1985 DATA"CALLED RACIAL SEGREGATION 'NOT{7 SPACE}HUMILIATING BUT A BENEFIT'"
1990 DATA"USED THE REELECTION SLOGAN 'HE KEPT{2 SPACE}US OUT OF WAR'"
2000 DATA"WAS OUR 28TH PRESIDENT, AND SERVED{3 SPACE}FROM 1913 TO 1921"
2010 DATA"IS OFTEN ASSOCIATED WITH THE LEAGUE{2 SPACE}OF NATIONS","W WILSON"
2020 DATA"SAID 'I AM NOT FIT FOR THIS OFFICE{3 SPACE}AND NEVER SHOULD HAVE BEEN HERE'"
2030 DATA"WAS OUR 29TH PRESIDENT, AND SERVED{3 SPACE}FROM 1921 TO 1923"
2040 DATA"DIED AMIDST CORRUPTION AND MAY HAVE{2 SPACE}BEEN POISONED"
2050 DATA"WAS LINKED WITH THE TEAPOT DOME{6 SPACE}SCANDAL","W HARDING"
2060 DATA"WAS FAMOUS FOR BREAKING BOSTON'S{5 SPACE}POLICE STRIKE"
2070 DATA"WAS OUR 30TH PRESIDENT, AND SERVED{3 SPACE}FROM 1923 TO 1929"
2080 DATA"WAS 'WEANED ON A PICKLE,' ACCORDING{2 SPACE}TO ALICE LONGWORTH"
2090 DATA"WAS KNOWN AS 'SILENT CAL'","C COOLIDGE"
2100 DATA"LATER WORKED UNDER PRESIDENTS TRUMAN AND EISENHOWER"
2110 DATA"WAS A MINING ENGINEER BEFORE ENTERING{3 SPACE}POLITICS"
2120 DATA"WAS OUR 31ST PRESIDENT, AND SERVED{3 SPACE}FROM 1929 TO 1933"
2130 DATA"LOST HIS REELECTION BID BECAUSE OF{3 SPACE}THE GREAT DEPRESSION","H HOOVER"
2140 DATA"WAS OUR 32ND PRESIDENT, BEGINNING{4 SPACE}HIS TERM IN 1933"
2150 DATA"IS CREDITED WITH SAVING AMERICA FROM THE GREAT DEPRESSION"
2160 DATA"WAS PRESIDENT DURING MOST OF WORLD{3 SPACE}WAR II"
2170 DATA"SERVED BEFORE PRESIDENTS WERE{8 SPACE}ALLOWED ONLY TWO TERMS"
2175 DATA"F ROOSEVELT"
2180 DATA"WAS THE ONLY 20TH CENTURY PRESIDENT{2 SPACE}WHO NEVER WENT TO COLLEGE"
2190 DATA"WAS OUR 33RD PRESIDENT, AND SERVED{3 SPACE}FROM 1945 TO 1953"
2200 DATA"WAS A HABERDASHER BEFORE ENTERING{4 SPACE}POLITICS"
2210 DATA"WAS PRESIDENT WHEN ATOMIC BOMBS WERE DROPPED ON JAPAN","H TRUMAN"
2220 DATA"WAS WANTED BY BOTH PARTIES AS A{6 SPACE}PRESIDENTIAL CANDIDATE"
2230 DATA"PLAYED FOOTBALL FOR WEST POINT"
2240 DATA"WAS OUR 34TH PRESIDENT, AND SERVED{3 SPACE}FROM 1953 TO 1961"
2250 DATA"WAS THE ONLY PRESIDENT TO BE A{7 SPACE}5-STAR GENERAL","D EISENHOWER"
2260 DATA"LIVED A SHORTER LIFE THAN ANY OTHER{2 SPACE}PRESIDENT"
2270 DATA"WAS THE YOUNGEST MAN EVER ELECTED{4 SPACE}PRESIDENT"
2280 DATA"MAY HAVE BEEN ELECTED BECAUSE HE{5 SPACE}LOOKED GOOD ON T.V"
2290 DATA"WAS OUR 35TH PRESIDENT, AND SERVED{3 SPACE}FROM 1961 TO 1963","J KENNEDY"
2300 DATA"BECAME SENATE DEMOCRAT LEADER DURING HIS 1ST SENATE TERM"
2310 DATA"WAS PRESIDENT DURING THE VIETNAM WAR ESCALATION"
2320 DATA"WAS OUR 36TH PRESIDENT, AND SERVED{3 SPACE}FROM 1963 TO 1969"
2330 DATA"WAS KNOWN FOR THE 'GREAT SOCIETY'","L JOHNSON"
2340 DATA"WAS PRESIDENT WHEN THE VOTING AGE WAS{3 SPACE}LOWERED TO 18"
2350 DATA"WAS PRESIDENT WHEN MAN SET FOOT ON{3 SPACE}THE MOON"
2360 DATA"WAS OUR 37TH PRESIDENT, AND SERVED{3 SPACE}FROM 1969 TO 1974"
2370 DATA"WAS THE ONLY PRESIDENT TO RESIGN IN{2 SPACE}THE MIDDLE OF A TERM","R NIXON"
2380 DATA"WAS BORN WITH THE NAME OF LESLIE{5 SPACE}LYNCH KING, JR"
2390 DATA"WAS NEVER ELECTED PRESIDENT OR{7 SPACE}VICE-PRESIDENT"
2400 DATA"WAS A MALE MODEL AND FOOTBALL PLAYER BEFORE ENTERING POLITICS"
2410 DATA"WAS OUR 38TH PRESIDENT, AND SERVED FROM 1974 TO 1977","G FORD"
2420 DATA"MAY HAVE LOST HIS REELECTION DUE TO{2 SPACE}PROBLEMS IN THE MIDDLE EAST"
2430 DATA"WAS PRESIDENT DURING THE IRAN HOSTAGE{3 SPACE}CRISIS"
2440 DATA"WAS OUR 39TH PRESIDENT, AND SERVED{3 SPACE}FROM 1977 TO 1981"
2450 DATA"WAS A PEANUT FARMER, AND HAD A BROTHER{3 SPACE}NAMED BILLY","J CARTER"
2460 DATA"STARTED HIS CAREER AS AN ILLINOIS{4 SPACE}LIFE-GUARD"
2470 DATA"WAS THE 1ST PRESIDENT TO HAVE BEEN{3 SPACE}DIVORCED"
2480 DATA"RAN ON A PLATFORM OF 'NEW{12 SPACE}FEDERALISM'"
2490 DATA"WAS OUR 40TH PRESIDENT, AND TOOK{5 SPACE}OFFICE IN 1981","R REAGAN"
2899 DATA"$SONG"
2900 DATA4817,6430,6430,7217,8101,6430,8101,7217,4817,6430
2910 DATA6430,7217,8101,6430,0,6069,4817,6430,6430,7217,8101
2920 DATA8583,8101,7217,6430,6069,4817,5407,6069,6430,0,6430
3000 PRINTCHR$(147);:FORX1=1TO4
3010 PRINTF1$;F2$:NEXTX1:F4$=LEFT$(F3$,41)
3020 PRINTF1$;"{13 DOWN}";F3$;"{DOWN}";F4$;"{OFF}";
3030 POKESM+999,160:POKECM+999,2
3040 RETURN
3500 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
3510 FORX1=0TO31:X2=INT(NT%(X1)/256)
3520 POKEMC+0,NT%(X1)-(X2*256)
3530 POKEMC+1,X2:POKEMC+4,17
3540 FORX2=1TO150:NEXTX2:POKEMC+4,16
3550 FORX2=1TO20:NEXTX2,X1
3560 POKEMC+24,0
3570 RETURN
