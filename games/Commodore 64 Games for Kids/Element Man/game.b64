1 REM ELEMENT MAN
90 POKE 53272,23
100 NW%=103:MC=54272:VC=53248:CB=828:PRINTCHR$(147);
110 DIM NT%(15),WT$(NW%-1),WI$(19),LL$(NW%-1),XY$(NW%-1)
120 S1=((PEEK(53272)AND240)/16)*1024:S2=55296:POKEVC+32,6:POKEVC+33,15
125 FORX=54272TO54296:POKEX,0:NEXTX
130 FORX=0TO15:READZ:NT%(X)=Z:NEXTX:FORX=1TONW%:READX$:WT$(X-1)=X$:NEXTX
135 FORX=1TONW%:READX$,XX$:LL$(X-1)=X$:XY$(X-1)=XX$:NEXTX
140 PRINT"{BLK}{DOWN} {RVS}{13 SPACE}eLEMENT{2 SPACE}mAN{13 SPACE}{OFF}"
150 PRINT"{2 DOWN} tHE COMPUTER WILL PICK A CHEMICAL"
160 PRINT" ELEMENT AND SHOW YOU THE NUMBER OF"
165 PRINT" LETTERS IT CONTAINS."
170 PRINT"{2 DOWN} yOU MUST GUESS THE ELEMENT BY SELECTING ONE LETTER AT A TIME."
180 PRINT"{2 DOWN} iF YOU PICK MORE THAN 11 INCORRECT"
190 PRINT" LETTERS, THE FORMIDABLE ELEMENT MAN{5 SPACE}WILL APPEAR!!"
200 PRINT"{2 DOWN}{9 SPACE}PRESS ANY KEY TO START":GOSUB2500
205 PRINT"{2 DOWN}{11 SPACE}(15 SECOND DELAY)"
210 GETX$:IFX$=""THEN210
220 FORX=1TONW%:POKECB+X,0:NEXTX
230 FORX=1TONW%:GOSUB800:POKECB+Z,X:NEXTX
300 FORLC=1TONW%
310 X=PEEK(CB+LC):WO$=WT$(X-1):SY$=LL$(X-1):XZ$=XY$(X-1)
320 LW%=LEN(WO$)
330 FORX=0TOLW%-1
340 WI$(X)=MID$(WO$,X+1,1):NEXTX
350 LC%=0:OF%=881:BC%=0:GOSUB1000:LW%=LW%-1
400 GETX$:IFX$=""THEN400
410 X=ASC(X$):IFX<65ORX>90THEN400
500 HI=0:FORY=0TOLW%:IFWI$(Y)<>X$THEN530
510 HI=1:WI$(Y)="$":POKES1+600+CC+(Y*2),X-64
520 LC%=LC%+1
530 NEXTY:IFHI=1THEN560
540 GOSUB3000:BC%=BC%+1
550 IFBC%>10THENGOSUB2000:LO%=LO%+1:FORX=1TO3000:NEXTX:GOTO700
560 IFLC%<=LW%THEN400
570 GOSUB4000:WI%=WI%+1:GOTO700
700 PRINTCHR$(147);SPC(11);"{BLK}{3 DOWN}aNOTHER eLEMENT?"
710 PRINT"{2 DOWN}";SPC(13);"eNTER {RVS}y{OFF} OR {RVS}n{OFF}"
720 GETX$:IFX$=""THEN720
730 IFX$="Y"THENNEXTLC:POKE198,1:GOTO220
740 IFX$<>"N"THEN720
750 PRINT"{3 DOWN}{11 SPACE}nUMBER cORRECT =";WI%
760 PRINT"{2 DOWN}{11 SPACE}nUMBER mISSED{2 SPACE}=";LO%
770 GOSUB2500:FORX=1TO500:NEXTX:PRINTCHR$(147)
775 POKE53272,21
780 END
800 Y=0
810 Z=INT(RND(1)*NW%)+1:IFPEEK(CB+Z)=0THENRETURN
820 Y=Y+1:IFY<10THEN810
830 Z=1
840 IFPEEK(CB+Z)=0THENRETURN
850 Z=Z+1:GOTO840
1000 PRINT"{CLR}{BLK}{DOWN}{9 SPACE}e L E M E N T{3 SPACE}m A N"
1110 PRINT"{7 DOWN}sYMBOL:"SY$"{2 DOWN}"
1120 PRINT"aTOMIC":PRINT"nUMBER:"XZ$
1500 PRINT"{2 DOWN}{BLK}{40 S*}";
1510 PRINT"{2 DOWN} ENTER A LETTER:"
1515 CC=INT((40-(2*LW%))/2)
1520 X=(LW%-1)
1530 FORY=0TOX*2STEP2
1540 POKES1+600+CC+Y,100:POKES2+600+CC+Y,0:NEXTY
1550 RETURN
2000 FORX=0TOLW%:IFWI$(X)="$"THEN2020
2010 Y=ASC(WI$(X)):POKES1+600+CC+(X*2),Y-64
2020 NEXTX
2030 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
2040 FORX=40TO10STEP-2
2050 POKEMC+1,X:POKEMC+4,17:FORY=1TO40:NEXTY
2060 POKEMC+4,16:NEXTX
2070 POKEMC+24,0
2080 RETURN
2500 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
2510 FORX=0TO15
2520 Y=INT(NT%(X)/256)
2530 POKEMC+0,NT%(X)-(Y*256)
2540 POKEMC+1,Y:POKEMC+4,17
2550 FORY=1TO75:NEXTY:POKEMC+4,16
2560 FORY=1TO15:NEXTY,X
2570 POKEMC+24,0
2580 RETURN
3000 PRINT"{BLU}";
3005 ON(BC%+1)GOTO3010,3020,3030,3040,3050,3060,3070,3080,3090,3100,3110,3120
3010 PRINT"{HOME}{3 DOWN}";SPC(18);"{C@}x{C@}":GOTO3500
3020 PRINTSPC(17);"{CM}o{S*}o{CG}":GOTO3500
3030 PRINTSPC(17);"< {S-} >":GOTO3500
3040 PRINTSPC(17);"{CN} {S*} {CG}":GOTO3500
3050 PRINTSPC(18);"{CT}{C+}{CT}":GOTO3500
3060 PRINTSPC(18);"{3 C+}":GOTO3500
3070 PRINTSPC(16);"{CA}{2 C+}{RVS}E{OFF}{2 C+}{CS}":GOTO3500
3080 PRINTSPC(16);"{S-} {RVS}{3 SPACE}{OFF} {S-}":GOTO3500
3090 PRINTSPC(16);"{CE} {3 C+} {CE}":GOTO3500
3100 PRINTSPC(18);"{C+} {C+}":GOTO3500
3110 PRINTSPC(17);"{C#}{C+} {C+}{C#}"
3500 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15:POKEVC+33,0
3505 POKEMC+1,20
3510 POKEMC+4,17:FORY=1TO300:NEXTY
3520 POKEMC+4,16:POKEVC+33,15:POKEMC+24,0
3530 POKES1+OF%,X-64:POKES2+OF%,0:OF%=OF%+1
3540 RETURN
4000 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
4010 FORX=2TO15:POKEVC+33,X:POKEMC+1,(X*5):POKEMC+4,17
4020 FORY=1TO40:NEXTY:POKEMC+4,16:NEXTX
4030 POKEMC+24,0
4040 RETURN
5000 DATA6430,6069,6430,7217,8101,8583,8101,7217,6430,6069,5407,5728
5010 DATA4817,6430,5407,4291
10000 DATAACTINIUM,ALUMINUM,AMERICIUM,ANTIMONY,ARGON
10010 DATAARSENIC,ASTATINE,BARIUM,BERKELIUM,BERYLLIUM
10020 DATABISMUTH,BORON,BROMINE,CADMIUM,CALCIUM
10030 DATACALIFORNIUM,CARBON,CERIUM,CESIUM,CHLORINE
10040 DATACHROMIUM,COBALT,COPPER,CURIUM,DYSPROSIUM
10050 DATAEINSTEINIUM,ERBIUM,EUROPIUM,FERMIUM,FLUORINE
10060 DATAFRANCIUM,GADOLINIUM,GALLIUM,GERMANIUM,GOLD
10070 DATAHAFNIUM,HELIUM,HOLMIUM,HYDROGEN,INDIUM
10080 DATAIODINE,IRIDIUM,IRON,KRYPTON,LANTHANUM
10090 DATALAWRENCIUM,LEAD,LITHIUM,LUTETIUM,MAGNESIUM
10100 DATAMANGANESE,MENDELEVIUM,MERCURY,MOLYBDENUM,NEODYMIUM
10110 DATANEON,NEPTUNIUM,NICKEL,NIOBIUM,NITROGEN
10120 DATANOBELIUM,OSMIUM,OXYGEN,PALLADIUM,PHOSPHORUS
10130 DATAPLATINUM,PLUTONIUM,POLONIUM,POTASSIUM,PRASEODYMIUM
10140 DATAPROMETHIUM,PROTACTINIUM,RADIUM,RADON,RHENIUM
10150 DATARHODIUM,RUBIDIUM,RUTHENIUM,SAMARIUM,SCANDIUM
10160 DATASELENIUM,SILICON,SILVER,SODIUM,STRONTIUM
10170 DATASULFUR,TANTALUM,TECHNETIUM,TELLURIUM,TERBIUM
10180 DATATHALLIUM,THORIUM,THULIUM,TIN,TITANIUM
10190 DATATUNGSTEN,URANIUM,VANADIUM,XENON,YTTERBIUM
10200 DATAYTTRIUM,ZINC,ZIRCONIUM
11000 DATA"aC",89,"aL",13,"aM",95,"sB",51,"aR",18
11010 DATA"aS",33,"aT",85,"bA",56,"bK",97,"bE",4
11020 DATA"bI",83,"b",5,"bR",35,"cD",48,"cA",20
11030 DATA"cF",98,"c",6,"cE",58,"cS",55,"cL",17
11040 DATA"cR",24,"cO",27,"cU",29,"cM",96,"dY",66
11050 DATA"eS",99,"eR",68,"eU",63,"fM",100,"f",9
11060 DATA"fR",87,"gD",64,"gA",31,"gE",32,"aU",79
11070 DATA"hF",72,"hE",2,"hO",67,"h",1,"iN",49
11080 DATA"i",53,"iR",77,"fE",26,"kR",36,"lA",57 
11090 DATA"lR",103,"pB",82,"lI",3,"lU",71,"mG",12
11100 DATA"mN",25,"mD",101,"hG",80,"mO",42,"nD",60
11110 DATA"nE",10,"nP",93,"nI",28,"nB",41,"n",7
11120 DATA"nO",102,"oS",76,"o",8,"pD",46,"p",15
11130 DATA"pT",78,"pU",94,"pO",84,"k",19,"pR",59
11140 DATA"pM",61,"pA",91,"rA",88,"rN",86,"rE",75
11150 DATA"rH",45,"rB",37,"rU",44,"sM",62,"sC",21
11160 DATA"sE",34,"sI",14,"aG",47,"nA",11,"sR",38
11170 DATA"s",16,"tA",73,"tC",43,"tE",52,"tB",65
11180 DATA"tL",81,"tH",90,"tM",69,"sN",50,"tI",22
11190 DATA"w",74,"u",92,"v",23,"xE",54,"yB",70
11200 DATA"y",39,"zN",30,"zR",40
