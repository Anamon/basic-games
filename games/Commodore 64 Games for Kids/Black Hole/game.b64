1 REM BLACK HOLE
100 DIMRT$(9),NT%(44,1),PT%(39,1),CH%(3),C%(3),DN$(23)
105 VC=53248:MC=54272:K1=960
110 CH%(0)=8:CH%(1)=10:CH%(2)=12:CH%(3)=14:RC%=PEEK(53272):GOSUB 5600
115 FOR I=0 TO 3:C%(I)=1024*CH%(I):CH%(I)=(RC%AND240)ORCH%(I):NEXT
120 FORX=54272TO54296:POKEX,0:NEXTX
125 P1=((PEEK(53272)AND240)/16)*1024:P2=55296
130 FORX=0TO9:READX$:RT$(X)=X$:NEXTX
135 FORX=0TO44:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
140 GOSUB5000
150 GOSUB2000:PRINTCHR$(147):SD%=0
155 FORX=0TO24:POKEP1+(X*40),160:POKEP2+(X*40),0:NEXTX
160 GOSUB1000
165 FOREC=1TOEC%
170 Y=INT(RND(1)*25):L1=(Y*40)+1+P1:L2=(Y*40)+1+P2
180 X=0
190 IFPEEK(L1+X)=81THEN400
200 POKE L2+X,2:POKEX1,32:POKEL1+X,90
205 FOR KK=0 TO 3:POKE 53272,CH%(KK):NEXT KK
210 FOR Z=0 TO SP%:NEXTZ:POKE53272,CH%(0)
220 A=PEEK(197):IF A=6 OR A=9 OR(PEEK(56321)AND1)=0THEN600
230 IF A=23 OR A=3 OR(PEEK(56321)AND2)=0THEN650
235 X1=L1+X
240 X=X+1:IFX<=38THEN190
250 GOSUB6500:NEXTEC:GOTO3000
400 POKE X1,32
405 SD%=SD%+1:POKEMC+0,0:POKEMC+1,10
410 POKEMC+5,15:POKEMC+6,15:POKEMC+4,17
420 POKEVC+32,0:POKEVC+33,0:POKEMC+24,9
430 FORQ=1TO200:NEXTQ:POKEMC+4,16
440 POKEMC+24,0:POKEVC+32,2:POKEVC+33,15
450 GOTO250
600 POKE X1,32:IFY<1THEN235
605 X1=L1+X
610 Y=Y-1:L1=L1-40:L2=L2-40:GOTO240
650 POKE X1,32:IFY>23THEN235
655 X1=L1+X
660 Y=Y+1:L1=L1+40:L2=L2+40:GOTO240
1000 FORX=1TOBH%
1010 Y=INT(RND(1)*25)
1020 Z=INT(RND(1)*30)+10
1030 Y=(Y*40)+Z
1040 A=PEEK(P1+Y):IFA<>32THEN1010
1050 POKEP1+Y,81:POKE P2+Y,0:NEXTX
1060 FORX=1TO1000:NEXTX
1070 PRINTDN$(23)
1080 RETURN
2000 PRINTCHR$(147);"{2 DOWN}{3 SPACE}ENTER # OF STARSHIPS{2 SPACE}(10 TO 50)"
2010 INPUT"{DOWN} ";X$:EC%=VAL(X$):IFEC%<10OREC%>50THEN2010
2020 PRINT"{2 DOWN}{3 SPACE}ENTER STARSHIP SPEED":PRINT"{3 SPACE}(0=FAST -TO- 9=SLOW)"
2030 INPUT"{DOWN} ";X$:SP%=VAL(X$):IFSP%<0ORSP%>9THEN2030
2040 PRINT"{2 DOWN}{3 SPACE}ENTER # OF BLACK HOLES":PRINT"{3 SPACE}(50 TO 200)"
2050 INPUT"{DOWN} ";X$:BH%=VAL(X$):IFBH%<50ORBH%>200THEN2050
2060 SP%=SP%*7
2070 RETURN
3000 POKE 53272,RC%:PRINTCHR$(147);SPC(11);"{2 DOWN}{RVS}G A M E{4 SPACE}O V E R{OFF}"
3002 X$=STR$(EC%)
3004 IFLEN(X$)<4THENX$=" "+X$:GOTO3004
3005 PRINT"{3 DOWN} NUMBER OF STARSHIPS =";X$:POKE198,0
3006 X$=STR$(SD%)
3008 IFLEN(X$)<4THENX$=" "+X$:GOTO3008
3010 PRINT"{DOWN} NUMBER DESTROYED{4 SPACE}=";X$
3012 PRINT "{DOWN} AVERAGE SURVIVAL ="STR$(INT(SC/EC%))" PARSEC(S)"
3030 X=INT(SC/(4*EC%)):X=INT(X/(200/(BH%+(200-BH%)/2)+((SP%/7)-1)/4))
3035 IF EC%=SD% AND SC<5 THEN X=0
3040 IFX<0THENX=0
3050 IFX>9THENX=9
3060 PRINT"{2 DOWN}{9 SPACE}YOUR RANK IS:{DOWN}":PRINTSPC(9);RT$(X)
3080 PRINT"{2 DOWN}{9 SPACE}PLAY AGAIN?"
3085 PRINT"{DOWN}{4 SPACE}{RVS}S{OFF}AME GAME":PRINT"{DOWN}{4 SPACE}{RVS}N{OFF}EW GAME":PRINT"{DOWN}{4 SPACE}{RVS}Q{OFF}UIT"
3090 GETX$:IFX$=""THEN3090
3100 IFX$="N"THENSC=0:GOTO 150
3110 IFX$="Q"THENGOSUB7000:PRINTCHR$(147):END
3120 IFX$="S"THENSC=0:SD%=0:PRINT "{CLR}":GOTO 155
3130 GOTO 3090
5000 POKEVC+32,2:POKEVC+33,15
5005 PRINTCHR$(147);"{DOWN}{BLK}{RVS}{10 SPACE}B L A C K{4 SPACE}H O L E{10 SPACE}{OFF}";
5010 PRINT"{2 DOWN}YOU MUST PILOT A FLEET OF STARSHIPS"
5020 PRINT"ACROSS SPACE, AVOIDING THE BLACK HOLES."
5030 PRINT"{2 DOWN}EACH SHIP TRAVELS FROM THE LEFT SIDE OF THE SCREEN TO THE RIGHT."
5040 PRINT"{2 DOWN}YOU CAN CAUSE EACH SHIP TO CLIMB OR DIVE";
5050 PRINT"BY USING THE JOYSTICK OR THE KEYBOARD:"
5060 PRINT"{2 DOWN}{4 SPACE}({RVS}W OR F5{OFF}=CLIMB{3 SPACE}{RVS}X OR F7{OFF}=DIVE)"
5070 IF PEEK(653)>1 THEN 5090
5080 GOSUB 7000:GOSUB 6000
5090 PRINT"{CLR}{2 DOWN}YOU CAN CONTROL THE DIFFICULTY OF THE{3 SPACE}GAME BY THREE FACTORS:"
5100 PRINT"{2 DOWN}{3 SPACE}+ NUMBER OF STARSHIPS":PRINT"{DOWN}{3 SPACE}+ STARSHIP SPEED"
5120 PRINT"{DOWN}{3 SPACE}+ NUMBER OF BLACK HOLES"
5130 FOR I=0 TO 3:FOR J=C%(I)+256 TO C%(I)+263:POKE J,0:NEXTJ
5135 FOR J=C%(I)+1280 TO C%(I)+1287:POKE J,255:NEXTJ
5140 FOR J=C%(I)+648 TO C%(I)+655:READ ZZ:POKE J,ZZ:NEXTJ
5145 FOR J=C%(I)+720 TO C%(I)+727:READ ZZ:POKE J,ZZ:NEXTJ,I
5150 IF PEEK(653)>1 THEN RETURN
5160 GOSUB 6000
5175 PRINT "{CLR}"DN$(12)TAB(16)"{BLK}STARSHIPS":GOSUB 5500
5180 PRINT DN$(12)TAB(16)"{RED}z{2 SPACE}z{2 SPACE}z{2 SPACE}z":GOSUB 5510
5185 PRINT DN$(12)TAB(15)"{BLK}BLACK HOLES":GOSUB 5500
5190 PRINT DN$(12)TAB(14)"{BLK}q{2 SPACE}q{2 SPACE}q{2 SPACE}q{2 SPACE}q"
5195 GOSUB 5510:RETURN
5500 FOR I=0 TO 999:NEXT:PRINT "{CLR}":POKE 53272,CH%(0):RETURN
5510 FOR I=0 TO 9:FOR KK=0 TO 3:POKE 53272,CH%(KK):FOR J=0 TO 19:NEXTJ,KK,I
5520 PRINT "{CLR}":POKE 53272,RC%:RETURN
5600 DN$(0)="{HOME}":FOR I=1 TO 23:DN$(I)=DN$(I-1)+"{DOWN}":NEXTI:RETURN
6000 PRINTDN$(23)TAB(12)"(PRESS ANY KEY)";
6010 GETX$:IFX$=""THEN6010
6020 RETURN
6500 REM *** SCROLL THE SCREEN ***
6505 SC=SC+X-8-10*(X=39)
6515 POKE X1,32:IFEC=EC%THENRETURN
6520 FORX=0TO39
6525 PT%(X,0)=PEEK(P1+X)
6530 PT%(X,1)=PEEK(P2+X)
6540 NEXTX
6550 PRINT"{DOWN}";
6560 FORX=0TO39
6570 POKEP1+K1+X,PT%(X,0)
6580 POKEP2+K1+X,PT%(X,1)
6590 NEXTX
6600 RETURN
7000 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
7010 FORX=0TO44
7020 Y=INT(NT%(X,0)/256):Z=NT%(X,0)-(Y*256)
7030 POKEMC+0,Z:POKEMC+1,Y:POKEMC+4,17
7040 FORZ=1TONT%(X,1)*75:NEXTZ
7050 POKEMC+4,16:FORZ=1TO10:NEXTZ,X
7060 POKEMC+24,0
7070 RETURN
9000 DATA PRIVATE,SERGEANT,LIEUTENANT,CAPTAIN,MAJOR,COLONEL
9010 DATA "* GENERAL","** GENERAL","*** GENERAL","**** GENERAL"
9100 DATA2145,2,0,.3,2145,2,2864,2,2864,2,2864,2,2864,2,3215,2,0,.3,4291,2,3823
9110 DATA2,3608,2,2864,2,0,3,2145,2,2408,2,4817,2,4291,2,3823,2,3608,2,3215,2
9120 DATA2864,2,2864,4,2703,2,3215,1,3215,2,3215,2,3215,2,3215,2,3215,2,3608,2
9130 DATA4050,2,4291,2,3215,2,0,3,4291,2,4050,2,3608,2,3215,2,2864,2
9140 DATA2703,2,2408,2,3215,2,2025,2,2145,4
63000 DATA0,124,254,254,254,254,254,124
63001 DATA0,248,28,31,28,248,0,0
63002 DATA0,68,254,124,124,124,254,68
63003 DATA0,248,28,47,28,248,0,0
63004 DATA0,0,68,56,56,56,68,0
63005 DATA0,248,28,79,28,248,0,0
63006 DATA0,0,0,40,16,40,0,0
63007 DATA0,248,28,143,28,248,0,0
