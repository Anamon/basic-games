1 REM PERSONAL STORY
100 DIMNT%(14,1,7):MC=54272:VC=53248
105 POKEVC+32,4:POKEVC+33,1:PRINTCHR$(147)
110 FORX=0TO7:Y=0
120 READW,Z:NT%(Y,0,X)=W:NT%(Y,1,X)=Z:Y=Y+1:IFW<>0THEN120
130 NEXTX:C$="{BLK}{RED}{CYN}{PUR}{GRN}{BLU}{YEL}"
135 FORX=54272TO54296:POKEX,0:NEXTX
140 READN1$,N2$,SX$,PR$,AG$,BD$,AD$,CT$,F1$,PO$,FT$
150 X$=N1$+" "+N2$
160 IFLEN(X$)>40THEN175
170 Y$="{SSPACE}"+X$:X$=Y$+"{SSPACE}":GOTO160
175 X$=RIGHT$(X$,LEN(X$)-1)
180 PRINT"{BLK}{7 DOWN}";SPC(11);"THIS IS YOUR LIFE!":PRINTSPC(16);"{3 DOWN}STARRING{3 DOWN}"
185 PRINTX$:SS=2:GOSUB8000
190 PRINT"{HOME}";:FORY=0TO998:X=INT(RND(1)*7)+1:X$=MID$(C$,X,1)
200 PRINTX$;"{RVS} {OFF}";:NEXTY
210 POKEVC+32,6:PRINT"{CLR}{BLK}{DOWN}{3 SPACE}THIS IS A STORY ABOUT A VERY NICE"
220 PRINT"{DOWN}{3 SPACE}";SX$;" NAMED ";N1$;"."
230 PRINT"{3 DOWN}{3 SPACE}";PR$;" HAD A BIRTHDAY ON":PRINT"{DOWN}{3 SPACE}";BD$;", AND"
240 PRINT"{DOWN}{3 SPACE} IS ";AG$;" YEARS OLD."
250 PRINT"{3 DOWN}{13 SPACE}{YEL}q{SSPACE}q{SSPACE}q{BLK}{3 SPACE}HAPPY"
260 PRINT"{13 SPACE}{S-}{SSPACE}{S-}{SSPACE}{S-}{3 SPACE}BIRTHDAY"
270 PRINT"{12 SPACE}{RVS}{RED}{7 SPACE}{OFF}{2 SPACE}";N1$
280 PRINT"{12 SPACE}{RVS}{RED}{7 SPACE}{OFF}{2 SPACE}";N2$;"{BLK}!"
290 SS=3:GOSUB8000:GOSUB7800
300 POKEVC+32,2:PRINT"{CLR}{BLK}{2 DOWN}";SPC(11);N1$;" LIVES IN"
305 PRINTSPC(11);"{DOWN}A NICE HOUSE AT":PRINTSPC(11);"{DOWN}";AD$
310 PRINTSPC(11);"{DOWN}IN ";CT$;"."
315 PRINTSPC(11);"{BLU}{2 DOWN}{5 SPACE}rrrrrrrrr"
320 PRINTSPC(11);"{4 SPACE}nmmmmmmmmmm"
330 PRINTSPC(11);"{3 SPACE}n{2 SPACE}mmmmmmmmmm"
340 PRINTSPC(11);"{2 SPACE}y{3 S*}{S+}{9 S*}t"
345 PRINTSPC(11);"{2 SPACE}y{3 SPACE}{S-}{9 SPACE}t"
350 PRINTSPC(11);"{2 SPACE}y{3 SPACE}{S-} {BLK}{2 C+}{2 SPACE}{C+}{BLU}{3 SPACE}t"
360 PRINTSPC(11);"{2 SPACE}y{3 SPACE}{S-} {BLK}{2 C+}{2 SPACE}{C+}{BLU}{3 SPACE}t"
370 PRINTSPC(11);"{2 SPACE}y{3 SPACE}{S-}{5 SPACE}{BLK}{C+}{BLU}{3 SPACE}t"
380 PRINTSPC(10);"eeeeeeeeeeeeeeeeeeeeee{BLK}":PRINTSPC(11);AD$
390 SS=4:GOSUB8000:GOSUB7800
400 POKEVC+32,6:PRINT"{CLR}{BLK}{DOWN}";SPC(08);N1$;" HAS A GOOD FRIEND"
405 PRINTSPC(08);"{DOWN}NAMED ";F1$;"."
410 PRINTSPC(08);"{3 DOWN}THINKING ABOUT GOOD"
415 PRINTSPC(08);"{DOWN}FRIENDS MAKES":PRINTSPC(08);"{DOWN}";N1$;" SMILE."
420 PRINTSPC(11);"{RED}{3 DOWN}"
430 PRINTSPC(11);"{3 SPACE}{CO} {CO}{DOWN}"
440 PRINTSPC(11);"{3 SPACE}q{SSPACE}q{7 SPACE}";F1$
450 PRINTSPC(11);"{4 SPACE}{S-}{9 SPACE}MAKES"
460 PRINTSPC(11);"{4 SPACE}{S+}{8 SPACE}";N1$
480 PRINTSPC(11);"{2 SPACE}j{3 S*}k{7 SPACE}SMILE!"
490 SS=6:GOSUB8000:GOSUB7800
500 PRINT"{CLR}{BLK}{2 DOWN}"SPC(12);F1$;" WILL":PRINTSPC(12);"SOMETIMES CALL"
505 PRINTSPC(12);N1$; " ON"
510 PRINTSPC(12);"THE TELEPHONE.{2 DOWN}"
520 FORX=1TO14:PRINT"{12 SPACE}{RVS}{YEL}{15 SPACE}":NEXTX
530 PRINT"{HOME}{BLK}{11 DOWN}{14 RIGHT}PHONE{2 SPACE}BOOK"
532 PRINT"{2 DOWN}{14 RIGHT}";N1$:PRINT"{14 RIGHT}";N2$
540 PRINT"{2 DOWN}{14 RIGHT}";PO$
550 SS=5:GOSUB8000:GOSUB7800
600 POKEVC+32,12:POKEVC+33,0
604 PRINT"{CLR}{WHT}{DOWN}";SPC(9);"THEN ";F1$:PRINTSPC(9);"{DOWN}AND ";N1$
606 PRINTSPC(9);"{DOWN}WILL DO SOMETHING FUN"
610 PRINTSPC(9);"{DOWN}LIKE WATCHING THE MOON"
615 PRINTSPC(9);"{DOWN}COME UP OVER{DOWN}":PRINTSPC(9);CT$;"."
620 PRINT"{2 DOWN}{4 SPACE}*{8 SPACE}*{5 SPACE}*{6 SPACE}*{4 SPACE}*{4 SPACE}*"
625 PRINT"{2 SPACE}*{6 SPACE}*{2 SPACE}*{7 SPACE}*{4 SPACE}*{4 SPACE}*{4 SPACE}*"
628 PRINT"*{3 SPACE}*{2 SPACE}*{6 SPACE}*{2 SPACE}*{5 SPACE}*{3 SPACE}ui{4 SPACE}*{4 SPACE}*"
630 PRINT"{3 SPACE}*{6 SPACE}*{3 SPACE}*{5 SPACE}*{2 SPACE}*{3 SPACE}jk *{3 SPACE}*{4 SPACE}*"
640 PRINT"{5 SPACE}{C+}{SSPACE}{C+}{2 SSPACE}{C+} {C+}{4 SPACE}{C+}{5 SPACE}{C+}{SSPACE}{C+} {C+}{2 SPACE}{C+}{2 SSPACE}{2 C+}{SSPACE}{C+}"
650 PRINT"{3 SPACE}{C+} {3 C+} {2 C+} {2 C+}{2 SSPACE}{3 C+}{4 SSPACE}{5 C+}{2 SPACE}{2 C+}{SSPACE}{2 C+}{SSPACE}{C+}"
660 PRINT"{3 SPACE}{34 C+}"
670 SS=0:GOSUB8000:GOSUB7800
700 POKEVC+32,13:POKEVC+33,1:PRINT"{CLR}{BLK}{2 DOWN}";SPC(9);"IF ";F1$
710 PRINTSPC(9);"{DOWN}IS BUSY, ";N1$
715 PRINTSPC(9);"{DOWN}WILL PLAY WITH A"
720 PRINTSPC(9);"{DOWN}FAVORITE TOY LIKE A":PRINTSPC(9);"{DOWN}";FT$;"."
730 PRINT"{DOWN}":FORX=1TO9:PRINT"{RED}{10 SPACE}{RVS}{19 SPACE}{OFF}":NEXTX
740 PRINT"{BLK}{HOME}{15 DOWN}{15 RIGHT}TOY CHEST{BLK}"
750 SS=1:GOSUB8000:GOSUB7800
800 POKEVC+32,10:PRINT"{CLR}{BLK}{DOWN}";SPC(9);"GOOD FRIENDS AND FUN"
805 PRINTSPC(9);"TOYS MAKE EVERY DAY"
810 PRINTSPC(9);"SEEM LIKE CHRISTMAS":PRINTSPC(9);"AT THE ";N2$
815 PRINTSPC(9);"HOUSE.{2 DOWN}"
820 PRINTSPC(9);"{YEL}{10 SPACE}z"
830 PRINTSPC(9);"{GRN}{9 SPACE}{RVS}{S#} {C*}"
840 PRINTSPC(9);"{8 SPACE}{RVS}{S#}{3 SPACE}{C*}"
850 PRINTSPC(9);"{8 SPACE}{RVS}{5 SPACE}"
860 PRINTSPC(9);"{7 SPACE}{RVS}{S#}{5 SPACE}{C*}"
870 PRINTSPC(9);"{7 SPACE}{RVS}{7 SPACE}"
875 PRINTSPC(9);"{7 SPACE}{RVS}{7 SPACE}"
880 PRINTSPC(9);"{6 SPACE}{RVS}{S#}{7 SPACE}{C*}{BLK}"
885 PRINTSPC(9);"{10 SPACE}{S-}"
890 PRINTSPC(9);"{10 SPACE}{S-}"
900 PRINTSPC(9);"{10 SPACE}{S-}"
910 SS=7:GOSUB8000
920 FORX=832TO832+63:READZ:POKEX,Z:NEXTX
925 SS=-1:GOSUB7800:Y=58
930 POKEVC+0,50:POKEVC+1,Y:POKE2040,13
940 POKEVC+16,0:POKEVC+23,1:POKEVC+29,1
950 POKEVC+28,0:POKEVC+39,6:POKEVC+21,1
960 PRINT"{HOME}";:FORX=1TO24
970 POKEVC+1,Y:Y=Y+8
980 PRINT"{BLK}{RVS}{40 SPACE}{OFF}";
990 FORZ=1TO75:NEXTZ,X
1000 POKEVC+33,0:POKEVC+21,0:SS=.3
1010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,11:POKEMC+6,13:POKEMC+24,15:Y=10:PRINT"{HOME}";
1020 FORX=1TO111
1030 POKEMC+1,INT(Y):Y=Y+SS:POKEMC+4,17
1040 Z=INT(RND(1)*6)+2:X$=MID$(C$,Z,1)
1050 PRINTX$;"THE END{2 SPACE}";:POKEMC+4,16
1055 IFX>55THENSS=-.3
1060 NEXTX
1070 POKEMC+24,0:POKEVC+32,6:POKEVC+33,15
1080 PRINT "{CLR}{BLK}"
1090 END
7800 PRINT "{HOME}{24 DOWN}{12 SPACE}(PRESS ANY KEY)";
7810 GETX$:IFX$=""THEN7810
7815 IFSS<0THENRETURN
7820 PRINT"{HOME}";:FORY=1TO24:X=INT(RND(1)*7)+1:X$=MID$(C$,X,1)
7830 PRINTX$;"{RVS}{40 SPACE}{OFF}";
7840 FORZ=1TO40:NEXTZ,Y
7850 RETURN
8000 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15:Y=0
8010 Z=INT(NT%(Y,0,SS)/256)
8020 POKEMC+0,NT%(Y,0,SS)-(Z*256)
8030 POKEMC+1,Z:POKEMC+4,17
8040 FORZ=1TONT%(Y,1,SS)*90:NEXTZ
8050 POKEMC+4,16:FORZ=1TO10:NEXTZ
8060 IFNT%(Y,0,SS)>0THENY=Y+1:GOTO8010
8070 POKEMC+24,0
8080 RETURN
9000 DATA4291,2,4291,2,6430,2,6430,2,7217,2,7217,2,6430,4,5728,2,5728,2
9010 DATA5407,2,5407,2,4817,2,4817,2,4291,4,0,1
9020 DATA7217,2,8583,4,7217,2,5728,2,5407,2,5728,2,6430,2
9030 DATA6430,2,5407,2,4291,4,0,1
9040 DATA4817,2,5407,2,6430,2,6430,2,6430,2,6430,2
9050 DATA5407,2,4817,2,4050,2,4817,2,6430,8,0,1
9060 DATA4817,1,4817,1,5407,2,4817,2,6430,2,6069,4,4817,1
9070 DATA4817,1,5407,2,4817,2,7217,2,6430,4,0,1
9080 DATA6430,2,6430,2,6430,2,4817,2,5407,2,5407,2,4817,4,8101,2,8101,2
9090 DATA7217,2,7217,2,6430,4,0,1
9100 DATA4291,2,4291,1,4817,2,4817,1,5407,2,6430,2,5407,2
9110 DATA4291,2,7217,2,4817,2,5728,1,5407,2,4291,2,0,1
9120 DATA10814,3,9634,3,1,1,8101,2,8583,3,8101,2,7217,2,6430,2,8101,2
9130 DATA4817,2,5407,2,6430,4,0,1
9200 DATA4291,2,4291,2,4291,2,5728,1,5728,1,5728,2
9210 DATA5407,2,5728,2,6430,2,7217,2,7647,2,6430,2,7217,2,0,1
10000 DATA"JOHN"
10010 DATA"DOE"
10020 DATA"BOY"
10030 DATA"HE"
10040 DATA"7"
10050 DATA"DECEMBER 31ST"
10060 DATA"1605 MAPLE STREET WEST"
10070 DATA"NEW YORK CITY"
10080 DATA"SANDRA"
10090 DATA"582-1234"
10100 DATA"ELECTRIC TRAIN"
20000 DATA60,0,0,24,124,0,24,130,0,25,171,0,24,146,0,24,130,0,24,124
20010 DATA0,24,16,0,31,255,0,15,255,0,0,59,0,0,59,0,0,59,128,0,59,128 
20020 DATA0,56,0,0,254,0,0,198,0,3,199,128,3,1,128,3,1,128,7,1,192,0 
