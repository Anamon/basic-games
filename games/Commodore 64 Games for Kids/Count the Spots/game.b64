1 REM COUNT THE SPOTS
10 VC=53248:POKEVC+32,0:POKEVC+33,1:PRINTCHR$(147)
20 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
50 DIMDI$(5),DI%(5,2),S%(11,1)
60 A$="{RVS}{40 SPACE}{OFF}"
70 B$="{RVS}{6 SPACE}{OFF}{11 SPACE}{RVS}{6 SPACE}{OFF}{11 SPACE}{RVS}{6 SPACE}{OFF}"
100 FORX=0TO5:READX$:DI$(X)=X$:NEXTX:CT$="{CYN}{PUR}{GRN}{BLU}{YEL}"
110 FORX=0TO5:FORY=0TO2:READZ:DI%(X,Y)=Z:NEXTY,X:HS=0
120 PRINT"{RED}{CLR}{2 DOWN}{13 SPACE}{RVS}COUNT THE SPOTS"
125 PRINT"{2 DOWN} EACH GAME CONSISTS OF TEN ROLLS OF THE{2 SPACE}DICE."
130 PRINT"{2 DOWN} THE PLAYER MUST COUNT THE NUMBER OF"
135 PRINT" SPOTS ON {RVS}BOTH{OFF} DICE AND ENTER THAT{7 SPACE}NUMBER."
140 PRINT"{2 DOWN} ONE POINT IS RECEIVED FOR EACH CORRECT{2 SPACE}ANSWER."
150 PRINT"{3 DOWN}{15 SPACE}{RVS}GOOD LUCK!{OFF}"
160 X=RND(-TI):POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
170 READX,Y:IFX<0THEN194
175 Z=INT(X/256)
180 POKEMC+0,X-(Z*256):POKEMC+1,Z
190 POKEMC+4,17:FORX=1TOY:NEXTX:POKEMC+4,16
192 FORX=1TO10:NEXTX:GOTO170
194 POKEMC+24,0
200 PRINT"{BLK}{2 DOWN}{8 SPACE}(PRESS ANY KEY TO START)"
210 GETX$:IFX$=""THEN210
220 FORX=0TO11:READZ,Y:S%(X,0)=Z:S%(X,1)=Y:NEXTX
300 S=0:FORX=0TO9
310 D1=INT(RND(1)*6)+1:D2=INT(RND(1)*6)+1
320 Y=INT(RND(1)*5)+1:X$=MID$(CT$,Y,1)
330 PRINT"{CLR}";X$;:FORY=0TO4:PRINTA$;:NEXTY:FORY=0TO8:PRINTB$;:NEXTY
335 FORY=0TO4:PRINTA$;:NEXTY
340 Z$="{BLK}{HOME}{6 DOWN}":FORY=0TO2:X$=DI$(DI%(D1-1,Y)):Y$=DI$(DI%(D2-1,Y))
350 PRINTZ$;"{7 RIGHT} ";X$;" {8 RIGHT} ";Y$;"{SSPACE}":Z$=Z$+"{3 DOWN}":NEXTY
360 INPUT"{7 DOWN} HOW MANY{SSPACE}SPOTS";X$:Y=VAL(X$):IFY=(D1+D2)THEN400
370 PRINTCHR$(147):POKEVC+33,0:POKEMC+0,0:POKEMC+1,10:POKEMC+5,15
375 POKEMC+6,15:POKEMC+24,7:POKEMC+4,33:FORZ=1TO100:NEXTZ:POKEMC+4,32
380 FORZ=1TO50:NEXTZ:POKEMC+24,0:POKEVC+33,1
390 GOTO500
400 REM *** ANSWER CORRECT ***
410 S=S+1
420 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,10
430 FORZ=8TO1STEP-1
440 POKEVC+33,Z:POKEMC+1,(10-Z)*5:POKEMC+4,17
450 FORY=1TO50:NEXTY:POKEMC+4,16
460 FORY=1TO10:NEXTY,Z
470 POKEMC+24,0
500 NEXTX
600 Z=0:IFS>HSTHENHS=S:Z=1
610 PRINT"{BLU}{CLR}{2 DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}{DOWN}"
620 PRINT"{2 DOWN}{5 SPACE}YOUR SCORE =";S
630 PRINT"{2 DOWN}{5 SPACE}HIGH SCORE =";HS 
640 IFZ=1THENPRINT"{2 DOWN}{5 SPACE}{RVS}YOU SET A NEW HIGH!{OFF}"
650 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
660 FORX=0TO11:Y=INT(S%(X,0)/256)
665 POKEMC+0,S%(X,0)-(Y*256)
670 POKEMC+1,Y:POKEMC+4,17
675 FORY=1TOS%(X,1):NEXTY:POKEMC+4,16
680 FORY=1TO30:NEXTY,X
685 POKEMC+24,0
690 PRINT"{BLK}{3 DOWN}{5 SPACE}PRESS {RVS}Y{OFF} TO REPLAY"
700 PRINT"{2 DOWN}{5 SPACE}PRESS {RVS}N{OFF} TO QUIT"
710 GETX$:IFX$=""THEN710
720 IFX$="Y"THEN300
730 IFX$<>"N"THEN710
740 PRINTCHR$(147)
750 END
2000 DATA"{7 SPACE}","{3 SPACE}q{3 SPACE}","q{6 SPACE}","{6 SPACE}q","q{5 SPACE}q","q{2 SPACE}q{2 SPACE}q"
2010 DATA0,1,0,2,0,3,2,1,3,4,0,4,4,1,4,5,0,5
2020 DATA4817,300,5407,300,4291,300,2145,300,3215,600,-1,-1
2030 DATA4817,175,0,5,4817,175,0,5,4817,175,5407,100
2040 DATA6069,200,6069,100,5407,100,6069,100,6430,100,7217,200
