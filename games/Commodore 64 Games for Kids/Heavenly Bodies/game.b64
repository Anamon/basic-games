1 REM HEAVENLY BODIES
100 NQ=100:S=((PEEK(53272)AND240)/16)*1024
110 CB=828:X=RND(-TI):VC=53248:MC=54272
112 FORX=54272TO54296:POKEX,0:NEXTX
115 POKEVC+32,6:POKEVC+33,0
120 PRINTCHR$(147);"{WHT}{DOWN}{RVS}{11 SPACE}HEAVENLY{2 SPACE}BODIES{13 SPACE}{OFF}";
125 READX$:IFX$<>"$SPECIAL"THEN125
127 DIM NT%(26,1):FORX=0TO26:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
128 FORX=832TO832+63:READZ:POKEX,Z:NEXTX
130 PRINT"{2 DOWN}YOU WILL BE PRESENTED WITH FACTS ABOUT"
140 PRINT"EACH OF THE PLANETS IN THE SOLAR SYSTEM.";
150 PRINT"{DOWN}YOU MUST INDICATE THE PLANET TO WHICH{3 SPACE}THAT FACT APPLIES."
160 PRINT"{2 DOWN}{14 SPACE}--SCORING--"
170 PRINT"{DOWN}{12 SPACE}CORRECT{3 SPACE}={2 SPACE}+10":PRINT"{12 SPACE}INCORRECT ={3 SPACE}-1"
175 PRINT"{DOWN}{9 SPACE}PRESS {RVS}F1{OFF} TO QUIT EARLY"
180 PRINT"{2 DOWN}{15 SPACE}{RVS}GOOD LUCK!{OFF}"
185 GOSUB8000
190 PRINT"{2 DOWN}{8 SPACE}(PRESS ANY KEY TO START){YEL}"
195 GETX$:IFX$=""THEN195
200 S%=0:QA=0:QC=0:FORX=1TONQ
210 POKECB+X,0:NEXTX:PRINT"{DOWN}{4 SPACE}(GAME WILL START IN TEN SECONDS){WHT}";
220 FORX=1TONQ
230 Y=0
240 Z=INT(RND(1)*NQ)+1:IFPEEK(CB+Z)=0THEN290
250 Y=Y+1:IFY<6THEN240
260 Z=1
270 IFPEEK(CB+Z)=0THEN290
280 Z=Z+1:GOTO270
290 POKECB+Z,X:NEXTX:POKEVC+33,15:PRINTCHR$(147)
300 X=1
305 Y=PEEK(CB+X)-1
310 RESTORE:IFY=0THEN330
320 FORZ=1TOY:READQ$,A$:NEXTZ
330 READQ$,A$
400 PRINTCHR$(147);"{BLK}{24 DOWN}";SPC(15);"SCORE =";S%;
403 PRINT"{HOME}{RVS}**********{2 SPACE}HEAVENLY{2 SPACE}BODIES{2 SPACE}**********{OFF}";
405 FORZ=1TO8:PRINT"{BLU}{RVS}{40 SPACE}{OFF}";:NEXTZ
410 PRINT"{BLK}{2 DOWN}{3 SPACE}ENTER YOUR ANSWER:"
420 PRINT"{DOWN}{4 SPACE}M = MERCURY"
430 PRINT"{4 SPACE}V = VENUS"
440 PRINT"{4 SPACE}E = EARTH"
450 PRINT"{4 SPACE}A = MARS"
460 PRINT"{4 SPACE}J = JUPITER"
470 PRINT"{4 SPACE}S = SATURN"
480 PRINT"{4 SPACE}U = URANUS"
490 PRINT"{4 SPACE}N = NEPTUNE"
500 PRINT"{4 SPACE}P = PLUTO"
510 PRINT"{4 SPACE}X = PLANET X"
600 PRINT"{BLU}{RVS}{HOME}{4 DOWN}";Q$;".{OFF}"
610 POKE198,0
620 GETX$:IFX$=""THEN620
630 IFASC(X$)=133THEN910
635 QA=QA+1
637 IFX$=A$THEN820
640 IFX$="M"ORX$="V"ORX$="E"ORX$="A"ORX$="J"ORX$="U"ORX$="S"THEN670
650 IFX$="N"ORX$="P"ORX$="X"THEN670
660 GOTO620
670 Y=1:IFA$="V"THENY=2
680 IFA$="E"THENY=3
690 IFA$="A"THENY=4
700 IFA$="J"THENY=5
710 IFA$="S"THENY=6
720 IFA$="U"THENY=7
730 IFA$="N"THENY=8
740 IFA$="P"THENY=9
750 IFA$="X"THENY=10
760 Y=(Y*40)+484+S
770 POKEMC+0,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15:POKEVC+32,2
780 FORZ=1TO4:POKEMC+1,10:POKEMC+4,17
790 FORW=YTOY+30:POKEW,PEEK(W)+128:NEXTW:FORW=1TO10:NEXTW
800 POKEMC+1,15:FORW=YTOY+30:POKEW,PEEK(W)-128:NEXTW:POKEMC+4,16:NEXTZ
810 POKEMC+24,0:POKEVC+32,6:S%=S%-1:GOTO900
820 POKEMC+0,0:POKEMC+5,15:POKEMC+6,15:POKEMC+1,0:POKEMC+24,15
830 FORW=4TO15:POKEVC+33,W:POKEMC+1,W*2:POKEMC+4,17:FORZ=1TO60:NEXTZ
840 POKEMC+4,16:NEXTW:POKEMC+24,0
850 S%=S%+10:QC=QC+1
900 X=X+1:IFX<=NQTHEN305
910 PRINTCHR$(147);"{BLK}{2 DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}"
920 PRINT"{3 DOWN}{7 SPACE}# QUESTIONS =";QA
930 PRINT"{DOWN}{7 SPACE}# CORRECT{3 SPACE}=";QC
940 IFQA=0THENQA=1
950 X=INT((QC*100)/QA)
960 PRINT"{DOWN}{7 SPACE}% CORRECT{3 SPACE}=";X;"{LEFT}%"
970 PRINT"{3 DOWN}{9 SPACE}SCORE ="S%
980 PRINT"{3 DOWN}{7 SPACE}PLAY AGAIN? ({RVS}Y{OFF} OR {RVS}N{OFF})"
990 GETX$:IFX$=""THEN990
1000 IFX$="Y"THEN200
1010 IFX$<>"N"THEN940
1020 PRINTCHR$(147):END
2000 DATA"THE SECOND-SMALLEST KNOWN PLANET, ITS{3 SPACE}DIAMETER IS 3100 MILES",M
2010 DATA"IT MOVES AROUND THE SUN AT 30 MILES PER SECOND",M
2020 DATA"IT CIRCLES THE SUN EVERY 88 'EARTH'{5 SPACE}DAYS",M
2030 DATA"IT TAKES 59 'EARTH' DAYS TO TURN ONCE ON ITS AXIS",M
2040 DATA"ITS SUN SIDE IS 800 DEGREES FAHRENHEIT",M
2050 DATA"THE SIDE AWAY FROM THE SUN MAY BE ABOUT ROOM TEMPERATURE",M
2060 DATA"IT MAY HAVE A THIN ATMOSPHERE OF{8 SPACE}HYDROGEN AND HELIUM",M
2070 DATA"IT HAS A PERMANENT MAGNETIC FIELD{7 SPACE}DESPITE ITS SLOW ROTATION",M
2080 DATA"IT IS APPROXIMATELY 36 MILLION MILES{4 SPACE}FROM THE SUN",M
2090 DATA"IT IS THE NEAREST PLANET TO THE SUN",M
2100 DATA"IT IS USUALLY THE 2ND PLANET FROM THE{3 SPACE}SUN",V
2110 DATA"ITS APPROXIMATE DISTANCE FROM THE SUN IS 67 MILLION MILES",V
2120 DATA"ONCE EVERY 19 MONTHS, IT IS THE NEAREST PLANET TO THE SUN",V
2130 DATA"EXCEPT FOR THE SUN AND OUR MOON, IT IS{2 SPACE}THE SKY'S BRIGHTEST OBJECT",V
2140 DATA"ITS BRIGHTNESS IS CAUSED BY ITS THICK,{2 SPACE}WHITE CLOUD COVER",V
2150 DATA"ITS ATMOSPHERE IS PROBABLY COMPOSED OF{2 SPACE}SULPHURIC ACID",V
2160 DATA"ITS SURFACE TEMPERATURE IS 600 DEGREES{2 SPACE}FAHRENHEIT",V
2170 DATA"ITS CARBON DIOXIDE ATMOSPHERE IS 100{4 SPACE}TIMES EARTH'S PRESSURE",V
2180 DATA"ITS DAY IS 117.4 'EARTH' DAYS, AND ITS{2 SPACE}YEAR IS 225 DAYS",V
2190 DATA"IT HAS HIGH ALTITUDE WINDS OF 200 M.P.H",V
2200 DATA"THE 5TH LARGEST PLANET, ITS DIAMETER IS ALMOST 8,000 MILES",E
2210 DATA"ITS CORE IS PROBABLY A SOLID METALLIC{3 SPACE}AND MAGNETIC SUBSTANCE",E
2220 DATA"EVERY 200 FEET BELOW ITS SURFACE, THE{3 SPACE}TEMPERATURE RISES 1 DEGREE",E
2230 DATA"ITS ATMOSPHERE CONSISTS OF OXYGEN,{6 SPACE}HYDROGEN AND ARGON",E
2240 DATA"ITS ATMOSPHERE EXTENDS 300,000 FEET{5 SPACE}ABOVE THE PLANET'S SURFACE",E
2250 DATA"THE AVERAGE DAY IS 23 HOURS, 56 MINUTES OF 'EARTH' TIME",E
2260 DATA"ONE OF ITS OCEANS COVERS MORE THAN 125% OF ITS LAND MASS",E
2270 DATA"ITS MOST PREVALENT LIFE FORM IS INSECT",E
2280 DATA"IT IS THE THIRD PLANET FROM THE SUN",E
2290 DATA"WATER VAPOR ACCOUNTS FOR UP TO 4% OF ITS ATMOSPHERIC VOLUME",E
2300 DATA"IT IS THE 4TH PLANET FROM THE SUN, AND{2 SPACE}THE 1ST PLANET BEYOND EARTH",A
2310 DATA"ITS YEAR IS 687 'EARTH' DAYS LONG",A
2320 DATA"ITS DIAMETER IS 4,200 MILES",A
2330 DATA"ITS DAY IS 24 HOURS AND 37 MINUTES LONG IN 'EARTH' TIME",A
2340 DATA"ITS APPROXIMATE DISTANCE FROM THE SUN IS 141 MILLION MILES",A
2350 DATA"ITS CARBON DIOXIDE ATMOSPHERE IS 1-2%{3 SPACE}THE PRESSURE OF EARTH'S",A
2360 DATA"MOST OF ITS SURFACE IS A DESERT OF{6 SPACE}REDDISH SAND AND ROCK",A
2370 DATA"ITS MYSTERIOUS 'CANALS' MAY BE GIANT{4 SPACE}CANYONS",A
2380 DATA"IT HAS TWO MOONS-- DEIMOS AND PHOBOS",A 
2390 DATA"IT HAS A VOLCANIC SURFACE BUT WAS ONCE{2 SPACE}PROBABLY COVERED WITH WATER",A
2400 DATA"ITS 88,000-MILE DIAMETER MAKES IT THE{3 SPACE}LARGEST KNOWN PLANET",J
2410 DATA"ONE OF ITS DAYS IS ONLY 10 'EARTH' HOURS LONG",J
2420 DATA"ITS APPROXIMATE DISTANCE FROM THE SUN IS 480 MILLION MILES",J
2430 DATA"ITS YEAR IS NEARLY 12 'EARTH' YEARS{5 SPACE}LONG",J
2440 DATA"NO KNOWN LIFE FORM COULD SURVIVE ITS{4 SPACE}ATMOSPHERE OF METHANE",J
2450 DATA"IT IS COVERED WITH CLOUDS OF AMMONIA ICE CRYSTALS",J
2460 DATA"IT MAY BE A GASEOUS PLANET WITH NO SOLID CORE",J
2470 DATA"IT HAS AT LEAST 16 MOONS, 4 OF WHICH ARE ALMOST MERCURY'S SIZE",J
2480 DATA"ITS MASS IS DOUBLE THAT OF ALL THE OTHER PLANETS COMBINED",J
2490 DATA"ITS MOST FAMOUS MOONS ARE IO AND{8 SPACE}GANYMEDE",J
2500 DATA"IT IS LOCATED 1.8 BILLION MILES FROM THE SUN",U
2510 DATA"ITS YEAR IS 84 'EARTH' YEARS LONG",U
2520 DATA"ITS DAY IS 15.5 'EARTH' HOURS LONG",U
2530 DATA"ITS DIAMETER IS 32,000 MILES",U
2540 DATA"IT WAS DISCOVERED IN 1781 BY SIR WILLIAM HERSCHEL",U
2550 DATA"ITS NORTH POLE IS TILTED 98 DEGREES OFF A PERPENDICULAR ORBIT",U
2560 DATA"IT HAS AT LEAST FIVE MOONS AND EIGHT{4 SPACE}RINGS ALONG ITS EQUATOR",U
2570 DATA"ITS SURFACE IS PROBABLY ROCKY WITH AN{3 SPACE}ICY CRUST",U
2580 DATA"ITS EXTREME TILT CAUSES 'SUMMER' TO BE{2 SPACE}42 'EARTH' YEARS LONG",U
2590 DATA"IT WAS THE FIRST PLANET DISCOVERED{6 SPACE}AFTER BODE'S THEORY",U
2600 DATA"IT IS 900 MILLION MILES FROM THE SUN--{2 SPACE}TWICE JUPITER'S DISTANCE",S
2610 DATA"ITS EQUATORIAL DIAMETER IS 71,000 MILES",S
2620 DATA"ITS DAY IS 10 'EARTH' HOURS LONG",S
2630 DATA"ITS ATMOSPHERE AND SURFACE RESEMBLE A{3 SPACE}COLDER VERSION OF JUPITER",S
2640 DATA"OF ITS KNOWN 16 MOONS, SIX WERE{9 SPACE}DISCOVERED AFTER 1979",S
2650 DATA"ITS RINGS ARE 170,000 MILES IN DIAMETER",S
2660 DATA"THE THICKNESS OF ITS RINGS IS ONLY 10{3 SPACE}MILES",S
2670 DATA"ITS RINGS ARE COMPOSED OF MATERIAL THAT MAY BE A SHATTERED MOON",S
2680 DATA"ITS YEAR IS 29.5 'EARTH' YEARS LONG",S
2690 DATA"ITS MOON TITAN IS UNIQUE BECAUSE IT HAS AN ATMOSPHERE",S
2700 DATA"IT IS USUALLY CLOSER TO THE SUN THAN{4 SPACE}PLUTO",N
2710 DATA"UNTIL 1989, IT WILL BE FARTHEST FROM THE SUN OF THE KNOWN PLANETS",N
2720 DATA"ITS DISTANCE FROM THE SUN IS ABOUT 2.8{2 SPACE}BILLION MILES",N
2730 DATA"ITS YEAR IS 165 'EARTH' YEARS LONG",N
2740 DATA"IT HAS AT LEAST 3 MOONS, THE LARGEST OF WHICH IS TRITON",N
2750 DATA"LIKE JUPITER, IT MAY PRODUCE HEAT{7 SPACE}INDEPENDENTLY OF THE SUN",N
2760 DATA"ITS INCREASE IN BRIGHTNESS MAY HINT HUGE SURFACE STORMS",N
2770 DATA"ITS DIAMETER IS ABOUT 30,000 MILES",N
2780 DATA"IT HAS A DAY OF 16 'EARTH' HOURS",N
2790 DATA"ASTRONOMERS FOUND IT AFTER LOOKING LESS THAN 30 MINUTES",N
2800 DATA"IT IS USUALLY ABOUT 5,900 BILLION MILES FROM THE SUN",P
2810 DATA"ITS ERRATIC ORBIT MAY BRING IT CLOSER{3 SPACE}TO THE SUN THAN NEPTUNE",P
2820 DATA"IT TAKES 247.7 'EARTH' YEARS TO CIRCLE{2 SPACE}THE SUN",P
2830 DATA"ITS DAY IS 6.4 'EARTH' DAYS LONG",P
2840 DATA"USING MATHEMATICAL COMPUTATION, IT WAS{2 SPACE}DISCOVERED IN 1930",P
2850 DATA"IT HAS AT LEAST ONE MOON, CHARON",P
2860 DATA"ITS DIAMETER IS ABOUT 1,500 MILES",P
2870 DATA"IT WAS DISCOVERED BY AMERICAN CLYDE{5 SPACE}TOMBAUGH",P
2880 DATA"IT MAY HAVE ONCE BEEN A MOON OF NEPTUNE",P
2890 DATA"IT TAKES SUNLIGHT 5.5 HOURS TO REACH{4 SPACE}THIS PLANET",P
2900 DATA"CLYDE TOMBAUGH WAS LOOKING FOR THIS{5 SPACE}PLANET WHEN HE FOUND PLUTO",X
2910 DATA"ITS MASS IS ESTIMATED AT 286 TIMES THAT OF EARTH",X
2920 DATA"ITS YEAR IS ABOUT 464 'EARTH' YEARS",X
2930 DATA"BODE'S LAW ESTIMATES IT IS 7,172 MILLION MILES FROM THE SUN",X
2940 DATA"IT HAS A FAMILY OF 16 COMETS",X
2950 DATA"ITS ORBIT IS ESTIMATED TO BE 120 DEGREES OFF PERPENDICULAR",X
2960 DATA"ITS MASS IS THREE TIMES THAT OF SATURN",X
2970 DATA"IT LIES IN THE SAME GENERAL DIRECTION AS CASSIOPEIA",X
2980 DATA"IT HAS NEVER BEEN SEEN WITH THE NAKED{3 SPACE}EYE, OR WITH A TELESCOPE",X
2990 DATA"IT MADE NEPTUNE AND URANUS DEVIATE FROM THEIR CALCULATED ORBITS",X
8000 POKEVC+0,160:POKEVC+1,0:Z=9:POKEVC+16,0
8010 POKEVC+23,0:POKEVC+27,0:POKEVC+28,0
8020 POKEVC+29,1:POKEVC+39,7:POKE2040,13
8030 POKEMC+5,79:POKEMC+6,129:POKEMC+0,0:POKEMC+1,1
8040 POKEMC+24,15:POKEVC+21,1
8050 FORX=0TO26:Z=Z+9:POKEVC+1,Z
8060 Y=INT(NT%(X,0)/256):POKEMC+1,Y
8070 POKEMC+0,NT%(X,0)-(Y*256):POKEMC+4,17
8080 FORY=1TONT%(X,1)*70:NEXTY:POKEMC+4,16
8090 FORY=1TO20:NEXTY,X
8100 POKEVC+21,0:POKEMC+24,0
8110 RETURN
9000 DATA"$SPECIAL"
9010 DATA1204,4,0,.5,1607,4,2025,4,1607,4,1351,2,1607,2,1607,1,1351,1
9020 DATA1607,1,1351,1,1607,2,1607,2,1351,1,1204,2,0,2,1204,4,1607,4,2025,4
9030 DATA1607,4,2408,2,2025,2,2025,1,1607,1,1804,1,1570,1,1607,8
9100 DATA128,248,8,67,254,16,39,255,32,15,255,128,31,255,192,63,255,224,63,255
9110 DATA224,120,112,240,124,249,240,255,223,248,255,223,248,255,143,248
9120 DATA251,254,248,121,252,240,126,115,240,63,143,224,31,255,192,15
9130 DATA255,128,39,255,32,67,254,16,128,248,8,0
