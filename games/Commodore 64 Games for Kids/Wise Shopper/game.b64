1 REM WISE SHOPPER
100 DIMP$(19,1),NT%(17,1),PT%(19)
104 VC=53248:POKEVC+32,6:POKEVC+33,15:PRINTCHR$(147):C$="{CYN}{PUR}{GRN}{BLU}{YEL}"
106 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
108 B1$="{RVS}{40 SPACE}{OFF}"
110 B2$="{RVS}{15 SPACE}{OFF}{10 SPACE}{RVS}{15 SPACE}{OFF}"
120 PRINT"{BLK}{DOWN} {RVS}{6 SPACE}$$$$${2 SPACE}WISE SHOPPER{2 SPACE}$$$$${6 SPACE}{OFF}"
125 PRINT"{3 DOWN} YOU ARE GOING TO THE STORE TO BUY TEN{3 SPACE}ITEMS."
130 PRINT"{DOWN} AS YOU BUY EACH ITEM YOU MUST CHOOSE"
135 PRINT" BETWEEN BRAND {RVS}A{OFF} AND BRAND {RVS}B{OFF}."
140 PRINT"{DOWN} YOU MUST SELECT THE BRAND WHICH IS THE{2 SPACE}BEST BARGAIN."
150 PRINT"{DOWN} DETERMINE THIS BY CALCULATING THE UNIT{2 SPACE}COST PER OUNCE."
160 FORX=0TO17:FORY=0TO1:READZ:NT%(X,Y)=Z:NEXTY,X
170 GOSUB1000
180 PRINT"{3 DOWN}{8 SPACE}(PRESS ANY KEY TO START)"
190 GETX$:IFX$=""THEN190
195 FORX=0TO19:READX$,Y$:P$(X,0)=X$:P$(X,1)=Y$:NEXTX
200 EC=0:RC=0:XS=0
210 FORX=0TO19:PT%(X)=0:NEXTX
300 QA=INT(RND(1)*7)+3
310 QB=INT(RND(1)*7)+3:IFQA=QBTHEN310
320 PA=INT(RND(1)*11)+30:X=INT(RND(1)*5)+1:IFRND(1)>.5THENX=X*-1
330 PB=PA+X
340 X=INT(RND(1)*20):IFPT%(X)=1THEN340
350 X$=P$(X,0):Y$=P$(X,1):PT%(X)=1
360 X=INT(RND(1)*5)+1:Z$=MID$(C$,X,1)
400 PRINT"{CLR}";Z$;:FORX=1TO4:PRINTB1$;:NEXTX
410 FORX=1TO10:PRINTB2$;:NEXTX:FORX=1TO3:PRINTB1$;:NEXTX
420 PRINT"{BLK}{HOME}{7 DOWN}{17 RIGHT}";X$:PRINT"{DOWN}{17 RIGHT}";Y$
430 X=QA*PA:GOSUB8000
440 PRINT"{HOME}{19 DOWN} {RVS}A{OFF} -";QA;"OUNCES FOR ";X$
450 X=QB*PB:GOSUB8000
460 PRINT"{DOWN} {RVS}B{OFF} -";QB;"OUNCES FOR ";X$
470 PRINT"{2 DOWN}{5 SPACE}ENTER {RVS}A{OFF} OR {RVS}B{OFF}";
480 PRINT"{HOME}{16 RIGHT}{RVS}ITEM #";EC+RC+1;"{OFF}"
500 GETX$:IFX$=""THEN500
510 IFX$<>"A"ANDX$<>"B"THEN500
520 IFX$="B"ANDPB>PATHEN600
530 IFX$="A"ANDPA>PBTHEN600
540 GOTO700
600 REM *** MADE WRONG CHOICE ***
610 POKEVC+33,0:POKEMC+0,0:POKEMC+1,30:POKEMC+4,33:POKEMC+5,15:POKEMC+6,15
620 POKEMC+6,15:POKEMC+24,9
630 FORY=1TO300:NEXTY:POKEMC+4,32
640 FORY=1TO30:NEXTY
650 POKEMC+24,0:PRINTCHR$(147):POKEVC+33,15
660 EC=EC+1
670 GOTO790
700 REM *** MADE RIGHT CHOICE ***
710 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,9
720 FORY=5TO15
730 POKEVC+33,Y:POKEMC+1,Y*2:POKEMC+4,33
740 FORZ=1TO50:NEXTZ:POKEMC+4,32
750 FORZ=1TO20:NEXTZ,Y:POKEMC+24,0
760 RC=RC+1
770 IFX$="A"THENXS=XS+((PB-PA)*QA):GOTO790
780 XS=XS+((PA-PB)*QB)
790 IFRC+EC<10THEN300
800 PRINT"{CLR}{DOWN}{13 SPACE}{RVS}SHOPPING OVER!{OFF}"
810 PRINT"{2 DOWN}{7 SPACE}RIGHT ITEMS =";RC
820 PRINT"{DOWN}{7 SPACE}WRONG ITEMS =";EC
830 X=RC*10
840 PRINT"{DOWN}{7 SPACE}CORRECT %{3 SPACE}=";X;"{LEFT}%"
850 IFRC=0THEN900
860 X=XS:GOSUB8000
870 PRINT"{2 DOWN}{3 SPACE}TODAY YOU SAVED ";X$
880 PRINT"{DOWN}{3 SPACE}BY WISE SHOPPING."
900 PRINT"{2 DOWN}{3 SPACE}PLAY AGAIN? ({RVS}Y{OFF}/{RVS}N{OFF})"
910 GETX$:IFX$=""THEN910
920 IFX$="Y"THEN200
930 IFX$<>"N"THEN910
935 GOSUB1000
940 PRINTCHR$(147)
950 END
1000 REM *** PLAY THE SONG ***
1010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
1020 FORX=0TO17
1030 Y=INT(NT%(X,0)/256)
1040 POKEMC+0,NT%(X,0)-(Y*256)
1050 POKEMC+1,Y:POKEMC+4,17
1060 FORY=1TONT%(X,1)*50:NEXTY
1070 POKEMC+4,16:FORY=1TO30:NEXTY,X
1080 POKEMC+24,0
1090 RETURN
8000 Y$=STR$(X):X=LEN(Y$):ONXGOTO8010,8010,8020,8030
8010 X$="$0.0"+RIGHT$(Y$,1):RETURN
8020 X$="$0."+RIGHT$(Y$,2):RETURN
8030 X$="$"+MID$(Y$,2,1)+"."+RIGHT$(Y$,2):RETURN
9000 DATA8583,2,8583,2,7647,1,7217,1,7217,2,6812,1,7217,1,7217,4
9010 DATA6812,1,7217,1,7217,2,6812,1,7217,1,8583,2,7217,1,8583,1
9020 DATA7647,4,6430,2
9100 DATACORN,FLAKES,DISH,SOAP,CAKE,MIX,GINGER,SNAPS,BREAD,CRUMBS
9110 DATARICE," ",BROWN,SUGAR,SUGAR,CUBES,OAT,MEAL,CORN,MEAL
9120 DATACORN,CHIPS,POTATO,FLAKES,FUDGE,MIX,PIZZA,MIX,FRUIT,BARS
9130 DATAPASTA," ",BAKING,SODA,BAKING,POWDER,WAFFLE,MIX,RED,BEANS
