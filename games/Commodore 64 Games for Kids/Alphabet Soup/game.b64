1 REM ALPHABET SOUP
100 VC=53248:MC=54272:POKEVC+32,3:POKEVC+33,1
102 PRINTCHR$(147):CB=828
104 LP=((PEEK(53272)AND240)/16)*1024:LP=LP+44
106 CO=55296-LP+44:CP=LP+840
108 FORX=54272TO54296:POKEX,0:NEXTX
120 DIML%(25,2),W$(7):X=RND(-TI)
130 PRINT"{BLK}{11 SPACE}{RVS}* ALPHABET{2 SPACE}SOUP *{OFF}"
140 PRINT"{2 DOWN}16 RANDOM LETTERS AND THEIR POINT VALUESWILL BE PRINTED."
150 PRINT"{2 DOWN}BUILD WORDS OF 2 OR MORE LETTERS EACH."
160 PRINT"{2 DOWN}END A ROUND EARLY BY USING ALL 16"
165 PRINT"LETTERS OR BY PRESSING {RVS}F1{OFF}."
170 PRINT"{2 DOWN}CANCEL A WORD BY USING '@' FOLLOWED BY{2 SPACE}THE WORD."
180 PRINT"{2 DOWN}YOUR SCORE IS BASED ON WORD LENGTH AND{2 SPACE}LETTER VALUES."
185 GOSUB7000
190 PRINT"{2 DOWN}{8 SPACE}(PRESS ANY KEY TO START)"
200 FORW=0TO25:READX,Y,Z:L%(W,0)=X:L%(W,1)=Y:L%(W,2)=Z:NEXTW
210 GETX$:IFX$=""THEN210
220 PRINTCHR$(147);"{3 DOWN}HOW MANY MINUTES SHOULD EACH GAME LAST??";
225 PRINT"{3 DOWN}{7 SPACE}ENTER A NUMBER FROM {RVS}1{OFF} TO {RVS}4{OFF}"
230 GETRL$:IFRL$=""THEN230
240 IFRL$<"1"ORRL$>"4"THEN230
250 TL$="000"+RL$+"00"
300 POKEVC+32,10:WC=0:LC=0:PRINTCHR$(147)
305 FORW=0TO25
310 FORX=1TOL%(W,1):POKECB+LC,L%(W,0):LC=LC+1:NEXTX,W
320 FORX=0TO7:W$(X)=".":NEXTX
330 FORX=0TO30STEP2
340 Y=INT(RND(1)*LC):Z=PEEK(CB+Y):IFZ=0THEN340
350 POKECB+Y,0:POKELP+X,Z:POKELP+X+CO,0:W=L%(Z-1,2)+48
360 POKELP+X+40,W:POKELP+X+40+CO,6:NEXTX
370 POKECP-1,62:POKECP-1+CO,4:TI$="000000"
400 WB$="":LE=0:D%=0
410 IFTI$>TL$THEN800
415 J$=MID$(TI$,3,2)+"."+MID$(TI$,5,2)
416 PRINT"{HOME}{23 DOWN}";SPC(17);J$;
420 GETX$:IFX$=""THEN410
430 X=ASC(X$):IFX=13THEN600
435 IFX=133THEN800
437 IFX=20THEN1700
440 IFX$="@"ANDLE=0ANDD%=0ANDWC>0THEND%=1:GOTO500
450 IFX<65ORX>90THENGOSUB1800:GOTO410
455 IFD%=1THEN490
460 W=0
465 IFPEEK(LP+W)=(X-64)THEN480
470 W=W+2:IFW<32THEN465
475 GOSUB1800:GOTO410
480 POKELP+W,32:POKELP+W+CO,1:POKELP+W+40,32:POKELP+W+40+CO,1
490 WB$=WB$+X$
500 POKECP+LE,X-64:POKECP+LE+CO,9:LE=LE+1:IFLE<17THEN410
600 X=LEN(WB$):IFX<2THENGOSUB1800:GOTO410
610 IFD%<>1THEN700
620 W=0
625 IFWB$=W$(W)THEN640
630 W=W+1:IFW<8THEN625
635 GOSUB1800:GOTO750
640 W$(W)=".":X$="":FORY=1TO(W*2)+6:X$=X$+"{DOWN}":NEXTY
645 PRINT"{HOME}";X$;"{38 SPACE}"
650 WC=WC-1:FORY=1TOX:Z=0
660 IFPEEK(LP+Z)=32THEN680
670 Z=Z+2:GOTO660
680 X$=MID$(WB$,Y,1):W=ASC(X$)-64:POKELP+Z,W:POKELP+Z+CO,0
690 W=L%(W-1,2):POKELP+Z+40,W+48:POKELP+Z+40+CO,6:NEXTY:GOTO750
700 X=0
705 IFW$(X)="."THEN720
710 X=X+1:IFX<8THEN705
720 W$(X)=WB$:X$="":FORY=1TO(X*2)+6:X$=X$+"{DOWN}":NEXTY:PRINT"{HOME}";X$;"{4 SPACE}";WB$;
730 WC=WC+1
750 FORX=0TO18:POKECP+X,32:POKECP+X+CO,1:NEXTX
760 X=0
765 W=PEEK(LP+X):IFW<>32THEN400
770 X=X+2:IFX<32THEN765
800 POKEVC+32,3:W=0:PRINTCHR$(147);"{2 SPACE}WORD{12 SPACE}SCORE"
805 PRINT"{2 SPACE}---------------------"
810 X=0
815 IFW$(X)="."THEN900
820 WB$=W$(X):Y=LEN(WB$):Z=Y:IFZ>8THENZ=8
830 S=Z-2:FORZ=1TOY:X$=MID$(WB$,Z,1):A=ASC(X$)-65:S=S+L%(A,2):NEXTZ
840 WB$=WB$+" "
850 IFLEN(WB$)<17THENWB$=WB$+"-":GOTO850
860 X$=STR$(S)
870 IFLEN(X$)<4THENX$=" "+X$:GOTO870
880 PRINT"{DOWN}{2 SPACE}";WB$;X$
890 W=W+S:X=X+1:IFX<8THEN815
900 PRINT"{DOWN}TOTAL SCORE ={7 SPACE}";W
930 PRINT"{DOWN}REPLAY OPTION?{3 SPACE}{RVS}Y{OFF} - YES"
932 PRINT"{17 SPACE}{RVS}N{OFF} - NO"
934 PRINT"{17 SPACE}{RVS}R{OFF} - NEW TIME LIMIT"
940 GETX$:IFX$=""THEN940
950 IFX$="Y"THEN300
955 IFX$="R"THEN220
960 IFX$<>"N"THEN940
970 PRINTCHR$(147)
980 END
1700 IFLE<1THENGOSUB1800:GOTO410
1710 LE=LE-1
1720 X=PEEK(CP+LE):POKECP+LE,32:POKECP+LE+CO,1
1725 IFD%=1THEN1760
1730 W=0
1735 IFPEEK(LP+W)=32THEN1750
1740 W=W+2:IFW<32THEN1735
1750 POKELP+W,X:POKELP+W+CO,0:POKELP+W+40,L%(X-1,2)+48:POKELP+W+40+CO,6
1760 IFLE=0THENWB$="" :D%=0:GOTO410
1770 X$=LEFT$(WB$,LE):WB$=X$:GOTO410
1800 POKEMC+1,14:POKEMC+0,0:POKEMC+5,15:POKEMC+6,15
1810 POKEMC+24,15:POKEMC+4,17
1820 FORW=0TO200:NEXTW:POKEMC+4,16
1830 FORW=1TO20:NEXTW:POKEMC+24,0
1840 RETURN
7000 POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
7010 READX,Y:IFX<0THEN7050
7020 Z=INT(X/256):X=X-(Z*256)
7030 POKEMC+0,X:POKEMC+1,Z:POKEMC+4,33
7040 FORZ=1TO(Y*75):NEXTZ:POKEMC+4,32:FORZ=1TO20:NEXTZ:GOTO7010
7050 POKEMC+24,0
7060 RETURN
7100 DATA4291,2,4291,1,4050,1,3608,2,3608,1,3608,1 
7110 DATA3215,1,2864,1,3215,1,3608,1,2864,4,-1,0
8000 DATA1,9,1,2,2,3,3,2,3,4,4,2,5,12,1,6,2,4,7,3,2,8,2,4,9,9,1
8010 DATA10,1,8,11,1,5,12,4,1,13,2,3,14,6,1,15,8,1,16,2,3,17,1,9,18,6,1
8020 DATA19,4,1,20,6,1,21,4,1,22,2,4,23,2,4,24,1,8,25,2,4,26,1,9
