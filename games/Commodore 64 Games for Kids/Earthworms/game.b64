1 REM EARTHWORMS
100 MC=54272:VC=53248:POKEVC+32,6:POKEVC+33,15
110 PRINTCHR$(147):M=((PEEK(53272)AND240)/16)*1024:C=55296
150 PRINT"{BLK}{DOWN}{RVS}{14 SPACE}EARTHWORMS{16 SPACE}{OFF}{DOWN}";
155 GOSUB2300
160 PRINT"{DOWN}AS YOU CRAWL THROUGH THE DIRT YOU MUST{2 SPACE}EAT ALGAE ({GRN}q{BLK}) AND AVOID"
170 PRINT"SHARP PEBBLES ({BLU}z{BLK})."
175 PRINT"{DOWN}HITTING PEBBLES OR THE BORDER OF THE"
180 PRINT"SCREEN OR CROSSING A PREVIOUS TUNNEL{4 SPACE}WILL END THE GAME."
185 GOSUB2400
190 PRINT"{DOWN}CONTROL DIRECTION WITH THE JOYSTICK."
200 PRINT"{DOWN}TEN POINTS FOR EACH ALGAE BALL YOU EAT."
210 GOSUB2500:GOSUB2000
220 PRINTCHR$(147);"{DOWN}{2 SPACE}ENTER SKILL LEVEL:":PRINT"{3 DOWN}{3 SPACE}{RVS}1{OFF} BEGINNER"
225 PRINT"{2 DOWN}{5 SPACE} -TO-":PRINT"{2 DOWN}{3 SPACE}{RVS}5{OFF} ADVANCED"
230 GETX$:IFX$=""THEN230
240 SC=0:SL=VAL(X$):IFSL<1ORSL>5THEN230
245 LT=(11-(SL*2)):IFLT>7THENLT=7
250 PRINTCHR$(147):FORY=1TO(SL*6):GOSUB2100:POKEM+X,90:POKEC+X,6:NEXTY
260 AC=SL*20:FORY=1TOAC:GOSUB2100:POKEM+X,81:POKEC+X,5:NEXTY
270 H1=20:V1=12:POKEM+500,160:POKEC+500,2
275 {20 SPACE}
280 H2=0:V2=0:X=1:IFRND(1)>.5THENX=-1
290 LC=0:H2=X:IFRND(1)>.5THENH2=0:V2=X
300 H3=0:V3=0:GOSUB2200:IFV3<>0ORH3<>0THENH2=H3:V2=V3:GOTO360
340 LC=LC+1:IFLC<LTTHEN300
350 LC=0
360 H1=H1+H2:V1=V1+V2:IFH1<0ORH1>39ORV1<0ORV1>24THEN700
370 X=PEEK(M+H1+(V1*40))
380 IFX=32ORX=81THENPOKEM+H1+(V1*40),160:POKEC+H1+(V1*40),2:GOTO410
390 IFX=160ORX=90THEN700
400 GOTO300
410 IFX=32THEN300
420 REM ***** EAT ALGAE
430 POKEMC+0,0:POKEMC+1,0:POKEMC+24,15
440 FORX=50TO10STEP-10
445 POKEMC+1,X:POKEMC+4,129
450 FORY=1TO10:NEXTY:POKEMC+4,128:NEXTX
460 POKEMC+24,0
470 SC=SC+10:AC=AC-1
480 IFAC<1THEN250
490 GOTO300
700 REM ***** GAME OVER
701 POKEMC+0,0:POKEMC+1,20:POKEMC+4,17:POKEMC+24,15
702 FORY=1TO500:NEXTY:POKEMC+4,16:POKEMC+24,0
710 PRINTCHR$(147);"{3 DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}"
720 PRINT"{3 DOWN}{4 SPACE}SCORE =";SC
730 PRINT"{5 DOWN}{4 SPACE}REPLAY? ({RVS}Y{OFF} OR {RVS}N{OFF})"
740 GETX$:IFX$=""THEN740
750 IFX$="Y"THEN220
760 IFX$<>"N"THEN740
770 GOSUB2400:GOSUB2500
780 PRINTCHR$(147)
790 END
2000 PRINT"{HOME}{23 DOWN}{8 SPACE}(PRESS ANY KEY TO START)"
2010 GETX$:IFX$=""THEN2010
2020 RETURN
2100 X=INT(RND(1)*1000):IFPEEK(M+X)<>32THEN2100
2110 RETURN
2200 REM ***** READ JOYSTICK
2210 X=PEEK(56321)
2220 IF(XAND1)=0THENV3=-1
2230 IF(XAND2)=0THENV3=1
2240 IF(XAND4)=0THENH3=-1
2250 IF(XAND8)=0THENH3=1
2260 RETURN
2300 REM ***** LOAD SPRITE DEFINITIONS
2310 FORX=832TO832+63:POKEX,0:NEXTX
2320 FORX=896TO896+63:READZ:POKEX,Z:NEXTX
2330 FORX=832+48TO832+56:READZ:POKEX,Z:NEXTX:RETURN
2400 REM ***** SETUP FOR SPRITE & NOTES
2410 FORX=54272TO54296:POKEX,0:NEXTX
2420 POKEMC+1,10:POKEMC+4,17:POKEMC+5,15
2430 POKEMC+6,15
2440 POKEVC+0,0:POKEVC+1,200:POKEVC+16,0
2450 POKEVC+23,1:POKEVC+28,0:POKEVC+29,0
2460 POKEVC+39,2:POKE2040,13
2470 RETURN
2500 REM ***** DRAW SPRITE
2510 POKEMC+24,15:POKEVC+21,1:X=5:Z=25
2520 POKE2040,13:FORY=1TO50:NEXTY:POKEMC+4,16
2525 POKEVC+0,Z:Z=Z+10
2530 POKE2040,14
2540 POKEMC+1,X:X=X+3:POKEMC+4,17
2550 IFZ=265THENPOKEVC+0,0:POKEVC+16,1:Z=0
2560 IFZ<>100THEN2520
2570 POKEMC+24,0:POKEVC+21,0
2580 RETURN
3000 DATA0,62,0,0,99,0,0,99,0,0,65,0,0,193,128,0,193,128,0,128,128,1,128,192
3010 DATA1,128,192,1,0,64,3,0,96,3,0,96,2,0,32,6,0,48,6,0,48,4,0,16,4,0
3020 DATA16,4,0,16,252,0,31,0,0,0,0,0,0,0
3030 DATA0,60,0,0,102,0,255,195,255
