1 REM MATCHMAKER
100 DIMHT(1,3),ST(9),CT(3),S%(7),NT%(20):CB=828:VC=53248:MC=54272
110 SM=((PEEK(53272)AND240)/16)*1024:CO=55296-SM
120 FORX=54272TO54296:POKEX,0:NEXTX
130 FORX=0TO3:READZ:CT(X)=Z:NEXTX:FORX=0TO9:READZ:ST(X)=Z:NEXTX
140 FORX=0TO20:READZ:NT%(X)=Z:NEXTX:GOSUB8000
200 PRINT"{CLR}{2 DOWN} ENTER SKILL LEVEL:":PRINT"{2 DOWN}{3 SPACE}{RVS}1{OFF} - BEGINNING{DOWN}":PRINT"{7 SPACE}TO"
210 PRINT"{DOWN}{3 SPACE}{RVS}4{OFF} - ADVANCED"
220 GETX$:IFX$=""THEN220
230 IFX$<"1"ORX$>"4"THEN220
240 SL=VAL(X$)
250 PRINT"{3 DOWN} ENTER NUMBER OF PLAYERS (1 TO 8)"
252 GETX$:IFX$=""THEN252
253 IFX$<"1"ORX$>"8"THEN252
254 NP=VAL(X$)
255 PRINT"{5 DOWN}{10 SPACE}(SCREEN BEING BUILT)"
260 FORX=1TONP:S%(X-1)=0:NEXTX:FORX=1TO(SL*20):POKECB+X-1,0:NEXTX
270 FORX=1TOSL:FORY=0TO9:FORZ=1TO2
280 GOSUB1000:POKECB+R1,ST(Y):POKECB+R1+(SL*20),CT(X-1):NEXTZ,Y,X
300 PRINT"{CLR}{4 DOWN}{BLK}":FORX=1TOSL*2
310 PRINT"{5 SPACE}";:FORY=0TO9:PRINT"{PUR}{RVS} {OFF}{2 SPACE}";:NEXTY
320 PRINT"{DOWN}"
330 NEXTX:M%=0:AP=245:POKESM+AP,30:POKESM+CO+AP,0
400 W=1
410 FORX=1TO2:PRINT"{HOME}{DOWN} PLAYER #{3 SPACE}{3 LEFT}";W:PRINT"{DOWN} SQUARE #";X"
420 GOSUB2000:Y=(R*10)+C:Z=PEEK(CB+Y)
430 HT(X-1,0)=Z:HT(X-1,1)=PEEK(CB+Y+(SL*20)):HT(X-1,2)=Y
440 HT(X-1,3)=AP-40
450 POKESM+HT(X-1,3),HT(X-1,0):POKESM+CO+HT(X-1,3),HT(X-1,1)
455 IF(PEEK(56321)AND16)=0THEN455
456 POKESM+HT(X-1,3),160:POKESM+CO+HT(X-1,3),4
457 NEXTX
470 IFHT(0,0)=HT(1,0)ANDHT(0,1)=HT(1,1)ANDHT(0,3)<>HT(1,3)THEN475
473 GOTO600
475 FORX=0TO1
480 POKESM+HT(X,3),32:POKESM+CO+HT(X,3),1:NEXTX
485 POKECB+HT(0,2),0:POKECB+HT(1,2),0
490 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,8
492 FORY=40TO20STEP-5
494 POKEMC+1,Y:POKEMC+4,17:FORZ=1TO30:NEXTZ
496 POKEMC+4,16:NEXTY:POKEMC+24,0
500 S%(W-1)=(S%(W-1))+1
510 M%=M%+1:IFM%=(SL*10)THEN700
600 W=W+1
610 IFW<=NPTHEN410
620 GOTO400
700 PRINT"{CLR}{BLK}{DOWN}{16 SPACE}{RVS}SCORES{OFF}{3 DOWN}"
710 FORX=1TONP:PRINT"{2 SPACE}PLAYER #";X;"=";S%(X-1);:PRINT"{DOWN}":NEXTX
720 GOSUB4000
740 PRINTCHR$(147);"{3 DOWN}{2 SPACE}PLAY AGAIN ?"
742 PRINT"{2 DOWN}{5 SPACE}{RVS}Y{OFF} = YES"
744 PRINT"{2 DOWN}{5 SPACE}{RVS}N{OFF} = NO"
750 GETX$:IFX$=""THEN750
760 IFX$="Y"THEN200
770 IFX$<>"N"THEN750
780 PRINTCHR$(147)
790 END
800 REM *** PLAY SONG
810 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
820 FORX=0TO20
830 Y=INT(NT%(X)/256)
840 POKEMC+0,NT%(X)-(Y*256)
850 POKEMC+1,Y:POKEMC+4,17
860 FORY=1TO150:NEXTY:POKEMC+4,16
870 FORY=1TO10:NEXTY,X:POKEMC+24,0
880 RETURN
1000 W=0
1010 R1=INT(RND(1)*(SL*20)):IFPEEK(CB+R1)=0THENRETURN
1020 W=W+1:IFW<5THEN1010
1030 R1=0
1040 IFPEEK(CB+R1)=0THENRETURN
1050 R1=R1+1:GOTO1040
2000 REM *** READ JOYSTICK
2005 JQ=PEEK(56321):IF(JQAND16)=0THEN2200
2010 Z=AP:IF(JQAND1)=0THENZ=Z-40
2020 IF(JQAND2)=0THENZ=Z+40
2030 IF(JQAND4)=0THENZ=Z-1
2040 IF(JQAND8)=0THENZ=Z+1
2050 IFAP=ZORZ<240ORZ>839THEN2000
2060 IFPEEK(SM+Z)=160THEN2000
2070 POKESM+AP,32:POKESM+CO+AP,1:POKESM+Z,30:POKESM+CO+Z,0
2080 AP=Z:GOTO2000
2200 IFPEEK(SM+AP-40)<>160THEN2000
2210 Z=AP-245:R=INT(Z/80):C=Z-(R*80)
2220 C=INT(C/3)
2230 RETURN
4000 PRINT"{2 DOWN}{12 SPACE}(PRESS ANY KEY)"
4010 GOSUB800
4015 POKE198,0
4020 GETX$:IFX$=""THEN4020
4030 RETURN
8000 POKEVC+32,6:POKEVC+33,1:PRINTCHR$(147);
8005 PRINT"{BLK}{DOWN} {RVS}{11 SPACE}** MATCHMAKER **{11 SPACE}{OFF}"
8100 PRINT"{2 DOWN}MATCHMAKER IS A MEMORY GAME FOR 1 TO 8"
8105 PRINT"PLAYERS, WITH FOUR SKILL LEVELS." 
8110 PRINT"{2 DOWN}EACH PLAYER MUST UNCOVER TWO SQUARES AND";
8115 PRINT"TRY TO MATCH THE SHAPES UNDER THEM."
8120 PRINT"SKILL LEVELS 2-4 MUST ALSO MATCH THE{4 SPACE}OBJECTS' COLOR."
8140 PRINT"{2 DOWN}MOVE UNDER A SQUARE WITH THE JOYSTICK,"
8145 PRINT"AND EXPOSE IT WITH THE FIRE BUTTON."
8150 GOSUB4000
8160 RETURN
9000 DATA0,8,5,14,65,42,81,83,86,87,78,90,95,105
9010 DATA6430,8101,8101,8583,6430,7217,8101,7217,6430,5728,4291
9020 DATA4291,5407,5728,6430,7217,6430,5728,6430,4291,4291
