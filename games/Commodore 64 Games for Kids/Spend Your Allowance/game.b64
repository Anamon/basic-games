1 REM SPEND..ALLOWANCE
100 MC=54272:VC=53248:POKEVC+32,6:POKEVC+33,12:PRINTCHR$(147)
110 DIMST%(12,1),MT$(11),BT$(99,2),CT$(29,2),RT(2)
120 FORX=54272TO54296:POKEX,0:NEXTX
150 PRINT"{CLR}{WHT}{DOWN}{RVS}{9 SPACE}SPEND YOUR ALLOWANCE{9 SPACE}{OFF}"
160 PRINT"{2 DOWN} YOUR PARENTS GIVE YOU $10.00 ALLOWANCE{2 SPACE}PER MONTH."
170 PRINT"{2 DOWN} YOUR PARENTS MAY RAISE OR LOWER YOUR"
175 PRINT" ALLOWANCE, BASED ON HOW WISELY YOU{6 SPACE}SPEND IT."
180 PRINT"{2 DOWN} UNEXPECTED THINGS CAN SOMETIMES HAPPEN"
185 PRINT" WHICH WILL INCREASE OR DECREASE THE"
190 PRINT" MONEY YOU HAVE."
200 PRINT"{2 DOWN} A GAME CONSISTS OF 12 MONTHS, WITH"
205 PRINT" FOUR SPENDING DECISIONS PER MONTH."
210 FORX=0TO12:FORY=0TO1:READZ:ST%(X,Y)=Z:NEXTY,X
220 GOSUB1100:FORX=0TO11:READX$:MT$(X)=X$:NEXTX:GOSUB1000
300 CB=0:AA=1000:IQ=0
310 RESTORE
320 READX$:IFX$<>"$"THEN320
330 FORX=0TO99:FORY=0TO2:READX$:BT$(X,Y)=X$:NEXTY,X
340 FORX=0TO29:FORY=0TO2:READX$:CT$(X,Y)=X$:NEXTY,X
400 FORL1=0TO11:GOSUB900:IQ=0
410 FORL2=0TO3
420 IFL2=0THEN500
430 IFRND(1)>.10THEN500
440 X=INT(RND(1)*30):X$=CT$(X,0):IFX$="."THEN440
450 PRINT"{CLR}":CT$(X,0)="."
452 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
454 FORY=2TO12:POKEVC+33,Y:POKEMC+1,(3*Y):POKEMC+4,17
460 FORZ=1TO30:NEXTZ:POKEMC+4,16:NEXTY:POKEMC+24,0
470 Y=VAL(CT$(X,1)):Z=VAL(CT$(X,2)):PRINT"{HOME}{3 DOWN}";X$;".":X$=STR$(Y)
480 GOSUB1200:IFZ<0THENPRINT"{3 DOWN} YOU SPEND ";X$
485 IFZ>0THENPRINT"{3 DOWN} YOU MAKE ";X$
490 CB=CB+(Y*Z):GOSUB1000
500 FORX=0TO2
510 Y=INT(RND(1)*100):IFBT$(Y,0)="."THEN510
520 RT(X)=Y:NEXTX
530 IFRT(0)=RT(1)ORRT(0)=RT(2)ORRT(1)=RT(2)THEN500
540 PRINT"{CLR}{DOWN} ";MT$(L1):PRINT"{DOWN} DECISION #";L2+1
545 PRINT"{DOWN} ON WHICH ITEM LISTED BELOW WOULD YOU"
550 PRINT" SPEND YOUR ALLOWANCE?{DOWN}"
560 FORZ=1TO3:X$=STR$(Z):Y$=RIGHT$(X$,1):PRINT"{DOWN}{RVS}";Y$;"{OFF} ";BT$(RT(Z-1),0)
570 X$=BT$(RT(Z-1),1):GOSUB1200:PRINT"{3 SPACE}COST = ";X$;"{DOWN}":NEXTZ
580 PRINT"{2 DOWN}{3 SPACE}ENTER {RVS}1{OFF} {RVS}2{OFF} OR {RVS}3{OFF}";
590 GETX$:IFX$=""THEN590
600 X=VAL(X$):IFX<1ORX>3THEN590
610 Y=VAL(BT$(RT(X-1),1)):CB=CB-Y:Y=VAL(BT$(RT(X-1),2)):IQ=IQ+Y
620 BT$(RT(X-1),0)="."
630 NEXTL2
700 NEXTL1
800 PRINT"{CLR}{DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}":X$=STR$(CB):GOSUB1200
810 PRINT"{2 DOWN} TOTAL CASH ON HAND IS ";X$:X$=STR$(AA):GOSUB1200
820 PRINT"{DOWN} CURRENT MONTHLY ALLOWANCE IS {SSPACE}";X$:GOSUB1100
830 PRINT"{3 DOWN} ENTER REPLAY OPTION:{DOWN}"
840 PRINT"{DOWN} {RVS}1{OFF} START GAME OVER":PRINT"{DOWN} {RVS}2{OFF} CONTINUE GAME"
841 PRINT"{DOWN} {RVS}3{OFF} END GAME"
850 GETX$:IFX$=""THEN850
860 X=VAL(X$):ONXGOTO300,310,880
870 GOTO850
880 PRINTCHR$(147)
890 END
900 PRINT"{CLR}{2 DOWN}{4 SPACE}START OF ";MT$(L1)
905 X=IQ/4:IFCB<0ORX<4THEN2000
910 AA=AA+100
920 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15:FORX=2TO11
925 POKEMC+1,(X*3):POKEMC+4,17
930 FORY=1TO30:NEXTY:POKEMC+4,16:NEXTX
935 POKEMC+24,0
940 PRINT"{2 DOWN} BECAUSE OF YOUR WISE SPENDING HABITS,"
945 PRINT" YOUR PARENTS HAVED RAISED YOUR"
950 PRINT" ALLOWANCE BY $1 PER MONTH."
960 X$=STR$(CB):GOSUB1200:PRINT"{2 DOWN} OLD BALANCE =";X$:X$=STR$(AA):GOSUB1200
965 PRINT"{DOWN} ALLOWANCE{3 SPACE}=";X$:CB=CB+AA
970 X$=STR$(CB):GOSUB1200:PRINT "{DOWN} NEW BALANCE =";X$:GOSUB1000
980 RETURN
1000 PRINT"{HOME}{24 DOWN}{12 SPACE}(PRESS ANY KEY)";
1010 GETX$:IFX$=""THEN1010
1020 RETURN
1100 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129:POKEMC+24,15
1110 FORX=0TO12
1120 Y=INT(ST%(X,0)/256)
1130 POKEMC+0,ST%(X,0)-(Y*256)
1140 POKEMC+1,Y:POKEMC+4,17
1150 FORY=1TOST%(X,1)*75:NEXTY
1160 POKEMC+4,16:FORY=1TO20:NEXTY,X
1170 POKEMC+24,0
1180 RETURN
1200 X=VAL(X$):X$=STR$(X):X=LEN(X$):IFX>3THEN1230
1210 IFX=2THENY$=LEFT$(X$,1)+"$0.0"+RIGHT$(X$,1):GOTO1280
1220 Y$=LEFT$(X$,1)+"$0."+RIGHT$(X$,2):GOTO1280
1230 X=X-3:Y$=LEFT$(X$,1)+"$"+MID$(X$,2,X)+"."+RIGHT$(X$,2)
1280 X$=Y$:IFLEN(X$)>7THENRETURN
1290 Y$=" "+Y$:GOTO1280
2000 IFX=>2ORAA<600ORIQ=0THEN960
2005 AA=AA-100
2010 POKEMC+0,0:POKEMC+1,0:POKEMC+5,15:POKEMC+6,15:POKEMC+24,15
2015 FORX=11TO2STEP-1
2020 POKEMC+1,X*3:POKEMC+4,17
2030 FORY=1TO30:NEXTY:POKEMC+4,16:NEXTX
2040 POKEMC+24,0
2050 PRINT"{2 DOWN} UNWISE SPENDING HAS CAUSED YOUR"
2060 PRINT"PARENTS TO LOWER YOUR ALLOWANCE BY $1{3 SPACE}PER MONTH."
2070 GOTO960
3000 DATA6430,2,4817,1,6430,1,4817,1,6430,1,7217,1,8101,3
3010 DATA6430,3,8583,2,8583,1,6430,2,7217,2,8101,4
3020 DATAJANUARY,FEBRUARY,MARCH,APRIL,MAY,JUNE,JULY,AUGUST,SEPTEMBER,OCTOBER
3030 DATANOVEMBER,DECEMBER,"$"
3100 DATA"JOE'S MATH NOTES",100,1,"KUNG FU POSTER",150,1
3110 DATA"PET FROG",125,1,"BOX OF MATCHES",75,1
3120 DATA"LIVE BABY BAT",175,1,"PET SNAKE",150,1
3130 DATA"SODA POP",50,1,"FIRECRACKERS",100,1
3140 DATA"STINK BOMB",150,1,"PET MOUSE",125,1
3150 DATA"WHITE RAT",200,1,"LOCKPICKING SET",75,1
3160 DATA"PET SPIDER",100,1,"PELLET GUN",250,1
3170 DATA"BUBBLE GUM",50,1,"MOTHER AND BABY RABBITS",80,1
3180 DATA"BROKEN ROLLER SKATE",200,1,"DRUM SET",300,1
3190 DATA"ROTTEN GOOSE EGG",200,1,"LOCK WITHOUT A KEY",175,1
3210 DATA"BASEBALL CARDS",75,2,"CAT'S EYE MARBLE",50,2
3220 DATA"COMIC BOOK",50,2,"NOISY ROCK RECORD",200,2
3230 DATA"HOT FUDGE SUNDAE",150,2,"PET GOLDFISH",175,2
3240 DATA"PAIR OF DICE",100,2,"PEN KNIFE",100,2
3250 DATA"BOX OF CHOCOLATES",200,2,"BUMPER STICKER",100,2
3260 DATA"FALSE MUSTACHE",125,2,"PHONY NOSE",75,2
3270 DATA"PEA SHOOTER",50,2,"NERF BALL",100,2
3280 DATA"JELLY BEANS",50,2,"WOLFMAN MODEL",200,2
3290 DATA"DRACULA FANGS",100,2,"TATTOO KIT",150,2
3300 DATA"WATER PISTOL",150,2,"PET HAMSTER",150,2
3310 DATAHARMONICA,100,3,"HULA HOOP",75,3
3320 DATA"ROLLER SKATES",300,3,"CHAMPION YO-YO",200,3
3330 DATABASKETBALL,300,3,"T-SHIRT",175,3
3340 DATA"MATCHBOX CAR",125,3,"MOVIE TICKET",200,3
3350 DATA"COMPUTER GAME",300,3,"STUFFED ANIMAL",300,3
3360 DATA"ICE CREAM CONE",75,3,"BUBBLE STUFF",50,3
3370 DATABANJO,400,3,"CAP GUN",200,3
3380 DATAFOOTBALL,300,3,"CATCHER'S MITT",400,3 
3390 DATA"BASEBALL BAT",250,3,"BUG COLLECTION",200,3
3400 DATA"CAMPING TENT",350,3,CANTEEN,150,3
3410 DATA"STAMP COLLECTION",200,4,"POSTAGE STAMPS",200,4
3420 DATA"BICYCLE PUMP",300,4,"TENNIS SHOES",500,4
3430 DATA"FROZEN YOGURT",200,4,PENCILS,175,4
3440 DATAKITE,300,4,"DOG FOOD",500,4
3450 DATA"CAMERA FILM",250,4,"FILM DEVELOPING",350,4
3460 DATA"FINGER PAINTS",350,4,"COIN COLLECTION",500,4
3470 DATA"SHELL COLLECTION",300,4,"ELECTRIC TRAIN",500,4
3480 DATA"CHECKER SET",275,4,"SCOUT KNIFE",300,4
3490 DATA"MICKEY MOUSE WATCH",500,4,COMPASS,250,4
3500 DATA"BOOK OF MAGIC",300,4,"TOY TYPEWRITER",500,4
3510 DATA"BIRTHDAY CARD",150,5,NOTEBOOK,300,5
3520 DATADICTIONARY,600,5,"POCKET CALCULATOR",650,5
3530 DATA"SILVER DOLLAR",700,5,DIARY,400,5
3540 DATA"TOM SAWYER BOOK",300,5,"CHESS SET",450,5
3550 DATADOMINOES,400,5,"SAVINGS ACCOUNT",500,5
3560 DATA"GET-WELL PRESENT",300,5,"BIRTHDAY PRESENT",350,5
3570 DATA"WEDDING PRESENT",500,5,"FLEA COLLAR",250,5
3580 DATA"KITTY LITTER",350,5,"ANT FARM",500,5
3590 DATA"DRAWING PAPER",550,5,"GIFT FOR MOM",600,5
3600 DATA"GIFT FOR DAD",600,5,"WATERCOLOR SET",575,5
4000 DATA" GRANDMA SENDS YOU A CHECK FOR YOUR{6 SPACE}BIRTHDAY",500,1
4010 DATA" MOM PAYS YOU FOR BABYSITTING YOUR{7 SPACE}LITTLE BROTHER",300,1
4020 DATA" YOU SELL YOUR BASEBALL GLOVE TO YOUR{4 SPACE}FRIEND TOMMY",400,1
4030 DATA" YOU MOW YOUR NEIGHBOR'S LAWN TO GET{5 SPACE}EXTRA MONEY",300,1
4040 DATA" MOM PAYS YOU FOR CLEANING YOUR ROOM{5 SPACE}WITHOUT BEING ASKED",300,1
4050 DATA" YOU WIN A PRIZE AT SCHOOL FOR BEING A{3 SPACE}GOOD STUDENT",500,1
4060 DATA" YOU RETURN A WALLET THAT YOU FIND AND{3 SPACE}GET A REWARD",500,1
4070 DATA" YOU WIN A CONTEST FROM A LOCAL RADIO{4 SPACE}STATION",500,1
4080 DATA" HELP JEFF DELIVER PAPERS ON HIS{9 SPACE}NEWSPAPER ROUTE",300,1
4090 DATA" YOU GET A GOOD REPORT CARD,AND DAD IS{2 SPACE}VERY PLEASED",400,1
4100 DATA" YOU AND SUSIE MAKE MONEY RUNNING A{6 SPACE}LEMONADE STAND",500,1
4110 DATA" YOU LOSE A TOOTH AND THE TOOTH FAIRY{4 SPACE}LEAVES YOU SOME COINS",75,1
4120 DATA" YOU RAKE THE YARD AND DAD PAYS YOU FOR{2 SPACE}IT",200,1
4130 DATA" YOU CLEAN THE HOUSE AND MOM PAYS YOU{4 SPACE}FOR YOUR HARD WORK",250,1
4140 DATA" YOU DON'T WATCH TV FOR A WHOLE WEEK AND DAD REWARDS YOU",300,1
4150 DATA" YOUR BICYCLE TIRE POPS AND YOU HAVE TO{2 SPACE}BUY A NEW ONE",250,-1
4160 DATA" YOU JOIN A SECRET CLUB AND HAVE TO PAY{2 SPACE}DUES",200,-1
4170 DATA" YOU BUY A BIRTHDAY GIFT FOR YOUR{8 SPACE}SISTER",150,-1
4180 DATA" YOU BREAK YOUR SISTER'S DOLL AND HAVE{3 SPACE}TO BUY HER A NEW ONE",500,-1
4190 DATA" YOU BREAK MOM'S LAMP AND HAVE TO{8 SPACE}REPLACE IT",500,-1
4200 DATA" YOU LOSE YOUR WALLET ON THE WAY HOME{4 SPACE}FROM SCHOOL",400,-1
4210 DATA" YOU KNOCK YOUR BALL THROUGH A WINDOW{4 SPACE}AND MUST FIX IT",400,-1
4220 DATA" PAY YOUR FRIEND TO TEACH YOU YO-YO{6 SPACE}TRICKS",100,-1
4230 DATA" YOU AND BETTY GO TO THE CIRCUS",200,-1
4240 DATA" DAD MAKES YOU BUY BACK A BROKEN TOY{5 SPACE}YOU SOLD TO SAM",200,-1
4250 DATA" YOU BUY DAD A PRESENT FOR HIS BIRTHDAY",400,-1
4260 DATA" YOU BUY A GIFT TO TAKE TO SALLY'S{7 SPACE}BIRTHDAY PARTY",350,-1
4270 DATA" YOUR FAVORITE CANDY BARS ARE ON SALE,{3 SPACE}AND YOU BUY A CASE",500,-1
4280 DATA" YOU PAY YOUR LITTLE BROTHER TO WASH THE DOG FOR YOU",300,-1
4290 DATA" YOU MUST PAY PART OF THE DOCTOR BILL TO GET A BEAN OUT OF YOUR EAR"
4300 DATA400,-1
