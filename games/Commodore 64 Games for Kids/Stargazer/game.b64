1 REM STARGAZER
100 NS=32
110 DIMCT$(NS-1,3),ST%(NS-1,19),NT%(14,2)
120 PRINTCHR$(147):VC=53248:MC=54272
122 FORX=54272TO54296:POKEX,0:NEXTX
124 C$="{40 SPACE}"
126 POKEVC+32,6:POKEVC+33,15
130 FORX=0TO14:FORY=0TO2:READZ:NT%(X,Y)=Z:NEXTY,X:X=RND(-TI)
140 PRINT"{BLK}{RVS}{15 SPACE}STARGAZER!{15 SPACE}{OFF}";
150 PRINT"{DOWN}YOU WILL BE ASKED TO NAME CONSTELLATIONSIN THE NIGHT SKY."
160 PRINT"{DOWN}THE SKY DIRECTION AND OPTIMUM VIEWING{3 SPACE}MONTH WILL ALSO BE PROVIDED."
170 PRINT"{DOWN}A CLUE WILL BE GIVEN IF YOU CAN'T GUESS THE NAME BY ITS SHAPE."
180 PRINT"{DOWN}YOU GET 10 POINTS FOR EACH CORRECT"
185 PRINT "ANSWER BASED ON SHAPE ALONE."
190 PRINT"{DOWN}YOU GET 2 POINTS IF YOU NEED THE EXTRA{2 SPACE}CLUE."
195 PRINT"{DOWN}TYPE {RVS}END{OFF} TO STOP THE GAME EARLY."
200 GOSUB1000
210 CB=828:M=((PEEK(53272)AND240)/16)*1024:CC=55296
215 PRINT"{DOWN}{3 SPACE}(PRESS ANY KEY -- 10 SECOND DELAY) "
220 FORX=0TONS-1
230 FORY=0TO3:READX$:CT$(X,Y)=X$:NEXTY
240 Y=0
250 READZ:ST%(X,Y)=Z:Y=Y+1:IFZ>0THEN250
260 NEXTX
280 GETX$:IFX$=""THEN280
300 S%=0:FORX=1TONS:POKECB+X,0:NEXTX
310 FORX=1TONS:GOSUB2000:POKECB+Z,X:NEXTX
320 POKEVC+32,0:POKEVC+33,1
400 LC=1
410 CI=PEEK(CB+LC)-1
420 PRINTCHR$(147);:FORX=1TO14
430 PRINT "{BLU}{RVS}{40 SPACE}{OFF}";:NEXTX
440 X$=CT$(CI,1):X=40-LEN(X$)-LEN(CT$(CI,2))
450 FORY=1TOX:X$=X$+"{SSPACE}":NEXTY
460 X$=X$+CT$(CI,2)
465 PRINT"{BLK}{HOME}{23 DOWN}";SPC(15);"SCORE =";S%;
470 PRINT"{BLU}{HOME}{13 DOWN}{RVS}";X$;"{OFF}";
480 X=0
490 Y=ST%(CI,X):IFY=0THEN510
500 POKEM+Y,174:X=X+1:GOTO490
510 PRINT"{2 DOWN}{BLK}{2 SPACE}CONSTELLATION?{DOWN}"
520 X=10:GOSUB3000
530 IFX$="q"THEN600
535 IFX$="END"THEN610
537 PRINT"{BLK}{HOME}{23 DOWN}";SPC(15);"SCORE =";S%;
540 PRINT"{HOME}{16 DOWN}";CT$(CI,3);".{DOWN}"
550 X=2:GOSUB3000
560 IFX$="q"THEN600
565 IFX$="END"THEN610
568 PRINT"{BLK}{HOME}{23 DOWN}";SPC(15);"SCORE =";S%;
570 PRINT"{BLU}{HOME}{16 DOWN}{2 SPACE}";CT$(CI,0)
580 FORX=1TO3000:NEXTX
600 LC=LC+1:IFLC<=NSTHEN410
610 POKEVC+32,6:POKEVC+33,15
620 PRINTCHR$(147);"{RED}{DOWN}{15 SPACE}{RVS}GAME OVER!{OFF}"
630 PRINT"{3 DOWN}{4 SPACE}TOTAL POINTS{2 SPACE}=";S%
635 X=LC-1:IFX<1THENX=1
640 X=INT((S%*100)/(X*10))
650 PRINT"{DOWN}{4 SPACE}TOTAL SCORE{2 RIGHT} =";X;"{LEFT}%"
660 PRINT"{3 DOWN}{4 SPACE}PLAY AGAIN? ({RVS}Y{OFF} OR {RVS}N{OFF})"
670 GETX$:IFX$=""THEN670
680 IFX$="Y"THEN300
690 IFX$<>"N"THEN670
700 GOSUB1000
710 PRINTCHR$(147):END
1000 POKEMC+24,15:POKEMC+5,79:POKEMC+6,129
1010 FORX=0TO14
1020 POKEMC+0,NT%(X,0)
1030 POKEMC+1,NT%(X,1)
1040 POKEMC+4,17
1050 FORY=1TONT%(X,2)*80:NEXTY
1060 POKEMC+4,16:FORY=1TO30:NEXTY,X
1070 POKEMC+24,0
1080 RETURN
2000 Y=0
2010 Z=INT(RND(1)*NS)+1:IFPEEK(CB+Z)=0THENRETURN
2020 Y=Y+1:IFY<9THEN2010
2030 Z=1
2040 IFPEEK(CB+Z)=0THENRETURN
2050 Z=Z+1:GOTO2040
3000 X$="":POKE198,0:INPUTX$
3010 IFX$=CT$(CI,0)THEN3100
3015 IFX$="END"THENRETURN
3020 POKEVC+33,0:POKEMC+24,15:POKEMC+0,49:POKEMC+1,28
3030 POKEMC+5,15:POKEMC+6, 15:POKEMC+4,17
3040 FORY=1TO100:NEXTY:POKEMC+4,16
3050 PRINT"{HOME}{14 DOWN}";C$;C$;C$;C$;C$;C$;C$;C$;C$;
3060 POKEMC+24,0:POKEVC+33,1
3070 RETURN
3100 X$="q":S%=S%+X
3110 POKEMC+24,15:POKEMC+0,0:POKEMC+5,15:POKEMC+6,15:POKEMC+1,0
3120 FORX=15TO1STEP-1
3130 POKEVC+33,X:POKEMC+1,Y:POKEMC+4,17
3140 FORZ=1TO50:NEXTZ:POKEMC+4,16
3150 Y=Y+10:NEXTX
3160 POKEMC+24,0
3170 RETURN
9000 DATA104,9,2,104,9,2,24,14,2,24,14,2,210,15,2,210,15,2,24,14,4,0,0,1
9010 DATA143,12,2,143,12,2,218,11,2,218,11,2,143,10,2,143,10,2,104,9,8
9100 DATACEPHEUS,JUNE,NORTH
9110 DATA"THE PULSATING STARS, OR CEPHEIDS, FIND{2 SPACE}THEIR PROTOTYPE HERE"
9120 DATA100,170,147,300,309,0
9130 DATACASSIOPEIA,JUNE,NORTH
9140 DATA"TYCHO BRAHE OBSERVED A NOVA HERE IN{5 SPACE}1572"
9150 DATA129,107,294,178,301,0
9160 DATABOOTES,JUNE,SOUTH
9170 DATA"IT WAS ERRONEOUSLY CALLED 'ORION' IN{4 SPACE}AMOS 5:8"
9180 DATA14,61,172,221,258,383,420,427,0
9190 DATALIBRA,JUNE,SOUTH
9200 DATA"ITS STARS WERE ONCE KNOWN AS SCORPIO'S{2 SPACE}CLAWS"
9210 DATA16,132,183,380,0
9220 DATADRACO,JUNE,NORTH
9230 DATA"ITS BODY WINDS BETWEEN THE BIG AND THE{2 SPACE}LITTLE DIPPERS"
9240 DATA16,36,60,74,78,90,116,141,205,223,241,381,429,465,0
9250 DATA"URSA MAJOR",SEPTEMBER,NORTH
9260 DATA"THE BIG DIPPER IS 'BEARLY' HALF OF THIS CONSTELLATION"
9270 DATA1,5,87,130,176,210,223,254,269,300,289,412,421,447,0
9280 DATA"URSA MINOR",JUNE,NORTH
9290 DATA"THE NORTH STAR IS THE TIP OF ITS TAIL"
9300 DATA18,96,101,180,301,378,454,0
9310 DATACYGNUS,JUNE,EAST
9320 DATA"THE NORTHERN CROSS FORMS THIS BIRD'S{4 SPACE}NECK AND BODY"
9330 DATA9,47,130,241,249,263,272,370,450,0
9340 DATAHERCULES,JUNE,EAST
9350 DATA"HE KNEELS ON THE HEAD OF THE DRAGON{5 SPACE}KILLED"
9360 DATA7,92,118,139,149,218,252,281,343,369,432,0
9370 DATA"CORONA BOREALIS",JUNE,EAST
9380 DATA"BACCHUS GAVE ARIADNE THIS WREATH OF{5 SPACE}GEMS"
9390 DATA132,145,227,252,338,341,307,0
9400 DATASCORPIUS,JULY,SOUTH
9410 DATA"ANTARES IS ONE REASON IT SHINES SO{6 SPACE}BRIGHTLY"
9420 DATA29,32,74,149,1 58,185,223,234,296,298,301,334,341,372,421,454,460,0
9430 DATAHYDRA,JUNE,WEST
9440 DATA"'THE MAINSAIL' STAR GROUP RIDES ON THIS SERPENT'S BACK"
9450 DATA1,40,129,94,176,220,345,229,312,356,398,436,478,0
9460 DATALEO,JUNE,WEST
9470 DATA"ITS MANE IS FORMED BY 'THE SICKLE' STAR GROUP"
9480 DATA5,87,50,216,220,263,303,327,336,374,454,0
9490 DATASAGITTARIUS,JULY,SOUTH
9500 DATA"THE CENTAUR LOOKS MORE LIKE A TEAPOT IN THE SKY"
9510 DATA60,90,94,167,181,225,249,340,0
9520 DATAAQUARIUS,SEPTEMBER,SOUTH
9530 DATA"THE WATER FROM HIS JAR FEEDS THE{8 SPACE}'SOUTHERN FISH'"
9540 DATA5,9,47,89,52,178,203,267,325,361,403,0
9550 DATAVIRGO,JULY,WEST
9560 DATA"THE STAR SPICA IS A STALK OF GRAIN IN{3 SPACE}HER HAND"
9570 DATA56,72,147,170,225,349,434,0
9580 DATASERPENS,SEPTEMBER,WEST
9590 DATA"IT IS AESCULAPIUS' SNAKE, THE SYMBOL OF MEDICINE"
9600 DATA7,85,203,287,370,376,378,461,347,392,389,312,272,276,278,238,0
9610 DATAPISCES,SEPTEMBER,EAST
9620 DATA"THE TAILS OF THESE FISHES ARE BOUND BY{2 SPACE}RIBBON"
9630 DATA29,32,65,109,105,125,143,147,165,169,210,260,292,298,374,376,454,0
9640 DATACAPRICORNUS,SEPTEMBER,SOUTH
9650 DATA"THIS 'SEA GOAT' MAY BE PAN IN DISGUISE"
9660 DATA147,149,170,172,187,296,340,381,0
9670 DATATAURUS,OCTOBER,EAST
9680 DATA"THE PLEIADES FORM THE APEX OF ITS{7 SPACE}SHOULDER"
9690 DATA54,190,212,216,229,243,258,261,292,296,349,365,0
9700 DATAAQUILA,OCTOBER,WEST
9710 DATA"JUPITER IN DISGUISE (THE EAGLE) FORMS{3 SPACE}THE TAIL OF SERPENS"
9720 DATA50,61,141,145,300,307,414,0
9730 DATAPERSEUS,NOVEMBER,EAST
9740 DATA"IT CONTAINS ALGOL, A VARIABLE STAR THAT IS REALLY TWO STARS"
9750 DATA3,47,129,136,140,210,294,336,378,343,381,0
9760 DATAORION,NOVEMBER,EAST
9770 DATA"BETELGEUSE AND RIGEL ARE FOUND HERE"
9780 DATA92,96,180,187,220,260,245,285,289,290,385,0
9790 DATAGEMINI,NOVEMBER,EAST
9800 DATA"ITS MAJOR STARS ARE CASTOR AND POLLUX"
9810 DATA109,170,187,330,343,349,429,0
9820 DATAARIES,OCTOBER,EAST
9830 DATA"ITS GOLDEN FLEECE WAS WORN BY JASON"
9840 DATA212,138,181,0
9850 DATACANCER,APRIL,WEST
9860 DATA"WHEN ITS CLUSTER PRAESEPE IS VISIBLE,{3 SPACE}RAIN IS FORETOLD"
9870 DATA132,149,218,263,0
9880 DATACETUS,DECEMBER,SOUTH
9890 DATA"DENEB KAITOS IS THE 'SEA MONSTER'S'{5 SPACE}MAJOR STAR"
9900 DATA1,5,85,129,254,180,263,378,430,238,0
9910 DATAANDROMEDA,DECEMBER,SOUTH
9920 DATA"IT FEATURES A SPIRAL THAT CAN BE SEEN ON MOONLESS NIGHTS"
9930 DATA25,220,229,261,250,292,336,338,458,0 
9940 DATA"CANIS MAJOR",JANUARY,SOUTH
9950 DATA"'SIRIUS' ASTRONOMY STUDENTS WILL{8 SPACE}RECOGNIZE THIS"
9960 DATA18,61,105,220,298,336,347,381,414,423,0
9970 DATALEPUS,JANUARY,SOUTH
9980 DATA"THIS RABBIT IS FLEEING FROM ORION,{6 SPACE}'THE HUNTER'"
9990 DATA92,136,220,187,294,336,301,345,389,0
10000 DATAERIDANUS,JANUARY,SOUTH
10010 DATA"'THE RIVER' IS THOUGHT TO REPRESENT{5 SPACE}CHINA'S RIVER PO"
10020 DATA14,50,56,100,141,134,296,338,381,460,0
10030 DATAPEGASUS,DECEMBER,SOUTH
10040 DATA"THREE STARS OF THE 'GREAT SQUARE' FORM{2 SPACE}ITS TORSO"
10050 DATA12,105,178,185,149,298,349,316,378,463,0
