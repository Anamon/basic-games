1 REM LAWN MOWER
100 VC=53248:POKEVC+32,13:POKEVC+33,6:PRINTCHR$(147)
110 DIMMT$(4),ST%(20,1)
120 FORX=0TO20:FORY=0TO1:READZ:ST%(X,Y)=Z:NEXTY,X
130 FORX=0TO4:READX$:MT$(X)=X$:NEXTX
140 MC=54272:FORX=MCTO54296:POKEX,0:NEXTX
150 GOSUB4000
160 PRINT"{WHT}{7 DOWN} DAD LOANS YOU $15 SO YOU CAN RUN A"
170 PRINT" LAWN MOWING SERVICE FOR THE SUMMER."
175 PRINT"{DOWN} AT THE END OF THE SUMMER YOU MUST PAY{3 SPACE}DAD BACK THE $15."
180 PRINT"{DOWN} YOU GET $5 FOR EACH LAWN YOU MOW, BUT"
185 PRINT" YOU MUST DEDUCT EXPENSES SUCH AS:"
190 PRINT"{2 DOWN}{3 SPACE}GAS FOR THE MOWER"
193 PRINT"{3 SPACE}OIL FOR THE MOWER"
195 PRINT"{3 SPACE}LAWN AND LEAF BAGS"
200 PRINT"{3 SPACE}MOWER SHARPENING"
210 GOSUB1000:GOSUB1100
220 PRINT"{CLR}{2 DOWN} IF YOU RUN OUT OF SUPPLIES DURING THE"
225 PRINT" MONTH OR IF YOUR MOWER GETS DULL FROM"
230 PRINT" LACK OF SHARPENING, YOU LOSE YOUR"
240 PRINT" INCOME FOR THE REST OF THAT MONTH."
250 PRINT"{2 DOWN} THE OBJECT OF THE GAME IS TO BUY YOUR"
255 PRINT" SUPPLIES WISELY SO THAT YOU CAN EARN"
260 PRINT" AS MUCH AS POSSIBLE DURING THE SUMMER."
270 GOSUB1100:POKEVC+33,15:PRINT"{BLK}";CHR$(147)
300 MO=1500:GA=0:OA=0:BA=0:SA=0
310 FORL1=0TO4:LM=0:MM=0
320 X=INT(RND(1)*71)+80:D1$="GAS":D2$="GALLON":GOSUB1200
330 IFY=0THEN350
340 GA=GA+(Y*100):MO=MO-(X*Y)
350 X=INT(RND(1)*51)+90:D1$="OIL":D2$="QUART":GOSUB1200
360 IFY=0THEN380
370 OA=OA+(Y*100):MO=MO-(X*Y)
380 X=INT(RND(1)*151)+200:D1$="LAWN BAGS":D2$="DOZEN":GOSUB1200
390 IFY=0THEN410
400 BA=BA+(Y*12):MO=MO-(X*Y)
410 X=INT(RND(1)*201)+400:GOSUB1300:PRINT"{CLR}{2 DOWN}{3 SPACE}MONTH = ";MT$(L1)
420 PRINT"{2 DOWN}{3 SPACE}DO YOU WANT TO HAVE YOUR MOWER{10 SPACE}SHARPENED?"
422 PRINT"{2 DOWN}{3 SPACE}COST{2 SPACE}= ";X$
430 PRINT"{2 DOWN}{3 SPACE}ENTER {RVS}Y{OFF} OR {RVS}N{OFF}"
440 GETX$:IFX$=""THEN440
450 IFX$="N"THEN500
460 IFX$<>"Y"THEN440
470 MO=MO-X
480 X=INT(RND(1)*21)+20:SA=SA+X
500 GC=INT(RND(1)*51)+20:OC=INT(RND(1)*31)+10
510 BC=INT(RND(1)*4)+1
520 IFMO<0THENGC=GC*2:OC=OC*2:BC=BC*2
530 X=1:IF(GA-GC)<0THEN700
540 X=2:IF(OA-OC)<0THEN700
550 X=3:IF(BA-BC)<0THEN700
560 X=4:IF(SA-1)<0THEN700
570 GA=GA-GC:OA=OA-OC:BA=BA-BC:SA=SA-1
580 LM=LM+1:MM=MM+500
600 PRINT"{CLR}{2 DOWN}{3 SPACE}";MT$(L1):PRINT"{2 DOWN}{3 SPACE}LAWN NUMBER ";LM
610 X=MM:GOSUB1300:PRINT"{2 DOWN}{3 SPACE}MONEY MADE = ";X$
620 PRINT"{2 DOWN}{3 SPACE}GAS LEFT{3 SPACE}=";INT((GA+100)/100)
625 PRINT"{2 DOWN}{3 SPACE}OIL LEFT{3 SPACE}=";INT((OA+100)/100)
630 PRINT"{2 DOWN}{3 SPACE}BAGS LEFT{2 SPACE}=";BA
640 FORX=2TO10STEP2:GOSUB660:NEXTX
650 FORX=10TO0STEP-2:GOSUB660:NEXTX:GOTO500
660 POKEMC+0,0:POKEMC+1,10:POKEMC+5,15:POKEMC+6,15:POKEMC+7,0:POKEMC+8,0
670 POKEMC+11,15:POKEMC+12,15
675 POKEMC+24,X:POKEMC+8,40:POKEMC+4,129:POKEMC+10,17
680 FORY=1TO100:NEXTY
685 POKEMC+4,128:POKEMC+10,16
690 FORY=1TO20:NEXTY
695 RETURN
700 POKEMC+0,0:POKEMC+1,25:POKEMC+7,0:POKEMC+8,0:POKEMC+5,15:POKEMC+6,15
710 POKEMC+24,9:POKEMC+4,33:FORY=1TO500:NEXTY
715 POKEMC+4,32:FORY=1TO20:NEXTY:POKEMC+24,0:PRINTCHR$(147);
720 ONXGOTO730,740,750,760
730 PRINT"{2 DOWN}{3 SPACE}OUT OF GAS!":GOTO770
740 PRINT"{2 DOWN}{3 SPACE}OUT OF OIL!":GOTO770
750 PRINT"{2 DOWN}{3 SPACE}OUT OF LAWN BAGS!":GOTO770
760 PRINT"{2 DOWN}{3 SPACE}MOWER NOT SHARP!"
770 PRINT"{2 DOWN}{3 SPACE}LAWNS MOWED = ";LM
780 X=MM:GOSUB1300
785 PRINT"{2 DOWN}{3 SPACE}MONEY MADE{2 SPACE}={2 SPACE}";X$
790 MO=MO+MM:X=MO:GOSUB1300:PRINT"{2 DOWN}{3 SPACE}BALANCE{5 SPACE}={2 SPACE}";X$
795 GOSUB1100:NEXTL1
800 PRINT"{CLR}{2 DOWN}";SPC(14);"SUMMER OVER!":X=MO:GOSUB1300
805 PRINT"{2 DOWN}{3 SPACE}CASH ON HAND = ";X$
810 X=1500:GOSUB1300:PRINT"{2 DOWN}{3 SPACE}LESS LOAN{4 SPACE}= ";X$
820 MO=MO-1500:X=MO:GOSUB1300:PRINT"{2 DOWN}{3 SPACE}NEW BALANCE{2 SPACE}= ";X$
830 PRINT"{2 DOWN}{3 SPACE}REPLAY? ({RVS}Y{OFF} OR {RVS}N{OFF})"
840 GETX$:IFX$=""THEN840
850 IFX$="Y"THEN300
860 IFX$<>"N"THEN840
870 PRINTCHR$(147)
880 END
1000 POKEMC+24,15:POKEVC+21,1
1005 FORX=0TO20:TT$=RIGHT$(TT$,LEN(TT$)-2):TT$=TT$+"{2 SPACE}"
1010 PRINT"{HOME}{5 DOWN}";TT$;
1015 Y=INT(ST%(X,0)/256)
1020 POKEMC+0,ST%(X,0)-(Y*256)
1025 POKEMC+1,Y:POKEMC+4,17
1030 Y=PEEK(VC+0)-14
1035 IFY<1THENY=255:POKEVC+0,Y:POKEVC+16,0
1040 POKEVC+0,Y
1045 FORY=1TOST%(X,1)*60:NEXTY
1050 POKEMC+4,16:FORY=1TO30:NEXTY,X
1055 POKEMC+24,0:POKEVC+21,0
1060 PRINT"{HOME}{3 DOWN}";SPC(15);"{RVS}LAWN MOWER{OFF}";
1070 RETURN
1100 PRINT"{HOME}{24 DOWN}{12 SPACE}(PRESS ANY KEY)";
1110 GETX$:IFX$=""THEN1110
1120 RETURN
1200 PRINT"{CLR}{2 DOWN}{3 SPACE}MONTH = ";MT$(L1):GOSUB1300
1210 PRINT"{2 DOWN}{3 SPACE}ITEM{2 SPACE}= ";D1$:PRINT"{2 DOWN}{3 SPACE}COST{2 SPACE}= ";X$;" PER ";D2$
1220 PRINT"{2 DOWN}{3 SPACE}HOW MANY DO YOU WANT?":PRINT"{2 DOWN}{3 SPACE}ENTER {RVS}0{OFF} THROUGH {RVS}9{OFF}"
1230 GETD1$:IFD1$=""THEN1230
1240 IFD1$<"0"ORD1$>"9"THEN1230
1250 Y=VAL(D1$):RETURN
1300 X$=STR$(X):Z=LEN(X$)
1310 IFZ<3THENX$=LEFT$(X$,1)+"$0.0"+RIGHT$(X$,1):GOTO1370
1320 IFZ<4THENX$=LEFT$(X$,1)+"$0."+RIGHT$(X$,2):GOTO1370
1330 Z=Z-3:X$=LEFT$(X$,1)+"$"+MID$(X$,2,Z)+"."+RIGHT$(X$,2)
1370 IFLEFT$(X$,1)=" "THENX$=RIGHT$(X$,LEN(X$)-1):GOTO1370
1380 RETURN
3000 DATA8583,2,4291,2,4291,2,5407,1,5728,2,10814,2,10814,2,9634,2
3010 DATA8583,1,7217,2,8583,2,4291,2,4291,2,5407,2,6430,2,6430,2
3020 DATA6430,2,7217,2,8101,2,8101,2,8583,2
3100 DATAMAY,JUNE,JULY,AUGUST,SEPTEMBER
3200 DATA0,0,0,0,0,0,0,0,0,0,0,0,0,3,128,0,7,128,0,3,128,0,1,0,0,3
3210 DATA128,0,3,128,0,31,128,0,35,128,0,67,128,0,131,128
3220 DATA1,3,128,2,3,128,4,2,64,8,4,32,80,8,16,224,4,8,80,2,4,0
4000 REM *** SETUP FOR SPRITE ***
4010 FORX=832TO832+63:READZ:POKEX,Z:NEXTX
4020 POKEVC+0,60:POKEVC+1,55:POKEVC+16,1:POKEVC+23,1:POKEVC+28,0
4030 POKEVC+29,1:POKEVC+39,0:POKE2040,13
4040 POKEMC+0,0:POKEMC+1,0:POKEMC+5,79:POKEMC+6,129
4050 TT$="{24 SPACE}LAWN MOWER"
4060 RETURN
