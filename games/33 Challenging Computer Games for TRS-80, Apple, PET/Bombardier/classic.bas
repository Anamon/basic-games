10 CLS: PRINT: IF Q = 1 THEN 280
20 PRINT: PRINT TAB(10); "BOMBARDIER"
30 PRINT
40 PRINT: PRINT "INSTRUCTIONS";
50 INPUT A$
60 IF A$ <> "YES" THEN 80
70 GOTO 110
80 IF A$ <> "NO" THEN 100
90 GOTO 230
100 PRINT: PRINT "I AM ABLE TO RESPOND TO YES OR NO, ONLY": PRINT: GOTO 40
110 PRINT
120 PRINT "WELCOME, YOU WILL BE THE BOMBARDIER THROUGHOUT THIS"
130 PRINT "GAME AND I, YOUR COMPUTER, WILL BE NAVIGATOR AND PILOT."
140 PRINT "WE WILL BE GIVEN A TARGET TO DESTROY. YOU'LL HAVE TO"
150 PRINT "DECIDE WHEN TO OPEN THE BOMBAY DOORS AND RELEASE THE"
160 PRINT "BOMBS. BEFORE YOU DROP THE BOMBS YOU'LL HAVE TO TAKE"
170 PRINT "INTO CONSIDERATION THE FOLLOWING:"
180 PRINT "1) SURFACE WIND SPEED"
190 PRINT "2) DISTANCE TO TARGET"
200 GOSUB 210: GOTO 230
210 PRINT: PRINT "PRESS A KEY....."
215 A$ = INKEY$: IF A$ = "" THEN 215
220 CLS: PRINT: RETURN
230 PRINT "YOU'LL ONLY BE USING THE FOLLOWING KEYS:"
240 PRINT "A....TO ABORT MISSION"
250 PRINT "O....TO OPEN BOMBAY DOORS"
260 PRINT "D....TO DROP BOMBS"
270 PRINT "F....UPDATE ON SURFACE WIND SPEED,"
275 PRINT "MILES TO TARGET (CONTINUE FLIGHT)."
280 A = 0: D = 1: PRINT: IF Q = 1 THEN 310
290 PRINT "WE WILL RECEIVE INFORMATION IN A FEW SECONDS...."
300 FOR S = 1 TO 1500: NEXT
310 REM SET TARGET
320 I = INT(3 * RND(1) + 1): ON I GOTO 330, 340, 350
330 T$ = "AMMUNITION FACTORY": GOTO 355
340 T$ = "WEAPONS FACTORY": GOTO 355
350 T$ = "FUEL DEPOT"
355 IF T$ = S$ THEN 320
360 REM SET SECONDARY TARGET
370 I = I + 1: IF I = 4 THEN I = 3: READ W$(I)
380 DATA CONVOY OF TANKS,OIL DUMP,ENERGY AIRSTRIP
385 Q$ = W$(I)
390 REM SET MILES TO TARGET
400 M = 1000 + INT(200 * RND(1) + 5)
405 IF Q = 1 THEN 590
410 REM SET X AREA, BOMBS
420 ON I GOTO 430, 440, 450
430 X = INT(10 * RND(1) + 5): B = INT(X * RND(1) + 300): GOTO 460
440 X = INT(40 * RND(1) + 15): B = INT(X * RND(1) + 200): GOTO 460
450 X = INT(50 * RND(1) - 10): B = INT(X * RND(1) + 500)
460 REM SET SURFACE WIND SPEED, Y AREA
465 ON I GOTO 470, 480, 490
470 WS = INT(40 * RND(1) + 14): Y = ((WS - 10) + (Y + WS)): GOTO 500
480 WS = INT(30 * RND(1) + 13): Y = ((WS - 5) + (Y + WS)): GOTO 500
490 WS = INT(20 * RND(1) + 10): Y = ((WS - 3) + (Y + WS))
500 REM SET PLANES SPEED
510 S = 800 + INT(500 * RND(1) + 300)
520 REM SET < > AREA
530 L = ABS((S / WS) * (Y - X)) / 20: IF L < 10 OR L > 110 THEN 410
540 LL = INT(L)
550 REM MESSAGE
560 PRINT
570 PRINT "YOU WILL BE DROPPING ROCKET BOMBS"
580 PRINT "MADE FROM A NEW ALLOY."
585 PRINT "TRY TO GET WITHIN"; LL + 5; "MILES OF THE"
586 PRINT T$; " BEFORE DROPPING BOMBS...."
590 PRINT "THE STATUS RIGHT NOW:"
600 PRINT "SURFACE WIND SPEED: "; WS
610 PRINT "OUR SPEED :"; S
620 PRINT "BOMBS ON BOARD:"; B
630 PRINT "MILES TO TARGET: "; M
640 GOSUB 210
650 M = ABS(S - M) + 150
655 IF M < 300 THEN M = 275
660 PRINT: IF Q = 1 THEN 730
670 PRINT "WHEN YOU ENTER A COMMAND:"
680 PRINT "PRESS THE LETTER, THEN PRESS ENTER/RETURN."
690 PRINT "REMEMBER, TO CONTINUE FLIGHT, PRESS 'F',"
700 PRINT "THEN PRESS ENTER/RETURN."
720 GOSUB 210
730 PRINT "COMMAND";
740 INPUT B$: IF B$ <> "A" AND A = 2 THEN 1260
750 IF B$ = "A" THEN 1210
760 IF B$ = "O" THEN D = D: GOTO 845
770 IF B$ = "D" THEN 900
790 REM CONTINUE FLIGHT IF (F)
795 IF B$ <> "A" AND B$ <> "O" AND B$ <> "D" AND B$ <> "F" THEN PRINT "WHICH COMMAND IS THAT???": B$ = "": GOTO 730
800 IF M > 51 THEN M = M - INT(50 * RND(1) + 1)
805 IF M < 50 THEN M = M - 1: S = S - 10
806 IF S < 800 THEN S = 800
810 PRINT "MILES TO TARGET:"; M
820 PRINT "OUR SPEED:"; S
830 PRINT "SURFACE WIND SPEED:"; WS - 5
840 PRINT: B$ = " ": GOTO 730
845 IF D = 2 THEN PRINT "THEY'RE ALREADY OPEN !!": GOTO 840
850 PRINT "OPENING BOMBAY DOORS...."
860 FOR I = 1 TO S: NEXT
870 X = Y - 10: Y = X + 5
880 PRINT "BOMBAY DOORS OPEN...."
890 D = 2: GOTO 840
900 IF D <> 2 THEN 1140: REM DOORS NOT OPEN
910 PRINT "HOW MANY BOMBS TO BE RELEASED";
920 INPUT R
930 IF R > B THEN PRINT " YOU ONLY HAVE"; B; "BOMBS LEFT !!": GOTO 920
940 PRINT "IF YOU ARE READY"
950 PRINT "TO DROP"; R; "BOMBS, PRESS ENTER/RETURN";
960 INPUT X$
965 PRINT "BOMBS A W W W A A Y !!"
970 FOR Q = M TO S
980 X = X - 1: Y = Y - 1: IF INT((X + Y) / WS) + M <= 0 THEN 1010
990 IF INT((X + Y) / WS) + M = LL THEN 1090
995 IF LL > 15 THEN LL = LL - 2: IF LL < INT((X + Y) / WS) + M THEN 1010
1000 NEXT
1010 REM MISSED
1020 PRINT
1030 PRINT "YOU MISSED THE "; T$;: IF Z = 1 THEN PRINT " (AGAIN).": GOTO 1040
1035 PRINT: Z = 1
1040 IF LL < 20 THEN PRINT "BUT CAME CLOSE TO THE "; Q$
1050 B = B - R: IF B <= 0 THEN 1070
1060 PRINT "WE STILL HAVE"; B; "BOMBS LEFT....": GOTO 1080
1070 PRINT "YOU'VE USED YOUR AMOUNT OF BOMBS TOO !!": GOTO 1350
1080 Q = 1: RESTORE
1085 PRINT "BOMBAY DOORS CLOSED.....": GOTO 280
1090 REM ON TARGET
1100 PRINT
1110 PRINT "EXCELLENT BOMBING !!"
1120 PRINT "WE DONT HAVE TO WORRY ABOUT THE "; T$; " NOW."
1130 S$ = T$: GOTO 1050
1140 REM DOORS NOT OPEN
1150 PRINT
1160 PRINT "YOU'VE JUST DROPPED THE BOMBS - WITHOUT OPENING"
1170 PRINT "THE DOORS..... TURKEY !!"
1180 PRINT
1190 PRINT "ABORT MISSION TO DEFUSE BOMBS...."
1200 A = 2: GOTO 730
1210 REM ABORT MISSION
1220 PRINT
1230 PRINT "YOU'VE ABORTED THE MISSION......"
1240 PRINT "JUST REMEMBER WHY, SO THE SAME"
1250 PRINT "MISTAKE WON'T HAPPEN AGAIN !!"
1255 GOTO 1350
1260 REM DID NOT ABORT
1270 CLS
1280 PRINT
1290 PRINT "B B B B R R R R O O O O O M M !!!!"
1300 PRINT "YOU WERE ORDERED TO ABORT THE MISSION"
1310 PRINT "AND YOU DIDN'T...."
1320 PRINT "START FLAPPING YOUR WINGS, WE HAVEN'T"
1330 PRINT "ANY PARACHUTES ! !"
1350 REM PLAY AGAIN ?
1360 GOSUB 210
1370 PRINT "WOULD LIKE TO TRY AGAIN TODAY";
1380 INPUT A$
1390 IF A$ <> "YES" THEN 1410
1400 Z = 0: Q = 0: RESTORE: GOTO 10
1410 PRINT
1420 PRINT "TRY TO BE MORE CONSERVATIVE WITH"
1430 PRINT "THE BOMBS NEXT TIME!"
1440 END

