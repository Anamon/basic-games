1 DIM B$(100): RANDOMIZE TIMER
5 CLS: PRINT: PRINT: PRINT TAB(10); "* GENERAL *"
7 PRINT: PRINT
10 INPUT "DO YOU NEED INSTRUCTIONS LISTED"; A$
12 IF A$ = "YES" THEN GOSUB 700
20 CLS: PRINT: E(5) = 0: B = 0
23 PRINT TAB(8); "X POSITION IS BETWEEN 1 AND 10"
30 REM YOUR TROOPS
40 A = 2E+04
50 REM ENEMY TROOPS PER RUN
60 E1 = INT(500 * RND(1) + 50)
70 IF E1 < 350 THEN 60
75 E3 = E1 - INT(200 * RND(1) + 10): E2 = E1
80 REM SET ENEMY AREA, POSITION
90 X = INT(10 * RND(1) + 1)
95 Y = INT(4 * RND(1) + 1): ON Y GOTO 100, 110, 120, 130
100 Y$ = "NORTH": GOTO 140
110 Y$ = "EAST": GOTO 140
120 Y$ = "SOUTH": GOTO 140
130 Y$ = "WEST"
140 I = INT(2 * RND(1) + 1)
145 ON I GOTO 150, 210
150 PRINT
155 PRINT "I WILL RELEASE THE ENEMY'S X AREA, THIS TIME,"
160 PRINT "BUT YOU MUST SELECT THEIR POSITION."
170 PRINT "THEY'RE LOCATED AT AREA"; X; "BUT THIS IS ONLY"
180 PRINT "TEMPORARY AND COULD QUICKLY CHANGE."
190 IF X < 5 THEN X = INT(10 * RND(1) + 1)
200 GOTO 235
210 PRINT
215 PRINT "I WILL NOT RELEASE THE ENEMY'S AREA,"
220 PRINT "BUT I WILL TELL YOU THEIR ATTACKING FORCES ARE"
230 PRINT "GREATER THAN"; E3; "TROOPS; WATCH THE ENEMY!!"
235 PRINT
240 PRINT "WHERE DO YOU WANT YOUR TROOPS LOCATED?"
250 PRINT "INDICATE POSITION (N,E,S,W)";
255 INPUT B$
260 PRINT "INPUT AREA";
270 INPUT C
280 PRINT "AND HOW MANY OF YOUR TROOPS DO YOU WANT"
285 PRINT "LOCATED AT THIS AREA";
290 INPUT A1
295 IF D2 > 0 AND A1 > TL THEN PRINT "YOU HAVEN'T GOT THAT MANY TROOPS !!": GOTO 290
300 REM UPDATE
320 B = B + 1
325 IF B$ = LEFT$(Y$, 1) AND C = X THEN 340
330 GOSUB 1000: GOTO 460
340 PRINT: PRINT "WELL, YOU'VE ENTERED THE POSITION"
350 PRINT "AND AREA CORRECTLY!!"
360 IF E2 > A1 THEN 900
370 E(5) = E2 + E(5): IF E(5) > 12E+03 THEN 960
380 PRINT "YOU'VE CAPTURED AN AMOUNT OF"; E1
390 PRINT "ENEMY TROOPS, SENT OUT ON THIS BATTLE."
400 PRINT "BUT YOU COULD BE IN FOR ANOTHER ATTACK."
405 PRINT "TOTAL ENEMY CAPTURED OR LOST:"; E5
410 TL = TL + A1: GOTO 50
460 PRINT: PRINT "TOTAL BATTLES:"; B
470 PRINT "THE ENEMY WAS AT POSITION:"; Y$
480 PRINT " AND LOCATED AT AREA:"; X
481 PRINT: PRINT " YOUR STATUS THIS BATTLE:"
482 PRINT "TROOPS LOST:"; TT
483 PRINT "TROOPS THAT RETREATED:"; R1
484 PRINT "TROOPS THAT DESERTED:"; D
485 PRINT "TROOPS LEFT TO BATTLE ARE:"; TL
486 IF B$ = LEFT$(Y$, 1) AND C = X AND E2 > A1 THEN 488
487 GOTO 495
488 PRINT: PRINT "GENERAL, THE REASON YOU LOST THIS BATTLE WAS -"
490 PRINT "THE ENEMY FORCES WERE"; E2; " OUTNUMBERING YOURS !"
495 PRINT: INPUT "PRESS ENTER/RETURN"; X: CLS: IF TL < 50 THEN 520
500 R1 = 0: GOTO 50
520 T = INT(4 * RND(1) + 1): ON T GOTO 530, 560, 590
530 PRINT: PRINT "YOU'VE LOST THIS BATTLE, HAD"; B; "TRIES, AND"
540 PRINT "DIDN'T COME ALL THAT CLOSE, WITH THAT I SAY... SO LONG !"
550 GOTO 620
560 PRINT: PRINT "ALL I CAN SAY IS MAYBE NEXT TIME YOU'LL WATCH"
570 PRINT "THEIR ADVANCE MORE CLOSELY."
575 PRINT "YOU'VE LOST THE BATTLE GENERAL AND ALL YOUR MEN."
576 PRINT "TO BEAT IT ALL YOU HAD"; B; "TRIES....."
580 GOTO 620
590 PRINT: PRINT "AWAY... AWAY, ANCHORS THAT IS, MAYBE YOU SHOULD"
600 PRINT "TAKE UP SAILING; AT LEAST YOU COULD TAKE A SWIM."
610 PRINT "SO LONG, PRIVATE !!"
620 PRINT: PRINT
630 PRINT "WOULD YOU LIKE TO BATTLE THE COMPUTER'S FORCES AGAIN";
650 INPUT A$
660 IF A$ = "YES" THEN A = 0: TL = 0: TT = 0: D = 0: D2 = 0: GOTO 10
665 PRINT
670 PRINT "I DIDN'T THINK YOU COULD STAND ANOTHER"
680 PRINT "BATTLE THIS EARLY IN THE DAY...."
690 END
700 PRINT
710 PRINT "YOU ARE THE GENERAL OF AN ARMY, YOU'LL HAVE 20,000"
720 PRINT "MEN UNDER YOUR TOTAL COMMAND. THE COMPUTER WILL SELECT"
730 PRINT "AN AMOUNT OF ENEMY TROOPS PER RUN AND PLACE THEM IN"
740 PRINT "A RANDOM AREA. THE COMPUTER WILL THEN DECIDE IF IT"
750 PRINT "WILL OUTPUT THE ENEMY'S X AREA. THE X AREA WILL"
760 PRINT "RANGE FROM 1 TO 10."
765 PRINT: INPUT "TO CONTINUE INSTRUCTIONS PRESS ENTER/RETURN"; X: CLS
767 PRINT: PRINT
770 PRINT "YOU'LL PLACE YOUR MEN AT POSITIONS - NORTH, EAST"
780 PRINT "SOUTH OR WEST, AND SELECT AN AREA (BETWEEN 1 AND 10)."
790 PRINT "YOU'LL THEN INPUT AN AMOUNT OF MEN TO PLACE"
800 PRINT "AT THAT AREA."
810 PRINT "IF YOU MATCH THE ENEMY'S POSITION/AREA, THEY'LL BE"
820 PRINT "IN FOR A SURPRISE. IF YOU MISS, YOU'LL LOSE A"
825 PRINT "SIZEABLE AMOUNT OF MEN."
826 PRINT
827 PRINT "IF YOU CAPTURE 12,000 ENEMY TROOPS YOU WIN, IOF YOUR"
828 PRINT "TROOPS DECREASE BELOW 50--YOU'LL LOSE."
830 PRINT: INPUT "TO BEGIN PRESS ENTER/RETURN"; X: CLS
840 RETURN
900 R1 = INT(30 * RND(1) + 5): IF R1 < 20 THEN 900
910 PRINT
915 PRINT "I'M SORRY. THE ENEMY'S TROOPS WERE"; E1
920 PRINT "AND YOU ONLY SENT"; A1; "TO THAT AREA."
925 PRINT "ONLY"; R1; "OF YOUR TROOPS RETREATED....."
930 PRINT "AS THEY FIGURED THEY WERE OUT-NUMBERED."
950 TT = A1 - R1: F = 1: FOR I = 1 TO 2500: NEXT
955 GOSUB 1000: GOTO 460
960 PRINT
965 PRINT "GOOD SHOW GENERAL, YOU'VE FRACTURED THE ENTIRE"
970 PRINT "ENEMY FORCE OF"; E(5); "MEN AND YOUR TROOPS ARE"
980 PRINT "RESTED UP AND READY TO GO AGAIN!!"
990 GOTO 620
1000 REM UPDATE
1005 IF F > 0 THEN 1040
1010 D = INT(10 * RND(1) + 5)
1020 TT = A1 - INT(150 * RND(1))
1030 IF A1 < 150 THEN TT = A1 - INT(50 * RND(0))
1040 A = A - TT - D
1050 TL = A
1060 F = 0: D2 = 1: RETURN

