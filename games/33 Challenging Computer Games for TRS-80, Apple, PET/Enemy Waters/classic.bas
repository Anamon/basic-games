5 RANDOMIZE TIMER
10 CLS: PRINT TAB(8); "***** ENEMY WATERS *****"
15 PRINT: PRINT: PRINT
20 PRINT TAB(8); "DO YOU NEED INSTRUCTIONS";
30 INPUT A$
40 IF A$ = "YES" THEN 60
45 IF A$ = "NO" THEN CLS: PRINT: PRINT: GOTO 130
50 PRINT "I AM NOT PROGRAMMED TO RESPOND TO THAT ANSWER...": GOTO 20
60 CLS: PRINT
70 PRINT "IN THIS SIMULATION YOU AND THE COMPUTER"
80 PRINT "ARE IN CONTROL OF AN EXPERIMENTAL SUB."
92 PRINT "TORPEDOS WILL BE DETONATED BY TIMERS"
93 PRINT "INSTEAD OF IMPACT."
94 PRINT "YOU CAN SET THE TIMER FROM 2 TO 9 SECONDS."
95 PRINT "THE REASON FOR THE SHORT INTERVALS?"
96 PRINT "HIGH SPEED TORPEDOS.": GOSUB 127
97 PRINT "JUST REMEMBER...."
98 PRINT "YOUR TORPEDOS MUST EXPLODE WITHIN 25 YARDS."
99 PRINT "MEANING THE FARTHER AWAY THE SHIP THE LONGER"
100 PRINT "INTERVAL YOU'LL NEED. THE COMPUTER WILL PRINT YOUR"
110 PRINT "AMOUNT OF TORPEDOS ON BOARD."
120 PRINT "WHEN THE COMPUTER OUTPUTS TO YOU THE FORMAT WILL BE"
125 PRINT "COMPUTER: (MESSAGE TO YOU)": GOSUB 127: GOTO 130
127 PRINT: INPUT "TO EXIT PRESS ENTER/RETURN....."; X: CLS: PRINT: PRINT: RETURN
130 PRINT "**** INPUT COMMANDS *****"
140 PRINT "(1) FIRE TORPEDO ONE - 200 Y.P.S."
150 PRINT "(2) FIRE TORPEDO TWO - 250 Y.P.S."
160 PRINT "(3) FIRE TORPEDO THREE - 300 Y.P.S."
170 PRINT "(4) FIRE TORPEDO FOUR - 350 Y.P.S."
175 PRINT "(5) AMOUNT OF TORPEDOS LEFT"
180 PRINT "Y.P.S. = YARDS PER SECOND."
210 GOSUB 127
215 D = 0
220 PRINT "YOU ARE NOW BATTLE READY, AND IN ENEMY TERRITORY."
230 PRINT "COMPUTER: OUTPUT AMOUNT OF TORPEDOS IN 5 SECONDS."
235 FOR I = 1 TO 1500: NEXT
240 REM AMOUNT OF TORPEDOS
245 T = INT(30 * RND(1) + 5)
246 IF T < 15 THEN T = 15
250 PRINT "COMPUTER: "; T; "TORPEDOS FOR THIS MISSION."
270 PRINT
280 REM Y= AMOUNT OF YARDS TO ENEMY SHIP
300 Y = INT(2900 * RND(1) + 29): IF Y < 2400 THEN 300
310 PRINT "COMPUTER: RADAR SHOWS ENEMY SHIP AT"; Y; "YARDS."
400 FOR I = 1 TO 1500: NEXT
410 Y = Y - INT(500 * RND(1) + 50)
420 PRINT "SHIP NOW AT"; Y; "YARDS AND CLOSING."
425 PRINT "SELECT COMMAND";
430 IF T <= 1 THEN 650
432 IF D > 8 THEN 790
433 IF Y < 300 THEN 1500
435 INPUT N: IF N < 1 OR N > 5 THEN PRINT "SIR, THIS SUB NOT EQUIPPED WITH COMMAND"; N: GOTO 425
440 ON N GOTO 450, 500, 550, 610, 740
450 GOSUB 1200
460 PRINT "FIRING TORPEDO # "; N; "....."; T = T - 1
465 PRINT "AT 200 YARDS PER SECOND."
470 GOSUB 1060: CLS
480 PRINT "COMPUTER:"
485 M = (TI * 200)
490 GOTO 1300
495 GOSUB 1100
497 GOTO 700
500 GOSUB 1200: PRINT "FIRING TORPEDO #"; N; ".....": T = T - 1
505 PRINT "AT 250 YARDS PER SECOND."
510 GOSUB 1060: CLS
520 PRINT "COMPUTER:"
525 M = (TI * 250)
530 GOTO 1310
535 GOSUB 1100: IF I = 3 OR I = 4 THEN 425
545 GOTO 580
550 GOSUB 1200: PRINT "FIRING TORPEDO #"; N; ".....": T = T - 1
555 PRINT "AT 300 YARDS PER SECOND."
560 M = (TI * 300)
570 GOTO 1310
575 GOSUB 1100: IF I = 3 OR I = 4 THEN 425
580 PRINT "COMPUTER: LARGE TANKER....."
590 GOSUB 1400
600 PRINT "CLOSING FAST AT"; Y; "YARDS.....": GOTO 425
610 GOSUB 1200: PRINT "FIRING TORPEDO #"; N; ".....": T = T - 1
615 PRINT "AT 350 YARDS PER SECOND."
620 GOSUB 1060: CLS
630 PRINT "COMPUTER:"
635 M = (TI * 350)
640 GOTO 1310
645 GOSUB 1100: IF I = 3 OR I = 4 THEN 425
647 GOTO 700
650 PRINT: PRINT "COMPUTER:"
660 PRINT "YOU ARE OUT OF TORPEDOS....."
670 PRINT "BUT YOU HAVE DESTROYED OR DAMAGED"; D; "SHIPS."
680 PRINT "RETURN TO PORT BEFORE YOU ARE DEPTH CHARGED....."
690 GOTO 810
700 PRINT: PRINT "LARGE DESTROYER CLOSING AT"; Y; "YARDS, EQUIPPED"
710 PRINT "WITH ARMOR PLATING AND LOADED WITH DEPTH CHARGES."
720 PRINT "DON'T LET THIS ONE GET TOO CLOSE....."
730 GOTO 425
740 PRINT "AMOUNT OF TORPEDOS LEFT: "; T: PRINT: GOTO 425
790 PRINT: PRINT "COMPUTER:"
800 PRINT "RADAR SHOWS NO ENEMY SHIPS APPROACHING....."
810 PRINT ".....MISSION SUCCESSFUL."
820 PRINT: PRINT "COMPUTER:"
830 PRINT "WOULD YOU LIKE TO DESTROY SOMEMORE SHIPS TODAY";
840 INPUT A$
850 IF A$ = "YES" THEN 10
860 PRINT: PRINT "WELL THEN... SEE YOU NEXT TIME ! !": END
870 REM TOO FAR
880 PRINT "CHECK YOUR TIMING RATE.... THAT TORPEDO HAS JUST"
890 PRINT "PASSED UP THE SHIP AND EXPLODED"; (M - Y); "YARDS PAST"
900 PRINT "lT ! ! KEEP THIS UP AND YOU'LL END UP ON"
910 PRINT "THE BOTTOM WITH ALL THE FISHES....."
920 Y = Y - INT(200 * RND(1) + 50)
930 PRINT "SHIP NOW AT"; Y; "YARDS, CLOSING FAST"
940 GOTO 425
1060 PRINT: PRINT
1065 PRINT "COMPUTER: OUTPUT STATUS OF TORPEDO IN"; TI; "SECONDS."
1070 FOR TT = 1 TO 250
1075 FOR TN = 1 TO TI: NEXT TN, TT
1080 P = INT(10 * RND(1) + 1): IF P < 2 AND TI < 2 THEN 1450
1090 RETURN
1100 D = D + 1: I = INT(4 * RND(1) + 1): ON I GOTO 1110, 1130, 1150, 1170
1110 PRINT "TORPEDO HAS SEVERELY DAMAGED SHIP INOPERABLE."
1120 IF Y < 500 THEN GOSUB 1400
1122 Y = Y - 100 + INT(30 * RND(1) + 10)
1125 RETURN
1130 PRINT "TORPEDO #"; N; "HAS MADE HEAVY DAMAGE TO SHIP....."
1135 PRINT "IT'S BEGINNING TO SINK !!"
1140 IF Y < 500 THEN GOSUB 1400
1144 Y = Y - 200 + INT(40 * RND(1) + 10)
1145 RETURN
1150 PRINT "TORPEDO HAS BLOWN APART MAIN DECK OF SHIP."
1155 IF Y < 500 THEN GOSUB 1400
1160 Y = Y - 175 + INT(20 * RND(1) + 10): PRINT "ANOTHER SHIP AT"; Y; "YARDS....."
1165 RETURN
1170 PRINT "TORPEDO HAS COMPLETELY BLOWN APART SHIP. ANOTHER"
1172 IF Y < 500 THEN GOSUB 1400
1175 Y = Y - 25 + INT(50 * RND(1) + 5)
1180 PRINT "SHIP CLOSING ON YOU AT"; Y; "YARDS.....": RETURN
1200 PRINT "SET TIMER FOR HOW MANY SECONDS";
1210 INPUT TI
1220 IF TI > 9 THEN PRINT "CAN'T RESPOND -- TIMING RATE TO HIGH...": GOTO 1200
1230 RETURN
1250 PRINT "YOUR TIMER SETTING WAS OFF, TORPEDO EXPLODED"
1260 PRINT "PREMATURELY... NOTE DISTANCE TO SHIP BEFORE SETTING"
1270 PRINT "TIMER. TORPEDO EXPLODED"; (Y - M); "YARDS THIS SIDE OF SHIP.": REM NOT FAR ENOUGH
1275 IF Y < 500 THEN GOSUB 1400
1280 IF Y < 2000 THEN Y = Y - INT(100 * RND(1) + 30)
1285 IF Y > 2000 THEN Y = Y - INT(275 * RND(1) + 50)
1290 PRINT "DISTANCE TO SHIP CLOSING, NOW AT"; Y; "YARDS."
1300 GOTO 425
1310 IF ABS(Y - M) > 0 AND ABS(Y - M) < 31 THEN 1340
1320 IF (M - Y) < 0 THEN 1250
1330 IF ABS(Y - M) > 31 THEN 870
1340 ON N GOTO 495, 535, 575, 645
1400 Y = (2800 - INT(50 * RND(1) + 5))
1410 RETURN
1450 PRINT: PRINT "COMPUTER:"
1460 PRINT "CAN'T REPORT ANY DAMAGE TO THE ENEMY SHIP...."
1470 PRINT "THE TORPEDO YOU JUST FIRED CAUGHT IN THE TUBE"
1480 PRINT "AND BLEW OUR SUB ALL OVER THE OCEAN FLOOR !!!"
1490 GOTO 820
1500 FOR I = 1 TO 1000: NEXT
1510 CLS: PRINT
1520 PRINT "WE'RE SINKING !!"
1530 PRINT "HEAVY WATER DAMAGE COMING THROUGH"
1540 PRINT "ENGINE ROOM !!"
1550 PRINT: PRINT "SHIP WAS LESS THAN"; Y; "YARDS,"
1560 PRINT "AND DEPTH CHARGED US !!"
1570 PRINT "BULB.... BULB.... BULB....."
1590 FOR I = 1 TO 2500: NEXT
1600 CLS: PRINT: GOTO 820

