10 CLEAR 500: DIM A(20), W(20), A$(16), S$(16), T$(16), W$(16)
15 RANDOMIZE TIMER
20 CLS: PRINT: PRINT
30 FOR I = 1 TO 15: READ A$(I): NEXT
40 DATA COMPUTER,LOST,INTELLIGENT,WONDER,ABSENT,SOFTLY
50 DATA SECOND,HEREAFTER,MORNING,SYSTEM,QUESTIONS,SILVER
60 DATA SWATH,TEMPERATURE,ENTERTAINMENT
110 PRINT TAB(15); "***** MEMORY TEST *****"
120 PRINT: PRINT "RATE YOUR MEMORY (BY NUMBER ONLY)"
130 PRINT "(1) EXCELLENT"
140 PRINT "(2) GOOD"
150 PRINT "(3) FAIR"
160 PRINT "(4) POOR"
170 INPUT N
175 IF N < 1 OR N > 4 THEN PRINT "TRY A NUMBER THAT'S LISTED !!": GOTO 170
180 ON N GOTO 190, 200, 210, 220
190 T = 400: V = 15: Y = V: A$ = "EXCELLENT": GOSUB 1500: GOTO 230
200 T = 700: V = 10: Y = V: A$ = "GOOD": GOSUB 1500: GOTO 230
210 T = 900: V = 8: Y = V: A$ = "FAIR": GOSUB 1500: GOTO 230
220 T = 1000: V = 6: Y = V: A$ = "POOR": GOSUB 1500
230 PRINT: IF N <> 1 AND N <> 2 THEN 260
240 PRINT "WITH YOUR MEMORY ("; A$; "), YOU SHOULDN'T HAVE"
250 PRINT "TOO MANY PROBLEMS WITH THIS TEST."
260 PRINT "YOU WILL SEE"; V; "WORDS, PRINTED ON THE SCREEN."
270 PRINT "AFTER THESE WORDS ARE ERASED YOU WILL INPUT THEM"
280 PRINT "IN THE ORDER THEY APPEARED."
290 REM SET PRINT AREA / TIME LOOP
295 PRINT: INPUT "PRESS ENTER/RETURN TO BEGIN"; X$
300 X = 25: CLS: PRINT: PRINT: PRINT: PRINT: PRINT
310 FOR I = 1 TO V
320 PRINT TAB(X - 10); "("; I; ")"; TAB(X); T$(I)
330 ST = TIMER + INT(T / 400)
335 IF TIMER < ST THEN 335
340 CLS: PRINT: PRINT: PRINT: PRINT: PRINT
350 NEXT I
360 CLS: PRINT
370 PRINT "NOW YOU WILL INPUT THE WORDS, STARTING WITH THE FIRST"
380 PRINT "WORD THAT WAS PRINTED."
390 FOR W = 1 TO V
400 PRINT "ENTER WORD #"; W; " ";
410 INPUT W$(W)
420 IF W$(W) <> T$(W) THEN Y = Y - 1: A$(W) = "": W(W) = W(W) + W
430 NEXT
435 IF Y = V THEN 490
440 PRINT: PRINT "INCORRECT WORDS:"
450 FOR I = 1 TO V
460 IF W(I) > 0 THEN PRINT W(I); ",";
480 NEXT: PRINT
485 GOTO 700
490 REM ALL WORDS CORRECT
500 PRINT
510 PRINT "VERY WELL DONE--ALL WORDS CORRECT."
520 REM GOTO FINAL PART OF TEST
530 GOTO 800
700 PRINT "IF YOU MISSED MORE THAN 2 WORDS YOU SHOULD TRY THE"
710 PRINT "FIRST PART OF THE PROGRAM OVER, OR YOU MIGHT"
720 PRINT "NEVER NEVER MAKE IT THROUGH THE FINAL PART."
730 IF ABS(V - Y) > 2 THEN 750
740 PRINT "YOU DIDN'T SO.....": GOTO 800
750 PRINT "CHECKING YOUR SCORE, YOU MISSED"; (V - Y); "."
760 PRINT "WOULD YOU LIKE TO TRY THE FIRST PART AGAIN?";
770 INPUT I$
780 IF I$ = "YES" THEN FOR I = 1 TO V: W(I) = 0: NEXT: Y = V: GOTO 260
790 PRINT: PRINT "I HOPE YOU MAKE IT THROUGH THE FINAL PART !!"
800 PRINT
810 PRINT "PRESS ENTER/RETURN FOR FINAL PART OF TEST";
820 INPUT X$
830 CLS: PRINT
840 PRINT "KEEP A CLOSE EYE ON THESE WORDS, AS THE COMPUTER"
850 PRINT "MAY NOT ASK YOU TO INPUT THEM IN THE ORDER THEY"
860 PRINT "WILL BE PRINTED."
870 PRINT: INPUT "PRESS ENTER/RETURN TO BEGIN"; X$
880 PRINT
890 REM GET WORDS AGAIN
900 RESTORE
910 FOR I = 1 TO 15: READ A$(I): NEXT
920 FOR I = 1 TO 10
930 S = INT(10 * RND(1) + 1)
940 IF B(S) <> 0 THEN 930
950 S$(I) = A$(S)
960 B(S) = 1
970 NEXT
980 X = 15: Q = 1: R = 2: Z = X
990 ST = TIMER + 2
995 IF TIMER < ST THEN 995
996 CLS: PRINT: PRINT: PRINT: PRINT: PRINT
1000 PRINT TAB(X - 5); Q; ")"; TAB(X); S$(Q); TAB(X + 15); R; ")"; TAB(X + 20); S$(R)
1010 Q = Q + 2: R = Q + 1
1020 Z = Z - 2
1030 IF Z <> 5 THEN 990
1040 REM TIME LOOP
1050 ST = TIMER + 2
1055 IF TIMER < ST THEN 1055
1056 CLS: PRINT
1060 PRINT "YOU NOTICED THAT THE WORDS APPEARED IN PAIRS"
1070 PRINT "AND THERE WERE 10 OF THEM. TO SUCCEED THIS PART"
1080 PRINT "OF THE TEST, YOU MUST INPUT THE WORDS THE WAY"
1090 PRINT "THE COMPUTER ASKS YOU TO."
1100 I = INT(8 * RND(1) + 1): C = I
1110 IF I < 5 THEN 1100
1120 PRINT "YOU'LL ENTER"; I; "OF THE 10 WORDS."
1130 PRINT: PRINT "INPUT THE FOLLOWING WORDS:"
1140 FOR W = 1 TO I
1150 N = INT(10 * RND(1) + 1)
1160 IF E(N) <> 0 THEN 1150
1170 PRINT "ENTER WORD # "; N;
1180 INPUT W$(W): IF W$(W) <> S$(W) THEN C = C - 1
1190 E(N) = 1
1200 NEXT
1210 CLS: PRINT
1220 IF C < 5 THEN 1260
1230 PRINT "OUT OF THE"; I; "WORDS, YOU'VE REMEMBERED"; C
1240 PRINT "OF THEM, NOT BAD AT ALL !!"
1250 PRINT: PRINT "END OF PROGRAM.....": END
1260 PRINT "YOU COULD HAVE CHEATED AND COME UP WITH A BETTER"
1270 PRINT "SCORE THAN THIS. "; C; "CORRECT OUT OF ONLY"; I; "WORDS !!!"
1280 GOTO 1250
1500 REM SELECT NUMBER OF RANDOM WORDS
1505 FOR I = 1 TO 15
1510 M = INT(15 * RND(1) + 1)
1515 IF A(M) <> 0 THEN 1510
1520 T$(I) = A$(M)
1525 A(M) = 1
1530 NEXT
1540 RETURN

