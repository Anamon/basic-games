100 REM AUTO RACE
110 REM COPYRIGHT FREDERICK HOLTZ AND ASSOCIATES 2/23/83
120 REM PROGRAM RUNS IN TI BASIC
130 CALL CLEAR
140 GOSUB 520
150 RANDOMIZE
160 CALL COLOR(1,7,2)
170 CALL COLOR(2,5,2)
180 CALL COLOR(4,11,2)
190 CALL CLEAR
200 A$="FFFFFFFFFFFFFFFF"
210 B$="0000000000000000"
220 C$="0066FFFFFFFF6600"
230 CALL CHAR(60,A$)
240 CALL CHAR(34,B$)
250 CALL VCHAR(1,28,60,24)
260 CALL HCHAR(12,1,60,28)
270 CALL CHAR(33,C$)
280 CALL CHAR(40,C$)
290 FOR X=1 TO 29
300 CALL SOUND(500,-5,0)
310 ZZ=X+INT(RND*1.5)
320 YY=X+INT(RND*1.5)
330 CALL HCHAR(5,ZZ,33)
340 CALL HCHAR(20,YY,40)
350 IF ZZ>=27 THEN 420
360 IF YY>=27 THEN 420
370 FOR Y=1 TO 75
380 NEXT Y
390 CALL HCHAR(5,ZZ,34,2)
400 CALL HCHAR(20,YY,34,2)
410 NEXT X
420 IF ZZ=YY THEN 500
430 IF ZZ<YY THEN 460 ELSE 440
440 PRINT "RED CAR WINS"
450 GOTO 470
460 PRINT "BLUE CAR WINS"
470 PRINT "PRINT ENTER TO RACE AGAIN"
480 INPUT ER$
490 GOTO 190
500 PRINT "THE RACE ENDS IN A TIE"
510 GOTO 470
520 PRINT "WELCOME TO AUTO RACE"
530 PRINT "COMPUTER STYLE.  THIS IS"
540 PRINT "A GAME OF CHANCE WHICH"
550 PRINT "USES ON-SCREEN GRAPHICS"
560 PRINT "AND ANIMATION TO SIMULATE"
570 PRINT "AN AUTOMOTIVE DRAG RACE."
580 PRINT
590 PRINT "TWO DRAGSTERS WILL RACE"
600 PRINT "ACROSS THE DISPLAY SCREEN."
610 PRINT "YOU AND A PLAYING PARTNER"
620 PRINT "CAN CHOOSE A RACE CAR AND"
630 PRINT "SEE WHO THE WINNER WILL BE."
640 PRINT "YOU CAN ALSO PLAY ALONE BY"
650 PRINT "CHOOSING ONE CAR AND ALLOWING"
660 PRINT "THE COMPUTER TO BE REPRE-"
670 PRINT "SENTED BY THE OTHER.  GOOD"
680 PRINT "LUCK!!!"
690 PRINT
700 PRINT
710 PRINT "PRESS ENTER TO BEGIN"
720 INPUT ER$
730 RETURN
