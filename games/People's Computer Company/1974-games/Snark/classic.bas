100 REM *** SNARK ... CATCH HIM WITH A WELL PLACED CIRCLE
110 REM *** PEOPLE'S COMPUTER COMPANY, MENLO PARK CA
120 REM *** SOME COMPUTERS NEED 'RANDOM' HERE
130 RANDOMIZE TIMER
150 PRINT "WANT THE RULES (1=YES  0=NO)";
160 INPUT Z
170 IF Z = 0 THEN 510
200 REM *** HERE ARE THE RULES
210 PRINT
220 PRINT "A SNARK IS HIDING IN A 10 BY 10 GRID LIKE THE ONE"
230 PRINT "SHOWN BELOW:"
240 PRINT
245 PRINT " Y"
250 FOR Y = 9 TO 0 STEP -1
260 PRINT Y; " . . . . . . . . . ."
270 NEXT Y
280 PRINT
290 PRINT TAB(4); " 0 1 2 3 4 5 6 7 8 9 X"
300 PRINT
310 PRINT "TRY TO CATCH HIM. HERE'S HOW ... WHEN I ASK, YOU TYPE"
320 PRINT "THE X,Y COORDINATES OF A GRIDPOINT (IF YOU DON'T KNOW"
330 PRINT "WHAT THAT MEANS, ASK SOMEBODY!) AND PRESS THE RETURN"
340 PRINT "KEY. THEN, WHEN I ASK FOR 'RADIUS', YOU TYPE THE RADIUS"
350 PRINT "OF A CIRCLE CENTERED ON THE GRIDPOINT WHOSE X,Y"
360 PRINT "COORDINATES YOU JUST ENTERED. I WILL THEN TELL YOU"
370 PRINT "WHETHER THE SNARK IS 'INSIDE' YOUR CIRCLE, 'OUTSIDE'"
380 PRINT "YOUR CIRCLE, OR 'ON' YOUR CIRCLE."
390 PRINT
400 PRINT "!!! IMPORTANT !!! IF YOU THINK YOU KNOW WHERE HE IS"
410 PRINT "HIDING, ENTER 0 (ZERO) AS THE RADIUS. GOOD HUNTING."
500 REM *** HIDE THE SNARK
510 LET X = INT(10 * RND(1))
520 LET Y = INT(10 * RND(1))
530 PRINT
540 PRINT "SNARK IS HIDING ... START GUESSING!"
600 REM *** GUESSING BEGINS
610 K = 1
620 PRINT
630 PRINT "COORDINATES";
640 INPUT A, B
650 LET D2 = (X - A) * (X - A) + (Y - B) * (Y - B)
660 PRINT "RADIUS";
670 INPUT R
680 IF R <> 0 THEN 700
690 IF D2 = 0 THEN 910
700 IF D2 < R * R THEN 730
710 IF D2 > R * R THEN 750
720 IF D2 = R * R THEN 770
730 PRINT "SNARK IS INSIDE YOUR CIRCLE"
740 GOTO 780
750 PRINT "SNARK IS OUTSIDE YOUR CIRCLE"
760 GOTO 780
770 PRINT "SNARK IS ON YOUR CIRCLE"
780 K = K + 1
790 GOTO 620
900 REM *** WE GOT A WINNER
910 PRINT
920 PRINT "YOU CAUGHT HIM IN"; K; "GUESSES!!!"
930 PRINT "GOOD SHOW!"
940 PRINT
950 PRINT "DO YOU WANT TO PLAY AGAIN (1=YES  0=NO)";
960 INPUT Z
970 IF Z = 1 THEN 510
999 END

