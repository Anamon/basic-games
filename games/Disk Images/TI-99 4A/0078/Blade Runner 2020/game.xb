50 C=0 :: CALL CLEAR
100 PRINT
110 PRINT "BLADE RUNNER 2020"
120 PRINT
130 PRINT "THE YEAR IS 2020 AND YOUR"
140 PRINT "PLANET IS PLAGUED BY KILLER"
150 PRINT "ROBOTS."
160 PRINT
170 PRINT "YOU WORK THE NIGHT SHIFT."
180 PRINT "WHILE ON YOUR BEAT YOU MUST"
190 PRINT "DESTROY ALL THE RED ROBOT"
200 PRINT "SHIPS AND THE EVIL STAR"
210 PRINT "RAIDER BEFORE DAWN."
220 PRINT
230 PRINT "FIRE A MISSILE FROM YOUR"
240 PRINT "BASE BY PRESSING THE FIRE"
250 PRINT "BUTTON. TO DETONATE THE"
260 PRINT "MISSILE PRESS FIRE AGAIN."
270 PRINT "EXPLODE YOUR MISSILE IN"
280 PRINT "FRONT OF A SHIP FOR BEST"
290 PRINT "RESULTS."
300 PRINT
310 PRINT "PRESS ENTER TO CONTINUE"
320 CALL KEY(0,A,B)
330 IF B=0 THEN 320
340 CALL CLEAR
350 PRINT
360 PRINT "DESTROY ONLY THE RED"
370 PRINT "SHIPS AND THE EVIL STAR"
380 PRINT "RAIDER. IF YOU HIT AN"
390 PRINT "INNOCENT SHIP YOU WILL"
400 PRINT "LOSE A BASE."
410 PRINT
420 PRINT "IF YOU DESTROY ALL THE"
430 PRINT "RED SHIPS YOU WILL GO"
440 PRINT "HEAD TO HEAD AGAINST"
450 PRINT "THE STAR RAIDER."
460 PRINT
470 PRINT "THE STAR RAIDER HAS A"
480 PRINT "CLOAKING DEVICE. IT CAN"
490 PRINT "APPEAR AND DISAPPEAR AT"
500 PRINT "WILL. YOUR SHOTS CAN"
510 PRINT "DRAW THE RAIDER OUT."
515 PRINT "YOU EARN ANOTHER BASE EACH  WEEK."
520 PRINT
522 PRINT "PRESS ENTER TO CONTINUE."
524 CALL KEY(0,A,B)
526 IF B=0 THEN 524 ELSE CALL CLEAR
528 GOTO 600
530 PRINT "USE JOYSTICK TO ADVANCE"
540 PRINT "SKILL. PRESS FIRE TO SELECT."
545 PRINT "USE 0 TO STOP"
547 PRINT :: PRINT
550 CALL JOYST(1,A,B) :: IF A<>0 OR B<>0 THEN J=1 :: GOTO 580
560 CALL JOYST(2,A,B) :: IF A<>0 OR B<>0 THEN J=2 :: GOTO 580
570 GOTO 550
580 FOR C=0 TO 3 STEP 1
585 DISPLAY AT(23,5):C
587 CALL KEY(J,A,B) :: IF A=18 THEN 595
589 CALL JOYST(J,A,B) :: IF A=0 AND B=0 THEN CALL SOUND(500,-1,30) :: GOTO 587
591 CALL SOUND(500,-1,30) :: NEXT C
595 IF C>3 THEN 580
596 IF C<1 THEN STOP
597 GOTO 650
600 CALL CHARSET :: PRINT "ENTER SKILL LEVEL           1 FOR BEGINNER              2 FOR INTERMEDIATE          3 FOR ADVANCED  "
605 PRINT
610 GOTO 530
650 CALL CLEAR
1000 DIM SHIP(20)
1010 GOTO 1120
1020 CALL SCREEN(12) :: DAY=DAY+1
1025 CALL POSITION(#21,RA,RB)
1030 CALL DELSPRITE(ALL) :: W=0
1040 DISPLAY AT(15,5):"YOU HAVE SURVIVED" :: DISPLAY AT(16,5):"DAY" :: DISPLAY AT(16,9):DAY
1050 IF DAY/5=INT(DAY/5)THEN READ R$ :: BASES=BASES+1 :: IF DAY/50=INT(DAY/50)THEN RESTORE
1060 GOSUB 1930 :: DISPLAY AT(17,5):R$ :: IF DAY/5=INT(DAY/5)THEN GOSUB 2080
1070 FOR A=1 TO 9
1080 IF SHIP(A)=1 THEN BASES=BASES-1 :: IF BASES<1 THEN DISPLAY AT(15,5):"YOU HAVE NOT SURVIVED" :: CALL SOUND(999,44733,9) :: GOTO 600 ELSE 1110
1090 NEXT A
1100 IF RA<>0 AND RB<>0 THEN BASES=BASES-1 :: IF BASES<1 THEN DISPLAY AT(15,5):"YOU HAVE NOT SURVIVED" :: CALL SOUND(999,44733,9) :: GOTO 600
1110 GOTO 1220
1120 SCORE=0 :: BASES=5 :: DAY=0
1130 CALL COLOR(3,16,1,4,16,1,5,16,1,6,16,1,7,16,1,8,16,1)
1140 A$="0303030303070F3F7F71767177777F7FC0C0C0C0C0E0F0FCFE8EB6B6B68EFEFE"
1150 B$="00000304083F7FFFFF7F3F00000000000000C02010FCFEFFFFFEFC0000000000"
1160 C$="00000000387CEED7D7EF7E3C00000000003F3F040C7EFFFFFFFFFF7E0C043F3F"
1170 D$="000000000001010101010107050100000000000000808080808080E0A0800000"
1180 F$="00000804261B071E3F470B130100000000008488B0F8E078FCE2D0C880000000"
1190 G$="0000040203030303030301000000000000002040C0C0C0C0C0C0800000000000"
1200 H$="183C56FDAFFB6E3D020705070603010000000000000078EEBBFF6FFBFFB5FEFC"
1210 CALL CHAR(140,F$) :: CALL CHAR(136,A$) :: CALL CHAR(128,B$) :: CALL CHAR(120,C$) :: CALL CHAR(112,D$) :: CALL CHAR(132,G$) :: CALL CHAR(124,H$)
1220 C=C+1 :: CALL CLEAR :: XX=190-(2*C+DAY) :: YY=215-(2*C+DAY) :: IF C>7 THEN C=8
1230 FOR A=0 TO 19 :: SHIP(A)=0 :: NEXT A
1240 CALL DELSPRITE(ALL)
1250 CALL SCREEN(2)
1260 CALL MAGNIFY(3)
1270 DISPLAY AT(24,2):"SCORE: ";SCORE
1280 DISPLAY AT(24,15):"BASES: ";BASES
1290 RANDOMIZE
1300 B=0 :: Z=11 :: GOSUB 1370
1310 B=32 :: Z=14 :: GOSUB 1370
1320 B=64 :: Z=17 :: GOSUB 1370
1330 CALL SPRITE(#28,124,11,XX,YY,0,0)
1340 CALL SPRITE(#21,112,2,4,35,0,-C-2)
1350 CALL SPRITE(#1,136,5,160,132,0,0)
1360 GOTO 1460
1370 FOR A=Z TO Z+2
1380 COLUMN=(RND*255)+1
1390 IF RND<.34 THEN COLOR=7 ELSE COLOR=12
1400 CALL SPRITE(#A,128,COLOR,32+B,COLUMN,0,C+(RND*5))
1410 CALL COINC(#A,#A-1,20,H) :: IF H=-1 THEN 1380
1420 CALL COINC(#A,#A-2,20,H) :: IF H=-1 THEN 1380
1430 IF COLOR=7 THEN SHIP(A-10)=1
1440 NEXT A
1450 RETURN
1460 CALL POSITION(#20,G,H) :: IF G>170 OR G=0 THEN CALL DELSPRITE(#20) :: T=1
1470 CALL LOCATE(#28,XX,YY) :: XX=XX-1 :: YY=YY-1 :: IF XX=0 THEN XX=256 ELSE IF YY=0 THEN YY=256 :: CALL PATTERN(#28,140) :: CALL COLOR(#28,2)
1480 IF YY=240 THEN CALL COLOR(#28,12)ELSE IF YY=236 THEN 1020
1490 CALL POSITION(#22,E,H) :: IF E>170 THEN CALL DELSPRITE(#22) :: W=0
1500 IF E<120 THEN 1530 ELSE CALL DISTANCE(#1,#22,R)
1510 IF R>90 THEN 1530 ELSE CALL PATTERN(#1,140) :: CALL SOUND(500,-7,0) :: CALL DELSPRITE(#22,#1,#20) :: W=0 :: BASES=BASES-1 :: DISPLAY AT(24,22):BASES
1520 IF BASES=0 THEN 1020 ELSE 1920
1530 CALL KEY(J,D,P) :: IF D<>18 THEN 1630
1540 IF T=0 THEN 1620
1550 CALL POSITION(#1,E,F) :: CALL DELSPRITE(#20)
1560 CALL SOUND(200,-2,5)
1570 CALL SPRITE(#20,112,12,E-7,F,-C-2,0)
1580 CALL POSITION(#21,G,H) :: IF H=256 THEN H=H-1
1590 IF RND<.6 AND W=0 THEN CALL SPRITE(#22,132,7,G+1,H+1,C+3,0) :: W=1 :: CALL PATTERN(#21,120) :: CALL COLOR(#21,16) :: GOTO 1610
1600 IF G=0 AND H=0 THEN 1610 ELSE IF W=0 THEN CALL COLOR(#21,2) :: CALL SPRITE(#22,132,7,G+1,F,C+3,0) :: W=1
1610 T=0 :: GOTO 1460
1620 CALL PATTERN(#20,140) :: CALL SOUND(500,-7,0) :: CALL MOTION(#20,0,0) :: T=1 :: GOTO 1660
1630 CALL JOYST(J,A,B)
1640 CALL MOTION(#1,0,2*A)
1650 GOTO 1460
1660 CALL POSITION(#20,G,H)
1670 S=21 :: IF G>13 THEN S=11 :: IF G>43 THEN S=14 :: IF G>73 THEN S=17
1680 FOR X=S TO S+2
1690 CALL DISTANCE(#22,#1,Q) :: IF Q>90 THEN 1710 ELSE CALL PATTERN(#1,140) :: CALL SOUND(500,-7,0) :: W=1 :: CALL DELSPRITE(#20)
1700 BASES=BASES-1 :: DISPLAY AT(24,22):BASES :: IF BASES=0 THEN 1020 ELSE 1920
1710 CALL DISTANCE(#X,G,H,R)
1720 IF R>53 THEN 1890
1730 CALL DELSPRITE(#20)
1740 CALL PATTERN(#X,140)
1750 CALL SOUND(400,-7,3)
1760 IF SHIP(X-10)=1 OR X=21 THEN SCORE=SCORE+1 :: SHIP(X-10)=0 ELSE BASES=BASES-1
1765 IF X=21 THEN W=1
1770 DISPLAY AT(24,9):SCORE
1780 DISPLAY AT(24,15):"BASES: ";BASES :: IF BASES=0 THEN 1020
1790 CALL DELSPRITE(#X)
1800 FOR A=1 TO 9
1810 IF SHIP(A)=1 THEN GOTO 1460
1820 NEXT A
1830 CALL POSITION(#21,A,B)
1840 IF A=0 AND B=0 THEN 1020
1850 FOR A=11 TO 19 :: CALL DELSPRITE(#A) :: NEXT A :: CALL MAGNIFY(4)
1860 GOTO 1460
1870 CALL DELSPRITE(#X)
1880 GOTO 1460
1890 NEXT X
1900 CALL DELSPRITE(#20)
1910 GOTO 1460
1920 GOTO 1350
1930 T=300 :: V=0 :: CALL SOUND(2*T,262,V)
1940 CALL SOUND(T,247,V)
1950 CALL SOUND(2*T,262,V)
1960 CALL SOUND(T,44733,30)
1970 CALL SOUND(2*T,262,V)
1980 CALL SOUND(T,247,V)
1990 CALL SOUND(2*T,262,V) :: CALL SOUND(1*T,44733,30)
2000 CALL SOUND(2*T,262,V) :: CALL SOUND(50,44733,9)
2010 CALL SOUND(T,247,V)
2020 CALL SOUND(2*T,220,V)
2030 CALL SOUND(T,196,V)
2040 CALL SOUND(2*T,220,V)
2050 CALL SOUND(T,247,V)
2060 CALL SOUND(2*T,262,V) :: CALL SOUND(T,247,V) :: CALL SOUND(3*T,262,V)
2070 RETURN
2080 V=0 :: T=300 :: CALL SOUND(2*T,330,V)
2090 CALL SOUND(T,294,V)
2100 CALL SOUND(2*T,262,V)
2110 CALL SOUND(T,247,V)
2120 CALL SOUND(2*T,220,V)
2130 CALL SOUND(T,247,V)
2140 CALL SOUND(2*T,262,V)
2150 CALL SOUND(T,247,V)
2160 CALL SOUND(3*T,262,V)
2170 RETURN
2180 DATA POLICE CADET,POLICE ROOKIE,POLICE OFFICER,POLICE SERGEANT,POLICE LIEUTENANT,POLICE DETECTIVE,POLICE CAPTAIN,POLICE COMMISIONER
2190 DATA POLICE CHAMPION,BLADE RUNNER,CHAMPION BLADE RUNNER
