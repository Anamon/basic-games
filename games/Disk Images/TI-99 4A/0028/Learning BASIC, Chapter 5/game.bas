10 RANDOMIZE
20 DIM B$(15)
30 QA=600
40 CALL CLEAR
60 CALL CHAR(102,"C0F0FCFFFCF0C000")
70 GOTO 440
80 FOR Q=1 TO QA
90 NEXT Q
100 RETURN
110 LC=16.02
120 R=INT(LC)
130 QQ=0
140 C=100*(LC-R)
150 FOR Q=1 TO LEN(A$)
160 QQ=QQ+1
170 IF C+QQ<31 THEN 200
180 R=R+1
190 QQ=1
200 CALL HCHAR(R,C+QQ,ASC(SEG$(A$,Q,1)))
210 NEXT Q
220 LC=LC+1
230 RETURN
240 INPUT "PRESS ENTER ":Q$
250 CALL CLEAR
260 RETURN
270 A$=AA$
280 GOSUB 120
290 FOR II=1 TO 5
300 CALL HCHAR(R,C+1,131,LEN(AA$))
310 LC=LC-1
320 GOSUB 120
330 NEXT II
340 RETURN
350 LC=24.02
360 A$="PRESS ENTER"
370 GOSUB 120
380 CALL KEY(G,KE,ST)
390 IF ST=0 THEN 380
400 GOSUB 420
410 RETURN
420 CALL HCHAR(16,1,32,290)
430 RETURN
440 CALL CHAR(128,"0000000800000000")
450 CALL CHAR(129,"0000001038100000")
460 CALL CHAR(130,"00183C3C18000000")
470 CALL CHAR(131,"105438FE38541000")
500 CALL SCREEN(5)
510 CALL COLOR(13,16,1)
520 FOR I=1 TO 25
530 CALL HCHAR(23*RND+1,31*RND+1,128)
540 CALL HCHAR(23*RND+1,31*RND+1,128)
550 REM  CALL HCHAR(23*RND+1,31*RND+1,129) 
560 CALL HCHAR(23*RND+1,31*RND+1,129+2*RND)
570 NEXT I
580 CALL COLOR(9,7,1)
590 FOR I=1 TO 5
600 CALL HCHAR(9+I,5,102,20)
610 NEXT I
620 A$=" LEARNING BASIC "
630 LC=11.06
640 GOSUB 120
650 A$="  CHAPTER FIVE  "
660 GOSUB 120
670 A$="MORE PROGRAMMING"
680 GOSUB 120
690 CALL COLOR(9,7,7)
700 CALL SOUND(500,147,0)
710 CALL SOUND(500,147,0)
720 CALL SOUND(500,220,0)
730 CALL SOUND(500,220,0)
740 CALL SOUND(500,247,0)
750 CALL SOUND(500,247,0)
760 CALL SOUND(1200,220,0,147,0,110,0)
770 B$(0)="10 PRINT""DAY NUMBER PROGRAM"""
780 B$(1)="20 DIM A(6)"
790 B$(2)="30 A(1)=31"
800 B$(3)="40 A(2)=28"
810 B$(4)="50 A(3)=31"
820 B$(5)="60 A(4)=30"
830 B$(6)="70 A(5)=31"
840 B$(7)="80 A(6)=30"
850 B$(8)="90 INPUT""(DAY,MONTH)"":D,M"
870 B$(11)="110 FOR I=1 TO M-1"
880 B$(12)="120 T=T+A(I)"
890 B$(9)="100 T=0"
900 B$(13)="130 NEXT I"
910 B$(14)="140 PRINT""TODAY IS DAY NUMBER "";T+D"
920 B$(15)="150 END"
930 CALL SCREEN(12)
940 GOSUB 80
950 CALL COLOR(13,5,1)
980 GOTO 1150
990 PRINT "DAY NUMBER PROGRAM"
995 PRINT "(FIRST 6 MONTHS ONLY)"
1000 DIM A(6)
1010 A(1)=31
1020 A(2)=28
1030 A(3)=31
1040 A(4)=30
1050 A(5)=31
1060 A(6)=30
1070 INPUT "(DAY,MONTH)":D,M
1080 T=0
1090 IF M=1 THEN 1130
1100 FOR I=1 TO M-1
1110 T=T+A(I)
1120 NEXT I
1130 PRINT "TODAY IS DAY NUMBER ";T+D
1140 GOTO 1070
1150 INPUT "PLEASE ENTER YOUR NAME:":D$
1160 PRINT "HELLO AGAIN, ";D$: : :"IN THE LAST CHAPTER,":"YOU SAW PRINT STATEMENTS.":"LET'S LOOK AGAIN AT THEM.": :
1170 GOSUB 240
1180 PRINT "WHAT ABOUT THIS PROGRAM?": :"10 A$="""";D$;""""":"20 PRINT""PLEASED TO MEET YOU, "";A$": :
1190 GOSUB 80
1200 PRINT "THIS IS WHAT THE PROGRAM":"WOULD PRINT:": :"PLEASED TO MEET YOU, ";D$: : :
1210 PRINT "REMEMBER:":"THINGS IN QUOTE MARKS ARE":"PRINTED THE WAY":"THEY ARE WRITTEN."
1220 GOSUB 240
1230 PRINT "NOW ";D$:"LET'S DO ANOTHER PROGRAM.":"LET'S PROGRAM THE COMPUTER":"TO TELL US THE DAY NUMBER"
1240 PRINT "IN THE YEAR":"WHEN WE ENTER THE DATE.": :"FOR EXAMPLE":"FEBRUARY 7TH IS":"THE 38TH DAY OF THE YEAR."
1250 GOSUB 240
1260 PRINT "HERE IS THE FIRST STATEMENT": : :B$(0): : : : : : : : : : : : : : :
1270 GOSUB 80
1280 PRINT "YOU KNOW WHAT THIS STATEMENT":"DOES."
1290 QA=1000
1300 GOSUB 80
1310 CALL CLEAR
1320 PRINT B$(0):B$(8): : : : : : : : : : : : : : : : : : : :
1330 AA$="INPUT"
1340 LC=4.05
1350 GOSUB 270
1360 CALL HCHAR(15,1,102,32)
1370 CALL COLOR(9,13,13)
1380 A$="INPUT MEANS THAT INFORMATIONMUST BE ENTERED FROM THE    KEYBOARD."
1390 GOSUB 110
1400 GOSUB 350
1410 AA$="D,M"
1420 LC=4.24
1430 GOSUB 270
1440 A$="TWO NUMBERS (D AND M) ARE   ENTERED."
1450 GOSUB 110
1460 A$="MANY NUMBERS MAY BE ENTERED IN ONE INPUT STATEMENT, BY  PUTTING COMMAS BETWEEN EACH NUMBER."
1470 LC=19.02
1480 GOSUB 120
1490 GOSUB 350
1500 A$="IN THIS CASE 2 NUMBERS (D   AND M) ARE ENTERED FROM THE KEYBOARD SEPARATED BY A     COMMA."
1510 GOSUB 110
1520 GOSUB 350
1530 A$="FOR EXAMPLE, FOR MARCH 4TH    ENTER 4,3"
1540 GOSUB 110
1550 GOSUB 240
1560 PRINT "HERE IS A SHORT PROGRAM YOU":"CAN TRY, WORKING WITH":"INPUT STATEMENTS.": :"GO AHEAD AND TRY IT."
1570 PRINT "YOU CAN RUN IT OVER AND":"OVER AGAIN.": : :"WHEN YOU WANT TO GO ON,":"JUST ENTER 0,0,0.": :
1580 PRINT "NOTICE THAT F$ IS A STRING  VARIABLE.": :"10 INPUT""EXPERIMENT "":A,F$,C":"20 PRINT A,C,F$": :
1590 INPUT "EXPERIMENT ":X,F$,C
1600 IF X=0 THEN 1630
1610 PRINT X,C,F$
1620 GOTO 1550
1630 CALL CLEAR
1640 PRINT "NOW LET'S RETURN TO OUR":"PROGRAM TO CALCULATE":"DAY NUMBERS.": : :B$(0):B$(8): : : : : : : : :
1650 PRINT "WE WOULD LIKE THE COMPUTER":"TO KNOW HOW MANY DAYS THERE":"ARE IN EACH MONTH."
1660 GOSUB 240
1670 PRINT B$(0): : : : : : : :B$(8): : : : : : : : : : : : : : :
1680 GOSUB 80
1690 LC=3.02
1700 FOR I=1 TO 6
1710 A$=B$(I+1)
1720 GOSUB 120
1730 NEXT I
1740 CALL HCHAR(15,1,102,32)
1750 A$="HERE WE HAVE A NEW TYPE OF  VARIABLE CALLED A           SUBSCRIPTED VARIABLE."
1760 GOSUB 110
1770 A$="THE COLLECTION OF VARIABLES A(1) THROUGH A(6) IS CALLED AN ""ARRAY"". (A 1-DIMENSIONALARRAY)"
1780 LC=19.02
1790 GOSUB 120
1800 GOSUB 350
1810 A$="ARRAYS CAN HAVE MORE THAN 1 DIMENSION. D(3,1) WOULD BE  AN ELEMENT OF AN ARRAY OF 2 DIMENSIONS."
1820 GOSUB 110
1830 LC=20.02
1840 A$="WE CAN HAVE ARRAYS OF 1, 2, OR 3 DIMENSIONS."
1850 GOSUB 120
1860 GOSUB 350
1870 A$="A(1) IS THE NUMBER OF DAYS  IN JANUARY. A(2) THE NUMBER IN FEBRUARY, ETC."
1880 GOSUB 110
1890 A$="WE SHOULD HAVE A(1) TO A(12)BUT TO SAVE SPACE WE WILL   ONLY WORK WITH THE FIRST 6  MONTHS."
1900 LC=19.02
1910 GOSUB 120
1920 GOSUB 350
1930 A$="WHEN ARRAYS ARE USED, MEMORYSPACE MUST BE RESERVED FOR  THEM IN THE COMPUTER."
1940 GOSUB 110
1950 LC=19.02
1960 A$="YOU MUST TELL THE COMPUTER  HOW MANY ELEMENTS THE ARRAY HAS."
1970 GOSUB 120
1980 GOSUB 350
1990 A$="YOU DO THIS WITH A SPECIAL  STATEMENT CALLED A DIMENSIONSTATEMENT."
2000 GOSUB 110
2010 LC=2.02
2020 A$=B$(1)
2030 GOSUB 120
2040 AA$="DIM"
2050 LC=2.05
2060 GOSUB 270
2070 GOSUB 350
2080 A$="THIS SAYS THE ARRAY IS A 1- DIMENSIONAL ARRAY WITH 6    ELEMENTS."
2090 GOSUB 110
2100 GOSUB 350
2102 A$="ACTUALLY, IT SAYS THAT THEREARE 7 ELEMENTS, SINCE THE   FIRST ELEMENT IS ELEMENT 0."
2104 GOSUB 110
2106 GOSUB 350
2110 A$="IMPORTANT!! A DIM STATEMENT MUST APPEAR BEFORE AN       ELEMENT OF AN ARRAY IS USED.ALSO AN ARRAY"
2120 GOSUB 110
2130 A$="MUST NOT BE DIMENSIONED MORETHAN ONCE. DIM THEREFORE    APPEARS NEAR THE START OF A PROGRAM."
2140 LC=20.02
2150 GOSUB 120
2160 GOSUB 350
2170 A$="IF YOU USE AN ARRAY AND DO  NOT HAVE A DIM STATEMENT, ANARRAY OF 10 ELEMENTS IS     ASSUMED."
2180 GOSUB 110
2190 LC=21.02
2200 A$="THE NEXT STATEMENTS ARE NEW-"
2210 GOSUB 120
2220 GOSUB 350
2230 LC=10.02
2240 FOR I=1 TO 3
2250 A$=B$(10+I)
2260 GOSUB 120
2270 NEXT I
2280 AA$="FOR"
2290 LC=10.06
2300 GOSUB 270
2310 LC=12.06
2320 AA$="NEXT"
2330 GOSUB 270
2340 A$="THIS IS A FOR-NEXT LOOP.    THIS IS A WAY OF REPEATING APART OF A PROGRAM MANY TIMES"
2350 GOSUB 110
2360 A$="FOR IS THE KEY WORD."
2370 LC=20.02
2380 GOSUB 120
2390 GOSUB 350
2400 A$="THE STATEMENT, FOR I=1 TO   M-1 MEANS TAKE A VARIABLE   WHICH WE'LL CALL I,         AND MAKE I=1."
2410 GOSUB 110
2420 LC=20.02
2430 A$="CHECK TO SEE IF I IS GREATERTHAN M-1. IF IT IS NOT, DO  ALL THE STATEMENTS BETWEEN  FOR AND NEXT."
2440 GOSUB 120
2450 GOSUB 350
2460 A$="WHEN THE "NEXT" STATEMENT   IS REACHED, I IS INCREASED  BY 1 AND THE LOOP REPEATS."
2470 GOSUB 110
2480 LC=19.02
2490 A$="WHEN I=M-1 THE LOOP ONCE    MORE REPEATS. THE PROGRAM   THEN GOES TO THE STATEMENT  AFTER ""NEXT""."
2500 GOSUB 120
2510 GOSUB 240
2520 PRINT "FOR EXAMPLE, IF WE HAD":"THE PROGRAM;": :
2530 GOSUB 2550
2540 GOTO 2570
2550 PRINT "10 K=0":"20 FOR I=1 TO 10":"30 K=K+I":"40 PRINT K":"50 NEXT": :
2560 RETURN
2570 PRINT "WHAT WOULD WE GET?": :"FIRST K=0,": :
2580 GOSUB 240
2590 GOSUB 2550
2600 FOR I=1 TO 10
2610 PRINT "THEN K=K+";I;"  (K=";K;"+";I;")"
2620 K=K+I
2630 NEXT I
2640 GOSUB 240
2650 GOSUB 2550
2660 PRINT "YOU CAN SEE THAT THIS IS":"JUST A WAY OF ADDING":"TOGETHER ALL THE INTEGERS":"FROM 1 TO 10.": : : :
2670 PRINT "WELL, LET'S GET BACK TO OUR PROGRAM."
2680 GOSUB 240
2690 FOR I=0 TO 8
2700 PRINT B$(I)
2710 NEXT I
2720 PRINT:B$(11):B$(12):B$(13): : : :
2730 PRINT "THE FOR-NEXT LOOP JUST ADDS":"UP ALL OF THE DAYS IN THE":"MONTHS BEFORE THE ENTERED"
2740 PRINT "MONTH. FOR THIS TO WORK,":"T MUST START OUT AS ZERO":"STATEMENT 100 INSURES THIS."
2750 QA=1200
2760 GOSUB 80
2770 LC=11.02
2780 AA$=B$(9)
2790 GOSUB 270
2800 GOSUB 350
2810 A$=B$(14)
2820 LC=15.02
2830 GOSUB 120
2840 A$=B$(15)
2850 LC=17.02
2860 GOSUB 120
2870 LC=19.02
2880 A$="STATEMENT 140 JUST PRINTS   THE ANSWER, AND STATEMENT   150 ENDS THE PROGRAM."
2890 GOSUB 120
2895 PRINT
2900 GOSUB 350
2902 PRINT "WHAT WOULD HAPPEN IN THE FOR":"LOOP IF M=1?":"THE LOOP WOULD RUN FROM 1 TO":"0. IT CAN'T DO THAT, SO"
2904 PRINT "THE PROGRAM WOULD SKIP TO":"LINE 140."
2905 GOSUB 350
2910 FOR I=0 TO 15
2915 IF I=10 THEN 2930
2920 PRINT B$(I)
2930 NEXT I
2935 PRINT: :
2940 PRINT "BY THE WAY, STATEMENT 150 IS":"NOT NEEDED. WHEN THE PROGRAM":"FINISHES STATEMENT 140,":"IT WILL STOP."
2960 GOSUB 350
2970 FOR I=0 TO 15
2974 IF I=10 THEN 2990
2975 PRINT B$(I)
2990 NEXT I
3030 PRINT:"WE COULD HAVE THE COMPUTER":"DO THE PROGRAM OVER AND":"OVER AGAIN, IF WE REPLACE"
3040 PRINT "STATEMENT ""150 END"" BY":"""150 GOTO 90"""
3050 AA$="150 GOTO 90"
3060 LC=17.02
3070 GOSUB 270
3080 GOSUB 240
3090 PRINT "THAT COMPLETES THE PROGRAM.":"YOU WILL GET A CHANCE TO TRY":"IT SOON, BUT FIRST"
3100 PRINT "CONSIDER THIS SMALL PROGRAM,": :"10 PRINT 1,2,3,4":"20 PRINT 1;2;3;4":"30 PRINT 1:2:3:4":"40 END"
3110 GOSUB 80
3115 PRINT 1,2,3,4:1;2;3;4:1:2:3:4
3120 PRINT "THINGS SEPARATED BY COMMAS":"ARE PRINTED FAR  APART.":"THINGS SEPARATED BY":"SEMICOLONS ARE PRINTED"
3130 PRINT "CLOSE TOGETHER.":"THINGS SEPARATED BY COLONS":"ARE PRINTED ON NEW LINES."
3140 GOSUB 240
3150 PRINT "WELL ";D$:"THIS IS THE END OF CHAPTER 5": : :"THE DAY NUMBER PROGRAM":"STARTS AT LINE 990."
3160 PRINT "TO RUN IT, ENTER":"RUN 990":"(AND WAIT).": :"TO STOP IT, USE SHIFT-C."
