100 !
110 ! SOLITAIRE
120 !
130 ! 99'ER VERSION _._.2XB
140 !
150 ! BY DON CROSSMAN
160 !
170 RANDOMIZE :: DISPLAY AT(7,11)ERASE ALL:"SOLITAIRE" :: CALL TUN1
171 DISPLAY AT(10,1):"SPECIAL KEYS: "
172 DISPLAY AT(13,2):"Q      GIVE UP (QUIT)"
173 DISPLAY AT(16,2):"L      CHECK NUMBER"
174 DISPLAY AT(17,2):"       OF FACE DOWN CARDS"
175 DISPLAY AT(20,2):"ENTER  TO GET NEXT CARD"
180 ON WARNING NEXT :: CALL TUN2(4) :: CALL CHAR(96,"00081C3E7F6B081C",97,"00081C2A7F2A081C",99,"7E81BDA1A1BD817E")
190 CALL CHAR(112,"00367F7F3E1C1C08",113,"00081C3E7F3E1C08",98,"004ED151515151EE")
200 CALL CHAR(104,"000103070F1F3F3F",105,"0080C0E0F0F8FCFC",106,"7F7F7F7D39030307",107,"FEFEFEBE9CC0C0E0")
210 CALL CHAR(108,"00030307071B3D7F",109,"00C0C0E0E0D8BCFE",110,"7F7F3D1D01030307",111,"FEFEBCB880C0C0E0")
220 CALL CHAR(120,"001C3E7F7F7F7F7F",121,"00387CFEFEFEFEFE",122,"3F1F0F0703010101",123,"FCF8F0E0C0808080")
230 CALL CHAR(124,"010303070F0F1F3F",125,"80C0C0E0F0F0F8FC",126,"3F1F0F0F07030301",127,"FCF8F0F0E0C0C080")
240 FOR X=2 TO 10 :: CALL COLOR(X,2,12) :: NEXT X :: CALL COLOR(11,9,12,12,9,12,0,13,1)
250 DIM VL(52),CARD(52,6),P(11,18),PD(24),PC(7) :: DEF FVL(X)=VL(X)-13*S
260 Y1=INT(RND*26)+1 :: IF Y1/2<>INT(Y1/2)OR Y1=12 THEN 260 ELSE GM=GM+1 :: Z=INT(RND*52)+1! INITIALIZE SHUFFLE
270 FOR A=1 TO 7 :: FOR B=A+1 TO 18 :: P(A,B)=0 :: NEXT B :: NEXT A :: FOR A=1 TO 7 :: PC(A)=0 :: NEXT A
280 FOR A=8 TO 11 :: FOR B=1 TO 13 :: P(A,B)=0 :: NEXT B :: NEXT A! RESET POSITIONS TO ZERO
290 RESTORE :: FOR X=1 TO INT(RND*52)+1 :: READ DMP :: NEXT X :: IF DMP=13 THEN RESTORE! GET STARTING POINT
300 FOR X=1 TO 52 :: CALL SCREEN(INT(RND*15)+2) :: FOR Y=1 TO Y1! START SHUFFLE
310 READ DMP :: IF DMP=13 THEN RESTORE
320 NEXT Y :: IF X=32 AND GM=1 THEN CALL HCHAR(22,10,99) :: DISPLAY AT(22,10):"1983 DON CROSSMAN": :TAB(9);"USER'S GROUP EDITION"
330 IF GM>1 THEN IF X=32 THEN DISPLAY AT(12,9):"GET READY FOR": :TAB(12);"GAME";GM ELSE IF X=51 THEN DISPLAY AT(22,10):"ANY SECOND NOW..."
340 IF X=32 THEN CALL TUN3 ELSE IF X>49 THEN CALL TUN1
350 READ VL(X) :: IF VL(X)=13 THEN RESTORE
360 VL(X)=VL(X)+Z :: IF VL(X)>52 THEN VL(X)=VL(X)-52
370 FOR S=0 TO 3 :: IF FVL(X)>13 THEN 390! ASSIGN CARD GRAPHICS IN SUBROUTINES
380 ON FVL(X)GOSUB 1140,1150,1150,1150,1150,1150,1150,1150,1150,1160,1170,1180,1190 :: GOTO 400
390 NEXT S
400 NEXT X :: FOR C=9 TO 29 :: CALL VCHAR(10,C,32,5) :: NEXT C! CLEAR SCREEN CENTER
405 CALL CLEAR
410 X=0 :: FOR B=1 TO 7 :: FOR A=B TO 7 :: X=X+1 :: P(A,B)=VL(X) :: NEXT A :: NEXT B :: CALL SCREEN(13) :: CALL TUN3! PLACE CARDS
420 FOR X=1 TO 24 :: PD(X)=VL(X+28) :: NEXT X :: PDC,ST=0 :: LIM=24! ASSIGN DECK
430 C=6 :: X=1 :: R1=10 :: FOR Y=1 TO 7 :: GOSUB 1250 :: C=C+3 :: X=X+8-Y :: NEXT Y! DISPLAY CARDS
440 DISPLAY AT(2,10):"A  B  C  D" :: E=0 :: FOR X=4 TO 22 STEP 3 :: E=E+1 :: DISPLAY AT(8,X):E :: NEXT X :: DISPLAY AT(16,27):"8"
450 DISPLAY AT(22,7):"      P L A Y"! BEGIN PLAYING
460 V=0 :: FOR W=8 TO 11 :: IF P(W,13)<>0 THEN V=V+1
470 NEXT W :: IF V=4 THEN 1090! DID I WIN???
480 DISPLAY AT(24,7):"   FROM "
490 GOSUB 500 :: GOTO 570
500 CALL KEY(0,FF,STATUS) :: IF STATUS=0 THEN 500 :: IF(FF>48)AND(FF<57)THEN DIS$=CHR$(FF) :: FF=FF-48 :: GOTO 560 ELSE GOTO 510
510 IF(FF>64)AND(FF<69)THEN DIS$=CHR$(FF) :: FF=FF-56 :: GOTO 560 ELSE 520
520 IF FF=13 THEN DIS$="STACK" :: FF=0 :: GOTO 560 ELSE 530
530 IF FF=81 THEN DIS$="QUIT" :: FF=13 :: GOTO 560 ELSE 540
540 IF FF=76 THEN DIS$="LOOK UP" :: FF=21 :: GOTO 560
550 GOTO 500
560 RETURN
570 F=FF
580 DISPLAY AT(24,15):DIS$
590 IF F=0 THEN 610
600 IF F=13 THEN 1100 ELSE IF F=21 THEN CALL LOOK(PC(),PDC) :: GOTO 440 ELSE 720!    BRANCH TO OR FALL THROUGH
610 IF PDC<24 THEN 640
620 DISPLAY AT(22,10):"NO MORE CARDS" :: R=0 :: FOR Q=1 TO 7 :: IF PC(Q)+1>=Q THEN R=R+1! ALL CARDS FACE UP?
630 NEXT Q :: IF R=7 THEN DISPLAY AT(24,7):"SHALL WE CONTINUE? N" :: ACCEPT AT(24,26)VALIDATE("YN")SIZE(-1):G$ :: IF G$="Y" THEN 460 ELSE 1090 ELSE 460
640 FOR D1=24 TO 1 STEP-1 :: IF PD(D1)=0 THEN LIM=D1-1 ELSE 660! BEGIN DEAL THREE
650 NEXT D1
660 J,D1=0
670 ST=ST+1 :: D1=D1+1
680 IF ST>LIM THEN ST=0 :: GOTO 660 ELSE IF ST=LIM THEN 710 ELSE IF PD(ST)<>0 THEN 700
690 ST=ST+1 :: GOTO 680
700 IF D1<3 THEN 670
710 X=ST+28 :: DISPLAY AT(23,24):ST :: R1=18 :: C=27 :: GOSUB 1250 :: GOTO 460! DONE
720 DISPLAY AT(24,18):"TO" :: GOSUB 500 :: DISPLAY AT(24,21):DIS$ :: T=FF :: IF T=0 THEN 460
730 IF T=8 OR F>8 OR T>12 THEN CALL FAULT :: GOTO 460 ELSE IF T>8 THEN T=T-1
740 FOR K=1 TO 18 :: IF P(T,K)=0 THEN TK=K :: GOTO 760
750 NEXT K
760 IF F<>8 THEN 820
770 P(0,0)=PD(ST-J) :: GOSUB 1290 :: PD(ST-J)=0 :: PDC=PDC+1
780 J=J+1 :: X=ST+28-J :: IF ST-J=0 THEN X=0 :: GOTO 800
790 IF PD(ST-J)=0 THEN 780
800 R1=18 :: C=27 :: GOSUB 1250 :: IF X=0 THEN CALL HCHAR(23,27,32,2)ELSE DISPLAY AT(23,24):ST-J
810 GOTO 970
820 FOR K=1 TO 18 :: IF P(F,K)=0 THEN FK=K-1 :: GOTO 840
830 NEXT K
840 IF T<8 THEN 920
850 P(0,0)=P(F,FK) :: GOSUB 1290 :: P(F,FK)=0 :: IF P(F,FK-1)=0 THEN X=0 :: GOTO 880
860 FOR X=1 TO 52 :: IF P(F,FK-1)=VL(X)THEN 880
870 NEXT X
880 R1=10 :: IF FK>F-PC(F)THEN R1=R1+FK+PC(F)-F-1 ELSE PC(F)=PC(F)+1
890 IF R1>19 THEN R1=19
900 C=F*3+3 :: GOSUB 1250 :: IF R1<19 THEN CALL HCHAR(R1+3,C,32,2)
910 GOTO 970
920 FOR L=F-PC(F)TO FK :: P(0,PC(F)-F+L)=P(F,L) :: IF L=F-PC(F)THEN GOSUB 1290
930 P(F,L)=0 :: NEXT L :: PC(F)=PC(F)+1 :: IF F-PC(F)<=0 THEN X=0 :: GOTO 960
940 FOR X=1 TO 52 :: IF P(F,F-PC(F))=VL(X)THEN 960
950 NEXT X
960 R1=10 :: C=F*3+3 :: GOSUB 1250 :: FOR R=13 TO 21 :: CALL HCHAR(R,C,32,2) :: NEXT R
970 FOR PK=0 TO 14 :: IF P(0,PK)=0 THEN 990
980 NEXT PK
990 IF T>7 THEN C=T*3-13 ELSE C=T*3+3
1000 IF T>7 THEN 1010 ELSE IF PC(T)>=T THEN PC(T)=T-1
1010 FOR L=TK TO TK+PK-1 :: P(T,L)=P(0,L-TK) :: P(0,L-TK)=0
1020 IF T<8 THEN R1=10+L-T+PC(T)ELSE R1=4
1030 IF R1>19 THEN R1=19
1040 FOR X=1 TO 52 :: IF P(T,L)=VL(X)THEN 1060
1050 NEXT X
1060 GOSUB 1250 :: NEXT L :: GOTO 460
1070 DATA 34,37,4,6,41,47,7,22,48,12,18,39,11,1,2,38,27,28,29,21,40,30,3,32,25,51,45
1080 DATA 17,15,52,35,46,19,31,16,50,20,43,23,5,49,36,42,8,44,26,24,33,14,9,10,13
1090 DISPLAY AT(12,7):" CONGRATULATIONS": :TAB(8);"YOU WON GAME";GM :: CALL TUN2(0)
1100 DISPLAY AT(24,7):"   PLAY AGAIN? Y" :: ACCEPT AT(24,22)VALIDATE("YN")SIZE(-1):G$ :: IF G$="N" THEN CALL TUN3 :: GOTO 1120
1110 CALL TUN2(10) :: DISPLAY AT(10,9)ERASE ALL:"NOW SHUFFLING": :TAB(10);"PLEASE WAIT" :: GOTO 260
1120 FOR X=1 TO 18 :: PRINT :: NEXT X :: PRINT TAB(9);"HAVE A NICE DAY" :: FOR X=1 TO 6 :: PRINT
1130 NEXT X :: FOR X=1 TO 300 :: NEXT X :: END
1140 CARD(X,1)=65 :: GOTO 1200
1150 CARD(X,1)=48+FVL(X) :: GOTO 1200
1160 CARD(X,1)=98 :: GOTO 1200
1170 CARD(X,1)=74 :: GOTO 1200
1180 CARD(X,1)=81 :: GOTO 1200
1190 CARD(X,1)=75
1200 IF S>1 THEN 1230
1210 CARD(X,2)=96+S :: FOR Y=3 TO 6 :: IF S=0 THEN CARD(X,Y)=101+Y ELSE CARD(X,Y)=105+Y
1220 NEXT Y :: RETURN
1230 CARD(X,2)=110+S :: FOR Y=3 TO 6 :: IF S=2 THEN CARD(X,Y)=117+Y ELSE CARD(X,Y)=121+Y
1240 NEXT Y :: RETURN
1250 IF X=0 THEN 1280
1260 X2=0 :: FOR R=R1 TO R1+2 :: X2=X2+1
1270 CALL HCHAR(R,C,CARD(X,X2)) :: X2=X2+1 :: CALL HCHAR(R,C+1,CARD(X,X2)) :: NEXT R :: RETURN
1280 FOR G=1 TO 6 :: CARD(X,G)=32 :: NEXT G :: GOTO 1260
1290 CK1,CK2=0 :: FOR S=0 TO 3 :: IF P(0,0)-13*S>0 AND P(0,0)-13*S<14 THEN CK1=P(0,0)-13*S
1300 NEXT S :: IF T>7 THEN IF TK=1 THEN IF CK1-P(T,TK-1)=1 THEN 1340 ELSE 1350 ELSE IF P(0,0)-P(T,TK-1)=1 THEN 1340 ELSE 1350
1310 FOR S=0 TO 3 :: IF TK=1 THEN CK2=14 ELSE IF P(T,TK-1)-13*S>0 AND P(T,TK-1)-13*S<14 THEN CK2=P(T,TK-1)-13*S
1320 NEXT S :: IF CK2-CK1<>1 THEN 1350
1330 IF CK2=14 THEN 1340 ELSE IF P(T,TK-1)>26 AND P(0,0)>26 OR P(T,TK-1)<27 AND P(0,0)<27 THEN 1350 ELSE 1340
1340 CALL TUN1 :: RETURN
1350 CALL FAULT :: GOTO 460
1360 SUB FAULT
1370 DISPLAY AT(24,10)BEEP:"CANT DO THAT" :: FOR X=1 TO 300 :: NEXT X :: SUBEND
1380 SUB TUN1
1390 FOR X=8 TO 29
1400 CALL SOUND(-99,880,X+1,659,X,220,X-1) :: NEXT X :: SUBEND
1410 SUB TUN2(X)
1420 CALL SOUND(250,523,X,330,X+1,262,X+2) :: CALL SOUND(250,587,X,349,X+1,262,X+2)
1430 CALL SOUND(250,659,X,440,X+1,262,X+2) :: CALL SOUND(250,698,X,392,X+1,262,X+2)
1440 CALL SOUND(250,784,X,494,X+1,196,X+2) :: CALL SOUND(250,698,X,494,X+1,196,X+2)
1450 CALL SOUND(250,784,X+1,523,X+2,220,X+3) :: CALL SOUND(250,698,X+1,523,X+2,220,X+3)
1460 CALL SOUND(300,784,X+1,392,X+2,247,X+3) :: CALL SOUND(300,698,X+1,392,X+2,247,X+3)
1470 CALL SOUND(350,784,X+2,349,X+3,196,X+4) :: CALL SOUND(350,988,X+3,349,X+3,196,X+4)
1480 CALL SOUND(1000,1047,X+4,392,X+4,262,X+4) :: SUBEND
1490 SUB TUN3
1500 CALL SOUND(50,110,3,139,4,165,5) :: FOR X=1 TO 13 :: NEXT X
1510 CALL SOUND(2500,110,3,139,4,165,5) :: SUBEND
1520 SUB LOOK(PC(),PDC)
1530 DISPLAY AT(7,7):"CARDS FACE DOWN" :: C=4 :: FOR X=1 TO 7 :: K=X-PC(X)-1 :: IF K<0 THEN K=0
1540 DISPLAY AT(8,C):K :: C=C+3 :: NEXT X :: P$=STR$(24-PDC) :: IF LEN(P$)=1 THEN Q=1 :: GOTO 1560
1550 CALL HCHAR(19,30,ASC(SEG$(P$,1,1))) :: Q=2
1560 CALL HCHAR(20,30,ASC(SEG$(P$,Q,1)))
1570 ACCEPT AT(24,22):G$ :: CALL HCHAR(7,7,32,18) :: CALL VCHAR(19,30,32,2) :: SUBEND
