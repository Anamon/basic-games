100 CALL CLEAR :: RANDOMIZE
110 B=INT(5*RND+2) :: IF B=B2 THEN 110 ELSE B2=B
120 F=INT(5*RND+2) :: IF F=F2 THEN 120 ELSE F2=F
130 D=INT(5*RND+2) :: IF D=D2 THEN 130 ELSE D2=D
140 X=F*B*D
150 BB=INT(5*RND+2) :: IF BB=BB2 OR BB=B THEN 150 ELSE BB2=BB
160 DD=INT(5*RND+2) :: IF DD=DD2 OR DD=D THEN 160 ELSE DD2=DD
170 F=F*BB*DD
180 DISPLAY AT(3,1)ERASE ALL:"IF";B;"BOYS CAN CATCH";X;"FROGS IN";D;"DAYS,"
190 DISPLAY AT(6,1):"HOW MANY FROGS CAN";BB;"BOYS":"CATCH IN";DD;"DAYS?"
210 ACCEPT AT(7,19):Q
220 IF Q=F THEN DISPLAY AT(9,1):"THAT'S RIGHT!" :: GOTO 110
230 DISPLAY AT(9,1):"NO, THAT'S WRONG."
240 DISPLAY AT(11,1):"IF";B;"BOYS CAN CATCH";X;"FROGS IN";D;"DAYS"
250 DISPLAY AT(13,1):"THEN ONE BOY CAN CATCH";X/B;"FROGS IN";D;"DAYS"
260 DISPLAY AT(15,1):"AND ONE BOY CAN CATCH";X/B/D;"FROGS IN ONE DAY."
270 DISPLAY AT(17,1):"SO, IF ONE BOY CAN CATCH";X/B/D;"FROGS IN ONE DAY,"
280 DISPLAY AT(19,1):"THEN";BB;"BOYS CAN CATCH";X/B/D*BB;"FROGS IN ONE DAY"
290 DISPLAY AT(21,1):"AND";BB;"BOYS CAN CATCH";X/B/D*BB*DD;"FROGS IN";DD;"DAYS."
300 DISPLAY AT(24,1):"PRESS ANY KEY" :: CALL KEY(0,K,S) :: IF S=0 THEN 300 ELSE 110
