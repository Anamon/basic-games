100 CALL CLEAR :: !FORMATION by Jim Peterson - use the S and D keys
120 CALL SCREEN(5) :: FOR SET=1 TO 12 :: CALL COLOR(SET,16,1) :: NEXT SET
130 DISPLAY AT(3,10):"FORMATION": :TAB(7);"by Jim Peterson" :: DISPLAY AT(12,1):"Use S and D keys to maneuver"
140 DISPLAY AT(20,1):" " :: DISPLAY AT(20,1):"PRESS ANY KEY TO START" :: CALL KEY(0,K,S) :: IF S<1 THEN 140 ELSE CALL CLEAR
160 CALL CHAR(100,"381010FEFE383810103838FEFE10103838") :: CALL MAGNIFY(2) :: RANDOMIZE
180 V,W,P=0 :: FOR J=1 TO 7 :: CALL SPRITE(#J,100,7,1,250*RND+1,10,4) :: FOR D=1 TO 100 :: NEXT D :: NEXT J
190 CALL SPRITE(#11,101,16,160,128)
200 CALL KEY(3,K,S) :: W=W+1
210 IF W=150 THEN 280 ELSE IF W=300 THEN 300 ELSE IF K=68 THEN V=V+2+(V>125)*2 ELSE IF K=83 THEN V=V-2-(V<-125)*2
220 IF P=0 THEN CALL MOTION(#11,0,V)ELSE IF P=1 THEN CALL MOTION(#11,0,V,#12,0,V)ELSE CALL MOTION(#11,0,V,#12,0,V,#13,0,V)
240 CALL COINC(ALL,A) :: IF A=0 THEN 200
260 CALL SOUND(1000,-4,0) :: HS=MAX(HS,W) :: DISPLAY AT(24,1):"HIGH SCORE";HS :: CALL DELSPRITE(ALL) :: GOTO 140
280 P=1 :: CALL POSITION(#11,R,C) :: CALL SPRITE(#12,101,16,160,C-40-(C<40)*256) :: GOTO 220
300 P=2 :: CALL POSITION(#11,R,C) :: CALL SPRITE(#13,101,16,160,C+40+(C>216)*256) :: GOTO 220
