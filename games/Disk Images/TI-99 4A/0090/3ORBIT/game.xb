100 ! 3ORBIT -- An XB Physics experiment.
110 ! Copyright (C) 1986 By Milo Tsukroff
120 ! Version 1.01
130 ! V1A2300387R3103871
135 CALL MAGNIFY(1)
140 CALL CLEAR :: CALL KEY(3,K,S) :: FOR X=5 TO 8 :: CALL COLOR(X+4,16,5) :: CALL COLOR(X,16,7) :: NEXT X :: CALL COLOR(12,2,11)
145 CALL CHAR(124,"00") :: CALL CHAR(64,"00")
150 DEF ARCSIN(X)=ATN(X/SQR(1-X*X))
160 MM1=10000000 :: MM2=100 :: MM3=1
170 XX1=0 :: XX2=90 :: XX3=32.0
180 SS1=0 :: SS2=10.5 :: SS3=18.0
190 IMAGE ##########.##|####.##|###.##
200 CALL CLEAR! *** START ***
205 CALL HCHAR(1,1,124,768)
210 CALL SCREEN(15) :: DISPLAY AT(1,12):"3ORBIT"; :: DISPLAY AT(3,2):"(C)|1986|BY|MILO|TSUKROFF";
220 DISPLAY AT(6,1):"@@@@@@@@BODY@ONE@@@@@@@@@@@@|mass|||||||||distance|speed"; :: DISPLAY AT(9,1):USING 190:MM1,XX1,SS1
230 DISPLAY AT(11,1):"@@@@@@@@BODY@TWO@@@@@@@@@@@@|mass|||||||||distance|speed"; :: DISPLAY AT(14,1):USING 190:MM2,XX2,SS2
240 DISPLAY AT(16,1):"@@@@@@@BODY@THREE@@@@@@@@@@@|mass|||||||||distance|speed"; :: DISPLAY AT(19,1):USING 190:MM3,XX3,SS3
250 DISPLAY AT(21,1):"@@@@@@@@@@@@@@@@@@@@@@@@@@@@"; :: DISPLAY AT(23,5):"READY|TO|START||Y";
260 CALL VCHAR(1,1,64,24) :: CALL VCHAR(1,32,64,24)
270 ACCEPT AT(9,1)SIZE(-13)VALIDATE(NUMERIC)BEEP:MM1 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 360
280 ACCEPT AT(9,15)SIZE(-7)VALIDATE(NUMERIC)BEEP:XX1 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 270
290 ACCEPT AT(9,23)SIZE(-6)VALIDATE(NUMERIC)BEEP:SS1 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 280
300 ACCEPT AT(14,1)SIZE(-13)VALIDATE(NUMERIC)BEEP:MM2 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 290
310 ACCEPT AT(14,15)SIZE(-7)VALIDATE(NUMERIC)BEEP:XX2 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 300
320 ACCEPT AT(14,23)SIZE(-6)VALIDATE(NUMERIC)BEEP:SS2 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 310
330 ACCEPT AT(19,1)SIZE(-13)VALIDATE(NUMERIC)BEEP:MM3 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 320
340 ACCEPT AT(19,15)SIZE(-7)VALIDATE(NUMERIC)BEEP:XX3 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 330
350 ACCEPT AT(19,23)SIZE(-6)VALIDATE(NUMERIC)BEEP:SS3 :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 340
360 ACCEPT AT(23,21)VALIDATE("YN")SIZE(-1):YN$ :: CALL KEY(3,K,S) :: IF K=11 THEN GOTO 350 ELSE IF YN$<>"Y" THEN GOTO 270
370 CALL CLEAR :: DISPLAY AT(12,1):"@HOLD@DOWN@ANY@KEY@TO@STOP@";
380 G=.001
390 M1=MM1 :: M2=MM2 :: M3=MM3 :: X1=XX1 :: X2=XX2 :: X3=XX3 :: S1=SS1 :: S2=SS2 :: S3=SS3
400 Y1N=96 :: X1N=128-X1 :: Y2N=96 :: X2N=128-X2 :: Y3N=96 :: X3N=128-X3
410 FOR X=1 TO 500 :: NEXT X :: CALL SCREEN(2) :: FOR X=1 TO 500 :: NEXT X :: CALL CLEAR
420 CALL CHAR(140,"8")
430 CALL CHAR(141,"C0C")
440 CALL CHAR(142,"707070")
450 CALL CHAR(143,"60F0F060")
460 IF(Y1N<255)*(Y1N>=1)*(X1N<255)*(X1N>=1)THEN CALL SPRITE(#1,143,11,Y1N,X1N)
470 IF(Y2N<255)*(Y2N>=1)*(X2N<255)*(X2N>=1)THEN CALL SPRITE(#2,142,7,Y2N,X2N)
480 IF(Y3N<255)*(Y3N>=1)*(X3N<255)*(X3N>=1)THEN CALL SPRITE(#3,141,15,Y3N,X3N)
490 SX1=0 :: SY1=S1 :: SX2=0 :: SY2=S2 :: SX3=0 :: SY3=S3
500 ! LOOP START
510 Y1P=Y1N :: X1P=X1N
520 Y2P=Y2N :: X2P=X2N
530 Y3P=Y3N :: X3P=X3N
540 Y1=Y1P-96 :: Y2=Y2P-96 :: Y3=Y3P-96
550 X1=X1P-128 :: X2=X2P-128 :: X3=X3P-128
560 D12=SQR((X1-X2)^2+(Y1-Y2)^2)
570 D23=SQR((X2-X3)^2+(Y2-Y3)^2)
580 D31=SQR((X3-X1)^2+(Y3-Y1)^2)
590 A12=ARCSIN((Y2-Y1)/D12) :: A23=ARCSIN((Y3-Y2)/D23) :: A31=ARCSIN((Y1-Y3)/D31)
600 F12=G*M1*M2/D12^2 :: F23=G*M2*M3/D23^2 :: F31=G*M3*M1/D31^2
610 F1X=F12*COS(A12)*SGN(X2-X1)+F31*COS(A31)*SGN(X3-X1)
620 F1Y=F12*SIN(A12)-F31*SIN(A31)
630 F2X=F12*COS(A12)*SGN(X1-X2)+F23*COS(A23)*SGN(X3-X2)
640 F2Y=-F12*SIN(A12)+F23*SIN(A23)
650 F3X=F31*COS(A31)*SGN(X1-X3)+F23*COS(A23)*SGN(X2-X3)
660 F3Y=F31*SIN(A31)-F23*SIN(A23)
670 SX2=SX2+F2X/M2 :: SX1=SX1+F1X/M1 :: SX3=SX3+F3X/M3
680 SY2=SY2+F2Y/M2 :: SY1=SY1+F1Y/M1 :: SY3=SY3+F3Y/M3
690 X1=X1+SX1 :: X2=X2+SX2 :: X3=X3+SX3
700 Y1=Y1+SY1 :: Y2=Y2+SY2 :: Y3=Y3+SY3
710 X1N=X1+128 :: X2N=X2+128 :: X3N=X3+128
720 Y1N=Y1+96 :: Y2N=Y2+96 :: Y3N=Y3+96
730 IF(Y2N<255)*(Y2N>=1)*(X2N<255)*(X2N>=1)THEN CALL SPRITE(#2,142,7,Y2N,X2N)ELSE CALL DELSPRITE(#2)
740 IF(Y1N<255)*(Y1N>=1)*(X1N<255)*(X1N>=1)THEN CALL SPRITE(#1,143,11,Y1N,X1N)ELSE CALL DELSPRITE(#1)
750 IF(Y3N<255)*(Y3N>=1)*(X3N<255)*(X3N>=1)THEN CALL SPRITE(#3,141,15,Y3N,X3N)ELSE CALL DELSPRITE(#3)
760 CALL KEY(3,K,S) :: IF NOT S THEN GOTO 500
770 DISPLAY AT(24,2):"End Redo or Start over  R"; :: ACCEPT AT(24,26)VALIDATE("ERS")BEEPSIZE(-1):ER$ :: IF ER$="R" THEN GOTO 200
780 IF ER$="S" THEN GOTO 140
790 RUN "DSK1.LOAD"
