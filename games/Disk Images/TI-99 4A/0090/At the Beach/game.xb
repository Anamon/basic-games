100 ! AT THE BEACH : Copyright (C) 1985 by Milo Tsukroff : V1A2311085RUPITIPROG
110 ! Released to the PUBLIC DOMAIN
120 RANDOMIZE :: CALL MAGNIFY(1) :: CALL TITLE :: CALL PAUSE(6) :: CALL BEACH :: CALL SUN :: CALL GULLS :: CALL SHELLS
130 CALL WAVE2 :: CALL WAVE1 :: CALL CRASH :: CALL FLAP :: GOTO 130
140 !
150 SUB FLAP :: CALL CHAR(34,"00669918",34,"81661818") :: SUBEND
160 !
170 SUB GULLS :: CALL CHAR(34,"00669918") :: FOR B=1 TO RND*5+5
180 CALL SPRITE(#B,34,RND+15,RND*130+8,RND*250+3,0,INT(RND*10+1)*SGN(RND-.5)) :: NEXT B :: CALL BOAT(B+1) :: SUBEND
190 !
200 SUB BEACH :: CALL INVISIBLE :: CALL CHAR(120,"0",128,RPT$("0",64),136,"0") :: CALL SCREEN(13) :: CALL HCHAR(20,1,136,160)
210 CALL HCHAR(1,1,120,288) :: FOR X=12 TO 18 STEP 2 :: CALL HCHAR(X,1,130,32) :: CALL HCHAR(X+1,1,131,32) :: NEXT X
220 CALL HCHAR(10,1,128,32) :: CALL HCHAR(11,1,129,32) :: CALL FRONDS :: CALL COLORS :: SUBEND
230 !
240 SUB WAVE1 :: CALL CHAR(129,RPT$("00",X)&"FF") :: X=X+1+8*(X=7) :: SUBEND
250 !
260 SUB WAVE2 :: CALL CHAR(130,RPT$("0000",X)&"FFFF"&RPT$("0000",14-X)) :: X=X+1+8*(X=7) :: SUBEND
270 !
280 SUB SHELLS :: CALL CHAR(104,"3078FC7E3F00000018FFFE7CECC40000000C1E3E3E7CF800003F3E1C3E7FFFFF")
290 CALL COLOR(10,16,12) :: FOR X=1 TO RND*20+10 :: CALL HCHAR(20+RND*4,1+RND*31,104+RND*3) :: NEXT X :: SUBEND
300 !
310 SUB CRASH :: CALL SOUND(-4000,-6,2*X+15) :: X=X+1+8*(X=7) :: SUBEND
320 !
330 SUB BOAT(X) :: CALL CHAR(140,"103070F0F0100000000000000000FF7E") :: CALL SPRITE(#X,140,11,66,100,#(X+1),141,7,66,100)
340 S=INT(RND*5-2) :: IF S=0 THEN 340 ELSE CALL MOTION(#X,0,S,#(X+1),0,S)
350 SUBEND
360 !
370 SUB PAUSE(X) :: FOR P=1 TO X*500 :: NEXT P :: SUBEND
380 !
390 SUB TITLE :: DISPLAY AT(2,1)ERASE ALL:"        AT THE BEACH": :"     Copyright (C) 1985":"      by Milo Tsukroff"
400 DISPLAY AT(8,1):"  A pleasing window onto a":"   sunny South Seas isle."
410 DISPLAY AT(11,1):"           ENJOY!": : : : : : :"      Released to the":"       PUBLIC DOMAIN" :: SUBEND
420 !
430 SUB SUN :: CALL CHAR(75,"183C7EFFFF7E3C18") :: CALL COLOR(6,11,8) :: CALL HCHAR(RND*2+1,RND*31+1,75) :: SUBEND
440 !
450 SUB FRONDS :: CALL CHAR(96,"EF6F6627263316137FEE67F66FF62C287F77270200000000FFEF476260200000")
460 CALL CHAR(100,"776337713B713010FF7F3E1207020702FFFF5F3E0C1C0C08FFFEFE6420000000") :: DISPLAY AT(1,1):"`gfedcbgfa``adfa`ccdbacdcbfg"
470 CALL HCHAR(1,1,98) :: CALL HCHAR(1,2,97) :: CALL HCHAR(1,31,103) :: CALL HCHAR(1,32,98) :: SUBEND
480 !
490 SUB INVISIBLE :: FOR X=9 TO 14 :: CALL COLOR(X,1,1) :: NEXT X :: SUBEND
500 !
510 SUB COLORS :: CALL COLOR(6,2,8,9,13,8,12,2,8,13,16,6,14,2,12) :: SUBEND
520 !
