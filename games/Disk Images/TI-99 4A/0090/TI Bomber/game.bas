100 REM  TIBOMBER converted by Milo Tsukroff from CP/M
110 RANDOMIZE
120 CALL CLEAR
130 CALL KEY(3,K,S)
140 PRINT "       TI BOMBER": : : : :"YOU ARE NOW A PILOT IN A    WORLD WAR II BOMBER": : :
150 INPUT "WHAT SIDE --                ITALY(1), ALLIES(2),        JAPANESE(3), GERMANY(4)     >":A
160 IF(A>0)*(A<5)THEN 190
170 PRINT:"TRY AGAIN....": :
180 GOTO 150
190 ON A GOTO 200,320,460,530
200 INPUT "WHAT IS YOUR TARGET --      ALBANIA(1), GREECE(2),      NORTH AFRICA(3)             >":B
210 IF(B>0)*(B<4)THEN 240
220 PRINT "TRY AGAIN...."
230 GOTO 200
240 PRINT
250 ON B GOTO 260,280,300
260 PRINT:"SHOULD BE EASY. YOU'RE      FLYING A NAZI-MADE PLANE."
270 GOTO 650
280 PRINT:"BE CAREFUL!!!"
290 GOTO 650
300 PRINT:"YOU'RE GOING FOR THE OIL,   EH?"
310 GOTO 650
320 INPUT "AIRCRAFT --                 LIBERATOR(1), B-29(2),      B-17(3), LANCASTER(4)       >":G
330 IF(G>0)*(G<5)THEN 360
340 PRINT:"TRY AGAIN....": :
350 GOTO 320
360 PRINT
370 ON G GOTO 380,400,420,440
380 PRINT:"YOU'VE GOT 2 TONS OF BOMBS  FLYING FOR PLOESTI"
390 GOTO 650
400 PRINT:"YOU'RE DROPPING THE A-BOMB  ON HIROSHIMA."
410 GOTO 650
420 PRINT:"YOU'RE CHASING THE BISMARK  IN THE NORTH SEA."
430 GOTO 650
440 PRINT:"YOU'RE BUSTING A GERMAN     HEAVY WATER PLANT IN THE    RUHR."
450 GOTO 650
460 PRINT:"YOU'RE FLYING A KAMIKAZE    (SUICIDE BOMBER) OVER THE   USS LEXINGTON."
470 INPUT "YOUR FIRST KAMIKAZE MISSION?(Y OR N) >":F$
480 IF F$<>"N" THEN 510
490 S=0
500 GOTO 910
510 PRINT
520 IF RND>.65 THEN 790 ELSE 1010
530 PRINT:"A NAZI, EH?  OH WELL. ARE   YOU GOING FOR --            RUSSIA(1), ENGLAND(2)"
540 INPUT "OR FRANCE(3)                >":M
550 IF(M>0)*(M<4)THEN 580
560 PRINT:"WHAT??": :
570 GOTO 530
580 PRINT
590 ON M GOTO 600,620,640
600 PRINT:"YOU'RE NEARING STALINGRAD..."
610 GOTO 650
620 PRINT:"NEARING LONDON. BE CAREFUL, THEY'VE GOT A GOOD AIR-RAID DEFENCE."
630 GOTO 650
640 PRINT:"NEARING VERSAILLES.  DUCK   SOUP.  THEY'RE NEARLY       DEFENSELESS."
650 PRINT
660 INPUT "HOW MANY MISSIONS HAVE YOU  FLOWN?  >":D
670 IF D<160 THEN 710
680 PRINT:"MISSIONS, NOT MILES...."
690 PRINT "150 MISSIONS IS HIGH EVEN   FOR OLD-TIMERS.  NOW THEN,"
700 GOTO 660
710 PRINT
720 IF D<100 THEN 750
730 PRINT:"THAT'S PUSHING THE ODDS!"
740 GOTO 770
750 IF D>=25 THEN 770
760 PRINT:"FRESH OUT OF TRAINING, EH?"
770 PRINT
780 IF D<160*RNDTHEN 810
790 PRINT:"DIRECT HIT!!!!  ";INT(100*RND);"KILLED! MISSION SUCCESSFUL."
800 GOTO 1060
810 PRINT "MISSED TARGET BY";INT(2+30*RND);"MILES!!"
820 PRINT:"NOW YOU'RE REALLY IN FOR    IT!!"
830 PRINT
840 INPUT "DOES THE ENEMY HAVE --      GUNS(1), MISSILES(2), OR    BOTH(3)?                    >":R
850 IF(R>0)*(R<4)THEN 880
860 PRINT:"TRY AGAIN...."
870 GOTO 840
880 PRINT
890 IF R=2 THEN 950
900 INPUT "WHAT IS THE PERCENT HIT RATEOF THE ENEMY GUNNERS?       (10 TO 50)  >":S
910 IF S>=10 THEN 950
920 PRINT:"YOU LIE, BUT YOU'LL PAY....": :
930 PRINT
940 GOTO 1010
950 PRINT
960 IF R<=1 THEN 980
970 T=35
980 IF S+T>100*RNDTHEN 1010
990 PRINT: :"YOU MADE IT THROUGH         TREMENDOUS FLAK!!": : :
1000 GOTO 1060
1010 PRINT: :"* * * * * BOOM * * * * *": : :
1020 CALL SOUND(1500,-7,0)
1030 PRINT "YOU HAVE BEEN SHOT DOWN......."
1040 PRINT: :"DEARLY BELOVED, WE ARE      GATHERED HERE TODAY"
1050 PRINT "TO PAY OUR LAST TRIBUTE.."
1060 PRINT
1070 PRINT
1080 PRINT
1090 INPUT "PLAY AGAIN? (Y OR N)        >":U$
1100 PRINT
1110 PRINT
1120 IF U$="Y" THEN 150
1130 PRINT "CHICKEN!!!!!"
1140 RUN "DSK1.LOAD"
