100 ! RAIN  :  Copyright (C) 1985 by Milo Tsukroff   :  V1A0081285RUPITIPROG
110 ! Released to the PUBLIC DOMAIN
120 RANDOMIZE :: CALL MAGNIFY(3) :: CALL TITLE :: CALL PAUSE(5) :: CALL WINDOW :: CALL OUTSIDE :: CALL RAIN :: CALL DROPS
130 CALL PATTER :: GOTO 130
140 !
150 SUB PATTER :: X=X+1+3*(X=2) :: CALL SOUND(15,-5,12+RND*7) :: CALL SOUND(-2000,-5,28) :: CALL SOUND(-(RND*(600+1000*X)+50),-5,28) :: SUBEND
160 !
170 SUB RAIN :: CALL CHAR(128,RPT$("0",16)&"0307070703"&RPT$("0",13)&"2041838F0E0E0C0C08")
180 FOR X=1 TO 4 :: CALL SPRITE(#X,32,1,89,1) :: NEXT X :: FOR X=5 TO 28 :: CALL SOUND(-2000,-5,29+(X>22))
190 S=RND*30+40 :: CALL SPRITE(#X,128,1,RND*180+20,RND*236+20,S+RND*5,-S+RND*5) :: CALL COLOR(#X,5) :: NEXT X :: SUBEND
200 !
210 SUB DROPS :: CALL CHAR(132,"010101030307070F0F1F1F1F0F0F07030000008080C0C0E0E0F0F0F0E0E0C080")
220 FOR X=5 TO 10 :: CALL SOUND(15,-5,15) :: CALL SOUND(-2000,-5,28) :: CALL PATTERN(#X,132) :: CALL MOTION(#X,RND*5+2,RND*3-3) :: CALL COLOR(#X,6) :: NEXT X :: SUBEND
230 !
240 SUB OUTSIDE :: FOR X=1 TO 11 :: CALL COLOR(X,2,2) :: DISPLAY AT(X,1):RPT$(" ",28); :: DISPLAY AT(X+13,1):RPT$(" ",28); :: NEXT X
250 X$="0C0C0C0C0C0C0C0C33333333333333336C6C6C6C6C6C6C6CCFCFCFCFCFCFCFCF" :: CALL CHAR(104,X$,112,X$)
260 CALL CHAR(96,"FFFFFFFFFFFFFFFF0103070103070F010300010003070001033F1F070F1F3F0F")
270 CALL CHAR(100,"0010187C18387CFE00103810301EBFDC000000000020327220207C30FF3E30FF")
280 FOR X=1 TO 8 :: Z$=RPT$("00",X)&RPT$("FF",8-X) :: CALL CHAR(87+X,Z$,71+X,Z$) :: NEXT X
290 Z$="C0F0FCFFFFFFFFFF00000000C0F0FCFFFF7F3F1F0F070301FFFFFFFFFFFFFFFF" :: CALL CHAR(80,Z$,64,Z$) :: Z$=RPT$("0",16)
300 CALL CHAR(84,Z$,68,Z$) :: CALL HCHAR(15,1,84,96) :: CALL HCHAR(17,1,68,256) :: FOR X=17 TO 24 :: CALL HCHAR(X,X+3,66)
310 IF X<22 THEN CALL HCHAR(X,(X-16)*2+21,64) :: CALL HCHAR(X,(X-16)*2+22,65)
320 CALL HCHAR(X,X+4,67,X-15+(X-22)*2*(X>=22)) :: NEXT X
330 FOR X=1 TO 8 :: CALL HCHAR(18,9-X,X+71) :: CALL HCHAR(17,17-X,X+71) :: CALL HCHAR(15,9-X,X+87) :: CALL HCHAR(14,26-X,X+87) :: NEXT X
340 CALL HCHAR(17,1,84,8) :: DISPLAY AT(15,14):"RPQ"; :: DISPLAY AT(16,15):"RSPQ"; :: DISPLAY AT(14,7):"_^]\\]^_";
350 FOR X=8 TO 11 :: DISPLAY AT(X,24):"hijih"; :: NEXT X :: CALL VCHAR(8,31,107,4) :: CALL VCHAR(8,32,106,4)
360 FOR X=14 TO 16 :: DISPLAY AT(X,24):"pqrqp"; :: NEXT X :: CALL VCHAR(14,31,115,3) :: CALL VCHAR(14,32,114,3)
370 CALL CHAR(108,"0C2E0C0C0C0C0C0F333F3333F73333373333337B3333337B") :: CALL HCHAR(10,26,108) :: CALL HCHAR(8,30,108)
380 CALL HCHAR(10,30,108) :: CALL HCHAR(8,27,109) :: CALL HCHAR(10,27,110) :: CALL VCHAR(8,29,110,2) :: CALL HCHAR(10,29,109)
390 FOR X=3 TO 7 :: CALL HCHAR(X,26,96,7) :: NEXT X
400 CALL VCHAR(4,25,97,4) :: CALL HCHAR(5,25,98) :: CALL HCHAR(6,25,99) :: CALL HCHAR(3,26,99)
410 CALL HCHAR(2,27,100) :: CALL HCHAR(2,28,101) :: CALL HCHAR(2,29,102) :: CALL HCHAR(2,30,103) :: CALL HCHAR(2,31,101) :: CALL HCHAR(2,32,100)
420 CALL COLOR(1,2,15,5,2,3,6,3,4,7,2,4,8,4,15,9,13,15,10,10,15,11,10,4) :: SUBEND
430 !
440 SUB PAUSE(N) :: FOR X=1 TO 500*N :: NEXT X :: SUBEND
450 !
460 SUB WINDOW :: CALL COLOR(14,5,2) :: CALL CHAR(143,"000000FFFF") :: CALL SCREEN(2) :: CALL HCHAR(12,1,143,64) :: SUBEND
470 !
480 SUB TITLE :: DISPLAY AT(2,13)ERASE ALL:"RAIN": :" (C) 1985 by Milo Tsukroff"
490 DISPLAY AT(8,1):" A quiet white-noise program for work, play, or going to sleep." :: DISPLAY AT(15,12):"ENJOY!"
500 DISPLAY AT(19,7):"Released to the":"       PUBLIC DOMAIN" :: SUBEND
510 !
