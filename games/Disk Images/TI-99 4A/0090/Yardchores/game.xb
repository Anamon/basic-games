100 ! YARDCHORES
110 ! Supplied as MITI-WARE
120 ! COPYRIGHT (C) 1984,         1986 by Milo Tsukroff
130 RANDOMIZE :: CALL MAGNIFY(3) :: CALL SCRN1 :: CALL AD_SCREEN
140 CALL CLEAR :: CALL SCREEN(8) :: OPTION BASE 1
150 DIM ULIM(2,7),LLIM(2,7)
160 CALL LIMITS(LLIM(,),ULIM(,))
170 CALL SCRN :: CALL TIME(TIME,1) :: CALL TIME(TIME,0) :: HISCORE=MAX(HISCORE,SCORE) :: SCORE,FERT,MOWED,CLIPPED,BUGS=0 :: CALL HEDGE(-1) :: CALL FLOWER(-1)
180 CALL TIME_WARN(-1)
190 DISPLAY AT(23,22):HISCORE; :: ENDQ,SFLG=0
200 Y=1 :: RAINLEV=5
210 GFLG,JFLG,STOPTIME=0
220 CALL SPRITE(#9,60,12,20*Y,8)
230 !\\Main Loop//(DEL OK)
240 CALL JOYST(1,KX,KY) :: CALL TIME(TIME,0) :: CALL KEY(1,K,S) :: KX=-4*(K=3)+KX :: KY=4*(K-1=-1)-4*(K=5)+KY
250 IF RFLG THEN IF RFLG<TIME THEN CALL NOSPRITE :: RFLG=0
260 IF KY THEN Y=Y-(KY/4)+7*(Y=1)*(KY=4)-7*(Y=7)*(KY=-4) :: CALL SPRITE(#9,60,12,20*Y,8)
270 IF K=18 OR KX=4 THEN 360
280 IF SFLG=0 THEN CALL DROUGHT(-1,UHOH)ELSE CALL DROUGHT(TIME,UHOH) :: IF UHOH THEN CALL NEWLAWN :: CALL FLOWER(-1) :: CALL COLOR(10,3,10) :: SFLG,MOWED,BUGS=0
290 IF RND<.3 THEN CALL MOVGOAT(ENDQ)
300 IF TIME>2700 THEN CALL TIME_WARN(TIME)
305 IF KY THEN 240
310 IF(ENDQ<>0)+(TIME>3000)THEN CALL ENDING :: GOTO 170
320 IF RND<.5 THEN CALL FLOWER(0)
330 CALL HEDGE(TIME) :: IF FERT THEN IF MOWED=233 AND CLIPPED=25 AND BUGS=25 THEN CALL WIN :: GOTO 170
340 GOTO 240
350 !\\Select Icon//(DELok)
360 CALL SOUND1 :: ON Y GOSUB 790,820,840,860,880,910,930
370 IF JFLG THEN 210
380 CALL TIME(TIME,0) :: STOPTIME=TIME+55 :: CALL DELSPRITE(#9) :: CALL NOSPRITE
390 B=Y*20 :: A=32 :: CALL SPRITE(#9,JCHAR,2,B,A)
400 !\\IconLoop//(DEL ok)
410 CALL JOYST(1,KX,KY) :: CALL TIME(TIME,0) :: CALL KEY(1,K,S) :: KX=4*(K=2)-4*(K=3)+KX :: KY=4*(K-1=-1)-4*(K=5)+KY
420 IF A+KX<LLIM(1,Y)THEN 210
430 IF(B-KY>=LLIM(2,Y))*(A+KX<=ULIM(1,Y))*(B-KY<=ULIM(2,Y))THEN A=A+KX*2 :: B=B-KY*2 :: CALL SPRITE(#9,JCHAR,2,B,A)
440 IF(ENDQ<>0)OR TIME>3000 THEN CALL ENDING :: GOTO 170
450 IF K=-1 AND KX=0 AND KY=0 AND RND<.4 THEN CALL MOVGOAT(ENDQ)
460 IF K=-1 AND KX=0 AND KY=0 AND RND<.4 THEN CALL FLOWER(0)
470 IF TIME>2700 THEN CALL TIME_WARN(TIME)
480 IF K=18 THEN ON Y GOSUB 530,530,600,640,680,710,740
490 IF GFLG THEN CALL SPRITE(#8,132,2,B-9,A-10)
500 IF TIME<STOPTIME THEN 410 ELSE 210
510 !\END iconloop/(DEL ok)
520 !\\JoyButton Loop//
530 ! CLIPPER
540 CALL CHAR(JCHAR,"00000000609090FF7F8F887"&RPT$("0",23)&"FEFFFE"&RPT$("0",12))
550 CALL SOUND(-1,-5,10) :: CALL SOUND(-1,-6,5) :: CALL SOUND(-1,-7,2,3400,11,5200,13)
560 CALL CHAR(JCHAR,"00000000609093FF0F7FC898F00000000000030E3CF0C000FFFE000000000000")
570 IF TIME<300 THEN RETURN ELSE CALL GCHAR(B/8+1,A/8+2,GCH1) :: IF GCH1=97 THEN CALL HCHAR(B/8+1,A/8+2,98)
580 IF GCH1=98 THEN CALL HCHAR(B/8+1,A/8+2,99) :: SCORE=SCORE+10 :: CALL SCORE(SCORE) :: CLIPPED=CLIPPED+1
590 RETURN
600 !SHOOTER
610 CALL GCHAR(B/8+1,A/8+1,MCHAR) :: IF MCHAR<>118 THEN CALL SOUND(-80,-5,5) :: CALL SOUND(-80,-6,8) :: CALL SOUND(-80,-7,15) :: RETURN
620 CALL SOUND5 :: SCORE=SCORE+50 :: BUGS=BUGS+1
630 FOR X_X=1 TO 4 :: CALL VCHAR(11-X_X,A/8+1,ASC(SEG$("uutâ€¹",X_X,1))) :: NEXT X_X :: CALL SCORE(SCORE) :: CALL SOUND3 :: RETURN
640 ! FERTILIZER
650 FOR E=1 TO 22 STEP 2 :: CALL SOUND(-10,-6,E) :: NEXT E :: CALL COINC(#9,84,100,4,E) :: STOPTIME=0
660 IF E THEN CALL SOUND3 :: CALL TRFL :: CALL MUS(1) :: SCORE=SCORE+90 :: CALL SCORE(SCORE) :: FERT=-1
670 RETURN
680 CALL SOUND(-1000,-4,3,110,10,121,10,1200,29)!MOWER
690 CALL GCHAR(B/8+1,A/8+1,MCHAR) :: IF MCHAR=105 THEN SCORE=SCORE+10 :: MOWED=MOWED+1 :: CALL HCHAR(B/8+1,A/8+1,106) :: CALL SCORE(SCORE)
700 RETURN
710 ! CHAIN
720 IF GFLG THEN CALL GS(2) :: STOPTIME=0 ELSE CALL COINC(#9,#8,12,E) :: CALL SOUND2 :: IF E THEN CALL RGOAT :: GFLG=1 ELSE STOPTIME=0
730 CALL HCHAR(24,3,32,28) :: RETURN
740 ! SEEDER
750 CALL SOUND2 :: CALL GCHAR(B/8+2,A/8+1,GCH1) :: IF GCH1=104 THEN CALL HCHAR(B/8+2,A/8+1,105) :: SCORE=SCORE+5 :: CALL SCORE(SCORE)
760 RETURN
770 !\END IconButton/DELok
780 !\\SETUP IconGrphics//        DEL ok
790 ! RAIN
800 SCORE=SCORE+50 :: DISPLAY AT(24,1):"                            ";
810 CALL TIME(RFLG,0) :: CALL RAIN :: RFLG=RFLG+30 :: JFLG=1 :: CALL DROUGHT(-1,UHOH) :: CALL SCORE(SCORE) :: RETURN
820 ! CLIPPERS
830 JCHAR=92 :: RETURN
840 ! SHOOTER
850 JCHAR=124 :: RETURN
860 ! FERTILIZER
870 JCHAR=108 :: RETURN
880 ! MOWER
890 IF SFLG=0 THEN JFLG=1 ELSE JCHAR=100
900 RETURN
910 ! CHAIN
920 JCHAR=40 :: RETURN
930 ! SEEDER
940 SFLG=1 :: JCHAR=140 :: RETURN
950 RETURN
960 !\END IconGrphics/DELok
970 !*\\END of reg. pgm//*
980 SUB SCRN :: CALL CHAR(112,"FF303000000000000000001010301018000000006C7C101010101016DEF01010")
990 CALL CHAR(116,"1010101076FE1210101010D0F7FFD71017141E17FFD79F160000183C3C7E7E10")
1000 CALL CHAR(36,"0") :: CALL CHAR(96,"FFFFFFFFFFFFFFFF00000000000000000000000077FFFFFF0") :: CALL CHAR(120,"0")
1010 CALL CHAR(40,"000038283010382838103828381F0D070000000000382838103828383070D070")
1020 CALL CHAR(120,RPT$("F",16)&"1F1F3F3F7F7FFFFF") :: CALL CHAR(128,RPT$("0",16)&"030707070300000000000002041838F0E0E0C0C080000000")
1030 CALL CHAR(136,RPT$("F",16)&"FF7F7F3F17070703FFFEFEFAF8F0E0A0FF111101D7011111")
1040 CALL CHAR(104,"00000000000000000010525ADEDEFE00FFDFFFFBFFBEFCFF") :: CALL CHAR(60,RPT$("F",64))
1050 CALL CHAR(100,"0000C02010090402070F0F06"&RPT$("0",18)&"F0F0E0FCFEFE0C00000000")
1060 CALL CHAR(108,"271C101030202A242A2030343E3B7F6FC6780808080808A848A80818B070F8E8") :: CALL RGOAT
1070 CALL CHAR(124,"FF818191888482F0828488918181FF00FE0202122242821E824222120202FE00")
1080 CALL CHAR(140,"0000C0A0100804070301"&RPT$("0",26)&"FCF8F8F0F06040A040")
1090 CALL CHAR(92,"00000000609093FF0F7FC898F00000000000030E3CF0C000FFFE000000000000")
1100 FOR C=1 TO 8 :: CALL COLOR(C,16,2) :: NEXT C
1110 CALL COLOR(11,13,4,9,4,1,10,3,10,12,16,1,13,11,1,14,13,1)
1120 CALL CLEAR :: CALL HCHAR(1,1,128,192) :: CALL HCHAR(1,7,136,19) :: CALL HCHAR(2,8,136,16) :: CALL HCHAR(3,10,136,13)
1130 CALL HCHAR(4,14,136,5) :: CALL HCHAR(1,6,137) :: CALL HCHAR(2,7,137) :: CALL HCHAR(3,9,137) :: CALL HCHAR(4,19,138) :: CALL HCHAR(3,23,138)
1140 CALL HCHAR(2,24,138) :: CALL HCHAR(1,26,138)
1150 CALL HCHAR(7,1,96,160) :: CALL HCHAR(11,1,112,32) :: CALL HCHAR(12,1,104,320)
1160 CALL TRTR :: CALL VCHAR(1,1,120,96) :: CALL VCHAR(1,32,120,24) :: CALL VCHAR(1,31,121,24)
1170 CALL HCHAR(22,1,36,96) :: CALL HCHAR(6,5,97,26) :: CALL TRTR
1180 DISPLAY AT(22,3):"TIME:",TAB(16);"SCORE:":"WARNINGS:";TAB(13);"HI SCORE:":
1190 DISPLAY AT(21,3):"  MILO'S TI YARD CHORES"
1200 CALL SPRITE(#1,128,5,20,8,#2,92,13,40,8,#3,124,7,60,8)
1210 CALL SPRITE(#4,108,6,80,8,#5,100,14,100,8,#6,40,2,120,8)
1220 CALL SPRITE(#7,140,7,140,8,#8,132,2,100,180) :: CALL SCORE(0)
1230 SUBEND
1240 SUB RAIN :: CALL SCREEN(2) :: FOR X=1 TO 3 :: CALL SCREEN(16) :: CALL SOUND(10,110,30) :: CALL SCREEN(2) :: NEXT X
1250 CALL SOUND(200,-5,2) :: CALL SOUND(140,-6,2) :: CALL SOUND(300,-7,0) :: CALL SOUND(1,110,30)
1260 FOR X=1 TO 20 :: CALL SOUND(-20,-7,RND*5+5+X/2) :: NEXT X :: CALL SCREEN(15) :: CALL COLOR(10,4,10)
1270 FOR X=11 TO 28 :: S=RND*30+40 :: CALL SPRITE(#X,128,1,RND*180+20,RND*236+20,S+RND*5,-S+RND*5) :: CALL COLOR(#X,5)
1280 CALL SOUND(-4250,-5,X+1) :: NEXT X :: CALL COLOR(10,3,10) :: SUBEND
1290 SUB RGOAT :: CALL CHAR(132,RPT$("0",14)&"3F7FBFBF3F3020201"&RPT$("0",14)&"2E1E1F1FEFE2E2E2612") :: SUBEND
1300 SUB LGOAT :: CALL CHAR(132,RPT$("0",12)&"4087878F5F7F72726244"&RPT$("0",14)&"FCFEFDFDFC0C040408") :: SUBEND
1310 SUB NOSPRITE :: FOR X=11 TO 28 :: CALL DELSPRITE(#X) :: NEXT X :: CALL SCREEN(8) :: SUBEND
1320 SUB SOUND1 :: CALL SOUND(200,1400,2) :: SUBEND
1330 SUB SOUND2 :: CALL SOUND(-20,626,2,392,4) :: SUBEND
1340 SUB LIMITS(U(,),L(,)) :: RESTORE 1340 :: FOR X=1 TO 7 :: READ U(1,X),L(1,X),U(2,X),L(2,X) :: NEXT X
1350 DATA 0,0,0,0,24,230,38,42,24,230,42,80,24,230,65,145
1360 DATA 24,230,77,153,32,238,83,153,28,230,73,145
1370 SUBEND
1380 SUB SCRN1 :: CALL CLEAR :: CALL SCREEN(12) :: CALL HCHAR(1,1,42,768) :: DISPLAY AT(3,1): :"   MILO'S TI YARD CHORES": :"     V2A0080786R0807861": :
1390 DISPLAY AT(9,1): :" An Original Game, which is": :" Copyright (C) 1984,1986 by": :"      Milo Tsukroff.": :
1400 DISPLAY AT(17,1): :"   Either use Joystick 1":" (release ALPHA LOCK), or    arrow keys & 'Q' to fire.": : :: CALL MUS(4)
1410 CALL SOUND1 :: FOR C=1 TO 200 :: DISPLAY AT(23,3):" PRESS ANY KEY TO GO ON "; :: CALL KEY(0,K,S) :: CALL JOYST(1,A,B) :: CALL KEY(1,X,Y)
1420 DISPLAY AT(23,3):"                        "; :: IF S OR Y OR A OR B THEN 1440
1430 NEXT C
1440 CALL SOUND3 :: SUBEND
1450 SUB TRTR :: CALL VCHAR(4,14,36,9) :: SUBEND
1460 SUB TRFL :: RESTORE 1460 :: FOR X=1 TO 16 :: READ R,C :: CALL HCHAR(R,C,139) :: NEXT X
1470 DATA 1,8,1,13,2,11,3,12,2,14,1,22,1,19,3,16,1,16,2,17,3,19,2,20,2,23,2,9,4,17,3,22
1480 SUBEND
1490 SUB MUS(Q) :: RESTORE 1490 :: FOR X=1 TO 17 :: READ D,N1,N2 :: CALL SOUND(D*Q,N1,2,N2,8) :: CALL KEY(0,K,S)
1500 IF K=13 THEN SUBEXIT
1510 NEXT X
1520 DATA 125,311,311,250,523,415,125,523,415,125,523,415,125,554,466
1530 DATA 125,622,523,250,622,466,125,622,415,250,392,311,125,311,311
1540 DATA 125,466,277,125,466,311,125,466,392,125,466,392,125,523,415,125,554,466,375,523,415
1550 CALL SOUND(1,110,30) :: SUBEND
1560 SUB GS(D) :: FOR X=1 TO D :: A$=CHR$(96)&CHR$(0)&CHR$(50)&RPT$(CHR$(90),50) :: CALL SAY(,A$) :: NEXT X :: SUBEND
1570 SUB TIME(T,Q) :: IF Q THEN T,P,R=0 :: CALL SPRITE(#10,32,1,200,1,T,1) :: SUBEXIT
1580 CALL POSITION(#10,Y,X) :: Y=X :: X=X-256*(X<P) :: R=R+X-P :: P=Y :: T=INT(R/3.3) :: DISPLAY AT(22,8):USING "#####":T; :: SUBEND
1590 SUB MOVGOAT(Q) :: CALL POSITION(#8,Y,X) :: D=SQR((Y-80)^2+(X-96)^2) :: Q=0 :: IF D>4 THEN CN=0
1600 IF D<25 AND D>4 THEN DISPLAY AT(24,3):"GOAT TOO NEAR TREE!      "; :: CALL SOUND(100,220,5) :: CN=0
1610 IF D<=4 AND CN<=10 THEN DISPLAY AT(24,3):"GOAT IS EATING THE BARK! "; :: CALL SOUND(100,220,2,415,0,-6,5) :: CN=CN+1
1620 IF CN>10 THEN CALL HCHAR(12,14,96) :: CALL SOUND(4250,110,30,110,30,440,30,-4,0) :: DISPLAY AT(24,3):"GOAT ATE BARK. TREE DIED."; :: Q=-1
1630 CALL LOCATE(#8,Y-SGN(Y-80),X-SGN(X-96)) :: SUBEND
1640 SUB SOUND3 :: CALL SOUND(500,165,2,208,4,247,8) :: CALL SOUND(750,220,2,277,4,330,8) :: SUBEND
1650 SUB DROUGHT(T,U) :: U=0 :: IF T=-1 THEN X=0 :: FLG1=0 :: FLG2=0 :: FLG3=0 :: SUBEXIT
1660 IF X=0 THEN X=T+200 :: Y=T+300 :: Z=T+400
1670 IF T>X THEN CALL MOVGOAT(Q) :: IF FLG1=0 THEN CALL COLOR(10,11,10) :: DISPLAY AT(24,3):"LAWN NEEDS WATER BADLY!  "; :: CALL SOUND1 :: FLG1=1
1680 IF T>Y THEN CALL MOVGOAT(Q) :: IF FLG2=0 THEN CALL COLOR(10,7,10) :: DISPLAY AT(24,3):"GRASS IS NEARLY DEAD!    "; :: CALL SOUND1 :: FLG2=1
1690 IF T>Z THEN IF FLG3=0 THEN DISPLAY AT(24,3):"GRASS DEAD; RESEED LAWN. "; :: CALL SOUND1 :: FLG3=1 :: U=1
1700 SUBEND
1710 SUB SCORE(S) :: DISPLAY AT(22,22):S; :: SUBEND
1720 SUB NEWLAWN :: FOR X=12 TO 20 :: CALL HCHAR(X,5,104,26) :: NEXT X :: CALL TRTR :: SUBEND
1730 SUB HEDGE(T) :: IF XF=0 OR T=-1 THEN XF=T+2 :: A1,A2,A3,A4,A5,A6,A7,A8,A9,A0=0 :: CALL CHAR(97,"0") :: SUBEXIT
1740 IF T>XF+50 AND A1=0 THEN CALL CHAR(97,"0000000000000012") :: A1=-1 :: SUBEXIT
1750 IF T>XF+100 AND A2=0 THEN CALL CHAR(97,"0000000000001277") :: A2=-1 :: SUBEXIT
1760 IF T>XF+150 AND A3=0 THEN CALL CHAR(97,"00000000001277FF") :: A3=-1 :: SUBEXIT
1770 IF T>XF+200 AND A4=0 THEN CALL CHAR(97,"000000001277FFFF") :: A4=-1 :: SUBEXIT
1780 IF T>XF+250 AND A5=0 THEN CALL CHAR(97,"0000001277FFFFFF") :: A5=-1 :: SUBEXIT
1790 IF T>XF+300 AND A6=0 THEN CALL CHAR(97,"00001277FFFFFFFF") :: A6=-1 :: SUBEXIT
1800 IF T>XF+350 AND A7=0 THEN CALL CHAR(97,"001277FFFFFFFFEF") :: A7=-1 :: SUBEXIT
1810 IF T>XF+400 AND A8=0 THEN CALL CHAR(97,"1277FFFFFFFFEFFF") :: A8=-1 :: SUBEXIT
1820 IF T>XF+450 AND A9=0 THEN CALL CHAR(97,"77FFFFFFFFFFFFFF") :: A9=-1 :: SUBEXIT
1830 IF T>XF+550 AND A0=0 THEN CALL CHAR(97,"FFFFFFFFFFFFFFFF") :: A0=-1 :: SUBEXIT
1840 SUBEND
1850 SUB ENDING :: FOR X=7 TO 28 :: CALL VCHAR(13,X,32,7) :: NEXT X
1860 DISPLAY AT(14,6):"SORRY, YOU FAILED TO"; :: DISPLAY AT(15,6):"DO YOUR YARD CHORES"; :: DISPLAY AT(16,6):"PROPERLY.";
1870 DISPLAY AT(18,9):"TRY AGAIN: Y/N";
1880 CALL KEY(2,K,S) :: CALL KEY(1,F,S) :: IF F<>18 AND K<>15 AND K<>18 THEN 1880 ELSE IF K=15 THEN CALL DELSPRITE(ALL) :: CALL CLEAR :: RUN "DSK1.LOAD"
1890 CALL MUS(2) :: CALL NOSPRITE :: SUBEND
1900 SUB WIN :: CALL SOUND3 :: CALL SOUND2 :: CALL SOUND1 :: FOR X=1 TO 5 :: CALL SCREEN(5) :: FOR Y=1 TO 20 :: NEXT Y :: CALL SCREEN(16) :: FOR Y=1 TO 20 :: NEXT Y :: NEXT X
1910 FOR X=7 TO 28 :: CALL VCHAR(13,X,120,7) :: NEXT X
1920 DISPLAY AT(14,7):"CONGRATULATIONS!!!"; :: DISPLAY AT(15,7):"YOU HAVE DONE YOUR"; :: DISPLAY AT(16,10):"YARD CHORES!";
1930 CALL NOSPRITE :: CALL MUS(3)
1940 DISPLAY AT(18,8):"PLAY AGAIN: Y/N";
1950 CALL KEY(2,K,S) :: CALL KEY(1,F,S) :: IF F<>18 AND K<>15 AND K<>18 THEN 1950 ELSE IF K=15 THEN CALL DELSPRITE(ALL) :: CALL CLEAR :: RUN "DSK1.LOAD"
1960 CALL MUS(2) :: CALL SCREEN(8) :: SUBEND
1970 SUB FLOWER(T)
1980 IF T=-1 THEN A$="00000000090000000000000000" :: CALL NEWFLOWERS :: SUBEXIT
1990 P=INT(RND*26+1)
2000 Q=VAL(SEG$(A$,P,1)) :: IF Q=9 THEN 2160
2010 Q=Q+1
2020 IF P=1 THEN A$=STR$(Q)&SEG$(A$,2,25) :: GOTO 2050
2030 IF P=26 THEN A$=SEG$(A$,1,25)&STR$(Q) :: GOTO 2050
2040 A$=SEG$(A$,1,P-1)&STR$(Q)&SEG$(A$,P+1,26-P)
2050 P=P+4 :: ON Q GOTO 2060,2070,2080,2090,2100,2110,2120,2130,2140
2060 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("q```",X,1))) :: NEXT X :: GOTO 2160
2070 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("r```",X,1))) :: NEXT X :: GOTO 2160
2080 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("sr``",X,1))) :: NEXT X :: GOTO 2160
2090 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("tsr`",X,1))) :: NEXT X :: GOTO 2160
2100 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("utr`",X,1))) :: NEXT X :: GOTO 2160
2110 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("utsr",X,1))) :: NEXT X :: GOTO 2160
2120 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("uttw",X,1))) :: NEXT X :: GOTO 2160
2130 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$("uutw",X,1))) :: NEXT X :: GOTO 2160
2140 IF RND<.333 THEN Z$="uuvw" ELSE IF RND<.5 THEN Z$="uvtw" ELSE Z$="vutw"
2150 FOR X=1 TO 4 :: CALL VCHAR(11-X,P,ASC(SEG$(Z$,X,1))) :: NEXT X
2160 SUBEND
2170 SUB NEWFLOWERS :: FOR Y=7 TO 10 :: CALL HCHAR(Y,5,96,26) :: NEXT Y :: CALL TRTR :: SUBEND
2180 SUB TIME_WARN(T) :: IF T=-1 THEN B=0 :: SUBEXIT
2190 IF T>2700 AND B=0 THEN B=-1 :: DISPLAY AT(24,3):"ALMOST TIME TO GO IN!    "; :: CALL SOUND1
2200 IF T>2995 THEN DISPLAY AT(24,3):"YOU HAVE TO GO IN NOW.   "; :: CALL SOUND2
2210 SUBEND
2220 SUB SOUND5 :: FOR X=1 TO 10 :: CALL SOUND(-1000,-1,X*2,565,X*3) :: NEXT X :: SUBEND
2230 SUB AD_SCREEN :: CALL CHAR(142,"55AA55AA55AA55AA") :: RESTORE 2310 :: FOR X=1 TO 9 :: READ C,N :: CALL SCREEN(C)
2240 IF RND>.5 THEN CALL HCHAR(1,1,N,768)ELSE CALL VCHAR(1,1,N,768)
2250 NEXT X :: DISPLAY AT(2,1):" TO WIN THIS GAME: You must seed, mow, & water the lawn;  trim the hedge; fertilize "
2260 DISPLAY AT(5,1):"the tree; & kill the aphids  on the flowers. Also, keep the goat away from the tree!"
2270 DISPLAY AT(9,1):" This program is supplied    as 'SHAREWARE'. Please sup- port the Author by mailing payment of $5.00 (suggested)"
2280 DISPLAY AT(13,1):"to: Milo Tsukroff               MITI-WARE                   84 Ash St., Apt. 32         Willimantic, CT   06226 "
2290 DISPLAY AT(17,1):" BBS: 203/456-1933 any baud "
2300 DISPLAY AT(19,1):" Look for more high-quality  MITI-WARE programs such as  CHINESE CHESS.             "
2310 DATA 14,92,10,47,4,45,16,60,8,94,12,62,6,37,10,36,15,142
2320 CALL SOUND1 :: FOR C=1 TO 800 :: DISPLAY AT(23,3):" PRESS ANY KEY TO BEGIN "; :: CALL KEY(0,K,S) :: CALL JOYST(1,A,B) :: CALL KEY(1,X,Y)
2330 DISPLAY AT(23,3):"                        "; :: IF S OR Y OR A OR B THEN 1440
2340 NEXT C
2350 CALL SOUND3 :: SUBEND
