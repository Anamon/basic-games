90 REM  WORDS & MUSIC BY            BILLY JOEL                  PGM BY SAM MOORE JR.        06/23/82
100 CALL SCREEN(5) :: CALL CLEAR :: CALL COLOR(14,3,1) :: CALL COLOR(2,3,1) :: CALL COLOR(4,11,1) :: CALL COLOR(5,11,1)
110 CS=1 :: X=132 :: FOR CHAR=143 TO 96 STEP-1 :: READ A$ :: CALL CHAR(CHAR,A$) :: NEXT CHAR
120 FOR CHAR=55 TO 63 :: READ A$ :: CALL CHAR(CHAR,A$) :: NEXT CHAR
130 CALL CHAR(40,"0000000103070F1E") :: CALL CHAR(41,"3C3870F0E0C0C08") :: CALL CHAR(47,"031F7FF8708") :: CALL CHAR(43,"FFFF")
140 !@P-
150 DISPLAY AT(4,7):CHR$(96)&CHR$(97) :: DISPLAY AT(5,7):CHR$(98)&CHR$(99)&CHR$(135) :: DISPLAY AT(5,13):CHR$(107)&CHR$(108)
160 DISPLAY AT(6,7):CHR$(100)&CHR$(101)&CHR$(102)&CHR$(103)&CHR$(104)&CHR$(105)&CHR$(106)&CHR$(109)
170 DISPLAY AT(7,7):CHR$(110)&CHR$(101)&CHR$(101)&CHR$(111)&CHR$(112)&CHR$(101)&CHR$(113)
180 DISPLAY AT(8,7):CHR$(143)&CHR$(114)&CHR$(101)&CHR$(115)&CHR$(101)&CHR$(101)&CHR$(116)
190 DISPLAY AT(9,7):CHR$(117)&CHR$(101)&CHR$(118)&CHR$(119)&CHR$(101)&CHR$(120)
200 DISPLAY AT(10,7):CHR$(121)&CHR$(101)&CHR$(122)&CHR$(123)&CHR$(101)&CHR$(124)
210 DISPLAY AT(11,7):CHR$(125)&CHR$(126)&CHR$(142)&CHR$(127)&CHR$(128)&CHR$(129) :: DISPLAY AT(12,7):CHR$(130)&" "&CHR$(141)&" "&CHR$(131)
220 DISPLAY AT(13,9):CHR$(140)&CHR$(139) :: DISPLAY AT(14,10):CHR$(138)&"(/++++++" :: DISPLAY AT(15,10):CHR$(137)&")" :: DISPLAY AT(16,10):CHR$(136)
230 DISPLAY AT(12,14):"79;=" :: DISPLAY AT(13,14):"8:<>?"
240 !@P+
250 FOR CR=2 TO 16 :: CS=CR+5
260 IF CS>16 THEN CS=CR-4
270 CALL SPRITE(#1,X,CS,37,73,#2,X,CS,41,113,#3,X,CS,51,110,#4,X,CS,51,85,#5,X,CS,51,75,#6,X,CS,51,101,#7,X,CS,61,83)
280 CALL SPRITE(#8,X,CS,61,76,#9,X,CS,62,94,#10,X,CS,63,100,#11,X,CS,70,72,#12,X,CS,70,80,#13,X,CS,70,92,#14,X,CS,72,100)
290 CALL SPRITE(#15,X,CS,78,75,#16,X,CS,80,97)
300 IF CR=5 THEN 330
310 FOR CC=9 TO 13 :: CALL COLOR(CC,CR,1) :: NEXT CC
320 IF ZP=1 THEN 460
330 FOR DLY=1 TO 199 :: NEXT DLY :: NEXT CR
340 ZP=1 :: GOTO 250
350 DATA 402030381C1E0F07,C0E1E377763C3C1C,1C0C0E0E07070707,03030101,80C0C0C0E0E0E07,38381C1C1C0C0E0E
360 !@P-
370 DATA 0E06060607070707,0707070707070707,00000080C0E0F0F8,0,0,2070D8702,CE0E0F0F0F0F06,1C08,E080808,FFFFFFFFFFFEFCEC
380 DATA FFFFFFFFEF0F0E04,FFFFBBB,03030307070F1E1E,E0F0F0F0E0E0E0E,BF3F3F3F7F7F7F7F,F2F1F0E0E0E0C0C
390 DATA 0707030303070703,FCF8F0000080C0E,BFBF3F3F3F3F7F7F,FFFFFFFFFDFDFAFA,0103030303030307,C0C0C0C0808
400 DATA FFFFEFEFCFDFDFFF,FFFFFF7F3F1F1FFF,F8F0F0E0E0E0C0C,7367CF9F3F7FFFFF,D8D8C8E8E9EAEAFD,0303030303030301
410 DATA 8,00000000E0E0C0C,0000000000030F7F,FFFFFFFEFEFCFCF8,0003071F3F7FFFFF,0000000000001031,0000000000109898
420 DATA FCFCFEFFFFFFFFFF,FFFFFFFFFFFFFFFF,0F0F0F0F07070707,FEFCFFFFFFFFFFFF,1F1F1F1F1F1F0F0F,00000000000080F
430 DATA 00000000001E1F1F,000000D8D8242414,1E171F0D01,0000000001030307,0FAFFFFFFFFCFC6C,060F7F7F7FFFFDF8
440 DATA E0E080808,60F0F8FCFCFE7F3F,1F3F1F0F0F070303,869FBFFFFCFC78C
450 !@P+
460 FOR CC=5 TO 8 :: CALL COLOR(CC,16,5) :: NEXT CC :: CALL VERSION(VER)
470 IF VER=110 THEN 490
480 DISPLAY AT(20,1):"THIS PROGRAM WORKS BEST ON  THE NEW EXTENDED BASIC":"MODULE--YOU HAVE THE OLD    MODEL PLUGGED IN." :: FOR DLY=1 TO 999 :: NEXT DLY
490 S=400 :: LB=247 :: LLA=110 :: LLAS,LLBF=117 :: LLB=123 :: HHA=1760 :: HHB=1976 :: HHAS,HHBF=1864 :: Q=40000
500 LC=131 :: LCS,LDF=139 :: LD=147 :: LDS,LEF=156 :: LE=165 :: LF=175 :: LFS,LGF=185 :: LG=196 :: LGS,LAF=208 :: LA=220 :: LAS,LBF=233
510 HB=988 :: C=262 :: CS,DF=277 :: D=294 :: DS,EF=311 :: E=330 :: F=349 :: FS,GF=370 :: G=392 :: GS,AF=415 :: A=440 :: AS,BF=466 :: B=494
520 HC=523 :: HCS,HDF=554 :: HD=587 :: HDS,HEF=622 :: HE=659 :: HF=698 :: HFS,HGF=740 :: HG=784 :: HGS,HAF=831 :: HA=880 :: HAS,HBF=932
530 HHC=1047 :: HHCS,HHDF=1109 :: HHD=1175 :: HHDS,HHEF=1245 :: HHE=1319 :: HHF=1397 :: HHFS,HHGF=1480 :: HHG=1568 :: HHGS,HHAF=1661
540 VE,VERSE,VERS=0 :: DISPLAY AT(17,1):"****************************"
550 !@P-
560 DISPLAY AT(18,1):"DON'T GO CHANGING___":"TO TRY AND PLEASE ME___":"YOU NEVER LET ME DOWN BEFORE":"MM____MM____":"DON'T IMAGINE,"
570 DISPLAY AT(23,1):"YOU'RE TOO FAMILIAR":"AND I DON'T SEE YOU ANYMORE."
580 J=LD :: K=LA :: L=FS :: GOSUB 1380 :: L=HD :: GOSUB 1340 :: L=HFS :: GOSUB 1350
590 J=LGS :: K=FS :: L=HD :: GOSUB 1340 :: L=B :: GOSUB 1350 :: J=LLB :: GOSUB 1380!1
600 VERSE=VERSE+1
610 J=LG :: K,L=LB :: GOSUB 1350 :: L=FS :: GOSUB 1340 :: L=B :: GOSUB 1340 :: L=HD :: GOSUB 1340!2
620 J=LB :: K=D :: L=B :: GOSUB 1420 :: L=FS :: GOSUB 1420 :: J=LD :: K=C :: L=FS :: GOSUB 1380!3
630 J=LG :: K,L=LB :: GOSUB 1350 :: L=FS :: GOSUB 1340 :: L=G :: GOSUB 1340 :: L=A :: GOSUB 1350!4
640 J=LG :: K=LBF :: L=FS :: GOSUB 1420 :: L=D :: GOSUB 1340 :: K=D :: GOSUB 1350 :: K,L=LBF :: GOSUB 1420!5
650 J=LA :: K=D :: L=FS :: GOSUB 1380 :: J=LD :: GOSUB 1380!6
660 J=LLA :: K=C :: L=G :: GOSUB 1420 :: L=A :: GOSUB 1340 :: L=FS :: J=LD :: GOSUB 1350 :: L=E :: GOSUB 1420!7
670 J=LG :: K=LB :: L=FS :: GOSUB 1380 :: K=LLB :: GOSUB 1420 :: K=LB :: GOSUB 1340 :: L=E :: GOSUB 1340!8
680 J=LG :: K=LBF :: L=E :: GOSUB 1340 :: L=D :: GOSUB 1350 :: K=LLBF :: GOSUB 1380!9
690 J=LFS :: K,L=LA :: GOSUB 1350 :: L=D :: GOSUB 1340 :: L=FS :: GOSUB 1420 :: L=A :: GOSUB 1420!10
700 J=LLB :: K=FS :: L=HCS :: GOSUB 1420 :: L=HD :: FOR VE=1 TO 13 :: CALL SOUND(-S,J,VE,K,VE,L,VE) :: NEXT VE!11
710 L=LE :: K=D :: L=FS :: GOSUB 1350 :: L=B :: GOSUB 1340 :: K=LA :: GOSUB 1340 :: L=HCS :: GOSUB 1350!12
720 IF VERSE=6 THEN 1230
730 IF VERSE=4 THEN 920
740 IF VERS=3 THEN 1230
750 IF VERSE<>2 THEN 820
760 DISPLAY AT(18,1):"DON'T GO TRYING___":"SOME NEW FASHION___":"DON'T CHANGE THE COLOR OF   YOUR HAIR.  MM____"
770 DISPLAY AT(22,1):"YOU ALWAYS HAVE MY,":"UNSPOKEN PASSION,   ALTHOUGHI MIGHT NOT SEEM TO CARE."
780 J=LG :: K=B :: L=HD :: GOSUB 1420 :: L=HFS :: GOSUB 1340 :: L=B :: GOSUB 1340 :: J=LLA :: GOSUB 1420 :: L=HCS :: GOSUB 1420!17
790 J=LG :: K=B :: L=HD :: GOSUB 1420 :: L=HFS :: GOSUB 1340 :: L=B :: GOSUB 1340 :: J=LLA :: GOSUB 1420 :: L=HCS :: GOSUB 1420!17
800 J=LD :: K=FS :: L=HD :: GOSUB 1350 :: L=D :: GOSUB 1340 :: L=HD :: GOSUB 1340 :: L=HFS :: GOSUB 1350!18
810 GOTO 590
820 J=LGS :: K=FS :: L=HD :: GOSUB 1420 :: L=HE :: GOSUB 1420 :: J=LE :: GOSUB 1340 :: L=HCS :: GOSUB 1420 :: L=B :: GOSUB 1340!13
830 J=LG :: K=LB :: L=A :: GOSUB 1380 :: L=D :: GOSUB 1380!14
840 L=G :: GOSUB 1380 :: L=B :: GOSUB 1420 :: L=HD :: GOSUB 1420!15
850 IF VERSE<>1 THEN 870 :: DISPLAY AT(18,1):"I WOULD NOT LEAVE YOU,":"IN TIMES OF TROUBLE_WE NEVERCOULD HAVE COME THIS FAR."
860 DISPLAY AT(21,1):"I TOOK THE GOOD TIMES,":"I'LL TAKE THE BAD TIMES,":"I'LL TAKE YOU JUST THE WAY  YOU ARE." :: GOTO 900
870 IF VERS=4 THEN 900
880 DISPLAY AT(18,1):"I DON'T WANT CLEVER":"CONVERSATION.   I NEVER WANTTO WORK THAT HARD,   MM_____"
890 DISPLAY AT(21,1):"I JUST WANT SOMEONE":"THAT I CAN TALK TO,":"I WANT YOU JUST THE WAY":"YOU ARE."
900 J=LD :: K=FS :: L=HD :: GOSUB 1350 :: J=LA :: GOSUB 1340 :: J=LD :: GOSUB 1340 :: L=HFS :: GOSUB 1350!16
910 GOTO 590
920 J=LG :: K=B :: L=HD :: GOSUB 1420 :: L=HFS :: GOSUB 1340 :: L=B :: GOSUB 1340 :: J=LLA :: GOSUB 1420 :: L=HCS :: GOSUB 1420!19
930 J,K=LD :: L=HD :: GOSUB 1340 :: K=LA :: GOSUB 1340 :: L=D :: GOSUB 1340 :: J=LBF :: K=E :: L=G :: GOSUB 1340 :: J=LLBF :: GOSUB 1380!20
940 J,K,L=LD :: GOSUB 1340 :: K,L=LA :: GOSUB 1340 :: L=D :: GOSUB 1340 :: K=G :: L=B :: GOSUB 1340 :: K=LA :: GOSUB 1340 :: L=A :: GOSUB 1350!21
950 J,K,L=LD :: GOSUB 1340 :: K,L=LA :: GOSUB 1340 :: L=D :: GOSUB 1340 :: J=LBF :: K=E :: L=E :: GOSUB 1340 :: J=LLBF :: GOSUB 1340 :: L=FS :: GOSUB 1340 :: L=G :: GOSUB 1340!22
960 IF VERS=2 THEN 1200
970 K,L=A :: GOSUB 1340 :: K,L=B :: GOSUB 1340 :: K,L=A :: GOSUB 1340 :: K,L=FS :: GOSUB 1340 :: K,L=E :: J=C :: GOSUB 1420 :: K,L=D :: GOSUB 1420!23
980 DISPLAY AT(18,1):"":"":"I NEED TO KNOW THAT YOU WILL":"ALWAYS BE":"THE SAME OLD SOMEONE":"THAT I KNEW.":"":""
990 J=LG :: K,L=LB :: GOSUB 1350 :: L=D :: GOSUB 1340 :: L=B :: GOSUB 1420 :: L=HD :: GOSUB 1420!24
1000 J=LA :: K=E :: L=HCS :: GOSUB 1420 :: L=B :: GOSUB 1340 :: J=LLA :: GOSUB 1340 :: K=CS :: L=A :: GOSUB 1420 :: L=G :: GOSUB 1420!25
1010 J=LFS :: K=E :: L=A :: GOSUB 1350 :: J=FS :: GOSUB 1340 :: J=LFS :: GOSUB 1420 :: L=HCS :: GOSUB 1420!26
1020 J=LLB :: K=DS :: L=B :: GOSUB 1350 :: J=LB :: GOSUB 1340 :: J=LLB :: GOSUB 1380!27
1030 J=LE :: K,L=LG :: GOSUB 1350 :: L=E :: GOSUB 1340 :: K=D :: L=G :: GOSUB 1420 :: L=B :: GOSUB 1420!28
1040 J=LLA :: K=CS :: L=A :: GOSUB 1340 :: L=G :: GOSUB 1340 :: J=LA :: GOSUB 1340 :: J=LLA :: L=FS :: GOSUB 1420 :: L=E :: GOSUB 1420!29
1050 J=LD :: K=D :: L=FS :: GOSUB 1350 :: J=D :: GOSUB 1340 :: J=LD :: GOSUB 1380!30
1060 DISPLAY AT(20,1):"OH, WHAT WILL IT TAKE TILL":"YOU BELIEVE IN ME":"THE WAY THAT I BELIEVE IN":"YOU____":"":"" :: VERS=4
1070 J,K,L=LD :: GOSUB 1350 :: K=D :: GOSUB 1340 :: J,K,L=LCS :: GOSUB 1340 :: K=E :: L=HD :: GOSUB 1350!31
1080 J,K,L=LLBF :: GOSUB 1340 :: K=BF :: L=HD :: GOSUB 1420 :: J=LBF :: GOSUB 1340 :: J=LLBF :: GOSUB 1420 :: K=HF :: GOSUB 1420!32
1090 J=LC :: K=G :: L=HE :: GOSUB 1420 :: L=HD :: GOSUB 1420 :: K=E :: L=HC :: GOSUB 1340 :: L=HBF :: GOSUB 1350!33
1100 J=LLA :: K=E :: L=HC :: GOSUB 1350 :: K=LE :: GOSUB 1340 :: K=E :: GOSUB 1420 :: K=G :: L=HE :: GOSUB 1420!34
1110 J=LD :: K=FS :: L=HD :: GOSUB 1380 :: K=C :: GOSUB 1380!35
1120 J=LG :: K=LBF :: L=F :: GOSUB 1350 :: K=BF :: GOSUB 1340 :: K=LBF :: GOSUB 1420 :: K=HD :: GOSUB 1420!36
1130 J=LC :: K=E :: L=HC :: GOSUB 1420 :: L=BF :: GOSUB 1340 :: L=A :: GOSUB 1340 :: K=LE :: GOSUB 1420 :: L=G :: GOSUB 1420!37
1140 J=LLA :: K=D :: L=A :: GOSUB 1350 :: L=D :: GOSUB 1340 :: L=G :: GOSUB 1380!38
1150 J=LLA :: K=LG :: L=B :: GOSUB 1350 :: L=HD :: GOSUB 1340 :: K=G :: GOSUB 1380!39
1160 DISPLAY AT(19,1):"I SAID I LOVE YOU_AND THAT'SFOREVER__AND THIS I PROMISE FROM THE HEART, MM____MM____I COULD NOT LOVE YOU ANY"
1170 DISPLAY AT(23,1):"BETTER.  I LOVE YOU ":"JUST THE WAY YOU ARE."
1180 J=LD :: K=LA :: L=FS :: GOSUB 1420 :: L=HD :: GOSUB 1420 :: K=FS :: GOSUB 1420 :: L=HFS :: GOSUB 1420!40
1190 VERS=2 :: GOTO 590
1200 J,K,L=LD :: GOSUB 1340 :: K,L=LA :: GOSUB 1340 :: L=D :: GOSUB 1340 :: K=G :: L=B :: GOSUB 1340 :: K=LG :: GOSUB 1340 :: K,L=A :: GOSUB 1350!41
1210 J=LD :: K=LA :: L=FS :: GOSUB 1380 :: L=HD :: GOSUB 1340 :: L=HFS :: GOSUB 1350!42
1220 VERS=3 :: GOTO 590
1230 REM  PICK UP LAST 2 LINES
1240 J=LLA :: K=B :: L=HD :: GOSUB 1420 :: L=HFS :: GOSUB 1340 :: L=HE :: GOSUB 1340 :: K=LG :: GOSUB 1420 :: L=HD :: GOSUB 1420!43
1250 J=LLBF :: K=F :: L=HHD :: GOSUB 1350 :: K=LF :: GOSUB 1340 :: K=F :: GOSUB 1420 :: K=F :: GOSUB 1340 :: L=BF :: GOSUB 1340!44
1260 J=LC :: K=E :: L=C :: GOSUB 1350 :: K=LE :: GOSUB 1340 :: K=E :: GOSUB 1420 :: J=LLBF :: K,L=HC :: GOSUB 1340 :: K,L=BF :: GOSUB 1340!45
1270 J=LLA :: K=G :: L=C :: GOSUB 1350 :: J=LA :: GOSUB 1340 :: J=LLA :: GOSUB 1380!46
1280 J=LD :: K=FS :: L=HD :: GOSUB 1420 :: L=HC :: GOSUB 1340 :: K=LFS :: GOSUB 1340 :: K=FS :: L=BF :: GOSUB 1420 :: L=A :: GOSUB 1420!47
1290 J=LG :: K=F :: L=BF :: GOSUB 1350 :: J=G :: GOSUB 1340 :: J=LG :: GOSUB 1420 :: L=HD :: GOSUB 1420!48
1300 J=LLA :: K=G :: L=HCS :: GOSUB 1420 :: L=B :: GOSUB 1340 :: K=LG :: GOSUB 1340 :: K=CS :: L=A :: GOSUB 1420 :: L=G :: GOSUB 1420!49
1310 FOR VERS=1 TO 30 :: CALL SOUND(-S,LD,VERS,CS,VERS,FS,VERS) :: NEXT VERS!50
1320 END
1330 !@P+
1340 CALL SOUND(-S,J,1,K,1,L,1) :: CALL SOUND(-S,J,2,K,2,L,2) :: CALL SOUND(-S,J,3,K,3,L,3) :: RETURN
1350 CALL SOUND(-S,J,1,K,1,L,1) :: CALL SOUND(-S,J,2,K,2,L,2) :: CALL SOUND(-S,J,3,K,3,L,3)
1360 CALL SOUND(-S,J,4,K,4,L,4) :: CALL SOUND(-S,J,5,K,5,L,5) :: CALL SOUND(-S,J,6,K,6,L,6)
1370 CALL SOUND(-S,J,7,K,7,L,7) :: CALL SOUND(-S,J,8,K,8,L,8) :: CALL SOUND(-S,J,9,K,9,L,9) :: RETURN
1380 CALL SOUND(-S,J,1,K,1,L,1) :: CALL SOUND(-S,J,2,K,2,L,2) :: CALL SOUND(-S,J,3,K,3,L,3)
1390 CALL SOUND(-S,J,4,K,4,L,4) :: CALL SOUND(-S,J,5,K,5,L,5) :: CALL SOUND(-S,J,6,K,6,L,6)
1400 CALL SOUND(-S,J,7,K,7,L,7) :: CALL SOUND(-S,J,8,K,8,L,8) :: CALL SOUND(-S,J,9,K,9,L,9)
1410 CALL SOUND(-S,J,10,K,10,L,10) :: CALL SOUND(-S,J,11,K,11,L,11) :: CALL SOUND(-S,J,12,K,12,L,12) :: RETURN
1420 CALL SOUND(-S,J,1,K,1,L,1) :: CALL SOUND(-S,J,2,K,2,L,2) :: CALL SOUND(-S,J,3,K,3,L,3)
1430 CALL SOUND(-S,J,4,K,4,L,4) :: CALL SOUND(-S,J,5,K,5,L,5) :: CALL SOUND(-S,J,6,K,6,L,6) :: RETURN
