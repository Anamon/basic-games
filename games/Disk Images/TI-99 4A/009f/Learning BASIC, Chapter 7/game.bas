10 QA=600
20 DIM B$(12)
30 B$(0)="10 Z$=""ABCDEFGHIJKLMNOPQRSTUVWXYZ"""
40 B$(1)="20 INPUT""PLEASE ENTER YOUR NAME "":N$"
50 B$(2)="30 Y=POS(N$,"" "",1)"
60 B$(3)="40 IF Y=0 THEN 20"
70 B$(4)="50 F$=SEG$(N$,1,Y-1)"
80 B$(5)="60 FL=LEN(N$)-1"
90 B$(6)="70 FOR I=1 TO 26"
100 B$(7)="80 C$=SEG$(Z$,I,1)"
110 B$(8)="90 E=POS(N$,C$,1)"
120 B$(9)="100 IF E=0 THEN 120"
130 B$(10)="110 PRINT C$;"" IS IN NAME"""
140 B$(11)="120 NEXT I"
150 B$(12)="130 PRINT""YOUR NAME HAS "";FL;"" LETTERS """
160 GOTO 310
170 Z$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
180 INPUT "PLEASE ENTER YOUR NAME:     ":N$
190 Y=POS(N$," ",1)
200 IF Y=0 THEN 180
210 F$=SEG$(N$,1,Y-1)
220 FL=LEN(N$)-1
230 FOR I=1 TO 26
240 C$=SEG$(Z$,I,1)
250 E=POS(N$,C$,1)
260 IF E=0 THEN 280
270 PRINT C$;" IS IN NAME"
280 NEXT I
290 PRINT "YOUR NAME HAS ";FL;" LETTERS."
300 GOTO 2590
310 CALL CLEAR
330 CALL COLOR(11,5,5)
340 CALL COLOR(12,11,11)
350 CALL COLOR(13,7,7)
360 CALL COLOR(14,13,13)
370 CALL CHAR(112,"1818181818181818")
380 CALL CHAR(120,"000000FFFF000000")
390 CALL CHAR(128,"000000FFFF000000")
400 CALL CHAR(136,"1818181818181818")
410 GOTO 780
420 FOR Q=1 TO QA
430 NEXT Q
440 RETURN
450 LC=16.02
460 R=INT(LC)
470 QQ=0
480 C=100*(LC-R)
490 FOR Q=1 TO LEN(A$)
500 QQ=QQ+1
510 IF C+QQ<31 THEN 540
520 R=R+1
530 QQ=1
540 CALL HCHAR(R,C+QQ,ASC(SEG$(A$,Q,1)))
550 NEXT Q
560 LC=LC+1
570 RETURN
580 INPUT "PRESS ENTER ":Q$
590 CALL CLEAR
600 RETURN
610 A$=AA$
620 GOSUB 460
630 FOR II=1 TO 5
640 CALL HCHAR(R,C+1,112,LEN(AA$))
650 LC=LC-1
660 GOSUB 460
670 NEXT II
680 RETURN
690 LC=24.02
700 A$="PRESS ENTER"
710 GOSUB 460
720 CALL KEY(G,KE,ST)
730 IF ST=0 THEN 720
740 GOSUB 760
750 RETURN
760 CALL HCHAR(16,1,32,290)
770 RETURN
780 GOSUB 800
790 GOTO 990
800 CALL SCREEN(8)
810 FOR I=0 TO 7
820 CALL VCHAR(1,4*I+2,112,24)
830 NEXT I
840 FOR I=0 TO 5
850 CALL HCHAR(4*I+3,1,128,32)
860 NEXT I
870 FOR I=0 TO 6
880 CALL VCHAR(1,4*I+4,136,24)
890 NEXT I
900 FOR I=0 TO 5
910 CALL HCHAR(4*I+1,1,120,32)
920 NEXT I
930 FOR I=1 TO 32 STEP 4
940 FOR K=1 TO 24 STEP 4
950 CALL HCHAR(K,I+1,112)
960 NEXT K
970 NEXT I
980 RETURN
990 QA=600
1000 GOSUB 420
1010 CALL COLOR(11,5,1)
1020 CALL COLOR(12,11,1)
1030 CALL COLOR(13,7,1)
1040 CALL COLOR(14,13,1)
1050 GOSUB 420
1060 CALL SCREEN(16)
1070 A$="LEARNING BASIC  CHAPTER 7      STRINGS"
1080 LC=4.16
1090 GOSUB 460
1100 CALL SCREEN(4)
1110 GOSUB 420
1120 CALL CLEAR
1130 CALL COLOR(11,5,5)
1140 PRINT "PLEASE ENTER YOUR NAME:"
1150 INPUT NM$
1160 Y=POS(NM$," ",1)
1170 IF Y>0 THEN 1200
1180 PRINT "THIS TIME I NEED YOUR":"FIRST AND LAST NAMES.": :
1190 GOTO 1140
1200 BB$=SEG$(NM$,1,Y-1)
1210 PRINT "WELL ";BB$:"NOW WE WILL LEARN ABOUT":"STRINGS. STRINGS ARE VERY":"IMPORTANT IN PROGRAMMING."
1220 PRINT: :"STRINGS, AS YOU KNOW BY NOW,":"ARE COLLECTIONS OF":"CHARACTERS, BOTH NUMERIC AND"
1230 PRINT "ALPHABETIC, THAT THE":"COMPUTER DOES NOT REGARD AS":"NUMBERS TO CALCULATE UPON.": : :
1240 GOSUB 580
1250 PRINT """DOG""":"""QWERTYUIOP""":"""";BB$;"""":"""FJDFD23*(^/3""": : :
1260 PRINT "THESE ARE ALL EXAMPLES":"OF STRINGS.": :"ACTUALLY THE TEXT WITHIN":"THE QUOTE MARKS ARE"
1270 PRINT "THE STRINGS, BUT IF YOU WANT":"TO TELL THE COMPUTER THAT A":"THING IS A STRING, YOU MUST"
1280 PRINT "PUT QUOTE MARKS AROUND IT.": :
1290 GOSUB 580
1300 PRINT "AS AN EXAMPLE OF WHAT":"STRINGS CAN DO, LET'S MAKE":"A PROGRAM TO TELL WHAT"
1310 PRINT "LETTERS OF THE ALPHABET ARE":"IN YOUR NAME. IT WILL ALSO":"TELL YOU HOW MANY LETTERS"
1320 PRINT "ARE IN YOUR NAME.": : :
1330 GOSUB 580
1340 PRINT B$(1): : : :"I'M SURE YOU KNOW WHAT":"THIS STATEMENT MEANS.":"IT IS ASKING YOU TO ENTER"
1350 PRINT "YOUR NAME.":"YOUR NAME WILL BE THE":"STRING THAT THE COMPUTER":"WILL CALL N$.": : : :
1360 GOSUB 580
1370 PRINT B$(1):B$(2): : : : : : : :"THIS IS NEW!": :
1380 CALL HCHAR(15,1,112,32)
1390 GOSUB 580
1400 PRINT B$(2): : : : :"POS IS A STRING FUNCTION.":"IT IS A WAY OF FINDING WHERE":"ONE STRING OCCURS WITHIN"
1410 PRINT "ANOTHER STRING.": : :"Y=POS(N$," ",1)   SAYS,"
1420 PRINT "LOOK AT N$ AND SEE IF IT":"CONTAINS A BLANK.":"IF THERE IS A BLANK IN N$,":"Y WILL BE THE NUMBER OF"
1430 PRINT "CHARACTERS INTO N$ THAT THE":"FIRST BLANK OCCURS.":"IF THERE IS NO BLANK, THEN":"Y WILL BE ZERO.": : : :
1440 GOSUB 580
1450 PRINT "Y=POS(A$,B$,K)": : : :"MEANS SEARCH A$ FOR THE":"FIRST OCCURANCE OF B$.":"START THE SEARCH K LETTERS"
1460 PRINT "INTO A$. Y IS THE NUMBER OF":"CHARACTERS INTO A$ THAT B$":"BEGINS. IF THE SEARCH IS"
1470 PRINT "UNSUCCESSFUL, THEN Y IS SET":"TO ZERO.": : : : :
1480 GOSUB 580
1490 GOSUB 1510
1500 GOTO 1540
1510 PRINT "HERE IS A SHORT PROGRAM":"USING POS.": : :"10 INPUT""ENTER A STRING: "":V$"
1520 PRINT "20 INPUT""ENTER ANOTHER STRING: "":W$":"30 Y=POS(V$,W$,1)":"40 PRINT Y":"50 END": : :
1530 RETURN
1540 PRINT "THE STATEMENT Y=POS(V$,W$,1)":"SEARCHES V$ FOR THE FIRST":"OCCURANCE OF W$.": :
1550 PRINT "FOR EXAMPLE,":"IF V$=""CATDOGMOUSEHORSE""":"AND W$=""DOG"", THEN":"POS(V$,W$,1) WOULD BE 4.": :
1560 GOSUB 580
1570 GOSUB 1510
1580 PRINT "TRY THE PROGRAM.":"WHEN YOU WISH TO GO ON,":"JUST ENTER 0 FOR V$.": :
1590 INPUT "ENTER A STRING: ":V$
1600 IF V$="0" THEN 1650
1610 INPUT "ENTER ANOTHER STRING: ":W$
1620 PRINT POS(V$,W$,1)
1630 PRINT: : : :
1640 GOTO 1570
1650 CALL CLEAR
1660 PRINT B$(1):B$(2):B$(3): : : : : :
1670 PRINT "STATEMENT 40 JUST SAYS:":"IF THERE IS NO BLANK IN THE":"ENTERED NAME, GO BACK AND"
1680 PRINT "ASK FOR THE NAME AGAIN.": :"THIS MAKES SURE THAT MORE":"THAN THE FIRST NAME IS":"ENTERED.": :
1690 PRINT "WE WANT TWO NAMES":"TO BE ENTERED.": : :
1700 GOSUB 580
1710 PRINT B$(1):B$(2):B$(3):B$(4): : : : : : :"THIS IS ANOTHER":"STRING FUNCTION."
1720 AA$="SEG$"
1730 LC=15.08
1740 GOSUB 610
1750 PRINT "SEG$ LETS US TAKE A PART OF":"A STRING AND MAKE A NEW":"STRING OUT OF IT.": :
1760 GOSUB 580
1770 PRINT "SEG$(A$,B,C)": :"MEANS TAKE THE STRING A$ AND":"MAKE A NEW STRING STARTING"
1771 PRINT "FROM B CHARACTERS INTO A$,"
1780 PRINT "C CHARACTERS LONG.": :"FOR EXAMPLE,": :"A$=""CATDOGMOUSE""":"B$=SEG$(A$,4,3)": :
1790 PRINT "WOULD GIVE B$=""DOG""": : :
1800 GOSUB 580
1810 PRINT "HERE IS ANOTHER LITTLE":"PROGRAM FOR YOU TO TRY.": :"10 INPUT""ENTER A STRING: "":A$"
1820 PRINT "20 INPUT""ENTER A NUMBER: "":X":"30 INPUT""ENTER ANOTHER NUMBER: "":Y":"40 PRINT SEG$(A$,X,Y)": :
1830 PRINT "WHEN YOU WANT TO GO ON,":"JUST ENTER 0 FOR A$.": :
1840 INPUT "ENTER A STRING: ":AA$
1850 IF AA$="0" THEN 1900
1860 INPUT "ENTER A NUMBER: ":X
1865 IF X<=0 THEN 1860
1870 INPUT "ENTER ANOTHER NUMBER: ":Y
1875 IF Y<0 THEN 1870
1880 PRINT SEG$(AA$,X,Y): : :
1890 GOTO 1810
1900 CALL CLEAR
1910 PRINT B$(1):B$(2):B$(3):B$(4): : : : : : : : :"F$=SEG$(N$,1,Y-1)": :"JUST TAKES THE NAME UP TO"
1920 PRINT "THE FIRST BLANK, AND  CALLS":"THIS NEW STRING F$.":"AS YOU CAN SEE, F$ IS THEN":"THE FIRST NAME.": : :
1930 GOSUB 690
1940 CALL HCHAR(15,1,112,32)
1950 A$=B$(5)
1960 LC=7.02
1970 GOSUB 460
1980 AA$="LEN"
1990 LC=7.08
2000 GOSUB 610
2010 A$="LEN IS YET ANOTHER STRING   FUNCTION. IT GIVES THE      LENGTH OF A STRING."
2020 GOSUB 450
2030 LC=19.02
2040 A$="LEN(N$) GIVES THE LENGTH OF THE STRING YOU ENTERED FOR  YOUR NAME."
2050 GOSUB 460
2060 GOSUB 690
2070 A$="THERE IS A BLANK BETWEEN    YOUR FIRST AND SECOND NAMES.LEN(N$)-1 IS THEN THE LENGTHOF YOUR NAME."
2080 GOSUB 450
2090 GOSUB 580
2100 PRINT B$(0):B$(1):B$(2):B$(3):B$(4):B$(5):B$(6): : : : : : : : : : : : : : :
2110 AA$="70"
2120 LC=9.02
2130 GOSUB 610
2140 AA$="10"
2150 LC=1.02
2160 GOSUB 610
2170 CALL HCHAR(15,1,112,32)
2180 A$="70 IS JUST A NORMAL FOR-NEXTLOOP. STATEMENT 10 DEFINES  A STRING Z$ OF THE ALPHABET."
2190 GOSUB 450
2200 GOSUB 690
2210 A$=B$(7)
2220 LC=10.02
2230 GOSUB 460
2240 A$=B$(11)
2250 LC=14.02
2260 GOSUB 460
2270 A$="NOW AS I GOES FROM 1 TO 26, C$ GOES FROM ""A"" TO ""Z""."
2280 GOSUB 450
2290 GOSUB 690
2300 LC=11.02
2310 A$=B$(8)
2320 GOSUB 460
2330 A$="THE PROGRAM LOOKS AT YOUR   NAME(N$) TO SEE IF A CERTAINLETTER (C$) IS IN IT."
2340 GOSUB 450
2350 A$="THE NUMBER OF CHARACTERS    INTO YOUR NAME THAT C$      OCCURS IS E."
2360 LC=19.02
2370 GOSUB 460
2380 GOSUB 690
2390 A$=B$(9)
2400 LC=12.02
2410 GOSUB 460
2420 A$="STATEMENT 100 SAYS, IF YOUR NAME DOES NOT CONTAIN THE   LETTER C$, THEN GO ON TO THENEXT LETTER."
2430 GOSUB 450
2440 GOSUB 690
2450 LC=13.02
2460 A$=B$(10)
2470 GOSUB 460
2480 A$="IF YOUR NAME CONTAINS THE   LETTER C$, THE PROGRAM WILL TELL YOU SO."
2490 GOSUB 450
2500 GOSUB 580
2510 FOR I=0 TO 12
2520 PRINT B$(I)
2530 NEXT I
2540 PRINT:"THIS ENDS OUR PROGRAM.":"STATEMENT 130 PRINTS THE":"NUMBER OF LETTERS":"IN YOUR NAME.": :
2550 GOSUB 580
2560 PRINT "LET'S TRY THE PROGRAM.": : : :
2570 GOSUB 580
2580 GOTO 170
2590 GOSUB 580
2600 CALL CLEAR
2610 PRINT "WE HAVE COVERED THE MOST":"IMPORTANT STRING FUNCTIONS.":"THERE ARE, HOWEVER, A FEW"
2620 PRINT "MORE FUNCTIONS THAT WE":"SHOULD MENTION.": : :
2630 GOSUB 580
2640 PRINT "CHR$(A)    AND    ASC(A$)": : :"COMPUTERS STORE ALL":"INFORMATION AS NUMBERS.": :
2650 PRINT "FOR EXAMPLE, THE LETTER "B"":"IS STORED AS THE NUMBER 66.": :"CHR$(L)  GIVES THE CHARACTER"
2660 PRINT "REPRESENTED BY THE NUMBER L.":"CHARACTERS 96 TO 255 ARE":"INITIALLY UNDEFINED.": :
2670 PRINT "10 INPUT""ENTER A NUMBER: "":L":"20 PRINT CHR$(L)": :
2680 PRINT "HERE IS A PROGRAM FOR YOU":"TO TRY.":"(ENTER 0 TO GO ON)": :
2690 INPUT "ENTER A NUMBER              (BETWEEN 33 AND 255): ":L
2700 IF L=0 THEN 2740
2710 IF L>255 THEN 2690
2715 IF L<33 THEN 2690
2720 PRINT CHR$(L): : :
2730 GOTO 2670
2740 CALL CLEAR
2750 PRINT "ASC(A$)  IS THE OPPOSITE OF":"CHR$(A).": :"IF A$ IS A CHARACTER,":"ASC(A$) GIVES THE NUMBER"
2760 PRINT "CORRESPONDING TO A$.": :"FOR EXAMPLE,": :"PRINT ASC(""C"")  WOULD GIVE":"67.": : :
2770 GOSUB 580
2780 PRINT "STR$(N)":"      AND":"VAL(N$)": : : :
2790 PRINT "SOMETIMES IT IS USEFUL TO":"MAKE A STRING OUT OF A":"NUMBER.  STR$(N) DOES THIS.": :
2800 PRINT "SIMILARLY ONE CAN MAKE A":"NUMBER OUT OF A STRING MADE":"UP OF NUMBERS.":"VAL(N$) DOES THIS."
2810 GOSUB 580
2820 PRINT "10 A=1.7":"20 D$=STR$(A)":"30 PRINT LEN(D$)":"40 C=VAL(D$)":"50 PRINT C": :"WOULD GIVE,": :
2830 AA=1.7
2840 D$=STR$(AA)
2850 PRINT LEN(D$)
2860 PRINT VAL(D$): : :
2870 GOSUB 580
2872 CALL COLOR(12,11,11)
2874 CALL COLOR(13,7,7)
2876 CALL COLOR(14,13,13)
2880 GOSUB 800
2890 LC=15.02
2900 A$="THIS IS THE END OF CHAPTER 7"
2910 CALL SCREEN(8)
2920 GOSUB 460
2930 GOSUB 420
