100 L$=RPT$("-",80) :: M$="|      "&RPT$("|       ",4)&"|" :: M$=M$&M$
110 CALL MAGNIFY(2) :: RANDOMIZE :: DIM U(75,1),T$(7),U$(15) :: FOR I=0 TO 9 :: READ P(I) :: NEXT I
120 DATA 31599,18724,29671,31207,18925,31183,31695,4775,31727,31215
130 DISPLAY ERASE ALL AT(8,12):"TIngo" :: DISPLAY AT(10,7):"By Steve Karasek"
140 INPUT "HOW MANY CARDS TO PRINT? ":N :: IF N THEN OPEN #1:"PIO",OUTPUT,VARIABLE 255 :: PRINT #1:CHR$(27);"A";CHR$(7);
150 FOR I=1 TO(N+1)/2 :: PRINT #1 :: FOR J=1 TO 10 :: PRINT #1:TAB(J*8-4);SEG$("TINGOTINGO",J,1); :: NEXT J :: PRINT #1
160 FOR H=1 TO 5 :: PRINT #1:L$:M$ :: FOR M=0 TO 1 :: FOR J=0 TO 4
170 K=INT(RND*15)+1+J*15 :: IF U(K,M)THEN 170
180 C(J,M)=K :: U(K,M)=1 :: NEXT J :: NEXT M :: FOR K=0 TO 4 :: FOR M=0 TO 1 :: FOR J=0 TO 4 :: N$=N$&"|" :: IF H<>3 OR J<>2 THEN 210
190 IF K=2 THEN N$=N$&"*FREE**" ELSE N$=N$&"       "
200 GOTO 250
210 X=0 :: FOR W=1 TO 0 STEP-1 :: X=INT(C(J,M)/10^W)-X*10
220 FOR L=0-(J=0 AND W=1)TO 2 :: IF(P(X)AND 2^(L+K*3))>0 AND(W=0 OR X>0)THEN N$=N$&"#" ELSE N$=N$&" "
230 NEXT L :: IF W THEN N$=N$&" "
240 NEXT W
250 NEXT J :: N$=N$&"|" :: NEXT M :: FOR M=1 TO LEN(N$) :: IF SEG$(N$,M,1)="#" THEN PRINT #1:"@";ELSE PRINT #1:" ";
260 NEXT M :: PRINT #1:CHR$(13);N$ :: N$="" :: NEXT K :: PRINT #1:M$ :: NEXT H :: PRINT #1:L$
270 IF INT(I/2)*2=I THEN PRINT #1:CHR$(12);
280 GOSUB 470 :: NEXT I :: IF N THEN CLOSE #1
290 FOR I=2 TO 7 :: READ T$(I) :: NEXT I :: FOR I=1 TO 15 :: READ U$(I) :: NEXT I
300 DATA TWENTY,THIRTY,FORTY,FIFTY,SIXTY,SEVENTY
310 DATA ONE,TWO,THREE,FOUR,FIVE,SIX,SEVEN,EIGHT,NINE,TEN,ELEVEN,TWELVE,THIRTEEN,FOURTEEN,FIFTEEN
320 CALL CLEAR :: FOR I=9 TO 14 :: READ J :: CALL COLOR(I,J,J) :: NEXT I
330 DATA 6,7,13,5,14,3
340 Z=0 :: CALL DELSPRITE(ALL) :: INPUT "PRESS <ENTER> WHEN READY ":X$ :: DISPLAY ERASE ALL AT(1,5):"T    I    N    G    O"
350 J=4 :: FOR I=96 TO 136 STEP 8 :: CALL VCHAR(1,J,I,17) :: J=J+5 :: NEXT I
360 IF Z=75 THEN 460
370 J=INT(RND*75) :: IF U(J,0)THEN 370
380 Z=Z+1 :: U(J,0)=1 :: I=INT(J/15) :: J=J+1 :: DISPLAY AT(J-I*15+2,4+I*5)SIZE(2):USING("##"):J :: X$=SEG$("TINGO",I+1,1)
390 CALL SPRITE(#4,ASC(X$),2,144,104) :: Y$=STR$(J)&" " :: FOR I=1 TO LEN(Y$) :: CALL SPRITE(#I,ASC(SEG$(Y$,I,1)),2,144,114+I*14) :: NEXT I
400 CALL SAY(X$) :: IF J>15 AND J<20 THEN CALL SAY(U$(J-10),,"TEEN")ELSE X=INT(J/10)*-(J>19) :: CALL SAY(T$(X),,U$(J-X*10))
410 FOR I=1 TO 150 :: CALL KEY(0,X,S) :: IF S THEN 430
420 NEXT I
430 IF S=0 THEN 360 ELSE DISPLAY AT(22,1):"PRESS C TO CONTINUE OR N FORA NEW GAME"
440 CALL KEY(0,X,S) :: IF X=-1 THEN 440
450 X$=CHR$(X) :: IF X$="C" THEN CALL HCHAR(22,1,32,64) :: GOTO 360 ELSE IF X$<>"N" THEN 440
460 GOSUB 470 :: GOTO 340
470 FOR J=0 TO 75 :: U(J,0),U(J,1)=0 :: NEXT J :: RETURN
