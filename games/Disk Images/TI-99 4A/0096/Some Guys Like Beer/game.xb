100 ! *****DRUNKMAN*****       BY MARCELLO ZANNINI 1984    EDITED BY BARRY GIBBINS.
110 !----------------------
120 RANDOMIZE :: CALL CLEAR :: CALL SCREEN(2) :: A$="00183C7E7E3C18" :: CALL MAGNIFY(3) :: R=35 :: Q=97
130 FOR I=3 TO 8 :: CALL COLOR(I,9,1) :: NEXT I :: CALL COLOR(10,11,1,11,11,1) :: CALL CHAR(43,"0",45,"FF99FF")
140 CALL CHAR(33,"0",34,A$,R,"FFFF8181FFFF8181",36,"183CFE2E7E3E1C084C3C1C1C0C0C0C1C0",39,"0",40,"183C7F747E7C38103A3C3838303030380")
150 CALL CHAR(44,A$,60,"03020301010101010608304D4D26100FC040C0808080808060100CB2B26408F",64,"1C3E1C083E1C1C14")
160 CALL CHAR(92,"183C7E7E7E3C99997E3C3C3C242424660",95,"0",96,"FFA3C5FF898989DFFF2244FF080808DDFFA2C4FF888888DDFF2345FF111111BB",124,A$)
170 CALL CHAR(100,"818181FB919191FB808080DD080808DD808080DD888888DD818181FB111111FB",107,"000103000700070F")
180 CALL CHAR(108,"0F0F07000700030100C0E000F000F0F80F0F080A080A080FF8F8F000F000E0C",112,"F8F888A89898A8F800FFFF00FF00FFFFFFFF88BB88BB88FFFFFFFF00FF00FFFF")
190 CALL CHAR(116,"183CFE2E7E3E1C084E3D1D1C1C2427610",119,"0",120,"183C7F747E7C38107ABCB8383824E4860",123,"0")
200 CALL CHAR(128,"192640405F3F5F5B395A5B9B7B5B1F1FB8440204F8F0FEBF33B3B3BEBCB0F0F",132,"0",133,"00444444EEAAAAEE0",135,"00444444EEAAAAEE")
210 CALL CHAR(136,"0003070302020202020405050809080700C0E0C0404040404020A020909010E",140,"0003010101010101010204090908040300E0C0C0404040404020D00808C810E")
220 DISPLAY AT(3,1):RPT$(""",|",84) :: FOR I=5 TO 9 :: DISPLAY AT(I,5)SIZE(20):"" :: NEXT I :: DISPLAY AT(6,7):"S O M E  G U Y S"; :: DISPLAY AT(8,7):"L I K E  B E E R";
230 DISPLAY AT(18,7):"MARCELLO ZANNINI" :: DISPLAY AT(20,5):"ALL RIGHTS RESERVED" :: DISPLAY AT(22,13):"1984" :: DISPLAY AT(24,1):"EDITED BY BARRY GIBBINS 1988"
240 CALL SPRITE(#2,128,11,105,1,0,-8,#3,128,11,105,128,0,-8) :: A$=""
250 FOR I=1 TO 4 STEP .2 :: CALL COLOR(1,7,1,2,11,1,12,5,1) :: CALL SOUND(-80,110*I,3,113*I,9) :: CALL COLOR(1,5,1,2,7,1,12,11,1)
260 CALL SOUND(-80,140*I,3,142*I,9) :: CALL COLOR(1,11,1,2,5,1,12,7,1) :: CALL SOUND(-80,180*I,3,183*I,9) :: NEXT I :: I,X=7 :: J=11 :: K=5
270 CALL COLOR(1,I,1,2,J,1,12,K,1) :: CALL SOUND(-990,110*X,3,111,30,413*X,30,-4,5) :: Y=I :: I=J :: J=K :: K=Y :: X=X*.932 :: IF X>=1 THEN 270
280 CALL COLOR(1,I,1,2,J,1,12,K,1) :: CALL KEY(1,Y,X) :: IF Y<>18 THEN CALL KEY(3,Y,X) :: IF X=0 THEN X=I :: I=J :: J=K :: K=X :: CALL SOUND(10,X*200,20) :: GOTO 280
290 ! **** THE GAME ****
300 !
310 BI,SC=0 :: PH=1 :: M=4 :: CALL DELSPRITE(ALL)
320 A=0 :: I=BI=4 :: M=M-I :: PH=PH-I :: BI=BI+1+4*I :: CALL DELSPRITE(ALL) :: DISPLAY AT(1,9)ERASE ALL:"SCREEN";BI;" PHASE";PH
330 CALL HCHAR(3,6,45,27) :: C=240 :: CALL SPRITE(#20,40,16,1,43,#21,60,16,1,C) :: CALL HCHAR(2,2,64,M) :: ON BI GOTO 350,430,550,640
340 ! **** SCREEN 1 ****
350 CALL COLOR(1,5,1,9,12,7) :: DISPLAY AT(8,1):"!!!kqm!!!!!!!kqm!!!!!!!kqm!!!#baaaac!!!baaaaac!!!baaaac#!#feeeeg!!!feeeeeg!!!feeeeg#"
360 DISPLAY AT(11,1):"!#feeeeg!!!feeeeeg!!!feeeeg#!#d!!!fg!!!fg!!!fg!!!fg!!!d#!#dnrpfg!!!fgnrpfg!!!fgnrpd##baaaaaac#baaaaaaac#baaaaaac#feeeeeeg#feeeeeeeg#feeeeeeg"
370 DISPLAY AT(16,1):"#feeeeeeg#feeeeeeeg#feeeeeeg#fg!!!feg#feg!!!feg#feg!!!fg#fglsofeg#feglsofeg#feglsofgbaaaaaaaaaaaaaaaaaaaaaaaaaaa"
380 DISPLAY AT(20,1):"fg!!!!!feeeg!!!!!feeeg!!!!!ffg!!!!!feeeg!!!!!feeeg!!!!!ffg!!!!!feeeg!!!!!feeeg!!!!!faaaaaaaaaaaaaaaaaaaaaaaaaaaa"
390 CALL VCHAR(19,2,R,4) :: CALL HCHAR(23,2,98) :: CALL VCHAR(14,31,R,5) :: CALL VCHAR(19,31,99,5) :: CALL VCHAR(20,31,103,3)
400 D=41 :: E=33 :: CALL SPRITE(#2,140,14,129,60,O,-2-PH,#3,140,14,129,188,O,-2-PH,#4,136,4,89,80,O,4+PH)
410 CALL SPRITE(#5,136,4,89,208,O,4+PH,#6,132,7,49,40,O,-7-PH,#7,132,7,49,168,O,-7-PH) :: GOTO 730
420 ! **** SCREEN 2 ****
430 CALL COLOR(1,5,1,9,15,5) :: DISPLAY AT(5,24):"kqm"
440 DISPLAY AT(6,1):"!!!kqm!!!!!#baaac#!!!baaaaacbc#baaac#!!#d!!!d#!!!feeeeegfg#eeeeg#!!#dkqmd#!!!feeeeegfg#eeeeg#!!#fbaaac!!!fg!!!fg"
450 DISPLAY AT(10,1):"fg#!!!fg#!!#feeeeg!!!fgnrpfgfg#nrpfebaaceeeeeg!!!baaaaaafbaaac#feeeeg!!!fg!!!feeeeeefeeeeg#feeeegnrpfg!!!feeeeeebaaaaaaaaaaaaaaaac#!!fg!!!fe"
460 DISPLAY AT(15,1):"feg!!!feeeeeeeeeeg#!!fglsofefeglsofeeeeeeeeeeg#!!baaacegfg#bac#feeeeg!!!fg#!!feeeeegfg#feg#feeeeglsofg#!!feeeeeg#bceeebaaaaaaaaaaaaaaaaaaaaa"
470 DISPLAY AT(20,1):"#d!!!!!feeeg!!!!!feeeg!!!!!f#d!!!!!feeeg!!!!!feeeg!!!!!fac!!!!!feeeg!!!!!feeeg!!!!!feebaaaaaaaaaaaaaaaaaaaaaaaaa"
480 CALL VCHAR(7,2,R,12) :: CALL VCHAR(19,2,96) :: CALL VCHAR(20,2,100,2) :: CALL VCHAR(22,2,98) :: CALL VCHAR(23,2,102) :: CALL VCHAR(6,31,R,5)
490 CALL VCHAR(11,31,99) :: CALL VCHAR(12,31,103,4) :: CALL VCHAR(16,31,R,3)
500 CALL VCHAR(19,31,Q,5) :: CALL VCHAR(20,31,101,3) :: CALL VCHAR(11,32,R,5) :: CALL VCHAR(16,32,96)
510 CALL VCHAR(17,32,100,2) :: CALL VCHAR(19,32,99,5) :: CALL VCHAR(20,32,103,3)
520 D=25 :: E=17 :: CALL SPRITE(#2,140,14,129,60,O,-3-PH,#3,140,14,129,188,O,-3-PH,#4,136,4,89,80,O,5+PH)
530 CALL SPRITE(#5,136,4,89,208,O,5+PH,#6,132,7,33,70,O,PH+5,#7,132,7,65,198,O,-7-PH) :: GOTO 730
540 ! **** SCREEN 3 ****
550 CALL COLOR(1,14,1,9,8,13) :: DISPLAY AT(6,1):"!!!kqm!!!!!#baaac#!!!!!kqm!!baaaac#!!!!#d!!!d#!!!!baaaacfeeeeg#!!!!#dkqmd#!!!!feeeeg"
560 DISPLAY AT(9,1):"!feeeg#!!!!#baaaac#!!!feeeeg!fg!!!#!!!!#feeeeg#!!!d!!!fg!fgnrp#!!!!#feeeg!#!!!dnrpfg!#baaac!!!!#d!!!d!bc!!baac#!!#feeeg!!!!#dnrpd!fg!!feeg#"
570 DISPLAY AT(14,1):"!#feeeg!!!!baaaaaac!!!feeg#!!#d!!!d!!!!feeeeeeg!!!d!!!#!!#dlsod!!!!!feeeg!!!!!dlso#!baaaac#!!!!!d!!!d!!!!!#baaaafeeeeg#!!!!!dlsod!!!!!#feeee"
580 DISPLAY AT(19,1):"!baaaaac!!!baaaaac!!!baaaaaa!d!!!!!d!!!d!!!!!d!!!d!!!!!f!d!!!!!d!!!d!!!!!d!!!d!!!!!fac!!!!!d!!!d!!!!!d!!!d!!!!!febaaaaaaaaaaaaaaaaaaaaaaaaaa"
590 CALL VCHAR(7,2,R,15) :: CALL VCHAR(22,1,98) :: CALL VCHAR(22,2,Q) :: CALL VCHAR(23,1,102) :: CALL VCHAR(23,2,101) :: CALL VCHAR(7,31,R,10)
600 CALL VCHAR(17,31,99,7) :: CALL VCHAR(18,31,103) :: CALL VCHAR(20,31,103,3)
610 CALL SPRITE(#2,140,15,129,60,O,-2-PH,#3,140,15,113,188,O,3+PH,#4,136,4,73,80,O,4+PH) :: D=25 :: E=17
620 CALL SPRITE(#5,132,7,33,198,O,2+PH,#6,136,4,89,208,O,-2-PH,#7,132,7,49,70,O,-3-PH) :: GOTO 730
630 ! **** SCREEN 4 ****
640 CALL COLOR(1,7,1,9,12,14) :: DISPLAY AT(4,1):"";TAB(23);"kqm":TAB(14);"kqm!!!!#baaaaac" :: DISPLAY AT(6,13):"#bac#!!!#feeeeeg"
650 DISPLAY AT(7,1):"!!!kqm!!!!!!#feg#!!!#d!!!!fg#baaaaaac#bac!!!`!!bc#!!!!fg#feeeeeeg#feg!!!d!!fg#nrp!fg#d!!!!!!d#fegnrpd!!`#baaaaaa#d!!!!!!d#!!baaac#!d#feeeeeg"
660 DISPLAY AT(12,1):"#d!nrp!!d#!!dfeeg#!d#feeeeegbaaaaaac#`!!d!!!!#!d#!!!!!fgfeeeeeeg#d!!d!!!!baac!lso!fgd!!!!!!!`#!!dlso!feeg!bac#fg"
670 DISPLAY AT(16,1):"d!!!!!!!d#!fg#`#!d!fg!feg#fgd!!lso!!!#!fg#d#!d!fg!feg#fg#!!bac!!!`!febac!d!baceeebac#feeeeeg!d!feeeeeg!feeeeeeee"
680 DISPLAY AT(20,1):"#d!!!!!d!d!d!!!!!d!fg!!!!!fe#d!!!!!d!d!d!!!!!d!fg!!!!!fecd!!!!!d!d!d!!!!!d!fg!!!!!fefebaaace`e`ebaaace`eebaaacee"
690 CALL VCHAR(5,31,R,5) :: CALL VCHAR(10,31,99) :: CALL VCHAR(19,31,101,5) :: CALL VCHAR(19,32,103,5)
700 CALL VCHAR(13,2,R,5) :: CALL VCHAR(18,2,96) :: CALL VCHAR(19,2,100,3) :: CALL VCHAR(22,2,98) :: CALL VCHAR(23,2,102)
710 CALL SPRITE(#2,140,14,121,60,O,-2-PH,#3,140,14,97,188,O,3+PH,#4,136,4,81,80,O,4+PH,#5,136,4,65,198,O,4+PH)
720 CALL SPRITE(#6,132,7,41,70,O,-2-PH,#7,132,7,17,208,O,-3-PH) :: D=33 :: E=25
730 DISPLAY AT(24,1):"SCORE";SC;TAB(16);"HI SCORE";HS :: P=40 :: X=9 :: Y=153 :: CALL SPRITE(#1,P,16,Y,X)
740 C=C-.5 :: CALL LOCATE(#21,1,C) :: CALL COINC(ALL,I) :: IF I THEN 920 ELSE CALL KEY(1,K,I) :: CALL JOYST(1,H,V) :: IF H THEN P=38+H/2
750 IF I=0 THEN 780 ELSE K=H*8+X :: IF(K<1)+(K>249)THEN 740 ELSE CALL PATTERN(#1,P+80) :: CALL SOUND(-900,1800,5)
760 FOR I=-14 TO 14 STEP 2 :: CALL MOTION(#1,I,SGN(H)*5) :: CALL COINC(ALL,J) :: IF J THEN 920
770 FOR J=1 TO 8 :: NEXT J :: NEXT I :: CALL MOTION(#1,O,O) :: GOTO 820
780 IF V=O THEN 810 ELSE J=(V>0)+3 :: K=X/8+1
790 CALL GCHAR(Y/8+J,K,I) :: IF I<>R THEN 740 ELSE CALL PATTERN(#1,92) :: Y=Y-V :: CALL LOCATE(#1,Y,X) :: Y=Y-V :: CALL SOUND(50,-5,8)
800 CALL LOCATE(#1,Y,X) :: CALL SOUND(50,-5,8) :: CALL GCHAR(Y/8+J,K,I) :: IF I=R THEN 740 ELSE CALL PATTERN(#1,P) :: GOTO 740
810 IF H=O THEN 740 ELSE K=X+H :: IF(K<5)+(K>245)THEN 740 ELSE CALL PATTERN(#1,P+80) :: CALL LOCATE(#1,Y,K) :: K=K+H :: CALL SOUND(50,-5,8)
820 CALL LOCATE(#1,Y,K) :: CALL PATTERN(#1,P) :: CALL SOUND(50,-5,8) :: X=K :: CALL COINC(ALL,I) :: IF I THEN 920
830 V=INT(Y/8+2) :: H=INT(X/8+1) :: CALL GCHAR(V,H,K) :: CALL GCHAR(V+1,H,J) :: IF K<113 THEN IF(J>33)*(J<100)THEN 740 ELSE 960
840 IF V>20 THEN 740 ELSE CALL SOUND(100,345,1) :: IF K=115 THEN GOSUB 910 :: DISPLAY AT(22,H-3):"lso"; :: GOTO 740 ELSE IF K=113 THEN 860
850 IF V<14 THEN CALL GCHAR(V+5,H,K) :: IF K>33 THEN 740 ELSE GOSUB 900 :: DISPLAY AT(V+5,H-3):"nrp"; :: GOTO 740 ELSE GOSUB 910 :: DISPLAY AT(21,H-3):"nrp"; :: GOTO 740
860 CALL GCHAR(V+5,H,K) :: IF(K>33)*(V<14)THEN 740 ELSE F=F=O :: IF F=O THEN CALL SPRITE(#8,128,7,D,E)ELSE CALL DELSPRITE(#8)
870 IF V<9 THEN GOSUB 910 :: DISPLAY AT(V+5,H-3):"kqm"; :: GOTO 740
880 IF V<14 THEN GOSUB 900 :: DISPLAY AT(V+5,H-3):"kqm"; :: GOTO 740
890 GOSUB 910 :: GOSUB 910 :: DISPLAY AT(20,H-3):"kqm"; :: A=A+1 :: IF A<3 THEN 740 ELSE CALL POSITION(#21,Y,X) :: SC=SC+X*BI*A :: GOSUB 910 :: GOTO 320
900 SC=SC+25
910 SC=SC+50 :: DISPLAY AT(24,6):SC; :: DISPLAY AT(V,H-3):"!!!"; :: RETURN
920 CALL MOTION(#1,O,O) :: CALL COINC(#1,#8,15,I) :: IF I=O THEN CALL SOUND(-99,-6,0) :: I=2 :: GOTO 940 ELSE CALL DELSPRITE(#8)
930 FOR I=O TO 30 STEP 3 :: CALL SOUND(-90,880,I,2347,I) :: NEXT I :: SC=SC+INT(500*RND+250) :: DISPLAY AT(24,6):SC; :: C=240 :: GOTO 980
940 CALL COINC(#20,#21,7,J) :: IF J=0 THEN I=2 :: CALL SOUND(-99,-6,0) :: GOTO 960 ELSE CALL HCHAR(2,2,32,4) :: M=1
950 CALL DELSPRITE(#20) :: CALL LOCATE(#1,1,43) :: I=2
960 CALL POSITION(#1,Y,X) :: IF Y<200 THEN CALL SOUND(-90,950-Y*4,5) :: CALL MOTION(#1,I,0) :: I=I+1 :: GOTO 960
970 CALL MOTION(#1,O,O) :: FOR I=0 TO 30 :: CALL SOUND(-90,-5,I) :: NEXT I :: CALL HCHAR(2,M+1,32) :: M=M-1 :: IF M<1 THEN 990
980 P=40 :: Y=153 :: X=9 :: CALL SPRITE(#1,P,16,Y,X) :: GOTO 740
990 DISPLAY AT(4,9):"G A M E  O V E R"; :: FOR I=5 TO 1 STEP-1 :: CALL SOUND(240,I*115,1,-8,12) :: CALL SOUND(240,330,1,115,7,240,10) :: NEXT I :: IF HS<SC THEN HS=SC
1000 CALL KEY(1,I,J) :: IF I=18 THEN 310 ELSE CALL KEY(3,I,J) :: IF J=0 THEN 1000 ELSE IF I-69 THEN 310 ELSE END
