5 DIM A$(7, 7), Z(7, 7)
10 REM BULLSEYE
20 GOSUB 780: REM INITIALISE
30 GOSUB 440: REM PRINT TARGET
40 R = 0: S = 0
50 SHOT = SHOT + 1
60 H = 0
70 PRINT
80 PRINT "STOP ME FOR ACROSS...": PRINT: PRINT TAB(8);
90 IF INKEY$ <> "" THEN 90
100 H = H + 1
110 PRINT H;
120 GOSUB 1130
130 IF INKEY$ <> "" THEN R = H: GOTO 160
140 IF H < 7 THEN 100
150 R = 7
160 PRINT TAB(16); R; "   OK"
170 H = O
180 PRINT
190 PRINT "NOW FOR DOWN...": PRINT: PRINT TAB(8);
200 IF INKEY$ <> "" THEN 200
210 H = H + 1
220 PRINT H;
230 GOSUB 1130
240 IF INKEY$ <> "" THEN S = H: GOTO 270
250 IF H < 7 THEN 210
260 S = 7
270 PRINT TAB(16); H; "   OK"
280 GOSUB 1130: GOSUB 1130: GOSUB 1130
290 GOSUB 440
300 XX = 1.0: GOSUB 2000
310 R = 0: S = 0
320 GOSUB 440
330 IF SHOT < 10 THEN 40
340 PRINT: PRINT
350 PRINT TAB(7); "YOU SCORED A TOTAL OF"; SCR
360 PRINT: PRINT TAB(7); "AN AVERAGE OF"; SCR / 10; "PER SHOT"
370 IF SCR > HI THEN HI = SCR
380 PRINT: PRINT TAB(7); "THE HIGH SCORE IS"; HI
390 PRINT: PRINT TAB(7); "STAND BY FOR A NEW ROUND"
400 XX = 2.5: GOSUB 2000
410 SHOT = 0: SCR = 0: R = 0: S = 0
420 GOTO 30
430 REM ************
440 REM PRINT TARGET
450 CLS
460 IF R = 0 THEN 610
470 A$(S, R) = "=X="
480 IF S > 1 THEN A$(S - 1, R) = " : "
490 IF S < 7 THEN A$(S + 1, R) = " : "
500 IF R > 1 THEN A$(S, R - 1) = " --"
510 IF R < 7 THEN A$(S, R + 1) = "-- "
520 IF S > 1 AND R > 1 THEN A$(S - 1, R - 1) = " \\"
530 IF S < 7 AND R < 7 THEN A$(S + 1, R + 1) = "\\ "
540 IF S > 1 AND R < 7 THEN A$(S - 1, R + 1) = "// "
550 IF S < 7 AND R > 1 THEN A$(S + 1, R - 1) = " //"
560 HIT = Z(S, R)
570 SCR = SCR + HIT
580 PRINT: PRINT TAB(7); "THAT WAS SHOT NUMBER"; SHOT
590 PRINT: PRINT TAB(3); "YOU SCORED"; HIT; " SO YOUR SCORE IS"; SCR
600 IF R = 4 AND S = 4 THEN PRINT: PRINT TAB(12); ">> BULLSEYE <<"
610 PRINT
620 PRINT TAB(11); "1  2  3  4  5  6  7"
630 PRINT TAB(9); "-----------------------"
640 FOR J = 1 TO 7
650 PRINT TAB(4); J; ":  ";
660 FOR K = 1 TO 7
670 PRINT A$(J, K);
680 NEXT K
690 PRINT "  :"; J
700 NEXT J
710 PRINT TAB(9); "-----------------------"
720 PRINT TAB(11); "1  2  3  4  5  6  7"
730 RESTORE
740 GOSUB 840
750 RETURN
770 REM **********
780 REM INITIALISE
790 CLS
800 RANDOMIZE TIMER
820 SCR = 0: HIT = 0: SHOT = 0
830 R = 0: S = 0
840 FOR B = 1 TO 7
850 FOR C = 1 TO 7
860 READ A$(B, C)
870 NEXT C
880 NEXT B
890 FOR B = 1 TO 7
900 FOR C = 1 TO 7
910 READ Z(B, C)
920 NEXT C
930 NEXT B
940 RETURN
950 REM *******************
960 REM +=2; #=3; %=5; *=10
970 DATA "   "," + "," + "," + "," + "," + ","   "
980 DATA " + "," # "," # "," # "," # "," # "," + "
990 DATA " + "," # "," $ "," $ "," $ "," # "," + "
1000 DATA " + "," # "," $ "," * "," $ "," # "," + "
1010 DATA " + "," # "," $ "," $ "," $ "," # "," + "
1020 DATA " + "," # "," # "," # "," # "," # "," + "
1030 DATA "   "," + "," + "," + "," + "," + ","   "
1040 DATA 0,2,2,2,2,2,0
1050 DATA 2,3,3,3,3,3,2
1060 DATA 2,3,5,5,5,3,2
1070 DATA 2,3,5,10,5,3,2
1080 DATA 2,3,5,5,5,3,2
1090 DATA 2,3,3,3,3,3,2
1100 DATA 0,2,2,2,2,2,0
1110 REM *****
1120 REM ADJUST LOOP BELOW FOR MAXIMUM CHALLENGE ON YOUR SYSTEM
1130 REM DELAY
1140 XX = 0.1: GOSUB 2000
1150 RETURN
2000 ST = TIMER + XX
2010 IF TIMER < ST THEN 2010
2020 RETURN

