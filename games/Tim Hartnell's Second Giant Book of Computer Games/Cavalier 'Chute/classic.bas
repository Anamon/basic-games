10 REM CAVALIER 'CHUTE
20 P = 20
30 GOSUB 110: REM INTIALISE
40 REM **********
50 REM MAIN CYCLE
60 GOSUB 360: REM PRINT 'CHUTE
70 GOSUB 520: REM LANDING STRIP
80 GOSUB 580: REM GET KEYS
90 GOTO 60
100 REM **************
110 REM INITIALISATION
120 CLS
130 IF INKEY$ <> "" THEN 130
140 PRINT: PRINT "SELECT YOUR GAME:"
150 PRINT TAB(6); "A - EASY"
160 PRINT TAB(6); "B - MODERATE"
170 PRINT TAB(6); "C - DIFFICULT"
180 PRINT TAB(6); "D - INCREDIBLY HARD"
190 INPUT "                "; R$
200 IF R$ < "A" OR R$ > "D" THEN 190
210 DF = 1 / 2
220 IF R$ = "C" THEN DF = 1
230 IF R$ = "B" THEN DF = 2
240 IF R$ = "A" THEN DF = 3
250 STP = 0: REM SCREEN TOP
260 RANDOMIZE TIMER
270 STP = 0: REM SCREEN TOP
280 SBOT = 19: REM SCREEN BOTTOM
290 SWDE = 40: REM WIDTH OF SCREEN
300 SWDE = SWDE - 3
310 ACC = INT(RND(1) * (SWDE - 4))
320 PAD = INT(RND(1) * (SWDE - 10))
330 PLUS = 1
340 RETURN
350 REM ***************
360 REM PRINT PARACHUTE
370 CLS
380 FOR C = 0 TO STP
390 PRINT
400 NEXT C
410 PRINT TAB(ACC); " ^ "
420 PRINT TAB(ACC); "^^^"
430 PRINT TAB(ACC); " O "
440 PRINT TAB(ACC); " Y "
450 IF STP = SBOT - 4 THEN 490
460 FOR C = 6 TO SBOT - STP
470 PRINT
480 NEXT C
490 STP = STP + 1
500 RETURN
510 REM *************
520 REM LANDING STRIP
530 PAD = PAD + PLUS
540 IF PAD > SWDE - 6 THEN PLUS = -1
550 PRINT TAB(PAD); "@@@@"
560 RETURN
570 REM ********
580 REM GET KEYS
590 IF STP = SBOT - 4 THEN GOSUB 650
600 Y$ = INKEY$: REM or GET Y$
605 XX = 0.2: GOSUB 1000
610 IF Y$ = "Z" THEN ACC = ACC - DF
620 IF Y$ = "M" THEN ACC = ACC + DF
630 RETURN
640 REM **************
650 REM SEE IF DROWNED
660 IF ACC < PAD - 1 THEN 770
670 IF ACC > PAD + 2 THEN 770
680 IF ACC = PAD + 1 OR ACC = PAD THEN 730
690 PRINT TAB(8); "YOU MADE IT...BY THE"
700 PRINT TAB(9); "SKIN OF YOUR TEETH"
710 P = P + 5
720 GOTO 800
730 PRINT TAB(12); "CONGRATULATIONS!!"
740 PRINT TAB(13); "A SAFE LANDING!"
750 P = INT(P + 10 / DF)
760 GOTO 800
770 PRINT TAB(12); "SPLOSSSSSHHHHHHHHH!!"
780 PRINT TAB(14); "YOU GOT SOAKED!"
790 P = P - 2
800 AT = AT + 1
810 IF AT = 20 OR P < 0 THEN 890
820 PRINT TAB(11); "YOU HAVE"; P; "POINTS"
830 XX = 1.0: GOSUB 1000
840 IF INKEY$ <> "" THEN 840
850 GOSUB 250
860 RETURN
870 REM ********
880 REM DROWNED!
890 PRINT TAB(13); "YOU'VE DROWNED!"
900 PRINT: PRINT TAB(12); "YOU SCORED"; P; "POINTS"
910 PRINT TAB(16); "IN"; AT; "ATTEMPTS."
920 END
1000 ST = TIMER + XX
1010 IF TIMER < ST THEN 1010
1020 RETURN

