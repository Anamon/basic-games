10 REM CAR THIRTEEN
20 REM Engage CAPS LOCK before playing
30 REM ***************
40 RANDOMIZE TIMER
50 'DEF FNR(X)=INT(RND(1)*X)+1
60 N = 65: B1 = INT(RND(1) * 5) + 1: B2 = INT(RND(1) * 5) + 1
70 CLS
80 A = INT(RND(1) * 7) + 1: B = INT(RND(1) * 7) + 1
90 IF B1 = A OR B2 = B THEN 80
100 B = B + N: B$ = CHR$(B)
110 IF B$ = "E" AND A < 6 THEN 80
120 IF B$ = "F" AND A > 5 THEN 80
130 IF A = 6 AND (B$ > "E" OR B$ < "B") THEN 80
140 IF A = 7 AND B$ > "E" THEN 80
150 PRINT: PRINT "CAR 13! CAR 13!": PRINT
160 PRINT "A ROBBERY IS IN PROGRESS AT THE CORNER"
170 PRINT "OF"; A; "AND ";
180 C = INT(RND(1) * 6) + 1: IF C = A THEN 180
190 PRINT B$; " STREETS. RESPOND IMMEDIATELY"
200 D = INT(RND(1) * 7): IF D = B THEN 200
210 D = D + N
220 D$ = CHR$(D)
230 IF D$ = "E" AND C < 6 THEN 200
240 IF D$ = "F" AND C > 5 THEN 200
250 IF C = 6 AND (D$ > "E" OR D$ < "B") THEN 200
260 IF C = 7 AND D$ > "E" THEN 200
270 PRINT: PRINT "YOU'RE AT THE CORNER OF"; C; "& "; D$; " STREETS"
280 PRINT: PRINT "WHICH WAY: (";
290 GOSUB 1250
300 IF A$ <> "N" AND A$ <> "S" AND A$ <> "E" AND A$ <> "W" THEN 280
310 IF A$ = "N" THEN 420
320 IF A$ = "S" THEN 490
330 IF A$ = "E" THEN 560
340 REM ****
350 REM WEST
360 IF D = N OR C = 6 AND D = N + 1 THEN 630
370 D = D - 1
380 IF C < 6 AND D = N + 4 THEN D = N + 3
390 D$ = CHR$(D): GOTO 710
400 REM *****
410 REM NORTH
420 IF C = 7 THEN 630
430 IF (D = N + 5 OR D = N + 6) AND C = 5 THEN 630
440 C = C + 1
450 IF D = N AND C = 6 THEN C = 7
460 GOTO 710
470 REM *****
480 REM SOUTH
490 IF C = 1 THEN 630
500 IF D = N + 4 AND C = 6 THEN 630
510 C = C - 1
520 IF D = N AND C = 6 THEN C = 5
530 GOTO 710
540 REM ****
550 REM EAST
560 IF D = N + 6 THEN 630
570 IF (C = 7 OR C = 6) AND D = N + 4 THEN 630
580 D = D + 1
590 IF C < 6 AND D = N + 4 THEN D = N + 5
600 D$ = CHR$(D): GOTO 710
610 REM ************
620 REM RUN OFF ROAD
630 PRINT: PRINT "YOU DUMMY! YOU'VE RUN OFF THE ROAD!"
640 PRINT: PRINT "YOU'VE NOT ONLY DAMAGED EXPENSIVE"
650 PRINT "PROPERTY, BUT YOU'VE INJURED SEVERAL"
660 PRINT "PEOPLE AS WELL. IT'S TIME YOU TURNED IN"
670 PRINT TAB(14); "YOUR BADGE!"
680 GOTO 1220
690 REM **********
700 REM CROOK MOVE
710 IF A = C AND B = D THEN 1130
720 IF RND(1) < .4 THEN 750
730 PRINT: PRINT "CAR 13! CAR 13!"
740 PRINT: PRINT "CROOK HAS STOPPED AT RED LIGHT": GOTO 270
750 M = INT(RND(1) * 4) + 1
760 ON M GOTO 790, 860, 930, 1000
770 REM *****
780 REM NORTH
790 IF A = 7 OR (B = N + 5 AND A = 5) THEN 750
800 IF B = D AND A + 1 = C THEN 750
810 A = A + 1: W$ = "NORTH": N$ = CHR$(B)
820 IF B = N AND A = 6 THEN A = 7
830 GOTO 1070
840 REM *****
850 REM SOUTH
860 IF A = 1 OR (B = N + 4 AND A = 6) THEN 750
870 IF B = D AND A - 1 = C THEN 750
880 A = A - 1: W$ = "SOUTH": N$ = CHR$(B)
890 IF B = N AND A = 6 THEN A = 5
900 GOTO 1070
910 REM ****
920 REM EAST
930 IF B = N + 6 OR ((A = 7 OR A = 6) AND B = N + 4) THEN 750
940 IF A = C AND B + 1 = D THEN 750
950 B = B + 1: W$ = "EAST": N$ = STR$(A)
960 IF A < 6 AND B = N + 4 THEN B = N + 5
970 GOTO 1070
980 REM ****
990 REM WEST
1000 IF B = N OR (A = 6 AND B = N + 1) THEN 750
1010 IF A = C AND B - 1 = D THEN 750
1020 B = B - 1: W$ = "WEST": N$ = STR$(A)
1030 IF A < 6 AND B = N + 4 THEN B = N + 3
1040 REM **********
1050 REM WHICH WAY?
1060 CLS
1070 IF A = B1 AND B = B2 THEN 1190
1080 PRINT: PRINT "CAR 13! CAR 13!": PRINT
1090 PRINT "CROOK LAST SEEN HEADING "; W$
1100 GOTO 270
1110 REM ************
1120 REM CROOK CAUGHT
1130 PRINT: PRINT TAB(12); "GOTCHA!!"
1140 PRINT: PRINT "HEY, WELL DONE! YOU CAUGHT THE CROOK."
1150 PRINT: PRINT "NOW YOU'LL BE PROMOTED TO CAPTAIN!"
1160 GOTO 1220
1170 REM ********************
1180 REM CROOK EVADES CAPTURE
1190 PRINT: PRINT "CAR 13! CAR 13!"
1200 PRINT: PRINT "THE CROOK HAS DISAPPEARED INTO"
1210 PRINT "HIS SECRET HIDEOUT. HE GOT AWAY!"
1220 END
1230 REM *****************
1240 REM DIRECTION ROUTINE
1250 IF D = N OR (C = 6 AND D = N + 1) THEN 1270
1260 PRINT "-W";
1270 IF C = 7 OR ((D = N + 5 OR D = N + 6) AND C = 5) THEN 1300
1280 PRINT "-N";
1290 IF C = 1 OR (D = N + 4 AND C = 6) THEN 1310
1300 PRINT "-S";
1310 IF D = N + 6 OR ((C = 7 OR C = 6) AND D = N + 4) THEN 1330
1320 PRINT "-E";
1330 PRINT "-)";
1340 INPUT A$
1350 CLS
1360 RETURN

