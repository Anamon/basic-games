10 REM TAXI!!
20 GOSUB 120
30 GOSUB 1160
40 REM ***************
50 REM ** PLAY GAME **
60 GOSUB 400
70 GOSUB 980
80 GOSUB 520
90 GOTO 60
100 END
110 REM *****************
120 REM INITIALISE
130 CLS
140 RANDOMIZE TIMER
150 T = 100: F = 140: S = 0
160 DIM A(10, 10)
170 Z = 0: L = 1: D = 7: A = 4: P = 32
180 FOR X = 1 TO 10
190 FOR Y = 1 TO 10
200 READ R
210 IF R = 0 THEN R = 32
220 IF R = 1 THEN R = 64
230 A(X, Y) = R
240 NEXT Y
250 NEXT X
260 PRINT: PRINT " B - BUS STATION"
270 PRINT " P - PASSENGER"
280 PRINT " R - RAILWAY STATION"
290 PRINT " D - DESTINATION"
300 PRINT " T - TAXI", "      P AND D WILL"
310 PRINT " F - FUEL", "  APPEAR IN DUE COURSE"
320 PRINT
330 GOSUB 400
340 PRINT
350 PRINT "PRESS <RETURN> TO START THE GAME"
360 INPUT R$
370 CLS
380 RETURN
390 REM **********
400 REM PRINT MAP
410 PRINT TAB(5); "XXXXXXXXXXXX"
420 FOR X = 1 TO 10
430 PRINT TAB(5); "X";
440 FOR Y = 1 TO 10
450 PRINT CHR$(A(X, Y));
460 NEXT Y
470 PRINT "X"
480 NEXT X
490 PRINT TAB(5); "XXXXXXXXXXXX"
500 RETURN
510 REM ***********
520 REM ACCEPT MOVE
530 PRINT: INPUT "         "; M$
540 B = A: E = D
550 IF M$ = "D" AND D < 10 THEN D = D + 1
560 IF M$ = "U" AND D > 1 THEN D = D - 1
570 IF M$ = "R" AND A < 10 THEN A = A + 1
580 IF M$ = "L" AND A > 1 THEN A = A - 1
590 IF A(D, A) = 80 OR A(D, A) = 68 THEN 620
600 IF A(D, A) = 64 THEN D = E: A = B: GOTO 520
610 IF P <> 64 AND P <> 32 THEN GOSUB 860
620 A(E, B) = P
630 P = A(D, A)
640 A(D, A) = 84
650 IF T > 0 THEN T = T - 8
660 F = F - 4
670 IF F < 0 THEN GOSUB 1290
680 Z = Z + 6
690 CLS
700 IF P = 80 THEN GOSUB 740
710 IF P = 68 THEN GOSUB 1050
720 RETURN
730 REM *****************
740 REM PICK UP PASSENGER
750 H = Z: W = T
760 PRINT: PRINT "YOU HAVE COLLECTED YOUR PASSENGER"
770 A(R1, R2) = J
780 P = J
790 GOSUB 1160
800 A(R1, R2) = 68
810 PRINT ">> YOUR DESTINATION IS MARKED BY A 'D'"
820 Z = H
830 T = W + 10
840 RETURN
850 REM *********
860 REM BUS/TRAIN
870 IF P = 70 AND F < 100 THEN GOSUB 910
880 IF P = 66 OR P = 82 THEN F = F - 5
890 RETURN
900 REM ******
910 REM REFUEL
920 AMOUNT = 150
930 IF S < 150 THEN AMOUNT = S
940 F = F + AMOUNT
950 S = S - AMOUNT
960 RETURN
970 REM *****************
980 REM PRINT OUT RESULTS
990 PRINT: PRINT " FUEL"; F, "ENTER NEXT MOVE:"
1000 PRINT " FARE"; Z, "  U(P),   D(OWN)"
1010 PRINT "  TIP"; T, "  L(EFT), R(IGHT)"
1020 PRINT "SCORE"; S
1030 RETURN
1040 REM *******************
1050 REM DESTINATION REACHED
1060 PRINT "YOU'VE REACHED YOUR DESTINATION"
1070 PRINT "TOTAL FARE IS"; T + Z;
1080 IF T <= 0 THEN PRINT " (NO TIP)": GOTO 1100
1090 PRINT " - INCLUDES TIP OF"; T
1100 PRINT: PRINT "A NEW PASSENGER IS WAITING..."
1110 P = J: S = S + T + X
1120 GOSUB 1160
1130 XX = 2.0: GOSUB 2000: CLS
1140 RETURN
1150 REM ********************
1160 REM CREATE NEW PASSENGER
1170 R1 = INT(RND(1) * 9) + 1
1180 R2 = INT(RND(1) * 9) + 1
1190 IF A(R1, R2) = 84 THEN 1110
1200 IF A(R1, R2) <> 64 THEN 1240
1210 R = INT(RND(1) * 2)
1220 IF R = 1 THEN R1 = 2: R2 = 7
1230 IF R = 2 THEN R1 = 3: R2 = 9
1240 J = A(R1, R2)
1250 A(R1, R2) = 80
1260 T = 100: Z = 0
1270 RETURN
1280 REM **********
1290 REM OUT OF GAS
1300 PRINT: PRINT: PRINT
1310 PRINT "YOU'VE RUN OUT OF GAS..."
1320 PRINT ".....SO THE GAME IS OVER"
1330 PRINT: PRINT "YOUR SCORE IS"; S + Z - T
1340 END
1350 REM ********
1360 REM MAP DATA
1370 DATA 0,1,0,0,0,1,0,0,0,00
1380 DATA 0,1,1,1,0,0,82,1,1,1
1390 DATA 0,0,0,0,0,1,0,0,66,0
1400 DATA 1,0,1,1,0,1,0,1,1,00
1410 DATA 1,0,0,1,0,1,0,0,1,00
1420 DATA 1,0,1,1,0,1,0,1,1,00
1430 DATA 0,0,1,84,0,1,0,1,1,0
1440 DATA 70,0,0,0,1,1,0,0,0,0
1450 DATA 0,1,1,0,1,1,0,1,1,00
1460 DATA 0,0,1,0,0,0,0,0,1,00
2000 ST = TIMER + XX
2010 IF TIMER < ST THEN 2010
2020 RETURN

