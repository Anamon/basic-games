10 REM ENGULF
20 HSCRE = 0
30 DIM A(10, 10)
40 GOSUB 1030: REM INITIALISE
50 GOSUB 220: REM ALIEN MOVE
60 REM *********
70 REM MAIN LOOP
80 GOSUB 800: REM PRINT OUT
90 GOSUB 670: REM PLAYER MOVE
100 GOSUB 800
110 GOSUB 220: REM ALIEN MOVE
120 TME = TME - 1
130 SHOTS = SHOTS + 1
140 IF TME = 0 THEN 580
150 GOTO 80
160 REM *********
170 REM COLLISION
180 PRINT TAB(8); "YOU HIT AN ALIEN, CAPTAIN"
190 PRINT TAB(9); "AND HAVE BEEN DESTROYED"
200 GOTO 580
210 REM **********
220 REM ALIEN MOVE
230 REM * CHECK IF SURROUNDED *
240 H = 0
250 H = H + A(M - 1, N)
260 H = H + A(M + 1, N)
270 H = H + A(M, N - 1)
280 H = H + A(M, N + 1)
290 H = H + A(M - 1, N + 1)
300 H = H + A(M + 1, N + 1)
310 H = H + A(M + 1, N - 1)
320 H = H + A(M - 1, N - 1)
330 IF H = 16 THEN 500: REM SURROUNDED
340 REM * MOVE ALIEN *
350 E = M: F = N
360 CT = 0
370 M = M - INT(RND(1) * 2) + INT(RND(1) * 2)
380 CT = CT + 1: IF CT = 40 THEN M = E: N = F: GOTO 460
390 IF M < 2 OR M > 9 THEN 370
400 IF (M < 4 OR M > 7) AND RND(1) > .7 THEN 370
410 N = N - INT(RND(1) * 2) + INT(RND(1) * 2)
420 CT = CT + 1: IF CT = 40 THEN M = E: N = F: GOTO 460
430 IF N < 2 OR N > 9 THEN 410
440 IF (N < 4 OR N > 7) AND RND(1) > .7 THEN 410
450 IF A(M, N) = 2 THEN 370
460 A(E, F) = 0
470 A(M, N) = 1
480 RETURN
490 REM ********
500 REM SURROUND
510 GOSUB 800
520 PRINT "--------> ENGULFED! WELL DONE..."
530 PRINT "---------> IT TOOK YOU "; SHOTS; "SHOTS"
540 PRINT "AND YOU DID IT WITH"; TME; "TIME UNITS LEFT"
550 Q = TME * 1000 / SHOTS
560 PRINT "--------> YOUR RATING IS"; Q
570 IF Q > HSCRE THEN HSCRE = Q
580 PRINT "-----> HIGHEST SCORE SO FAR IS"; HSCRE
590 IF INKEY$ <> "" THEN 590
600 PRINT: PRINT "DO YOU WANT ANOTHER GAME (Y/N)?"
610 T$ = INKEY$
620 IF T$ <> "Y" AND T$ <> "N" THEN 610
630 IF T$ = "Y" THEN 40
640 PRINT "-------> OVER AND OUT, CAPTAIN!"
650 END
660 REM ***********
670 REM PLAYER MOVE
680 PRINT "WHICH SECTOR WILL YOU SHOOT AT?"
690 PRINT TAB(8); "ACROSS";
700 INPUT S
710 IF S < 2 OR S > 9 THEN 700
720 PRINT TAB(8); "AND DOWN";
730 INPUT R
740 IF R < 2 OR R > 9 THEN 730
750 IF A(R, S) = 1 THEN 170: REM ALIEN HIT
760 IF A(R, S) = 2 THEN PRINT "THAT SECTOR IS ALREADY DESTROYED!": RETURN
770 A(R, S) = 2
780 RETURN
790 REM *********
800 REM PRINT OUT
810 CLS
820 PRINT: PRINT "-> HIGHEST SCORE SO FAR IS"; HSCRE; " <-"
830 PRINT "-> ALIEN SENSES DANGER FACTOR"; INT(100 * H / 3) / 100; " <-"
840 PRINT "------------------------------------"
850 PRINT "----> ALIEN NOW AT"; N; M
860 PRINT "-> TIME LEFT:"; TME; "--- LASERS FIRED:"; SHOTS
870 PRINT "------------------------------------"
880 PRINT TAB(5); "12345678910"
890 FOR K = 1 TO 10
900 IF K < 10 THEN PRINT " ";
910 PRINT K;
920 FOR J = 1 TO 10
930 IF K < 2 OR K > 9 OR J < 2 OR J > 9 THEN PRINT "-";: GOTO 970
940 IF A(K, J) = 0 THEN PRINT "*";
950 IF A(K, J) = 1 THEN PRINT "A";
960 IF A(K, J) = 2 THEN PRINT " ";
970 NEXT J
980 PRINT
990 NEXT K
1000 PRINT
1010 RETURN
1020 REM **********
1030 REM INITIALISE
1040 CLS
1050 RANDOMIZE VAL(RIGHT$(TIME$, 2))
1060 PRINT: PRINT "PLEASE STAND BY FOR YOUR MISSION..."
1070 TME = 30
1080 SHOTS = 0
1090 H = 0
1100 FOR B = 1 TO 10
1110 FOR C = 1 TO 10
1120 A(B, C) = O
1130 IF B < 2 OR B > 9 OR C < 2 OR C > 9 THEN A(B, C) = 2
1140 NEXT C
1150 NEXT B
1160 M = INT(RND(1) * 7) + 2
1170 N = INT(RND(1) * 7) + 2
1180 A(M, N) = 1
1190 RETURN

