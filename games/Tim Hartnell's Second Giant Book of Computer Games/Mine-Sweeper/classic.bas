10 REM MINESWEEPER
20 REM NEAL CAVALIER-SMITH
30 CLS: RANDOMIZE TIMER
40 DEFINT A-Z
50 DIM A(11, 17)
60 L = 1: S = 0: M = 0
70 REM *********
80 REM LAY MINES
90 FOR X = 1 TO 10
100 FOR Y = 1 TO 15
110 K = INT(RND(1) * 25) + 1 - L
120 A(X, Y) = 46
130 IF K = 1 THEN A(X, Y) = 64
140 NEXT Y
150 A(X, 1) = 46
160 NEXT X
170 C = 5: B = 1
180 A(C, B) = 33
190 GOTO 500
200 REM ***************
210 REM PRINT MINEFIELD
220 CLS
230 PRINT: PRINT
240 FOR X = 1 TO 10
250 PRINT TAB(6); ">";
260 FOR Y = 1 TO 15
270 IF A(X, Y) <> 64 THEN PRINT CHR$(A(X, Y));
280 IF A(X, Y) = 64 THEN PRINT ".";
290 NEXT Y
300 PRINT "#"
310 NEXT X
320 PRINT
330 A(C, B) = 32
340 REM ************
350 REM MOVE SWEEPER
360 PRINT "ENTER YOUR MOVE U(P), D(OWN)"
370 PRINT , "  R(IGHT), L(EFT)"
380 INPUT B$
390 C1 = 0: B1 = 0
400 IF B$ = "U" THEN C1 = -1
410 IF B$ = "D" THEN C1 = 1
420 IF B$ = "R" THEN B1 = 1
430 IF B$ = "L" THEN B1 = -1
440 IF C + C1 > 10 OR C + C1 < 1 THEN 380
450 IF B + B1 > 15 THEN 600
460 C = C + C1: B = B + B1
470 IF A(C, B) = 64 THEN 800
480 REM ********************
490 REM COUNT ADJACENT MINES
500 N = 0
510 FOR K = -1 TO 1
520 FOR D = -1 TO 1
530 IF A(C + K, B + D) = 64 THEN N = N + 1
540 NEXT D
550 NEXT K
560 A(C, B) = 48 + N
570 M = M + 1
580 GOTO 210
590 REM ******************
600 REM NEXT LEVEL ROUTINE
610 CLS
620 L = L + 1
630 PRINT "CONGRATULATIONS..."
640 PRINT "YOU HAVE CLEARED A PATH THROUGH": PRINT
650 FOR X = 1 TO 10
660 FOR Y = 1 TO 15
670 PRINT CHR$(A(X, Y));
680 NEXT Y
690 PRINT "#"
700 NEXT X
710 PRINT
720 PRINT "IT TOOK YOU"; M; "MOVES"
730 S = S + 100 - M
740 PRINT "YOUR SCORE IS"; S
750 PRINT "YOU CAN NOW PROGRESS TO LEVEL"; L
760 M = 0
770 INPUT "PRESS <ENTER> TO CONTINUE"; Q$
780 GOTO 80
790 REM *****
800 REM SPLAT
810 PRINT "SPLAT!!!!"
820 PRINT: PRINT "YOU'VE HIT A MINE, TURKEY, SO THE"
830 PRINT "GAME IS OVER. YOUR SCORE IS"; S + L * 10 - M

