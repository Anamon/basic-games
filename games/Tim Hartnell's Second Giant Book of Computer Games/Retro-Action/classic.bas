10 REM RETRO-ACTION
20 CLS
30 RANDOMIZE TIMER
40 T = 1000
50 LS = 0: RS = 0
60 CLS
70 IF T > 50 THEN T = T - 40
80 PRINT "---------------------------------------"
90 PRINT: PRINT TAB(11); "O"; TAB(27); "O"
100 W = T: GOSUB 630
110 PRINT: PRINT TAB(13); "O"; TAB(25); "O"
120 GOSUB 630
130 PRINT: PRINT TAB(15); "O"; TAB(23); "O"
140 GOSUB 630
150 PRINT: PRINT TAB(17); "O"; TAB(21); "O"
160 GOSUB 630
170 N = INT(RND(1) * 4) + 1
180 ON N GOSUB 520, 580, 550, 580
190 P = 1
200 IF INKEY$ <> "" THEN 200
210 G$ = INKEY$
220 IF G$ = "" THEN 210
230 IF G$ = "A" AND (CT = 1 OR CT = 2) THEN 310
240 IF G$ = ";" AND (CT = 0 OR CT = 2) THEN 290
250 IF G$ = "A" AND CT = 0 THEN 470
260 IF G$ = ";" AND CT = 1 THEN 460
270 GOTO 190
280 REM *****
290 REM RIGHT
300 P = 23
310 PRINT: PRINT
320 IF CT = 2 THEN IF INKEY$ = "" THEN 320
330 PRINT TAB(P); "WELL DONE!"
340 IF P = 23 THEN 380
350 LS = LS + 10
360 PRINT: PRINT TAB(P); "SCORE"; LS
370 GOTO 400
380 RS = RS + 10
390 PRINT: PRINT TAB(P); "SCORE"; RS
400 IF LS > 99 OR RS > 99 THEN 660
410 XX = 1.5: GOSUB 1000
420 CLS
430 GOTO 60
440 REM *****
450 REM WRONG
460 P = 23
470 PRINT "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!": PRINT
480 PRINT: PRINT TAB(P); "YOU FOOL!!"
490 PRINT TAB(P); "YOU LOSE 15 POINTS"
500 IF P = 1 THEN LS = LS - 15: GOTO 410
510 RS = RS - 15: GOTO 410
520 PRINT: PRINT TAB(19); "*": PRINT TAB(20); "*": PRINT TAB(17); "*****"
530 PRINT TAB(20); "*": PRINT TAB(19); "*"
540 CT = 0: RETURN
550 PRINT: PRINT TAB(19); "*": PRINT TAB(18); "*": PRINT TAB(17); "*****"
560 PRINT TAB(18); "*": PRINT TAB(19); "*"
570 CT = 1: RETURN
580 PRINT: PRINT TAB(19); "*": PRINT TAB(19); "*": PRINT TAB(17); "* * *"
590 PRINT TAB(18); "***": PRINT TAB(19); "*"
600 CT = 2: RETURN
610 REM *****
620 REM DELAY
630 XX = W / 1000: GOSUB 1000
640 RETURN
650 REM *******
660 REM VICTORY
670 P = 1: IF RS > 99 THEN P = 23
680 CLS: PRINT: PRINT: PRINT
690 PRINT TAB(P); "CONGRATULATIONS!"
700 PRINT: PRINT TAB(P); "YOU'VE WON!"
710 XX = 1.5: GOSUB 1000
720 IF P = 23 THEN P = 1: GOTO 740
730 P = 23
740 PRINT TAB(P); "BAD LUCK, SUCKER!"
750 XX = 1.0: GOSUB 1000
760 END
1000 ST = TIMER + XX
1010 IF TIMER < ST THEN 1010
1020 RETURN

