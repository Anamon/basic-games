10 REM DANIEL MORGAN'S MILITIA
20 GOSUB 650: REM INITIALISE
30 GOTO 280
40 REM **********
50 REM MAIN CYCLE
60 CLS
70 COUNT = COUNT - LEV / 10
80 PRINT "MORALE IS"; INT(SCR); " YOU'VE FIRED"; SHOTS; "SHOTS"
90 IF COUNT < 1 THEN EDFLAG = 2
100 IF SHOTS > 35 - LEV THEN EDFLAG = 3
110 PRINT TAB(11); H$
120 FOR J = 1 TO INT(COUNT + .5)
130 PRINT TAB(11); "I          I": REM 10 SPACES
140 NEXT J
150 PRINT TAB(11); "I"; A$; "I"
160 PRINT TAB(11); "I"; B$; "I"
170 PRINT TAB(11); "I"; C$; "I"
180 PRINT TAB(11); "I"; D$; "I"
190 IF EDFLAG = 1 THEN PRINT: PRINT TAB(4); "YOU'VE BEATEN THE BRITISH!": GOTO 320
200 IF EDFLAG = 2 THEN PRINT: PRINT TAB(4); "THE BRITISH HAVE BEATEN YOU": GOTO 320
210 IF EDFLAG = 3 THEN PRINT: PRINT TAB(3); "YOU'RE OUT OF AMMUNITION!": GOTO 320
220 F$ = INKEY$
230 REM DELETE NEXT LINE FOR A MUCH MORE DIFFICULT GAME...
240 IF F$ <> "Z" AND F$ <> "M" AND F$ <> " " THEN 220
250 IF F$ = "Z" THEN PST = PST - 1: IF PST < 1 THEN PST = 1
260 IF F$ = "M" THEN PST = PST + 1: IF PST > 10 THEN PST = 10
270 IF F$ = " " THEN SHOTS = SHOTS + 1: GOSUB 410: REM FIRE
280 M$ = "^^^^^^^^^^"
290 H$ = LEFT$(M$, PST) + "V" + RIGHT$(M$, 11 - PST)
300 GOTO 60
310 REM ***********
320 REM END OF GAME
330 PRINT
340 EFFECT = SHOTS
350 IF EDFLAG = 1 THEN EFFECT = .1
360 IF EDFLAG = 3 THEN EFFECT = 97
370 PRINT TAB(4); "YOUR TROOPS' RATING IS"; INT((1974 * SCR) / (EFFECT * LEV + .0005))
380 IF INKEY$ <> "" THEN 380
390 END
400 REM ***************
410 REM FIRE SUBROUTINE
420 IF MID$(A$, PST, 1) = " " THEN 460
430 SCR = SCR + VAL(MID$(A$, PST, 1)) * LEV
440 MID$(A$, PST, 1) = " "
450 RETURN
460 IF MID$(B$, PST, 1) = " " THEN 500
470 SCR = SCR + 2 * VAL(MID$(B$, PST, 1)) * LEV / 3
480 MID$(B$, PST, 1) = " "
490 RETURN
500 IF MID$(C$, PST, 1) = " " THEN 540
510 SCR = SCR + 4 * VAL(MID$(C$, PST, 1)) * LEV / 2.5
520 MID$(C$, PST, 1) = " "
530 RETURN
540 IF MID$(D$, PST, 1) = " " THEN 580
550 SCR = SCR + 8 * VAL(MID$(D$, PST, 1)) * LEV / 1.3
560 MID$(D$, PST, 1) = " "
570 RETURN
580 IF A$ <> "          " THEN RETURN: REM 10 SPACES
590 IF B$ <> "          " THEN RETURN: REM 10 SPACES
600 IF C$ <> "          " THEN RETURN: REM 10 SPACES
610 IF D$ <> "          " THEN RETURN: REM 10 SPACES
620 EDFLAG = 1
630 RETURN
640 REM **************
650 REM INITIALISATION
660 CLS
670 GOSUB 1010: REM GET LEVEL
680 RANDOMIZE TIMER
690 FOR Z = 1 TO 10
700 IF RND(1) < .6 THEN 740
710 GOSUB 970
720 A$ = A$ + T$
730 GOTO 750
740 A$ = A$ + " "
750 IF RND(1) < .6 THEN 790
760 GOSUB 970
770 B$ = B$ + T$
780 GOTO 800
790 B$ = B$ + " "
800 IF RND(1) < .6 THEN 840
810 GOSUB 970
820 C$ = C$ + T$
830 GOTO 850
840 C$ = C$ + " "
850 IF RND(1) < .6 THEN 890
860 GOSUB 970
870 D$ = D$ + T$
880 GOTO 900
890 D$ = D$ + " "
900 NEXT Z
910 COUNT = 15
920 PST = INT(RND(1) * 9) + 1
930 SCR = 0
940 EDFLAG = 0
950 SHOTS = 0
960 RETURN
970 T$ = STR$(INT(RND(1) * 9) + 1)
980 T$ = RIGHT$(T$, 1)
990 RETURN
1000 REM *********
1010 REM GET LEVEL
1020 PRINT: PRINT: PRINT
1030 PRINT TAB(3); "IT IS 2 AM ON JANUARY 17, 1781"
1040 PRINT: PRINT
1050 PRINT TAB(3); "THIS IS YOUR TIME OF TESTING, GENERAL MORGAN"
1060 PRINT: PRINT
1070 PRINT "ENTER YOUR CHOICE OF LEVEL (1 TO 10)"
1080 PRINT: PRINT TAB(8); "10 IS THE MOST DIFFICULT"
1090 PRINT TAB(12); "1 IS THE EASIEST"
1100 PRINT: INPUT "             "; LEV
1110 IF LEV < 1 OR LEV > 10 THEN 1100
1120 RETURN

