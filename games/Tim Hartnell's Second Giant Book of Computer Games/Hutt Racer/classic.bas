10 REM HUTT RACER
20 GOSUB 100: REM INITIALISE
30 GOSUB 240: REM START GRID
40 GOSUB 360: REM RACE
50 GOSUB 740: REM SHOW RESULT
60 IF K$ = "Y" THEN RUN
70 PRINT: PRINT "OK, BYE..."
80 END
90 REM **********
100 REM INITIALISE
110 RANDOMIZE TIMER
120 DEFINT A-Z
130 CLS
140 REM *ADJUST VALUES IN NEXT 2 LINES*
150 REM *FOR YOUR OWN SYSTEM*
160 LL = 40: REM LINE LENGTH
170 LL = LL - 2
180 PL = 24: REM PAGE LENGTH
190 C$ = "W"
200 RW = LL / 4 * 3
210 DT = 0
220 RETURN
230 REM **********
240 REM START GRID
250 WL = (LL - RW) / 2: WR = WL + RW
260 FOR Y = 1 TO PL - 1
270 PRINT TAB(WL); "I"; TAB(WR); "I";
280 NEXT Y
290 PRINT TAB(WL); "I"; TAB(WL + ((WR - WL) / 2) - 2); "START"; TAB(WR); "I"
300 CX = INT(WL + ((WR - WL) / 2))
310 PRINT: PRINT "GO? "; TAB(WL); "I"; TAB(CX); C$; TAB(WR); "I";
320 GOSUB 850
330 PRINT: PRINT "GO..."; TAB(WL); "I"; TAB(CX); C$; TAB(WR); "I";
340 RETURN
350 REM ****
360 REM RACE
370 R$ = "FALSE": J$ = "": JP = 0
380 K$ = INKEY$
390 DT = DT + 1
400 IF K$ = "Z" THEN CX = CX - 1
410 IF K$ = "M" THEN CX = CX + 1
420 IF JP > 0 THEN J$ = "V"
430 IF RND(1) < .2 THEN GOSUB 680
440 IF JP = CX AND J$ = "V" THEN R$ = "TRUE"
450 GOSUB 590
460 IF CX = WL THEN R$ = "TRUE"
470 IF CX = WR THEN R$ = "TRUE"
480 IF (DT / PL) = INT(DT / PL) THEN WL = WL + 1: WR = WR - 1
490 IF RND(1) < .2 THEN WL = WL - 1: WR = WR - 1: GOTO 510
500 IF RND(1) > .8 THEN WL = WL + 1: WR = WR + 1
510 IF WL < 1 THEN WL = 1
520 IF WR > LL THEN WR = LL
530 IF WR - WL <= 1 THEN R$ = "WIN"
540 GOSUB 900: REM SPEED
550 IF R$ = "FALSE" THEN 380
560 RETURN
570 REM ********
580 REM SHOW CAR
590 PRINT
600 IF JP = 0 THEN PRINT TAB(WL); "I"; TAB(CX); C$; TAB(WR); "I";: GOTO 650
610 IF JP > CX THEN PRINT TAB(WL); "I"; TAB(CX); C$; TAB(JP); J$; TAB(WR); "I";
620 IF JP = CX THEN PRINT TAB(WL); "I"; TAB(CX); "+"; TAB(WR); "I";
630 IF JP < CX THEN PRINT TAB(WL); "I"; TAB(JP); J$; TAB(CX); C$; TAB(WR); "I";
640 IF J$ = "V" THEN J$ = "": JP = 0
650 RETURN
660 REM *************
670 REM START JAM CAR
680 JP = INT(WL + ((WR - WL) * RND(1)))
690 IF JP = WL THEN JP = JP + 1
700 IF JP = WR THEN JP = JP - 1
710 J$ = "!"
720 RETURN
730 REM ***********
740 REM SHOW RESULT
750 PRINT: PRINT: PRINT
760 IF R$ = "WIN" THEN PRINT "WELL DONE. YOU'VE COMPLETED"; DT; "MILES"
770 IF R$ = "TRUE" THEN PRINT "YOU CRASHED AT"; DT; "MILES"
780 PRINT: PRINT: PRINT "DO YOU WANT ANOTHER GO (Y OR N)?"
790 IF INKEY$ <> "" THEN 790
800 K$ = INKEY$: IF K$ = "" THEN 800
820 IF K$ <> "Y" AND K$ <> "N" THEN 800
830 RETURN
840 REM *****
850 REM DELAY
860 XX! = 1.0: GOSUB 1000
880 RETURN
890 REM *****
900 REM SPEED
910 REM CHANGE THE 199 FOR BEST EFFECT
920 REM          ON YOUR SYSTEM
930 XX! = (361 - DT!) / 800: GOSUB 1000
950 RETURN
1000 ST! = TIMER + XX!
1010 IF TIMER < ST! THEN 1010
1020 RETURN

