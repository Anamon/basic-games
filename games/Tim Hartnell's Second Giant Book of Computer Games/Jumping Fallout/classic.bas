5 DIM A$(11, 9)
10 REM JUMPING FALLOUT
20 GOSUB 870
30 REM **********
40 REM GAME CYCLE
50 GOSUB 140
60 GOSUB 420
70 GOSUB 290
80 GOSUB 580
90 IF V < 9 THEN GOTO 60
100 GOSUB 770
110 IF G$ <> "N" THEN 50
120 END
130 REM ******************
140 REM NEW GAME VARIABLES
150 S = 200: V = 0
160 FOR X = 2 TO 9
170 FOR Y = 1 TO 9
180 A$(X, Y) = " "
190 IF RND(1) > .16 THEN A$(X, Y) = "#"
200 NEXT Y
210 A$(X, INT(RND(1) * 8 + 1)) = " "
220 NEXT X
230 FOR C = 1 TO 9
240 A$(1, C) = RIGHT$(STR$(C), 1)
250 A$(10, C) = " "
260 NEXT C
270 RETURN
280 REM ************
290 REM DISPLAY MOVE
300 CLS
310 PRINT
320 FOR X = 1 TO 9
330 PRINT X - 1;
340 FOR Y = 1 TO 9
350 PRINT A$(X, Y);
360 NEXT Y
370 PRINT
380 NEXT X
390 PRINT: PRINT "SCORE:"; S
400 RETURN
410 REM ****************
420 REM FALLDOWN ROUTINE
430 FOR Y = 9 TO 1 STEP -1
440 FOR X = 1 TO 9
450 IF A$(Y, X) = " " OR A$(Y, X) = "#" THEN 470
460 GOTO 490
470 NEXT X
480 GOTO 550
490 IF A$(Y + 1, X) <> " " THEN 470
500 A$(Y + 1, X) = A$(Y, X)
510 IF Y >= 9 THEN A$(Y + 1, X) = " ": V = V + 1
520 A$(Y, X) = " "
530 Y = Y + 1
540 GOTO 490
550 NEXT Y
560 RETURN
570 REM ************
580 REM MOVE A LEVEL
590 IF V >= 9 THEN RETURN
600 PRINT: PRINT "WHICH LEVEL DO YOU WANT TO MOVE (1-8)"
610 INPUT "             "; L
620 L = L + 1
630 IF L > 9 OR L < 2 THEN 610
640 PRINT "HOW MANY MOVES TO THE LEFT";
650 INPUT P
660 IF P < 0 OR P > 10 THEN 650
670 FOR C = 1 TO P
680 T$ = A$(L, 1)
690 FOR X = 1 TO 8
700 A$(L, X) = A$(L, X + 1)
710 NEXT X
720 A$(L, 9) = T$
730 NEXT C
740 S = S - L - P
750 RETURN
760 REM ***********
770 REM END OF GAME
780 PRINT "WELL DONE, YOU CLEARED THE BOARD WITH A SCORE OF "; S
790 IF S > H THEN PRINT "YOU HAVE A NEW HIGH SCORE. PLEASE ENTER YOUR NAME ";: INPUT N$: H = S
800 PRINT: PRINT "THE BEST SCORE SO FAR IS"; H
810 PRINT "WHICH WAS GAINED BY "; N$
820 PRINT: PRINT "DO YOU WANT ANOTHER GAME (Y/N)";
830 INPUT G$
840 IF G$ <> "N" AND G$ <> "Y" THEN 830
850 RETURN
860 REM **********
870 REM INITIALISE
880 RANDOMIZE TIMER
890 CLS
910 H = 0
920 N$ = "NEAL"
930 RETURN

