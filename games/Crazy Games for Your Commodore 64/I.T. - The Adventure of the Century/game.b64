10 REM**I.T.**
20 GOSUB 5000
30 MV=8:NR=0:GOTO 50
40 GOSUB 200
50 GOSUB 500
60 GOSUB 400
70 IF NW<6 THEN 40
100 PRINT "{DOWN}CONGRATULATIONS,YOU SUCCEEDED IN KILLINGYOURSELF!"
110 END
200 REM**INPUT**
210 NR=0:MV=0
220 INPUT "WHAT NEXT";C$
230 FOR I=1 TO 14
240 IF LEFT$(M$(I),LEN(C$))=C$ THEN MV=I:I=14
250 NEXTI
260 IF MV>6 THEN GOTO 310
270 IF MV=0 THEN PRINT "I DON'T UNDERSTAND":GOTO 200
280 NR=R(RM,MV):IF NR<>0 THEN 310
290 PRINT "THE WAY IS BARRED"
300 GOTO 200
310 PRINT "{CLR}";
320 RETURN
400 REM**OUTPUT**
410 PRINT "YOU ARE IN THE ";:PRINT D$(RM)
420 IF RM>=25 OR (LO AND T(1)=0 OR T(1)=RM) THEN 430
425 PRINT "IT'S VERY DARK HERE":GOTO 440
430 FOR I=1 TO 12
432 IF T(I)=RM THEN PRINT "A ";T$(I);" IS HERE"
434 NEXT I
435 IF LO AND RL>120 THEN PRINT "YOUR LAMP HAS EXPIRED":LO=0
436 IF LO AND RL>100 THEN PRINT "YOUR LAMP BATTERIES ARE GETTING EXHAUSTED"
440 FOR I=1 TO 6
450 IF R(RM,I)<>0 THEN PRINT "YOU CAN GO ";M$(I)
460 NEXT I
470 PRINT TAB(8);"***"
480 RETURN
500 REM**OPERATIONS**
510 IF NR<>0 THEN RM=NR
520 TH=0
530 FOR I=1 TO 12
540 IF T(I)=RM THEN TH=I:I=12
550 NEXT I
560 HL=0
570 FOR I=1 TO 6
580 IF H(I,1)=RM THEN HL=I:I=6
590 NEXT I
600 REM*GENERAL OPERATIONS*
610 IF RM<12 AND RR<>8 THEN GOSUB 4200
620 RL=RL-LO
630 REM*PERSONAL OPERATIONS*
640 IF MV<7 THEN 660
650 ON MV-6 GOSUB 1000,1200,1300,1400,1600,1700,1800,1900
660 REM*HANDLERS*
670 ON HL GOSUB 2000,2300,2600,2700,2900,3100
680 RETURN
1000 REM**KILL**
1010 IFHL=0ORHL>4THENPRINT"TIME IS THE ONLY THING YOU CAN KI{2 L}":RETURN
1020 TP=7:GOSUB4100
1030 IF PO=0 THEN PRINT "WITH BARE HANDS? THAT'S SUICIDE!":RETURN
1040 PRINT "YOU TRY TO KILL THE ";:PRINT H$(HL)
1050 PRINT "YOU STRIKE IT WITH YOUR SWORD,";
1055 IF (HL=1 OR HL=2) AND (H(HL,2)=6 OR H(HL,2)=5) THEN 1070
1060 IF RND(0)<.5 THEN PRINT " BUT IT MOVES AWAY QUICKLY...":GOTO 1090
1070 PRINT " AND YOU GIVE IT A TERRIBLE BLOW!"
1080 H(HL,3)=H(HL,3)-1
1090 IF RND(0)>.2 THEN 1120
1100 PRINT "THIS IS YOUR CHANCE!! YOU CAN HIT IT AGAIN BEFORE IT RECOVERS."
1110 INPUT "DO YOU WANT TO(Y/N)";AN$:PRINT "{CLR}";
1115 IF AN$="J" THEN PRINT "YOU RAISE YOUR SWORD AGAIN,";:GOTO 1055
1120 IF H(HL,3)>1 THEN RETURN
1130 IF H(HL,3)=1 THEN PRINT "YOUR VICTIM IS BADLY WOUNDED":RETURN
1140 PRINT "YOU KILLED YOUR VICTIM"
1150 H(HL,1)=0:T(HL+8)=RM:HL=0
1160 RETURN
1200 REM**HELP**
1210 PRINT "{CLR}POSSIBLE COMMANDS:{DOWN}"
1220 FOR I=1 TO 14:PRINT M$(I):NEXT
1230 PRINT "{DOWN}HIT ANY KEY"
1240 GET IN$:IF IN$="" THEN 1240
1250 PRINT "{CLR}";
1260 RETURN
1300 REM**TAKE**
1305 IF (T(1)=0 OR T(1)=RM) AND LO OR RM>24 THEN 1310
1306 PRINT "YOU CAN'T SEE YOUR OWN FINGERS,LET ALONE SOMETHING TO TAKE!"
1307 RETURN
1310 IF TH=0 THEN PRINT "THERE'S NOTHING TO TAKE HERE":RETURN
1320 PRINT "YOU TAKE THE ";T$(TH)
1330 P(LP)=TH:LP=LP+1:T(TH)=0:TH=0
1340 RETURN
1400 REM**DROP**
1410 IF LP=1 THEN PRINT"YOU OWN NOTHING !":RETURN
1420 PRINT "WHAT DO YOU WANT TO DROP":INPUT D$:PRINT "{CLR}";
1430 TP=0:FOR I=1 TO 12
1440 IF D$=LEFT$(T$(I),LEN(D$)) THEN TP=I:I=12
1450 NEXT I
1460 IF TP=0 THEN PRINT "I DON'T UNDERSTAND":RETURN
1470 GOSUB 4100
1480 IF PO=0 THEN PRINT "YOU'RE NOT CARRYING A";:PRINT T$(TP):GOTO 1520
1490 PRINT "YOU DROP THE ";T$(TP)
1500 TH=P(PO):T(TH)=RM:LP=LP-1:P(PO)=P(LP)
1520 INPUT "DO YOU WANT TO DROP MORE";AN$
1530 PRINT "{CLR}";
1540 IF AN$="Y" THEN 1420
1550 RETURN
1600 REM**INVENTORY**
1610 IF LP=1 THEN PRINT "YOU OWN NOTHING AT ALL":GOTO 1640
1620 PRINT "YOU ARE CARRYING:"
1630 FOR I=1 TO LP-1:PRINT T$(P(I)):NEXT
1640 PRINT "YOU CAN SURVIVE";5-NW;"WOUNDS"
1650 RETURN
1700 REM**BANDAGE**
1710 TP=6:GOSUB 4100
1720 IF PO=0 THEN PRINT "YOU WON'T MANAGE WITHOUT A BANDAGE":RETURN
1730 PRINT "IT DOESN'T LOOK VERY HOPEFUL.I'LL NEED ALL YOU HAVE"
1750 PRINT "ALL RIGHT, IT WILL HOLD FOR A WHILE"
1760 NW=0:LP=LP-1:P(PO)=P(LP)
1770 RETURN
1800 REM**LANTERN ON/OFF**
1810 IF T(1)>0 AND T(1)<>RM THEN PRINT "GET A LANTERN FIRST!":RETURN
1820 IF RL>120 THEN PRINT "YOUR BATTERIES ARE FINISHED":RETURN
1830 LO=NOT LO
1840 PRINT "THE LANTERN IS NOW ";
1850 IF LO THEN PRINT "ON":RETURN
1860 PRINT "OFF"
1870 RETURN
1900 REM**READ**
1910 TP=4:GOSUB 4100
1920 IF PO<>0 THEN 1930
1925 PRINT "THERE IS NOTHING HERE TO READ!":RETURN
1930 IF NOT LO AND RM<25 THEN PRINT "IT'S TOO DARK TO READ":RETURN
1940 T$="SGDQD HR Z QDBHOD ENQ BNNJHDR HM SGHR{3 SPACE}ANNJ.":GOSUB 4000
1950 T$="HS RZXR: SZJD NMD GDKKGNTMC ZMC Z OHMBG NE VGDZS.":GOSUB 4000
1955 T$="OTS HS NM Z AZQADBTD.":GOSUB 4000
1980 PRINT "THAT'S ALL"
1990 RETURN
2000 REM**I.T.**
2010 IF TH=3 THEN H(1,2)=3:T(TH)=0:TH=0
2020 ON H(1,2) GOTO 2030,2060,2090,2190,2220
2030 PRINT TAB(5);"** I.T. **"
2034 PRINT "I.T. IS HERE, HE'S TALKING URGENTLY, BUT YOU DON'T UNDERSTAND ";
2038 PRINT
2050 H(1,2)=2:RETURN
2060 T$="H.S. FDRSTQDR SGZS GD HR UDQX SGHQRSX.GDKNNJR CDRODQZSD.":GOSUB 4000
2075 PRINT
2080 RETURN
2090 T$="H.S. CQHMJR ZR GD MDDCDC HS UDQX{5 SPACE}AZCKX.":GOSUB 4000
2105 T$="ZESDQ Z VGHKD GD RSZQSR SZKJHMF ZFZHM;":GOSUB 4000
2110 T$="ZMC XNT QDZKHYD XNT BZM TMCDQRSZMC GHL!":GOSUB 4000
2120 T$="GD RZXR: OKDZRD FN CNVM EHMC SGD MTKKHSXANLA.":GOSUB 4000
2133 T$="RNLD BQZYX OQNEDRRNQ VZMSR SN CDRSQNX{3 SPACE}ZKK KHED VHSG HS!":GOSUB 4000
2143 T$="H GZC Z EHFGS VHSG SGD LNMRSDQ SGZS":GOSUB 4000
2150 T$="OQNSDBSR HS,ATS MNV H ZL DWGZTRSDC,":GOSUB 4000
2160 T$="RN HS'R TO SN XNT SN RZUD SGD DZQSG!":GOSUB 4000
2170 PRINT "SUDDENLY I.T. COLLAPSES"
2180 H(1,2)=4:RETURN
2190 PRINT "IT LOOKS LIKE I.T. IS IN A COMA.NOW YOU'LL HAVE TO DO IT ALL ALONE!"
2205 PRINT "{2 SPACE}GOOD LUCK!"
2210 H(1,2)=5:RETURN
2220 PRINT "I.T. IS HERE.HE IS IN A COMA.":PRINT
2240 RETURN
2300 REM**DRAGON**
2310 IF TH=8 THEN H(2,2)=5:T(TH)=0:TH=0
2320 ON H(2,2) GOTO 2330,2370,2400,2430,2470,2520
2330 PRINT "THERE IS AN ENORMOUS MONSTER HERE. IT'S EYES ARE ROLLING"
2335 PRINT "IT YELLS: ARE YOU A COOKIE?"
2360 H(2,2)=2:RETURN
2370 PRINT "THE MONSTER YELLS LOUDER AND LOUDER:{DOWN}"
2380 PRINT "ARE YOU A COOKIE ?!!"
2390 H(2,2)=3:RETURN
2400 PRINT "THE MONSTER KEEPS YELLING AND BECOMES{3 SPACE}RATHER AGRESSIVE."
2420 H(2,2)=4:RETURN
2430 PRINT "THE MONSTER GIVES YOU A TERRIBLE BLOW.{2 SPACE}YOUR HEAD IS SPINNING"
2450 NW=NW+1
2460 H(2,2)=FNRD(3)+1:RETURN
2470 PRINT "THE MONSTER SAYS SURPRISED: COOKIES ?!"
2480 PRINT "AND STARTS TO EAT AT ONCE. AN ENORMOUS"
2495 PRINT "{3 SPACE}! ! B O N G ! !"
2497 PRINT "AND IT FALLS ASLEEP."
2500 R(16,6)=1
2510 H(2,2)=6:RETURN
2520 PRINT "THE MONSTER IS ASLEEP."
2530 RETURN
2600 REM**SNAKE**
2610 PRINT "THERE IS A SNEAKY SNAKE HERE."
2620 IF RND(0)<.4 THEN RETURN
2630 IF LP=1 OR RND(0)<.5 THEN 2670
2640 RN=FNRD(LP-1):T(P(RN))=12+FNRD(12):LP=LP-1:P(RN)=P(LP)
2650 PRINT "IT SNATCHES SOMETHING,AND ";
2670 PRINT "IT SNEAKS AWAY"
2680 H(3,1)=H(3,1)+3:IF H(3,1)>24 THEN H(3,1)=H(3,1)-8
2690 RETURN
2700 REM**HELLHOUND**
2710 ON H(4,2) GOTO 2720,2750,2780
2720 PRINT "THERE IS A GIANT HELLHOUND HERE"
2730 PRINT "IT LOOKS LIKE IT WANTS YOU FOR DINNER"
2740 H(4,2)=2:RETURN
2750 PRINT "THE HELLHOUND ATTACKS AND BITES YOU{5 SPACE}VIOLENTLY."
2760 NW=NW+1
2770 H(4,2)=3:RETURN
2780 PRINT "THE HELLHOUND GROWLS AND SEEMS TO PREPARE FOR ANOTHER ATTACK."
2800 H(4,2)=1+FNRD(2):RETURN
2900 REM**BOMB**
2910 PRINT "THE NULLITY-BOMB IS HERE. THERE ARE THREE";
2920 PRINT "WIRES CONNECTING THE BOMB AND THE TIMINGMECHANISM:"
2925 PRINT "A GREEN ONE (G), ";
2927 PRINT "A YELLOW ONE (Y), ";
2929 PRINT "AND A RED ONE (R)"
2930 PRINT "YOU MUST DISCONNECT TWO TO MAKE IT STOP"
2940 INPUT "WHICH ONE FIRST ";X$
2950 INPUT "AND SECOND ";Y$
2960 CB=0:C$="GYR":FOR I=1 TO 3
2970 CB=CB-(X$=MID$(C$,I,1))-(Y$=MID$(C$,I,1))
2980 NEXT I
2990 IF CB<2 THEN PRINT "{CLR}WATCH OUT!":PRINT "WRONG INPUT!":GOTO 2910
3000 IF ASC(X$)*ASC(Y$)=5822 THEN 3030
3005 FOR I=1 TO 10:POKE 53281,1
3010 PRINT "{CLR} * ENORMOUS EXPLOSION *{DOWN}"
3012 PRINT "{3 SPACE}! MUSHROOM CLOUD !{DOWN}"
3016 PRINT "YOU TRIED BRAVELY, BUT ALAS !! YOU "
3020 PRINT "DIDN'T SUCCEED..."
3022 FOR J=1 TO 4:POKE 53281,9:NEXT J,I
3025 END
3030 PRINT "{CLR}{10 RIGHT}*** CONGRATULATIONS**"
3040 PRINT "{DOWN}YOU SUCCEEDED WHERE EVERYONE ELSE FAILED"
3060 END
3100 REM**BARBECUE**
3130 IF T(2)=RM AND T(12)=RM THEN TH=8:T(TH)=RM:T(2)=0:T(12)=0:H(6,2)=2
3140 ON H(6,2) GOTO 3150,3180,3220
3150 PRINT "THERE IS A GIANT BARBECUE WITH A "
3165 PRINT "LARGE FIRE UNDER IT."
3170 RETURN
3180 PRINT "AN ENORMOUS FLASH LIGHTENS THE PLACE "
3185 FOR I=1 TO 40: POKE 53281,1:NEXT I
3186 POKE 53281,2
3187 PRINT "AND A PENETRATING SMELL FILLS YOUR NOSE."
3210 H(6,2)=3:RETURN
3220 PRINT "EVERYTHING IS QUIET NOW; EVEN THE PENE- TRATING SMELL "
3240 PRINT "HAS GONE."
3250 H(6,2)=1:RETURN
4000 REM**DECODE TEXT**
4010 TN$="":FOR I1=1 TO LEN(T$):C$=MID$(T$,I1,1)
4020 IF C$="Z" THEN C$="A":GOTO 4030
4025 IF C$>="A" AND C$<"Z" THEN C$=CHR$(ASC(C$)+1)
4030 TN$=TN$+C$
4040 NEXT:IF NOT IN THEN PRINT TN$
4050 RETURN
4100 REM**WHAT IS CARRIED**
4110 PO=0:FOR I=1 TO LP-1
4120 IF P(I)=TP THEN PO=I:I=LP-1
4130 NEXT I
4140 RETURN
4200 REM**PUZZLE**
4210 RT=RT+1
4220 IF RM=S(RT) THEN RR=RR+1
4230 IF RT<8 THEN RETURN
4240 IF RR=8 THEN 4310
4250 FOR Q=1 TO 80:POKE 53281,0:NEXT Q
4255 PRINT "AN ABSOLUTE DARKNESS COVERS YOU AND IT "
4265 PRINT "FEELS AS IF SOMETHING IS BEING PUSHED{3 SPACE}AWAY"
4270 PRINT "FOR A MOMENT YOU ARE UNCONCIOUS."
4280 POKE 53281,2
4290 RT=1:RR=1:RM=1
4300 RETURN
4310 PRINT "YOU HEAR A STRANGE SOUND AS IF SOMETHING "
4330 PRINT "IS BEING PUSHED AWAY."
4340 FOR DL=1 TO 3000:NEXT DL
4350 PRINT "NOW IT HAS STOPPED."
4360 R(2,1)=3
4370 RETURN
5000 REM**INITIALISE**
5010 PRINT  "{CLR}{DOWN}"SPC(10) "WAIT A MINUTE PLEASE."
5020 DIM R(36,6),D$(36),M$(14),H(6,3),H$(6),T(12),T$(12),P(12),D(8)
5030 DEF FNRD(X)=INT(X*RND(0)+1)
5100 REM*CONNECTIONS*
5110 FOR I=1 TO 36
5120 R(I,1)=I+1:R(I,2)=I-1:R(I,3)=I+4:R(I,4)=I-4
5130 NEXT I
5140 FOR I=0 TO 24 STEP 12
5150 FOR J=1 TO 9 STEP 4:R(I+J+3,1)=0:R(I+J,2)=0:NEXT
5160 FOR J=1 TO 4:R(I+J+8,3)=0:R(I+J,4)=0:NEXT
5170 NEXT I
5180 R(1,5)=16:R(7,5)=15:R(32,6)=13:R(13,5)=32:R(35,6)=18:R(18,5)=35
5190 FOR I=1 TO 15:READ RM:READ MV:R(RM,MV)=0:NEXT
5200 REM*INIT PARAMETERS*
5210 RM=36:PO=1:LP=1:RL=0:LO=0:NW=0:RT=0:RR=0
5300 REM*FILL ARRAYS*
5310 IN=-1
5320 FOR I=1 TO 14:READ M$(I):NEXT
5330 FOR I=1 TO 12:READ T$:GOSUB 4000:T$(I)=TN$:NEXT
5340 FOR I=1 TO 6:READ T$:GOSUB 4000:H$(I)=TN$:NEXT
5350 FOR I=1 TO 36:READ T$:GOSUB 4000:D$(I)=TN$:NEXT
5360 FOR I=1 TO 12:READ T(I):NEXT
5370 FOR I=1 TO 6:READ H(I,1),H(I,3):H(I,2)=1:NEXT
5380 FOR I=1 TO 8:READ S(I):NEXT
5390 IN=0
5400 RETURN
5500 DATA 21,1,22,2,22,1,23,2,18,1,19,2,16,3,20,4,11,1,12,2,7,1,8,2,7,4,3,3,2,1
5510 DATA EAST,WEST,NORTH,SOUTH,UP,DOWN,KILL,HELP,TAKE,DROP
5515 DATA INVENTORY,BANDAGE,LANTERN,READ
5520 DATA KZMSDQM,VGDZSOHKD,VZSDQRZBJ,BNNJANNJ,KDZEKDS,AZMCZFD,RVNQC
5522 DATA BNNJHD,ANCX HE H.S.
5525 DATA FHZMS BNQORD,RKHBDC RMZFD,CDZC GNTMC
5535 DATA H.S.,LNMRSDQ,RMZJD,GDKKDGNTMC,AZQADBTD,ANLA
5540 DATA QDRDSBZUD,S-BZUD,RDBQDS BNQQHCNQ,BNMSQNK QNNL,N-BZUD,H-BZUD
5545 DATA ROZBD-BZUD,AKZBJ QNNL,O-BZUD,D-BZUD,M-BZUD,DLOSHMDRR
5550 DATA RLZKK BZUD,QNBJX BZUD,RLDKKX BZUD,CQZFNM BZUD,RMZJD BZUD,XDKKNV BZUD
5555 DATA RSQDL AZMJ,RSHMJX OKZBD,ENNC BZUD,EHMZK BZUD
5560 DATA BNKNQDC BZUD,HBD BZUD,NODM OKZBD,VNNCR,VNNCR,VNNCR,VNNCR,VNNCR
5565 DATA VNNCR,VNNCR,VNNCR,VNNCR,VNNC,VNNCR
5570 DATA 34,30,28,21,14,15,13,0,0,0,0,0
5580 DATA 34,2,16,15,17,4,29,2,8,1,25,1
5590 DATA 1,5,9,10,11,7,6,2
