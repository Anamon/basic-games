10 REM**SHARK HUNT**
20 DIM X$(2),Y$(2),TU(3,8),CO$(2)
30 D$="{HOME}{15 DOWN}"
35 SID=54272
40 FOR I=0 TO 23:POKESID+I,0:NEXT:POKESID+24,15:POKESID+5,15:POKESID+6,255
50 B$="{RVS} {OFF}"
60 NG=0:TR=0:MR=0
70 FOR RE=0 TO 2
80 READ X$(RE),Y$(RE)
90 NEXT
100 DATA L,F,"","",R,B
110 REM TUNE
120 FOR TU=1 TO 3
130 FOR NO=1 TO 8
140 READ TU(TU,NO)
150 NEXT NO,TU
160 DATA 40,60,40,60,40,50,60,60
170 DATA 30,35,40,45,50,55,60,60
180 DATA 35,32,30,27,25,22,20,20
190 CO$(0)="SORRY, YOU WENT TOO{3 SPACE}FAR"
200 CO$(1)="THAT'S IT! CONGRATULATIONS!"
210 CO$(2)="YOU RAN OUT OF ENERGY"
230 GOTO 3000
300 REM**BEEP
310 POKE SID+4,17:POKESID+1,TN
320 FOR T=1 TO TM
330 FOR DL=1 TO 10:NEXT DL
340 NEXT T
360 RETURN
1000 REM**INITIALIZE
1010 NG=NG+1
1020 SX=INT(70*RND(0))-35
1030 SY=INT(70*RND(0))-35
1040 DI=ABS(SX)+ABS(SY)
1050 MD=DI:SD=DI
1060 EN=200:SH=0:HI=0:NS=0
1070 VX=0:VY=0
1080 RETURN
1100 REM**INPUT SPEED
1110 GET K$
1120 VX=4*((K$="{LEFT}")-(K$="{RIGHT}"))
1130 VY=4*((K$="{UP}")-(K$="{DOWN}"))
1140 SH=(K$=" ")
1150 RETURN
1300 REM**COMPUTE DISTANCE ETC
1310 SX=SX-VX
1320 SY=SY-VY
1330 EN=EN+(VX<>0)+(VY<>0)-1
1340 IF EN<=0 THEN EN=0:GOTO 1460
1350 REM SHARK MOVES AWAY
1360 SX=SX+SGN(SX)
1370 SY=SY+SGN(SY)
1380 DI=ABS(SX)+ABS(SY)
1390 IF DI<MD THEN MD=DI
1400 IF DI>100 THEN 1460
1410 IF NOT SH THEN 1460
1420 NS=NS+1
1430 EN=INT(EN-.5*DI)
1440 IF EN<=0 THEN EN=0:GOTO 1460
1450 HI=INT(DI*RND(0))<=1
1460 RETURN
1500 REM**OUTPUT
1510 GOSUB 2000:REM BLANK
1520 PRINT LEFT$(D$,8);TAB(SGN(VX)+19);X$(SGN(VX)+1);
1530 PRINT LEFT$(D$,8+SGN(VY));TAB(19);Y$(SGN(VY)+1);
1540 IF SH THEN TM=2:TN=10:GOSUB 300:GOTO 1560
1550 TM=1:TN=140-DI:GOSUB 300
1560 PRINT "{HOME}{DOWN}";TAB(14);EN;"{LEFT} ";
1570 RETURN
1600 REM**SCREEN
1620 PRINT "{CLR}{DOWN}";SPC(7)"ENERGY:"EN"ERGS"
1640 PRINT LEFT$(D$,15);SPC(7)"MOVE:{2 SPACE}USE ARROW KEYS"
1650 PRINT SPC(7)"SHOOT: PRESS SPACEBAR"
1660 GOSUB 2000
1670 PRINT LEFT$(D$,8); SPC(7)"DIRECTION:"
1680 PRINT"{HOME}{2 DOWN}";SPC(7)"<RETURN> TO START";
1685 INPUT AN$
1710 RETURN
1800 REM**END OF THIS GAME
1810 PRINT"{HOME}{DOWN}";SPC(5);CO$(CA-1)
1820 FOR NO=1 TO 8
1830 TN=TU(CA,NO):TM=10:GOSUB 300
1840 NEXT
1845 POKE SID+4,0
1850 PRINT "{CLR}{DOWN}YOU STARTED AT: ";SD;TAB(20);"M"
1860 PRINT "NUMBER OF SHOTS:";NS
1870 PRINT "YOU CAME WITHIN:";MD;TAB(20);"M"
1880 IF HI THEN PRINT "YOU HIT IT FROM:";DI;TAB(20);"M"
1890 IF HI THEN RE=50+EN/4+SD/10:GOTO 1900
1895 RE=50-DI/2
1900 RE=INT(RE)
1910 IF RE>100 THEN RE=100
1915 IF RE<0 THEN RE=0
1920 TR=TR+RE
1930 IF RE>MR THEN MR=RE
1940 PRINT "{DOWN}REWARDS (0-100)"
1945 PRINT "{15 S*}"
1950 PRINT "NUMBER OF GAMES:";NG
1955 PRINT "THIS GAME:{6 SPACE}";RE
1960 PRINT "AVERAGE:{8 SPACE}";INT(TR/NG)
1970 PRINT "MAXIMUM:{8 SPACE}";MR
1980 RETURN
2000 REM**BLANK SPACES
2010 PRINT LEFT$(D$,7);TAB(18);"{3 SPACE}"
2020 PRINT TAB(18);"{3 SPACE}"
2030 PRINT TAB(18);"{3 SPACE}"
2040 RETURN
3000 REM**MAIN PROGRAM
3010 GOSUB 1000:REM INITIALIZE
3020 GOSUB 1600:REM SCREEN
3030 GOSUB 1100:REM INPUT
3040 GOSUB 1300:REM COMPUTING
3050 GOSUB 1500:REM OUTPUT
3060 CA=-3*(EN=0)-2*HI-(DI>100)
3070 IF CA=0 THEN 3030
3080 GOSUB 1800:REM END
3090 PRINT
3095 FOR I=1 TO 10:GET K$:NEXT
3100 INPUT "{DOWN}ANOTHER GAME";AN$
3110 IF LEFT$(AN$,1)="Y" THEN 3010
3120 END
