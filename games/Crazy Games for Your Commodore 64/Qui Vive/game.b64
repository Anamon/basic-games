10 REM***QUI VIVE***
20 POKE646,13
30 DIM SF(41,4),SO(41)
40 DIM D(107,1),DF(15,8)
60 DIM CC(4,1),B(24),C(24)
70 GOTO3000
100 REM**COMPUTE POINTS OF DF**
110 GV=0:P1=0:P2=0:C1=0:C2=0
120 D1=B(DF(NF,0)):D2=B(DF(NF,1))
130 IF D1=1 OR D2=1 THEN P1=1
140 IF D1=10 OR D2=10 THEN C1=10
150 D1=B(DF(NF,2)):D2=B(DF(NF,3))
160 IF D1=1 OR D2=1 THEN P2=1
170 IF D1=10 OR D2=10 THEN C2=10
180 GV=B(DF(NF,4))+B(DF(NF,5))+B(DF(NF,6))
190 P=GV+P1+P2+C1+C2
200 IF GV=30 AND (C1=0 OR C2=0) THEN P=P-10
210 IF GV=3 AND(P1=0 OR P2=0) THEN P=P-1
220 RETURN
300 REM**INITIALIZE SINGLE FIGURES**
310 FOR I=0 TO 41:READ X$
320 FOR J=1 TO 5
330 SF(I,J-1)=ASC(MID$(X$,J,1))-65
340 NEXT J,I:RETURN
350 REM WINGS (AV><)
360 DATA UQMSY,PLHNT,KGCIO
370 DATA AGMIE,FLRNJ,KQWSO
380 DATA AGMQU,BHNRV,CIOSW
390 DATA EIMSY,DHLRX,CGKQW
400 REM TIMES (X)
410 DATA ACGKM,BDHLN,CEIMO
420 DATA FLHPR,GIMQS,HJNRT
430 DATA KMQUW,LNRVX,MOSWY
440 REM PLUS (+)
450 DATA BFGHL,CGHIM,DHIJN
460 DATA GKLMQ,HLMNR,IMNOS
470 DATA LPQRV,MQRSW,NRSTX
480 REM DIAGONALS (/)
490 DATA AGMSY,EIMQU
500 REM COLUMNS (I)
510 DATA AFKPU,BGLQV,CHMRW
520 DATA DINSX,EJOTY
530 REM ROWS (-)
540 DATA ABCDE,FGHIJ,KLMNO
550 DATA PQRST,UVWXY
555 REM
560 REM **INIT DOUBLE FIGURES**
570 FOR I=0 TO 107 STEP 12:READ X$
580 FOR J=0 TO 11:FOR K=0 TO 1
590 L=2*J+K+1:D(I+J,K)=ASC(MID$(X$,L,1))-49
600 NEXT K,J,I
610 RETURN
620 DATA "171:1A1C1E1M1O1P2>2@2B2J"
630 DATA "393<3=3?3G474:4=4?4A4G4O"
640 DATA "4P5@5B5D5J696<6C6E6M7=7A"
650 DATA "7C7I7O7P8>8B8D8J9?9E9K:?"
660 DATA ":A:E:K:O:P;>;@;D;J<=<C<I"
670 DATA "=G=I=O>F>H>J?G?K?P@F@J@L"
680 DATA "AGAIAKAMAOAPBHBJBNCICMCP"
690 DATA "DJDLDNEKEMEOFRFWGSGWHTHW"
700 DATA "IRIXJSJXKTKXLRLYMSMYNTNY"
790 REM
800 REM **COMPUTE DOUBLE FIGURE FIELDS**
810 TC=4:TN=0
820 FOR I=0 TO 4:C(SF(D(DF,0),I))=1:NEXT
830 FOR I=0 TO 4:SF=SF(D(DF,1),I)
840 IF C(SF)=1 THEN 860
850 DF(NF,TN)=SF:TN=TN+1:GOTO 870
860 DF(NF,TC)=SF:TC=TC+1:C(SF)=0
870 NEXTI
880 FOR I=0 TO 4:SF=SF(D(DF,0),I)
890 IF C(SF)=1 THEN DF(NF,TN)=SF:TN=TN+1:C(SF)=0
900 NEXT I:RETURN
1000 REM**DISPLAY BOARD**
1005 PRINT SPC(12)"{DOWN}{3 SPACE}ABCDE{DOWN}"
1010 FOR I=0 TO 4:PRINT SPC(12)5-I;:FOR J=0 TO 4
1020 F=I*5+J
1030 IF B(F)=0 THEN PRINT ".";
1040 IF B(F)=1 THEN PRINT "w";
1050 IF B(F)=10 THEN PRINT "q";
1060 NEXT J:PRINT 5-I:NEXT I
1070 PRINT SPC(12)"{DOWN}{3 SPACE}ABCDE{DOWN}"
1080 RETURN
1100 REM**DETERMINE OWNER OF SF'S**
1110 OM=-1:WM=-1
1120 FOR SF=0 TO 41
1130 P=B(SF(SF,0))+B(SF(SF,1))+B(SF(SF,2))+B(SF(SF,3))+B(SF(SF,4))
1140 IF P=0 OR P=10 OR P=20 OR P=30 THEN SO(SF)=10:GOTO 1240
1150 IF P<>40 THEN 1190
1160 GOSUB 1250
1170 GOSUB 1290
1180 WM=1:SF=41:GOTO 1240
1190 IF P>20 OR P<3 OR P=11 OR P=12 THEN SO(SF)=0:GOTO 1240
1200 SO(SF)=1
1210 IF P=4 AND OM=-1 THEN GOSUB 1250
1220 IF P=14 THEN GOSUB 1290
1230 IF P=5 THEN SF=41
1240 NEXT SF:RETURN
1250 REM**DETERMINE OBLIGATORY MOVE (OM)**
1260 FOR I=0 TO 4
1270 IF B(SF(SF,I))=0 THEN OM=SF(SF,I)
1280 NEXT I:RETURN
1290 REM**DETERMINE PINNED CHECKER**
1300 FOR I=0 TO 4
1310 IF B(SF(SF,I))<>10 THEN 1350
1320 FOR J=0 TO CC-1
1330 IF SF(SF,I)=CC(J,0) THEN CC(J,1)=1
1340 NEXT J
1350 NEXT I
1360 RETURN
1400 REM**DETERMINE OWNER OF DF'S**
1410 NC=0:NP=0:NF=0
1420 FOR I=0 TO 15
1430 DF(I,8)=-1
1440 DF(I,7)=0
1450 NEXT I
1460 FOR DF=0 TO 107
1470 O1=SO(D(DF,0)):O2=SO(D(DF,1))
1480 IF O1<>O2 OR O1=0 OR O2=0 THEN 1600
1490 GOSUB 800
1500 GOSUB 100
1510 IF P=4 OR P=14 THEN DF(NF,7)=1:GOTO 1540
1520 IF O1=1 OR P<=DF(NF,8) THEN 1600
1530 DF(NF,7)=10
1540 DF(NF,8)=P
1550 NF=0
1560 FOR I=0 TO 15
1570 IF DF(I,8)>=DF(NF,8) THEN 1590
1580 IF DF(I,7)=0 OR DF(I,7)=10 THEN NF=1
1590 NEXT I
1600 NEXT DF
1610 RETURN
1800 REM**DETERMINE STRATEGIC VALUE OF MOVE**
1810 IF WM=1 THEN 1960
1820 MC=0:N2=0
1830 FOR NF=0 TO 15
1840 IF DF(NF,7)=0 THEN 1900
1850 GOSUB 100
1860 IF DF(NF,7)=1 THEN 1890
1870 IF P=MC THEN N2=N2+1
1880 IF P>MC THEN MC=P:N2=1
1890 IF P=4 THEN NF=15
1900 NEXT NF
1910 IF PP=-1 THEN 1960
1920 IF P=4 THEN RETURN
1930 IF MC<MP THEN RETURN
1940 IF MC=MP AND N2<N1 THEN RETURN
1950 IF MC=MP AND N2=N1 AND RND(0)<.5 THEN RETURN
1960 MP=MC:N1=N2
1970 PP=CP:PT=CT
1980 RETURN
2000 REM**PLAYER MOVE**
2010 IF PC<5 THEN 2050
2020 PRINT "WHICH CHECKER DO YOU WANT TO MOVE";
2030 GOSUB 2130:PT=X
2040 IF B(PT)<>1 THEN PRINT"NOT POSSIBLE":GOTO 2020
2050 PRINT"WHERE DO YOU WANT TO  PUT YOUR CHECKER";
2060 GOSUB 2130:PP=X
2070 IF B(PP)<>0 THEN PRINT "NOT POSSIBLE":GOTO 2050
2080 IF PC=5 THEN B(PT)=0
2090 IF PC<5 THEN PC=PC+1
2100 B(PP)=1
2110 GOSUB 1000
2120 RETURN
2130 REM**INPUT**
2140 INPUT X$:IF LEN(X$)<>2 THEN 2190
2150 L$=LEFT$(X$,1):D$=RIGHT$(X$,1)
2160 IF L$<"A" OR L$>"E" OR D$<"1" OR D$>"5" THEN 2190
2170 X=ASC(L$)-5*VAL(D$)-40
2180 RETURN
2190 PRINT "WRONG INPUT,":PRINT"TRY AGAIN:";:GOTO 2140
2200 REM**COMPUTER MOVE**
2210 IF CC<5 THEN PT=CC:CC=CC+1:GOTO 2270
2220 PRINT SPC(10)"I TAKE "CHR$(65+PT-INT(PT/5)*5);5-INT(PT/5)
2230 B(PT)=0
2240 FOR CI=0 TO 4
2250 IF CC(CI,0)=PT THEN PT=CI:CI=4
2260 NEXT CI
2270 PRINT SPC(10)"I PUT IT AT "CHR$(65+PP-INT(PP/5)*5);5-INT(PP/5)
2280 B(PP)=10:CC(PT,0)=PP
2290 GOSUB 1000
2300 RETURN
3000 REM**MAIN PROGRAM**
3010 PRINT SPC(12)"PLEASE WAIT"
3020 GOSUB 300
3030 GOSUB 560
3060 CC=0:PC=0
3070 PP=12:GOTO 3300
3080 FOR I=0 TO 4:CC(I,1)=0:NEXT
3090 GOSUB 1100
3100 IF P=5 THEN END
3110 IF WM=1 THEN 3130
3120 GOSUB 1400
3130 MP=0:N1=0:PP=-1
3140 BF=0:EF=24
3150 IF OM<>-1 THEN BF=OM:EF=OM
3160 FOR CP=BF TO EF
3170 IF B(CP)<>0 THEN 3290
3200 B(CP)=10
3210 IF CC<5 THEN GOSUB 1800:GOTO 3280
3220 FOR CI=0 TO 4
3230 IF CC(CI,1) THEN 3270
3240 CT=CC(CI,0):B(CT)=0
3250 GOSUB 1800
3260 B(CT)=10
3270 NEXT CI
3280 B(CP)=0
3290 NEXT CP
3300 GOSUB 2200
3310 IF WM=1 THEN END
3320 GOSUB 2000
3330 PRINT SPC(12) "PLEASE WAIT"
3340 GOTO 3080
