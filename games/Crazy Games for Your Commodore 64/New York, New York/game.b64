10 REM**NEW YORK, NEW YORK**
15 POKE 53281,7
20 TI$="000000":TB=TI:PU=100
30 PRINT"{CLR}{6 C+} {S-} {22 C+} {S-} {6 C+}";
40 PRINT"{6 C+}A{S-} {22 C+}B{S-} {6 C+}";
50 PRINT"{7 SPACE}{S-} C{5 SPACE}{CR} D{6 SPACE}{CR} E{5 SPACE}{S-} F{5 SPACE}";
60 PRINT"{7 S*}{S+}{7 S*}{S+}{8 S*}{S+}{7 S*}{S+}{7 S*}";
70 PRINT"{5 SPACE}G {S-}{5 SPACE}H {S-}{6 SPACE}I {S-}{5 SPACE}J {S-}{7 SPACE}";
80 PRINT"{6 C+} {S-}K{5 C+} {S-}L{6 C+} {S-}M{5 C+} {S-}N{6 C+}";
90 A$="{6 C+} {S-} {5 C+} {S-} {6 C+} {S-} {5 C+} {S-} {6 C+}":PRINT A$ A$ A$;
100 PRINT"{6 C+}O{S-} {5 C+}P{S-} {6 C+}Q{S-} {5 C+}R{S-} {6 C+}";
110 PRINT"{6 C+} {S-} S{5 SPACE}{S-} T{6 SPACE}{S-} U{5 SPACE}{S-} {6 C+}";
120 PRINT"{6 C+}{CQ}{S+}{7 S*}{S+}{8 S*}{S+}{7 S*}{S+}{CW}{6 C+}";
130 PRINT"{6 C+} {S-}{5 SPACE}V {S-}{6 SPACE}W {S-}{5 SPACE}X {S-} {6 C+}";
140 PRINT"{6 C+} {S-}Y{5 C+} {S-}Z{6 C+} {S-}0{5 C+} {S-}1{6 C+}";
150 PRINTA$ A$ A$;
160 PRINT"{6 C+}2{S-} {5 C+}3{S-} {6 C+}4{S-} {5 C+}5{S-} {6 C+}";
170 PRINT"{7 SPACE}{S-} 6{5 SPACE}{S-} 7{6 SPACE}{S-} 8{5 SPACE}{S-} 9{5 SPACE}";
180 PRINT"{7 S*}{S+}{7 S*}{S+}{8 S*}{S+}{7 S*}{S+}{7 S*}";
190 PRINT"{5 SPACE}+ {S-}{5 SPACE}{#} {CE}{6 SPACE}@ {CE}{5 SPACE}* {S-}{7 SPACE}";
200 PRINT"{6 C+} {S-}^{22 C+} {S-}={6 C+}";
210 PRINT"{6 C+} {S-} {22 C+} {S-} {6 C+}";
300 PRINT"{6 C+} {S-} {22 C+} {S-} {6 C+}{HOME}";
400 AW=40:SC%=1023:CL=55295
500 DIMX%(AW),Y%(AW),R%(AW),L%(41),A%(3),F%(9)
510 FORI=0TO41:READL%(I):NEXT
520 DATA 47,72,90,98,107,115,166,174,183,191,209,217,226,234,367,375,384,392
530 DATA 410,418,427,494,503,511,529,537,546,554,687,695,704,712
535 DATA 730,738,747,755
540 DATA 806,814,823,831,849,874
550 FORI=0TO41:A=PEEK(L%(I)+SC%):POKEL%(I)+SC%,AOR128
560 POKEL%(I)+CL,2:NEXT
590 A%(0)=1:A%(1)=-1:A%(2)=40:A%(3)=-40
600 A=RND(-TI):DEFFNR(X)=INT(RND(1)*X)
1000 FORI=0TOAW:R%=R%(I):IFR%=OTHEN1099
1010 X%=X%(I):Y%=Y%(I)
1020 IFR%AND1THENX%=X%+1:F%=98:O0%=226:O%=64:S%=40:IFX%>40THEN1200
1030 IFR%AND2THENX%=X%-1:F%=226:O0%=98:O%=64:S%=-40:IFX%<1THEN1200
1040 IFR%AND4THENY%=Y%+1:F%=97:O0%=225:O%=93:S%=-1:IFY%>23THEN1200
1050 IFR%AND8THENY%=Y%-1:F%=225:O0%=97:O%=93:S%=1:IFY%<0THEN1200
1060 IFR%AND16THEN1100
1070 IFR%AND32THEN1400
1080 IFR%AND64THENO%=91:R%=R%-64:R%(I)=R%:PU=PU+10
1090 GOTO1300
1099 NEXT:GOTO2000
1100 IF(PEEK(X%(I)+40*Y%(I)+S%+CL)AND15)<>2THENR%(I)=(R%OR32)ANDNOT16:GOTO1300
1110 PU=PU-1:GOTO1099
1200 PU=PU+25:PN=PN+1:R%(I)=0:P%=-1:GOTO1330
1300 P%=X%+40*Y%:PS%=PEEK(P%+SC%):IFPS%=F%ORPS%=160THENPU=PU-1:GOTO1099
1310 A=PEEK(P%+S%+SC%):IFA<>32THENR%(I)=R%OR16
1320 IFPS%=O0%THENF%=160
1325 V%=PEEK(X%(I)+40*Y%(I)+SC%):IFV%=160THENO%=O0%
1327 IFV%=102THENR%(I)=0:AO=AO+1:PU=PU-100:GOTO1099
1330 POKEP%+SC%,F%:POKEX%(I)+40*Y%(I)+SC%,O%:X%(I)=X%:Y%(I)=Y%:GOTO1099
1400 P%=X%+40*Y%:A=PEEK(P%+SC%):IFA=F%THEN1099
1410 IFA<>91THEN1500
1420 VB=0:FORJ=0TO3:A=2^J:B=PEEK(P%+SC%+A%(J)):IFNOT(B=64ORB=93)THENVB=VBORA
1430 NEXT
1440 B=2^FNR(4):IF(BANDVB)=0THENR%(I)=BOR64:GOTO1330
1450 IFVB=15THEN1099
1460 GOTO1440
1500 AO=AO+1:R%(I)=0:IFA<>102THENF%(AF)=P%:AF=AF+1:PU=PU-200
1510 F%=102:GOTO1330
2000 IFR%(TL)=0THEN2100
2010 TL=TL+1:IFTL>AWTHENTL=0
2020 GOTO3000
2100 A=FNR(4):R%(TL)=2^A:ONA+1GOTO2110,2120,2130,2140
2110 X%(TL)=1:GOTO2200
2120 X%(TL)=40:GOTO2200
2130 Y%(TL)=0:GOTO2300
2140 Y%(TL)=23:GOTO2300
2200 IFFNR(2)=1THENY%(TL)=3:GOTO3000
2210 Y%(TL)=19:GOTO3000
2300 IFFNR(2)=1THENX%(TL)=8:GOTO3000
2310 X%(TL)=33:GOTO3000
3000 GETA$:IFA$=""THEN3090
3010 A=ASC(A$):IFA>64ANDA<91THENA=A-65:GOTO3100
3020 IFA>47ANDA<58THENA=A-22:GOTO3100
3030 IFA=43THENA=36:GOTO3100
3040 IFA=92THENA=37:GOTO3100
3050 IFA=64THENA=38:GOTO3100
3060 IFA=42THENA=39:GOTO3100
3070 IFA=94THENA=40:GOTO3100
3080 IFA=61THENA=41:GOTO3100
3090 GOTO4000
3100 P%=L%(A):A=PEEK(P%+CL):IF(AAND15)=2THENA=5:GOTO3110
3105 A=2
3110 POKEP%+CL,A
3120 GOTO3000
4000 REM *OUTPUT*
4010 PRINT"{HOME}{23 DOWN}"SPC(18)MID$(TI$,4,1)":"RIGHT$(TI$,2)"{HOME}"
4020 IFPU<0THENPU=0
4030 PRINT"{HOME}{RVS}"SPC(18)RIGHT$("{3 SPACE}"+STR$(PU),4)
4040 IFPU=0THEN9000
4100 IFFNR(25)ORF%(0)=0THEN4200
4110 POKEF%(0)+SC%,91:FORI=0TO8:F%(I)=F(I+1):NEXT:AF=AF-1
4200 IFTI-TB>18E3THEN9000
5000 GOTO1000
9000 FORI=0TO3E3:NEXT:PRINT"{HOME}{DOWN}{RVS} AGAIN ? (Y/N)"
9010 GETA$:IFA$="Y"THENRUN20
9020 IFA$="N"THEN9040
9030 GOTO9010
9040 IF PU>0 THEN PRINT"{CLR}{DOWN}YOU HAVE";PU;"POINTS"
9050 IF PU=0 THEN PRINT"{CLR}{DOWN}YOU DID NOT MANAGE 5 MINUTES"
