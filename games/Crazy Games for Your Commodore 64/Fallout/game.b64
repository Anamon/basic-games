10 REM **FALLOUT**
20 DIM H(16,8)
30 PRINT "{CLR}";
40 REM ** FILL WITH BARS
50 FOR X=0 TO 16
60 FOR Y=1 TO 8
70 H(X,Y)=1
80 NEXT Y,X
90 REM ** 4 RANDOM HOLES
100 FOR Y=1 TO 8
110 FOR B=1 TO 4
120 X=INT(RND(0)*16+1)
130 H(X,Y)=0
140 NEXT B,Y
150 REM ** PUT 1-8 ON TOP
160 FOR B=1 TO 8
170 H(2*B-1,0)=B+100
180 NEXT B
190 GOSUB 610:REM ** DRAW
200 GOSUB 910:REM ** MODIFY
210 GOSUB 610:REM ** DRAW
300 REM *** GET COMMAND
310 INPUT Z$
320 Y=VAL(LEFT$(Z$,1))
330 IF Y<1 OR Y>8 THEN 510:REM ** WIPE
340 D$=MID$(Z$,2,1)
350 DX=VAL(MID$(Z$,3))
360 IF DX=0THEN510:REM ** WIPE
370 IF D$<>"L" AND D$<>"R" THEN 510:REM ** WIPE
380 IF D$="L" THEN GOSUB 1110:REM ** SHIFT LEFT
390 IF D$="R" THEN GOSUB 1210:REM ** SHIFT RIGHT
400 BR=BR+1
410 GOSUB 910:REM ** MODIFY
420 GOSUB 610:REM ** DRAW
430 IF SC>=36 THEN END:REM ** WHEN ALL CHECKERS ARE DOWN
440 IF DX>1 THEN DX=DX-1:GOTO 380:REM ** REPEAT DX TIMES
500 REM *** WIPE
510 PRINT "{HOME}{9 DOWN}"
520 FOR Y=10 TO 21
530 PRINT "{21 SPACE}"
540 NEXT Y
550 PRINT "{21 SPACE}";
560 PRINT "{HOME}{9 DOWN}"
570 GOTO 310:REM ** GET COMMAND
600 REM *** DRAW
610 PRINT "{HOME}{8 RIGHT}";
620 FOR Y1=0 TO 8
630 REM * PRINT BARNUMBER (OR DON'T)
640 IF Y1=0 THEN PRINT "{4 SPACE}";
650 IF Y1>0 THEN PRINT SPC(8) STR$(Y1);"> ";
660 FOR X=0 TO 16
670 Z=H(X,Y1)
680 GOSUB 810:REM ** PRINT ELEMENT
690 NEXT X
700 PRINT
710 NEXT Y1
720 PRINT SPC(8) "{4 SPACE}SCORE";SC;" TURN";BR
730 RETURN
800 REM *** PRINT ELEMENT
810 IF Z=0 THEN PRINT " ";
820 IF Z=1 THEN PRINT "{RVS}{C@}{OFF}";
830 IF Z>100 THEN PRINT  CHR$(Z-52);
840 RETURN
900 REM *** MODIFY
910 FOR Y1=8 TO 0 STEP -1:REM * SEARCH FROM BOTTOM TO TOP
920 FOR X=0 TO 16
930 TY=Y1
940 IF H(X,Y1)<100 THEN 1020
950 IF TY>=8 THEN 1060:REM ** REACHED BOTTOM
960 IF H(X,TY+1)<>0 THEN 1020:REM ** BLOCKED
970 REM ** SHIFT CHIP DOWN
980 H(X,TY+1)=H(X,TY)
990 H(X,TY)=0
1000 TY=TY+1
1010 GOTO 950:REM CONTINUE TO SHIFT
1020 NEXT X
1030 NEXT Y1
1040 RETURN
1050 REM ** REACHED BOTTOM
1060 SC=SC+H(X,TY)-100
1070 H(X,TY)=0
1080 GOTO 1020
1100 REM *** SHIFT BAR LEFT
1110 TM=H(0,Y)
1120 FOR X=0 TO 15
1130 H(X,Y)=H(X+1,Y)
1140 NEXT X
1150 H(16,Y)=TM
1160 RETURN
1200 REM *** SHIFT BAR RIGHT
1210 TM=H(16,Y)
1220 FOR X=16 TO 1 STEP -1
1230 H(X,Y)=H(X-1,Y)
1240 NEXT X
1250 H(0,Y)=TM
1260 RETURN
