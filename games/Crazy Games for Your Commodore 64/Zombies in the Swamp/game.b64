10 REM***ZOMBIES IN THE SWAMP***
20 DIM A(14,9)
30 FOR C=0 TO 14
40 A(C,0)=-1:A(C,9)=-1
50 NEXT
60 FOR C=0 TO 9
61 A(0,C)=-1:A(14,C)=-1
63 NEXT
65 POKE 53280,2
70 A(1,0)=1:A(0,1)=1
80 DEF FNR(X)=INT(X*RND(0)+1)
90 EM$="{OFF}   {DOWN}{3 LEFT}   {DOWN}{3 LEFT}   "
110 PL$="{YEL}{RVS}###{DOWN}{3 LEFT}###{DOWN}{3 LEFT}###"
130 MO$="{WHT}{RVS}***{DOWN}{3 LEFT}***{DOWN}{3 LEFT}***"
150 DR$="{GRN}{RVS}   {DOWN}{3 LEFT} {RED}{OFF}*{GRN}{RVS} {DOWN}{3 LEFT}   "
170 D$="{HOME}{25 DOWN}"
180 GOTO 2000
200 REM**PRINT T$ ON X,Y**
210 PRINT LEFT$(D$,3*(Y-1)+1);TAB(3*(X-1));T$;
220 RETURN
300 REM**INITIALIZE**
310 PRINT "{CLR}";
320 FOR LI=1 TO 24
330 PRINT "{GRN}{RVS}{39 SPACE}":REM 39 SPACES
340 NEXT
350 M=0:PX=13:PY=8:A(13,8)=1
360 X=PX:Y=PY
370 T$=PL$:GOSUB 200
380 RETURN
400 REM**PLAYER'S MOVE**
410 REM INPUT
420 GET IN$:IF IN$="" THEN 420
430 DX=INT(IN$="{LEFT}")-(IN$="{RIGHT}")
440 DY=INT(IN$="{UP}")-(IN$="{DOWN}")
470 IF DX=0 AND DY=0 THEN 420
480 ON A(PX+DX,PY+DY)+1 GOTO 500,550
490 GOTO 420
500 REM*THROW RAFT*
510 X=PX+DX:Y=PY+DY
520 T$=EM$:GOSUB 200
530 A(X,Y)=1
540 RETURN
550 REM*MOVE*
560 X=PX:Y=PY
570 T$=EM$:GOSUB 200
580 PX=PX+DX:PY=PY+DY
590 IF PX=0 OR PY=0 THEN 630
600 X=PX:Y=PY
610 T$=PL$:GOSUB 200
620 RETURN
630 REM*WON*
640 PRINT D$;TAB(10) "{WHT}{OFF}CONGRATULATIONS !";
650 FOR I=1 TO 10:GET S$:NEXT
660 GET S$:IF S$="" THEN 660
670 RUN
700 REM**MONSTER**
710 MX=13:MY=8
715 X=MX:Y=MY
720 T$=MO$:GOSUB 200
730 ON FNR(2) GOSUB 880,890
740 LX=DX:LY=DY
750 REM*NEW DIRECTION*
760 IF A(MX+LX,MY+LY)<1 THEN 800
770 IF A(MX+LY,MY-LX)=1 OR A(MX-LY,MY+LX)=1 THEN 840
780 REM*SAME DIRECTION*
790 DX=LX:DY=LY
795 GOTO 820
800 REM*INTO SWAMP*
810 ON FNR(4) GOSUB 880,890,900,910
820 IF A(MX+DX,MY+DY)=-1 THEN 810
830 GOTO 920
840 REM*POSSIBLE MOVE*
850 ON FNR(4) GOSUB 880,890,900,910
860 IF DX=-LX AND DY=-LY OR A(MX+DX,MY+DY)<1 THEN 780
870 GOTO 920
880 DX= 0:DY=-1:RETURN
890 DX=-1:DY =0:RETURN
900 DX= 0:DY= 1:RETURN
910 DX= 1:DY= 0:RETURN
920 REM*MOVE*
930 X=MX:Y=MY
940 T$=EM$:GOSUB 200
950 MX=MX+DX
960 MY=MY+DY
970 LX=DX:LY=DY
980 IF A(MX,MY)=0 THEN 1030
990 X=MX:Y=MY
1000 T$=MO$:GOSUB 200
1010 IF MX=PX AND MY=PY THEN 1080
1020 GOTO 750
1030 REM*MONSTER DROWNED*
1040 A(MX,MY)=-1
1050 X=MX:Y=MY
1060 T$=DR$:GOSUB 200
1070 RETURN
1080 REM*PLAYER CAUGHT*
1090 PRINT D$;TAB(8) "{WHT}{OFF}YOU'VE JUST BEEN EATEN!"
1100 FOR I=1 TO 10:GET S$:NEXT
1110 GET S$:IF S$="" THEN 1110
1120 RUN
2000 REM**MAIN PROGRAM**
2010 GOSUB 300:REM INITIALIZE
2020 GOSUB 400:REM PLAYER'S MOVE
2030 M=M+1
2040 IF FNR(PX+PY)>4 OR M<5 THEN 2020
2050 GOSUB 700:REM MONSTER
2060 GOTO 2020
