10 REM **ONE TO FIVE**
15 D$="{11 RIGHT}"
20 M=0
30 DIM A%(2,2)
40 GOTO 1000: REM MAINPROGRAM
100 REM **SCREEN**
110 PRINT "{CLR}"
120 PRINT D$ " {CA}{S*}{CR}{S*}{CR}{S*}{CS}"
130 PRINT D$ " {S-} {S-} {S-} {S-}{2 SPACE}Q W E"
140 PRINT D$ " {CQ}{S*}{S+}{S*}{S+}{S*}{CW}"
150 PRINT D$ " {S-} {S-} {S-} {S-}{2 SPACE}A S D"
160 PRINT D$ " {CQ}{S*}{S+}{S*}{S+}{S*}{CW}"
170 PRINT D$ " {S-} {S-} {S-} {S-}{2 SPACE}Z X C"
180 PRINT D$ " {CZ}{S*}{CE}{S*}{CE}{S*}{CX}"
190 RETURN
200 REM **DRAW FIELD**
210 FOR A=1 TO 11
220 I=9*RND(0)+1
230 GOSUB 300:REM MOVE
240 GOSUB 500:REM PRINT FIELD
250 NEXT
260 RETURN
300 REM **MOVE**
310 Y=INT((I-1)/3)
320 X=I-1-3*Y
330 FOR Z=0 TO 2
340 A%(X,Z)=A%(X,Z)+1
350 A%(Z,Y)=A%(Z,Y)+1
360 NEXT
370 A%(X,Y)=A%(X,Y)-1
380 WF=1
390 FOR P=0 TO 2
400 FOR Q=0 TO 2
410 IF A%(P,Q)=6 THEN A%(P,Q)=0
420 WF=WF AND (A%(P,Q)=0)
430 NEXT: NEXT
440 RETURN
500 REM **PRINT FIELD**
510 PRINT "{HOME}" D$
520 FOR Q=0 TO 2
530 PRINT D$ "{DOWN}{RIGHT}";
540 FOR P=0 TO 2
550 PRINT "{RIGHT}";RIGHT$(STR$(A%(P,Q)),1);
560 NEXT P
570 PRINT
580 NEXT Q
590 RETURN
600 REM **INPUT**
610 GET A$:IF A$="" THEN 610
620 FOR A=1 TO 9
630 IF A$=MID$("QWEASDZXC",A,1) THEN I=A:A=9
640 NEXT
650 M=M+1
660 RETURN
1000 REM **MAIN PROGRAM**
1010 GOSUB 100: REM SCREEN
1020 GOSUB 200: REM DRAW FIELD
1030 GOSUB 600: REM INPUT
1040 GOSUB 300: REM MOVE
1050 GOSUB 500: REM PRINT FIELD
1060 IF WF=0 THEN 1030
1070 PRINT"{DOWN}"
1080 PRINT SPC(10) " {4 LEFT}THAT'S GREAT! IN";M;"MOVES!"
