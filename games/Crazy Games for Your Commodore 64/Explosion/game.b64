10 REM***EXPLOSION***
15 POKE53280,0:POKE53281,0
20 DIMRB(5,5),SB(5,5),ST(5,5)
30 D$="{HOME}{21 DOWN}"
40 GOTO3000
200 REM**COPY RB TO SB**
210 FOR X=1 TO SI
220 FOR Y=1 TO SI
230 SB(X,Y)=RB(X,Y)
240 NEXT:NEXT
250 RETURN
300 REM**COPY SB TO RB**
310 FOR X=1 TO SI
320 FOR Y=1 TO SI
330 RB(X,Y)=SB(X,Y)
340 NEXT:NEXT
350 RETURN
400 REM**INITIALIZE**
410 PRINT "{CLR}ENTER SIZE OF BOARD (3 OR 4)";
420 INPUT SI
430 IF SI<3 OR SI>4 THEN 410
440 FOR X=1 TO SI
450 FOR Y=1 TO SI
460 ST(X,Y)=4+(X=1)+(X=SI)+(Y=1)+(Y=SI)
470 NEXT:NEXT
490 PRINT"DO YOU WANT TO BEGIN (Y/N)";
500 INPUT AN$
510 CM=(LEFT$(AN$,1)<>"Y")
600 REM*SETUP SCREEN*
610 PRINT "{CLR}"
620 PRINT " m X"
630 PRINT "{2 SPACE}m ";
640 FOR X=1 TO SI:PRINT X;" ";:NEXT:PRINT
650 FOR Y=1 TO SI
660 IF Y=1 THEN PRINT" Y ";:GOTO 680
670 PRINT"{3 SPACE}";
680 FOR X=1 TO SI:PRINT "{S+}{3 S*}";:NEXT:PRINT "{CW}"
690 FOR I=1 TO 3
700 PRINT" ";
710 IF I=2 THEN PRINT STR$(Y);:GOTO 730
720 PRINT"{2 SPACE}";
730 FOR X=1 TO SI:PRINT "{S-}{3 SPACE}";:NEXT:PRINT "{S-}"
740 NEXT I,Y
760 PRINT"{3 SPACE}";
770 FOR X=1 TO SI:PRINT "{CE}{3 S*}";:NEXT:PRINT "{CX}"
780 RETURN
800 REM**PLAYER MOVE**
810 PRINT D$;
820 INPUT "{C7}YOUR MOVE (X,Y){6 SPACE}{6 LEFT}";MX,MY
830 IF MX<1 OR MX>SI OR MY<1 OR MY>SI THEN 810
840 IF RB(MX,MY)<0 THEN 810
850 GOSUB 200
860 X=MX:Y=MY:DI=-1
870 GOSUB 1000:REM EXPLOSION
880 IF ET THEN 2200:REM END
890 GOSUB 300
900 RETURN
1000 REM**OUTPUT EXPLOSION**
1010 SB(X,Y)=SB(X,Y)+PL
1020 NE=0
1030 IF DI THEN X1=X:Y1=Y:GOSUB 1400
1040 XP=0
1050 FOR Y=1 TO SI
1060 FOR X=1 TO SI
1070 IF ABS(SB(X,Y))<ST(X,Y) THEN 1160
1080 XP=-1
1090 NE=NE+1
1100 SB(X,Y)=SB(X,Y)-ST(X,Y)*PL
1110 IF DI THEN X1=X:Y1=Y:FOR I=1 TO 999:NEXT I:GOSUB 1400
1120 X1=X:Y1=Y-1:GOSUB 1300
1130 X1=X+1:Y1=Y:GOSUB 1300
1140 X1=X:Y1=Y+1:GOSUB 1300
1150 X1=X-1:Y1=Y:GOSUB 1300
1160 NEXT X,Y
1170 ET=(NE>1.5*SI*SI)
1180 IF XP AND NOT ET THEN 1040
1190 RETURN
1300 REM**ADD TO NEIGHBOURS**
1310 SB(X1,Y1)=PL*(ABS(SB(X1,Y1))+1)
1320 IF DI AND ST(X1,Y1)>0 THEN GOSUB 1400
1330 RETURN
1400 REM**PRINT FIELD**
1410 FOR I=1 TO 500:NEXT I
1420 PRINT LEFT$(D$,4*Y1+1);TAB(4*X1);
1430 PRINT "{3 SPACE}{DOWN}{3 LEFT}{3 SPACE}{DOWN}{3 LEFT}{3 SPACE}{2 UP}{3 LEFT}";
1440 IF SB(X1,Y1)=0 THEN 1480
1450 IF PL=-1 THEN PRINT "{WHT}{RVS}";
1455 IF PL=1 THEN PRINT"{RVS}{GRN}";
1460 PRINT "{3 SPACE}{DOWN}{3 LEFT}";ABS(SB(X1,Y1));"{LEFT} {DOWN}{3 LEFT}{3 SPACE}"
1470 PRINT "{OFF}";
1480 RETURN
1600 REM**COMPUTER MOVES**
1610 PRINT D$;"{C7}MY MOVE:
1620 BE=1000
1630 FOR TX=1 TO SI
1640 FOR TY=1 TO SI
1650 IF RB(TX,TY)>0 THEN 1720
1660 GOSUB 200
1670 X=TX:Y=TY:DI=0
1680 GOSUB 1000:REM OUTPUT EXPLOSION
1690 IF ET THEN MX=TX:MY=TY:GOTO 1800
1700 GOSUB 2000:REM EVALUATION
1710 IF EN<BE OR (EN=BE AND RND(0)<.4) THEN BE=EN:MX=TX:MY=TY
1720 NEXT:NEXT
1800 REM*ACTUAL MOVE*
1810 GOSUB 200
1820 X=MX:Y=MY:DI=-1
1830 PRINT D$;TAB(8);X;",";Y
1840 GOSUB 1000:REM OUTPUT EXPLOSION
1850 IF ET THEN 2200:REM END
1860 GOSUB 300
1870 RETURN
2000 REM**EVALUATE**
2010 EN=0
2020 FOR X=1 TO SI
2030 FOR Y=1 TO SI
2040 EN=EN+SB(X,Y)
2050 IF -SB(X,Y)<ST(X,Y)-1 THEN 2110
2060 EN=EN-2
2070 IF SB(X+1,Y)=ST(X+1,Y)-1 THEN EN=EN+10
2080 IF SB(X,Y+1)=ST(X,Y+1)-1 THEN EN=EN+10
2090 IF SB(X-1,Y)=ST(X-1,Y)-1 THEN EN=EN+10
2100 IF SB(X,Y-1)=ST(X,Y-1)-1 THEN EN=EN+10
2110 NEXT:NEXT
2120 RETURN
2200 REM**END**
2210 PRINT D$;"{C7}{UP}AN ETERNAL EXPLOSION"
2220 IF PL=1 THEN PRINT "YOU";:GOTO 2240
2230 PRINT "I";
2240 PRINT " WON IN";NT;"{LEFT} TURNS"
2250 INPUT "DO YOU WANT TO PLAY AGAIN";AN$
2260 IF LEFT$(AN$,1)="Y" THEN POKE646,14:CLR:GOTO10
2270 END
3000 REM**MAIN PROGRAM**
3010 GOSUB 400:REM INITIALIZE
3020 IF CM THEN 3040
3030 PL=1:GOSUB 800:REM PLAYER MOVE
3040 PL=-1:GOSUB 1600:REM COMPUTER MOVES
3050 NT=NT+1
3060 GOTO 3030
