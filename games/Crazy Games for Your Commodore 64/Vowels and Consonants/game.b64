10 REM**VOWELS AND CONSONANTS**
20 PRINT "{CLR}";
30 INPUT "NUMBER OF PLAYERS";NP
40 IF NP<1 THEN 20
50 DIM PO(NP),C$(7),WO$(20)
60 GOTO 1000
100 REM**CHOOSE CHARACTERS**
110 LE$=""
120 PRINT "{DOWN}TURN OF PLAYER";PL
130 PRINT "V OR C?"
140 FOR DU=1 TO 7
150 GET IN$
160 IF IN$<>"V" AND IN$<>"C" THEN 150
170 IF IN$="V" THEN CH$="AEIOU"
180 IF IN$="C" THEN CH$="BCDFGHJKLMNPQRSTVWXYZ"
190 C$=MID$(CH$,RND(0)*LEN(CH$)+1,1)
200 LE$=LE$+C$
210 PRINT C$;
220 NEXT DU
230 RETURN
300 REM**INPUT WORD**
310 LE=0
320 PRINT "{DOWN}TIME IN SECONDS:"
330 FOR TM=45 TO 0 STEP -1
340 PRINT"{HOME}{6 DOWN}{8 SPACE}";:IF TM<10 THEN PRINT" ";
345 PRINT TM
350 FOR DE=1 TO 48
360 GET IN$
370 IF (IN$<>CHR$(20) OR LE=0) AND (IN$<"A" OR IN$>"Z") THEN 410
375 IF LE>6 AND IN$<>CHR$(20) THEN 410
380 PRINT "{HOME}{8 DOWN}";TAB(LE+1);IN$
390 IF IN$=CHR$(20) THEN LE=LE-1:GOTO 410
400 LE=LE+1:WO$(LE)=IN$
410 NEXT DE,TM
420 RETURN
500 REM**COMPUTE POINTS**
510 FOR C=1 TO 7
520 C$(C)=MID$(LE$,C,1)
530 NEXT
540 FOR CO=1 TO LE
550 FOR C=1 TO 7
560 IF WO$(CO)=C$(C) THEN C$(C)="":GOTO 580
570 NEXT C:GOTO 600
580 NEXT CO
590 PO(PL)=PO(PL)+LE
600 PRINT "{HOME}{10 DOWN}";
610 FOR P=1 TO NP
620 PRINT "PLAYER";P;":";PO(P);"POINTS"
630 NEXT P
640 RETURN
1000 REM**MAIN PROGRAM**
1010 FOR PL=1 TO NP
1020 GOSUB 100:REM CHOOSE CHARACTERS
1030 PRINT"{CLR}MAKE A WORD OUT OF THESE{DOWN}":PRINT TAB(6);LE$
1040 GOSUB 300:REM INPUT WORD
1050 GOSUB 500:REM COMPUTE POINTS
1060 NEXT PL
1070 GOTO 1010
1080 END
