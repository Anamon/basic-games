10 REM**MOONPHASE ASTROLOGY**
20 GOTO 1000
100 REM**INPUT DATE**
110 PRINT "{CLR}MOONPHASE{2 DOWN}"
120 INPUT "DAY   DD ";DD
130 INPUT "MONTH MM ";M
140 INPUT "YEAR YYYY";Y
150 IF M<3 THEN M=M+12:Y=Y-1
160 RETURN
200 REM**CALCULATION**
210 T=INT(365.25*Y)+INT(30.6*(M+1))+DD-694038
220 T=T/36525
230 LA=350.737486+1236*T*360
240 LA=LA+307*T+6*T/60
250 LA=LA+51.18*T/3600-5.17*T*T/3600
260 LA=LA-INT(LA/360)*360
270 LA=INT(LA+.5)
280 RETURN
300 REM**DISPLAY RESULT**
310 PRINT "{2 DOWN}MOONPHASE="LA"DEGREES{2 DOWN}"
320 RETURN
1000 REM**MAIN PROGRAM**
1010 GOSUB 100:REM INPUT DATA
1020 GOSUB 200:REM CALCULATION
1030 GOSUB 300:REM DISPLAY RESULT
1040 PRINT "PRESS ANY KEY"
1050 GET A$:IF A$="" THEN 1050
1060 GOTO 1010
