10 REM **MINI-MANCALA**
20 DIM B(3),SU(3)
30 DEF FNSP(X)=15+80*(X AND 2)+6*(X AND 1)
40 FOR I=0 TO 3
50 B(I)=2
60 READ SU(I)
70 NEXT
80 DATA 2,0,3,1
90 E$="{HOME}{17 DOWN}{8 RIGHT}"
110 GOTO 1000
200 REM**DRAW SCREEN**
210 PRINT"{CLR}{DOWN}" SPC(13) "MINI-MANCALA"
220 PRINT
230 PRINT SPC(11) " A"SPC(13)"B"
240 PRINT SPC(11) "{2 SPACE}{CA}{5 S*}{CR}{5 S*}{CS}"
250 PRINT SPC(11) "{2 SPACE}{S-}{5 SPACE}{S-}{5 SPACE}{S-}"
260 PRINT SPC(11) "{2 SPACE}{S-}{2 SPACE}2{2 SPACE}{S-}{2 SPACE}2{2 SPACE}{S-}"
270 PRINT SPC(11) "{2 SPACE}{S-}{5 SPACE}{S-}{5 SPACE}{S-}"
280 PRINT SPC(11) "{2 SPACE}{CQ}{5 S*}{S+}{5 S*}{CW}"
290 PRINT SPC(11) "{2 SPACE}{S-}{5 SPACE}{S-}{5 SPACE}{S-}"
300 PRINT SPC(11) "{2 SPACE}{S-}{2 SPACE}2{2 SPACE}{S-}{2 SPACE}2{2 SPACE}{S-}"
310 PRINT SPC(11) "{2 SPACE}{S-}{5 SPACE}{S-}{5 SPACE}{S-}"
320 PRINT SPC(11) "{2 SPACE}{CZ}{5 S*}{CE}{5 S*}{CX}"
330 PRINT SPC(11) " C"SPC(13)"D"
340 RETURN
400 REM**INPUT MOVE**
410 PRINT E$"MOVE STONES FROM CUP (C,D)";
430 INPUT S$
440 IF S$="" THEN 410
450 S=-2*(S$="C")-3*(S$="D")
460 IF S=0 OR B(S)=0 THEN 410
470 RETURN
500 REM**COMPUTERS TURN**
510 PRINT E$"{4 SPACE}NOW IT'S MY TURN{19 SPACE}"
520 FOR DE=1 TO 500:NEXT DE
530 IF LE<3 AND RND(0)*LE<.6 THEN S=INT(RND(0)*2):GOTO 560
540 Q=B(2)+10*(B(1)+10*B(0))
550 S=1
560 IF Q=143 OR Q=134 OR Q=611 OR Q=116 THEN S=0
570 IF B(S)=0 THEN S=1-S
580 PRINT E$"I MOVE STONES FROM CUP{2 SPACE}"MID$("AB",S+1,1)
600 RETURN
700 REM**SPREAD COUNTER CLOCKWISE**
710 D=S
720 FOR C=1 TO B(S)
730 FOR DE=1 TO 400:NEXT DE
740 D=SU(D)
750 IF D=S THEN 740
760 B(S)=B(S)-1
770 PRINT"{HOME}{6 DOWN}"SPC(FNSP(S));B(S)
780 B(D)=B(D)+1
790 PRINT"{HOME}{6 DOWN}"SPC(FNSP(D));B(D)
800 NEXT C
810 RETURN
1000 REM**MAIN PROGRAM**
1010 GOSUB 200:REM DRAW SCREEN
1020 PRINT E$"{5 SPACE}LEVEL (1-3)";
1030 INPUT LE
1040 IF LE<1 OR LE>3 THEN 1020
1050 PRINT  E$"{3 SPACE}DO YOU BEGIN (Y/N)";
1060 INPUT AN$
1070 IF AN$="N" THEN 1120
1080 IF AN$<>"Y" THEN 1050
1090 GOSUB 400:REM INPUT MOVE
1100 GOSUB 700:REM SPREAD COUNTER CLOCKWISE
1110 IF B(3)=8 THEN PRINT E$"{6 SPACE}YOU WIN!{25 SPACE}":GOTO 1160
1120 GOSUB 500:REM COMPUTERS TURN
1130 GOSUB 700:REM SPREAD COUNTER CLOCKWISE
1140 IF B(0)=8 THEN PRINT E$"{6 SPACE}I WIN!{27 SPACE}":GOTO 1160
1150 GOTO 1090
1160 END
