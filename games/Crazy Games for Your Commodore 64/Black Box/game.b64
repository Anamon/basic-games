10 REM**BLACKBOX**
20 DIM B(9,9,9)
30 GOTO 1000
100 REM**INPUT SHOT**
110 PRINT "  PLEASE INPUT SHOT"
120 GOSUB 500:REM INPUT
130 DX=(PX=8)-(PX=1)
140 DY=(PY=8)-(PY=1)
150 DZ=(PZ=8)-(PZ=1)
160 IF DX=0 AND DY=0 AND DZ=0 THEN 120
170 RETURN
200 REM**COMPUTE RESULT**
210 FOR TX=-1 TO 1
220 FOR TY=-1 TO 1
230 FOR TZ=-1 TO 1
240 IF B(PX+TX,PY+TY,PZ+TZ)<>1 THEN 280
250 DX=DX-TX
260 DY=DY-TY
270 DZ=DZ-TZ
280 NEXT TZ,TY,TX
290 DX=SGN(DX):DY=SGN(DY):DZ=SGN(DZ)
300 PX=PX+DX:PY=PY+DY:PZ=PZ+DZ
310 IF (PX=0)+(PX=9)+(PY=0)+(PY=9)+(PZ=0)+(PZ=9)=0 THEN 210
320 PRINT "  RESULT:";PX-DX;PY-DY;PZ-DZ
330 RETURN
400 REM**INPUT GUESS**
410 PRINT "  PLEASE INPUT GUESS"
420 GOSUB 500:REM INPUT
430 IF B(PX,PY,PZ)=1 THEN PRINT "  RIGHT !":GOTO 450
440 PRINT "  WRONG!"
450 RETURN
500 REM**INPUT COORDINATES**
510 PRINT "  ENTER COORDINATES:"
520 INPUT "  P1=";PX
530 IF PX<1 OR PX>8 THEN 520
540 INPUT "  P2=";PY
550 IF PY<1 OR PY>8 THEN 540
560 INPUT "  P3=";PZ
570 IF PZ<1 OR PZ>8 THEN 560
580 RETURN
1000 REM**MAIN PROGRAM**
1005 PRINT "{CLR}"
1010 FOR AT=1 TO 5
1020 PX=6*RND(0)+2
1021 PY=6*RND(0)+2
1022 PZ=6*RND(0)+2
1023 IF B(PX,PY,PZ)=1 THEN 1020
1025 B(PX,PY,PZ)=1
1030 NEXT
1040 GOSUB 100:REM INPUT SHOT
1050 GOSUB 200:REM COMPUTE RESULT
1060 PRINT "  SHOOT OR GUESS?(S/G)"
1070 GET IN$
1080 IF IN$="S" THEN 1040
1090 IF IN$<>"G" THEN 1070
1100 GOSUB 400:REM INPUT GUESS
1110 GOTO 1060
