10 REM**TREASURE HUNT**
15 POKE 53281,1
20 SC=1024
30 D$="{HOME}{17 DOWN}"
40 GOTO 1000
100 REM**SCREEN**
110 PRINT "{CLR}";
120 PRINT "{18 C+} {C+} "
130 PRINT "{C+}{3 SPACE}3{C+}2{2 C+}{5 SPACE}6{C+}{3 SPACE}{C+} "
140 PRINT "{C+}5{C+} {2 C+}{4 SPACE}{2 C+}4{2 C+}{2 SPACE}{3 C+} "
150 PRINT "{3 C+}{3 SPACE}{C+} {C+}{2 SPACE}{2 C+}5{C+} {2 C+}7{C+} "
160 PRINT "{C+}{3 SPACE}{C+}3{C+} {2 C+}{4 SPACE}{C+}{4 SPACE}{C+} "
170 PRINT "{C+} {C+} {2 C+}{3 SPACE}{C+} {C+} {2 C+} {4 C+} "
180 PRINT "{C+} {C+}{2 SPACE}{C+} {C+} {C+} {C+}5{C+}{2 SPACE}{C+} 9{C+} "
190 PRINT "{C+} {2 C+}{3 SPACE}{C+}3{C+} {3 C+} {2 C+} {2 C+} "
200 PRINT "{C+}{3 SPACE}{2 C+} {3 C+} {C+}4{C+}{2 SPACE}{C+} 8{C+} "
210 PRINT "{C+}1{C+} {C+}3 {C+}{5 SPACE}{2 C+} {C+} {2 C+} "
220 PRINT "{3 C+} {3 C+}1 {4 C+}3{C+} {C+}{2 SPACE}{C+} "
230 PRINT "{C+}{4 SPACE}1{2 C+} {C+}{4 SPACE}{C+} {2 C+} {C+} "
240 PRINT "{C+} {C+} {2 C+}3{C+} {C+} {C+} {2 C+}{4 SPACE}{C+} "
250 PRINT "{C+} {C+}{4 SPACE}{C+}{3 SPACE}{C+}{4 SPACE}{2 C+}7{C+} "
260 PRINT "{C+} {18 C+} "
300 Y=14:X=1
310 PL=40*Y+X
320 POKE SC+PL,0
330 PRINT D$;P2,NC;
340 RETURN
400 REM**PLAYER MOVES**
410 T1=400-INT(30*NC*RND(0))
420 FOR DU=1 TO T1
430 GET DI$:IF DI$="" THEN 510
440 VX=(DI$="K")-(DI$=":")
450 VY=(DI$="O")-(DI$=".")
460 NP=40*(Y+VY)+(X+VX)
470 PK=PEEK(SC+NP):IF PK<>32 AND (PK<49 OR PK>57) THEN 510
480 POKE SC+PL,32:X=X+VX:Y=Y+VY:PL=NP
490 PO=PK-48:IF PO<0 THEN 510
500 P1=P1+PO:P2=P2+PO:PRINT D$;P2;
510 POKE SC+PL,0:IF PL=40 THEN DU=T1
520 NEXT DU:IF PL>40*15 THEN 400
530 RETURN
600 REM**BIRD ATTACKS**
610 DT=15-3*NC
615 FOR I=1 TO 10:GET I$:NEXT
620 BU=40*Y+20
630 PK=PEEK(SC+BU)
640 POKE SC+BU,31
650 FOR DE=1 TO DT:NEXT
660 GET DI$
670 IF DI$=" " OR PK=0 THEN RETURN
680 POKE SC+BU,PK
690 BU=BU-1
700 GOTO 630
1000 REM**MAIN PROGRAM**
1010 GOSUB 100:REM SCREEN
1020 GOSUB 400:REM PLAYER MOVES
1030 IF PL<40 THEN NC=NC+1:P2=P2+NC*P1:P1=0:GOTO 1010
1040 GOSUB 600:REM BIRD ATTACKS
1050 IF PK<>0 THEN POKE SC+BU,PK:P1=P1+1:GOTO 1020
