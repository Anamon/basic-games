10 REM *** KEYBOARD MEMORY ***
20 D$="{20 DOWN}"
30 R=INT(RND(0)*9+1)
40 PRINT"{CLR}"
50 DIM T$(18),W$(18),Q$(2),NN(2)
60 A$="QWERTYASDFGHZXCVBN"
70 B$="153997231546682847"
80 GOTO 500
90 REM**COMPOSE RANDOM LAYOUT**
100 FOR N=1 TO 18
110 T$(N)=MID$(A$,N,1)
120 DI=N+R
130 IF DI<19 THEN 150
140 DI=DI-18
150 W$(N)=MID$(B$,DI,1)
160 NEXT N
170 RETURN
180 REM**SCREEN**
190 FOR K=1TO 3
200 FOR J=1 TO 6
210 PRINT"{HOME}"
220 PRINT RIGHT$(D$,3*K);TAB(5*J);
230 PRINT T$((K-1)*6+J)
240 NEXTJ,K
250 RETURN
260 REM**INPUT**
270 PRINT
275 Q$(2)=""
280 FOR KK=1 TO 2
290 GET KY$
300 IF KY$="" THEN 290
305 IF KY$=Q$(1) THEN 290
310 Q$(KK)=KY$
320 TU=TU+1
330 REM**TURN CARD**
335 NN(KK)=0
340 FOR N=1 TO 18
350 K=INT((N-.5)/6)+1
360 J=N-(K-1)*6
370 IF T$(N)<>Q$(KK) THEN 410
380 PRINT"{HOME}";RIGHT$(D$,3*K+1);TAB(5*J);
390 PRINT W$(N);
400 NN(KK)=N
410 NEXT N
415 IF NN(KK)=0 THEN 290
420 NEXT KK
430 RETURN
440 REM**CHECK CARDS**
450 IF W$(NN(1))<>W$(NN(2)) THEN RETURN
460 TE=TE+1
470 T$(NN(1))=" "
480 T$(NN(2))=" "
490 RETURN
500 REM**MAIN PROGRAM**
510 GOSUB 100:REM RANDOM LAYOUT
520 GOSUB 190:REM SCREEN
530 GOSUB 270:REM INPUT
540 GOSUB 450:REM CHECK CARDS
550 IF TE<9 THEN 520
560 PRINT "{CLR}{DOWN}NUMBER OF TURNS=";TU
570 END
