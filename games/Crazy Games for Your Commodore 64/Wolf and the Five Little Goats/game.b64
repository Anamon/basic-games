10 REM**THE WOLF AND THE 5 LITTLE GOATS**
20 DIM B(10,10),R(5),C(5),NM(5)
30 C$(0)="{2 C+}"
40 C$(1)="{2 SPACE}"
50 D$="{HOME}{14 DOWN}"
60 GOTO2000
1000 REM
1010 ON CA+2 GOTO1020,1030,1040
1020 R=RW:C=CW:TX$="WW":GOTO 1070
1030 TX$=C$(R+C AND 1):GOTO 1070
1040 R=R(GT):C=C(GT)
1050 TX$="G"+RIGHT$(STR$(NM(GT)),1)
1060 CA=GT
1070 PRINT LEFT$(D$,3+R);TAB(2*C-1);SPC(7) TX$
1080 B(R,C)=CA
1090 RETURN
1100 REM**INITIALIZE**
1110 REM DRAW CHECKERBOARD
1120 PRINT "{CLR}";
1130 FOR R=1 TO 10
1140 FOR C=1 TO 10
1150 CA=0:GOSUB 1000
1160 NEXT C,R
1170 REM PLACE WOLF
1180 RW=1:CW=1:NM=INT(RND(1)*3+1)
1190 CA=-1:GOSUB 1000
1200 REM PLACE GOATS
1210 FOR GT=1 TO 5
1220 R(GT)=INT(RND(0)*10+1)
1230 C(GT)=INT(RND(0)*10+1)
1240 NM(GT)=INT(RND(0)*5+1)
1250 IF B(R(GT),C(GT))<>0 THEN 1220
1260 CA=1:GOSUB 1000
1270 NEXT
1280 RETURN
1300 REM**MOVE WOLF**
1310 PRINT D$;"{7 SPACE}YOU MAY ENTER";NM;"{LEFT} MOVES"
1320 FOR DU=1 TO NM
1330 GET MV$:IF MV$="" THEN 1330
1350 RD=(MV$="{UP}" AND RW>1)-(MV$="{DOWN}" AND RW<10)
1360 CD=(MV$="{LEFT}" AND CW>1)-(MV$="{RIGHT}" AND CW<10)
1370 TE=B(RW+RD,CW+CD)
1380 IF TE<0 OR TE>10 AND DU<NM GOTO 1330
1390 NM(TE)=0
1400 SC=SC+SGN(TE)
1410 R=RW:C=CW
1420 CA=0:GOSUB 1000
1430 RW=RW+RD:CW=CW+CD
1440 CA=-1:GOSUB 1000
1450 NEXT DU
1460 NM=INT(RND(0)*3+1)
1470 RETURN
1500 REM**COMPUTER MOVE**
1510 PRINT D$;"{11 SPACE}NOW IT'S MY TURN{5 SPACE}"
1520 REM EVALUATION
1530 MX=-400
1540 FOR GT=1 TO 5
1550 IF NM(GT)=0 THEN 1600
1560 CG=C(GT):RG=R(GT)+NM(GT):GOSUB 1800
1570 RG=R(GT)-NM(GT):GOSUB 1800
1580 RG=R(GT):CG=C(GT)+NM(GT):GOSUB 1800
1590 CG=C(GT)-NM(GT):GOSUB 1800
1600 NEXT
1610 REM
1620 C=C(GB):R=R(GB)
1630 CA=0:GOSUB 1000
1640 R(GB)=RB:C(GB)=CB
1650 NM(GB)=INT(RND(0)*5+1)
1660 GT=GB
1670 CA=1:GOSUB 1000
1680 IF MX<400 THEN RETURN
1690 PRINT D$"{15 SPACE}YOU LOSE{11 SPACE}"
1700 END
1800 REM**STATUS**
1810 IF ABS(R(GT)-RW)+ABS(C(GT)-CW)=NM THEN SQ=200:GOTO 1820
1815 SQ=0
1820 IF RG<1 OR RG>10 OR CG<1 OR CG>10 THEN RETURN
1830 IF B(RG,CG)>0 THEN RETURN
1840 DI=ABS(RW-RG)+ABS(CW-CG)
1850 IF DI=0 THEN SQ=500:GOTO 1930
1860 IF DI=MN OR DI=NM-2 THEN RETURN
1870 IF DI<=5 THEN SQ=SQ+35
1880 SQ=SQ-40*((RG=RW)+(CG=CW))
1890 FOR G=1 TO 5
1900 SQ=SQ-10*((RG<>R(G))+(CG<>C(G)))
1910 NEXT
1920 IF SQ+RND(0)<MX THEN 1940
1930 MX=SQ:GB=GT:RB=RG:CB=CG
1940 RETURN
2000 REM**MAIN PROGRAM**
2010 GOSUB 1100:REM INITIALIZE
2020 GOSUB 1300:REM MOVE WOLF
2030 IF SC=3 THEN 2060
2040 GOSUB 1500:REM COMPUTER MOVE
2050 GOTO 2020
2060 PRINT D$;"{15 SPACE}YOU WIN!{11 SPACE}"
2070 END
