2 POKE 53281,7
5 REM **LAS VEGAS A GOGO**
10 SC=1024:CL=55296:CR=54272
20 DEF FNR(X)=INT(RND(1)*X+1):X=RND(-TI)
100 DIMWF$(13),R(2,13),JA(13):JA(5)=-1:JA(6)=-1:JA(7)=-1
110 FOR V=1 TO 13:READ WF$(V),R(1,V),R(2,V):NEXT
120 DATAA,,,K,,,Q,,,J,,,*,8,40,"q",8,40,$,7,30
130 DATA"a",5,25,"s",3,20,"z",3,20,"x",2,10,#,2,10,%,,
200 DIM JP(4),JF(4),JC(4)
210 FOR JO=1 TO 4:READ JP(JO),JF(JO):NEXT
220 DATA 293,1,294,11,296,17,297,10
300 DIM WP(3),V(4),HV(3):WP(1)=211:WP(2)=213:WP(3)=215
310 DIM HP(3),HF$(3):HP(1)=453:HP(2)=455:HP(3)=457
320 HO$="{RVS}H{RIGHT}H{RIGHT}H"
400 DIM GP(4),GV(4):GP(1)=32:GP(2)=76:GP(3)=112:GP(4)=69
410 BR$="             "
500 NJ=4:NH=0:HG=0:GW=0:NI=0:NT=-1
510 PL$="{HOME}{24 DOWN}{39 SPACE}{UP}{RIGHT}{WHT}{RVS}"
515 REM 39 SPACES
520 H$=LEFT$(PL$,12)+"{13 RIGHT}"
530 FOR L=CR TO CR+24: POKE L,0: NEXT
540 POKE CR+6,134: POKE CR+5,16
550 HF=88:LF=115:DR=40
560 POKE CR+24,15
570 GOTO 3000
599 REM INCREMENT TO MONEY
600 FOR AD=SGN(IN) TO IN STEP SGN(IN)
610 PRINT"{HOME}TOTAL:"MO+AD"{LEFT}  "
620 IF IN>0 THEN GOSUB 2700
630 IF IN<0 THEN FOR I=0 TO 2:HF=38:LF=126:GOSUB 2700
640 NEXT AD
650 HF=88:LF=115
660 MO=MO+IN:RETURN
699 REM BLINK S1$/S2$ AND GET IN$
700 PRINT "{HOME}{DOWN}{BLK}{OFF}";S1$:FOR DE=1 TO 250:NEXT
710 GET IN$:IF IN$<>"" THEN PRINT "{HOME}{DOWN}{BLK}";S2$:RETURN
720 PRINT "{HOME}{DOWN}{BLK}";S2$:FOR DE=1 TO 250:NEXT
730 GET IN$:IF IN$<>"" THEN RETURN
740 GOTO 700
799 REM ** REMOVE DOUBLE
800 DO=0:PRINT"{HOME}{DOWN}       "
810 FOR JO=1 TO 4
820 POKESC+JP(JO),JF(JO):POKECL+JP(JO),0:JC(JO)=0:NEXT
830 NJ=4:RETURN
899 REM ** JACKPOT **
900 HF=64:LF=188
910 FOR I=1 TO 4:PRINT"{HOME}{3 DOWN}               {HOME}{3 DOWN}{GRN}";
920 FOR C=1 TO JC
930 PRINT "J";:GOSUB 2700
940 NEXT C,I
950 HF=88: LF=115
960 RETURN
1000 GOSUB 2700:PRINT"{CLR}{BLK}TOTAL: 0"
1010 PRINT"{5 DOWN}"TAB(12)"{PUR}{RVS}{CV}     {CC}{BLK}{CJ}"
1020 FOR I=1 TO 4:PRINTTAB(12)"{PUR}{RVS}      {CJ}":NEXT
1030 PRINTTAB(12)"{PUR}{RVS}        "
1040 PRINTTAB(12)"{PUR}{CC}{RVS}     {OFF}{CV}"
1050 FOR I=1 TO 6:PRINTTAB(13)"{PUR}{RVS}    ":NEXT
1060 PRINTTAB(12)"{PUR}{RVS}{CV}     {CC}"
1070 PRINTTAB(11)"{PUR}{RVS}{CV}       {CC}"
1100 PRINT"{HOME}{6 DOWN}{RED} %  =  1"
1105 PRINT"{HOME}{6 DOWN}"
1110 FOR V=12 TO 5 STEP-1
1120 PRINT"{RED}-"WF$(V)WF$(V)"-= "R(1,V)
1125 NEXT
1130 PRINT"{HOME}{5 DOWN}"
1140 FOR V=12 TO 5 STEP-1
1150 PRINT TAB(25)"{RED}"WF$(V)WF$(V)WF$(V)" ="R(2,V);
1160 IF JA(V) THEN PRINT"{LEFT}+J";
1170 PRINT:NEXT:RETURN
1199 REM ** ADAPT VARIABLES
1200 NT=NT+1:IF WI>0 THEN HB=-1:WI=0
1210 FOR HO=1 TO 3:HF$(HO)=" ":POKESC+HP(HO),160:NEXT
1220 IF DO THEN 1270
1230 FOR JO=1 TO 4:IF JC(JO)=0 THEN 1260
1240 JC(JO)=JC(JO)-1:IF JC(JO)>0 THEN 1260
1250 NJ=NJ+1:POKESC+JP(JO),JF(JO):POKE CL+JP(JO),0
1260 NEXT JO:GOTO 1280
1270 DC=DC-1:IF DC<=0 THEN GOSUB 800
1280 PRINT"{HOME}{4 DOWN}" SPC(11) "            "
1290 IF JA THEN JC=JC-1:PRINT"{HOME}{3 DOWN}" SPC(JC) " ":IF JC<=0 THEN JA=0
1300 IF MO>MM THEN MM=MO
1310 RETURN
1399 REM ** NOT ENOUGH MONEY
1400 HB=-1:GOSUB 800
1410 JA=0:PRINT"{HOME}{3 DOWN}               "
1420 PRINT PL$ TAB(10) "INSERT OR END";
1430 S1$="INSERT (I)":S2$=BR$:GOSUB 700
1440 IF IN$="P" THEN 1430
1450 RETURN
1499 REM ** HOLD POSSIBLE
1500 PRINT PL$ TAB(5)"INSERT,HOLD,PLAY,END";
1510 S1$=H$+HO$:S2$=H$+HF$(1)+"{RIGHT}{OFF}"+HF$(2)+"{RIGHT}{OFF}"+HF$(3)
1520 GOSUB 700:HO=VAL(IN$):IF HO=0 OR HO>3 THEN RETURN
1530 NH=NH+1:IF HF$(HO)=" " THEN HF$(HO)="{RVS}H":GOTO1510
1540 HF$(HO)=" "
1550 GOTO 1510
1599 REM NO HOLD
1600 PRINT PL$ TAB(8) "INSERT, PLAY OR END";
1610 S1$="{DOWN}???":S$="{DOWN}   ":GOSUB 700
1620 RETURN
1699 REM ** GAMBLE OR COLLECT
1700 PRINT PL$;:IF HB THEN 1720
1710 PRINT TAB(10) "HOLD, ";
1720 PRINT TAB(12) "GAMBLE,COLLECT";
1730 GOTO1610
1799 REM ** SPIN GAMBLE WHEELS
1800 FOR WD=1 TO 3:IF HF$(WD)<>" " THEN 1830
1810 FI=FNR(100):IF FI<5 THEN V(WD)=FI-8*(JC(FI)>0):GOTO 1830
1820 V(WD)=5-(FI>7)-(FI>10)-(FI>13)-(FI>23)-(FI>36)-(FI>49)-(FI>68)-(FI>87)
1830 FOR DE=2 TO 500:NEXT:HF=40:LF=200:GOSUB 2700
1840 POKE SC+WP(WD),(ASC(WF$(V(WD)))AND63)
1850 PRINTH$ "{2 UP}{RVS}{BLK}" SPC(WP(WD)-WP(1)) WF$(V(WD))
1860 HF=88:LF=115
1870 NEXT WD:RETURN
1899 REM ** TAKE CARE OF JOKERS
1900 J=0:FOR WD=1 TO 3:V=V(WD)
1910 IF V>4 THEN FV=V:GOTO 1950
1920 JW=WD:J=J+1
1930 IF JC(V)>0 THEN 1950
1940 PRINT"{HOME}{2 DOWN}" SPC(JP(V)-80) "{PUR} ":JC(V)=20:NJ=NJ-1
1950 NEXT WD:DO=(NJ=0):IF DO THEN DC=15:PRINT"{HOME}{DOWN}{GRN}DOUBLE!"
1960 RETURN
1999 REM ** COMPUTE WINNINGS
2000 HV=13:ON J GOTO 2030,2010,2070
2010 IF NOT(JA AND FV>10) THEN HV=FV
2020 GOTO 2070
2030 V(0)=V(3):V(4)=V(1):IF JA AND V(JW-1)<>V(JW+1) THEN 2070
2040 V(0)=15:V(4)=15
2050 IF V(JW+1)>V(JW-1) THEN HV=V(JW-1):GOTO 2070
2060 HV=V(JW+1)
2070 FOR WD=1 TO 3:HV(WD)=V(WD):IF V(WD)<5 THEN HV(WD)=HV
2080 NEXT WD:IF HV(1)<>HV(2) OR HV(2)<>HV(3) THEN 2100
2090 IF JA(HV(1)) THEN JA=-1:JC=15:GOSUB 900
2099 REM ** COMPUTE WINNINGS
2100 FOR WD=1 TO 3:IF HV(WD)=13 THEN WI=WI+1
2110 NEXT WD:IF JA THEN WI=10*WI
2120 NS=-(HV(1)=HV(2))-(HV(2)=HV(3)):WI=WI+R(NS,HV(2))
2130 IF DO THEN WI=2*WI
2140 RETURN
2199 REM ** GAMBLE
2200 DT=1:GV(1)=2*WI:GV(2)=0:GV(3)=INT(3*WI/2):GV(4)=INT(WI/2)
2210 NG=NG+1:PRINT PL$ TAB(8) "     STOP     ";
2220 FOR LI=1 TO 3:PRINT"{HOME}" SPC(40*LI-12) "{BLU}{RVS}            ":NEXT:REM 12 SPACES
2230 R=R+1:IF R>4 THEN R=1
2240 PRINT"{HOME}{RVS}" SPC(GP(R)) GV(R):HF=16*R:LF=47*R:GOSUB 2700:PRINT"{HOME}{2 DOWN}{OFF}  "
2250 IF IN$<>"S" THEN GET IN$:GOTO 2270
2260 DT=(1+RND(1))*DT:FOR DE=1 TO DT:NEXT:IF DT>500 THEN 2280
2270 PRINT"{HOME}{RVS}" SPC(GP(R)+1) "   ":GOTO 2230
2280 HF=88:LF=115:FOR LI=1 TO 3:PRINT"{HOME}" SPC(40*LI-12) "            ":NEXT
2290 GW=GW+GV(R)-WI:WI=GV(R):RETURN
2399 REM ** END OF GAME
2400 PRINT"{CLR}":HF=19:LF=63:GOSUB 2700
2410 PRINT"PUT IN:";:X=NI:GOSUB 2500
2420 PRINT"GOT BACK:";:X=MO/4:GOSUB 2500
2430 PRINT"MAX AT ONE TIME:";:X=MM/4:GOSUB 2500
2440 PRINT"BY GAMBLING:";:X=GW/4:GOSUB 2500
2450 PRINT"NR. OF HOLDS:   ";NH
2460 PRINT"NR. OF GAMBLES: ";NG
2470 PRINT"NR. OF TURNS:   ";NT
2480 RETURN
2500 IF X=0 THEN X$=BR$+".000":GOTO 2520
2510 X$=BR$+STR$(INT(X*100)/100+.001)
2520 PRINT TAB(18) "$" MID$(X$,LEN(X$)-7,7):PRINT
2530 RETURN
2600 PRINT"{HOME}{6 DOWN}{BLK}{RVS}" SPC(20);:GOSUB 2660
2610 FOR I=0 TO 3:PRINT"{OFF}{LEFT} {RVS}{DOWN}{LEFT}{CJ}";:GOSUB 2660:NEXT
2620 FOR WD=1 TO 3:IF HF$(WD)=" " THEN POKE SC+WP(WD),32
2630 NEXT WD
2640 FOR I=1 TO 3:PRINT"{LEFT}{PUR}{CJ}{UP}{LEFT}{BLK}{CJ}";:GOSUB 2660:NEXT
2650 RETURN
2660 FOR DE=0 TO 80:NEXT
2670 RETURN
2700 REM *SOUND*
2720 POKE CR+1,HF:POKE CR,LF
2735 POKE CR+4,17
2740 FOR T=1 TO DR:NEXT
2745 POKE CR+4,16
2750 RETURN
2999 REM *** MAIN PROGRAM
3000 GOSUB 1000:REM SCREEN
3010 GOSUB 1200:REM ADAPT
3020 ON -2*HB-(MO>1)+1 GOSUB 1400,1500,1400,1600
3030 CA=-(IN$="I")-2*(IN$="P")-3*(IN$="E")
3040 ON CA GOTO 3100,3200,4000
3050 GOTO 3020
3100 NI=NI+1:IN=4:GOSUB 600
3110 IF MO>MM THEN MM=MO
3120 GOTO 3020
3200 IN=-2:GOSUB 600
3210 GOSUB2600:GOSUB 1800:REM SPIN WHEELS
3220 IF DO THEN 3240
3230 GOSUB 1900:REM JOKERS
3240 GOSUB 2000:REM WINNINGS
3250 IF HF$(1)<>" " OR HF$(2)<>" " OR HF$(3)<>" " THEN 3280
3260 HB=0:LW=WI:IF WI>0 THEN 3300
3270 GOTO3010
3280 HB=-1:IF WI>LW THEN 3300
3290 PRINT"{HOME}{4 DOWN}{BLK}"SPC(11)" YOU LOST   ":HF=21:LF=154:GOSUB 2700:FORI=0TO2000
3295 NEXT:HF=88:LF=115:GOTO 3010
3300 PRINT"{HOME}{4 DOWN}{BLK}" SPC(11) "YOU WIN"WI"{LEFT}  "
3310 FOR DU=1 TO WI:HF=108:LF=223:GOSUB 2700:FOR I=0 TO 50:NEXT:HF=88:LF=115
3320 NEXT DU
3330 IF MO<2 THEN HB=-1
3340 IF WI>499 THEN 3400
3350 GOSUB 1700:REM GET INSTRUCTION
3360 CA=-(IN$="H" AND NOT HB)-2*(IN$="G")-3*(IN$="C")
3370 ON CA GOTO 3600,3500,3400
3380 GOTO 3340
3400 IN=WI:GOSUB 600:GOTO3010
3500 HB=-1:GOSUB 2200:REM GAMBLE
3510 IF WI>0 THEN 3300
3520 GOTO 3290
3600 WI=0:GOTO 3010
4000 GOSUB2400:REM END
4005 POKE 54296,0
4010 END
