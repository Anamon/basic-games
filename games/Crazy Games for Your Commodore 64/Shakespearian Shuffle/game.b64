10 REM **SHAKESPERIAN SHUFFLE**
15 POKE646,13
20 D$="{HOME}{10 DOWN}"
30 DIM HX(8),HY(8),B$(8,8),T$(8)
40 FOR K=1 TO 8
50 READ T$(K)
60 NEXT
70 DATA "TO BE OR","NOT TO B","E, THAT "
80 DATA "IS THE Q","UESTION."
90 DATA "WILLIAM ","SHAKESPE","ARE 1603"
100 FOR X=1 TO 8
110 FOR Y=1 TO 8
120 B$(X,Y)=MID$(T$(Y),X,1)
130 NEXT:NEXT
140 FOR K=1 TO 8
150 READ HX(K),HY(K)
160 NEXT K
170 DATA -1,-2,1,-2,2,-1,2,1,1,2,-1,2,-2,1,-2,-1
180 C1$="T":C2$="{RVS}T{OFF}"
190 XO=1:YO=1:XN=1:YN=1
200 GOTO 1000
300 REM**PRINT CHARACTER**
310 PRINT LEFT$(D$,Y+1);TAB(2*X);SPC(9) B$(X,Y);
320 RETURN
400 REM**RANDOM SHUFFLE**
410 INPUT"{CLR}{DOWN}LEVEL";LV
420 FOR DU=1 TO LV
430 NM=INT(RND(0)*8+1):GOSUB 500
440 IF XO=XN AND YO=YN THEN 430
450 XO=XN:YO=YN
460 NEXT
470 RETURN
500 REM**KNIGHT'S MOVE**
510 XS=HX(NM):YS=HY(NM)
520 IF XO+XS<1 OR XO+XS>8 OR YO+YS<1 OR YO+YS>8 THEN RETURN
530 XN=XO+XS:YN=YO+YS
540 W$=B$(XN,YN)
550 B$(XN,YN)=B$(XO,YO)
560 B$(XO,YO)=W$
570 IF NOT DI THEN RETURN
580 X=XO:Y=YO:GOSUB 300
590 X=XN:Y=YN:GOSUB 300
600 XO=XN:YO=YN
610 RETURN
700 REM**SCREEN**
710 PRINT"{CLR}"
720 FORX=1TO8
730 FORY=1TO8
740 GOSUB300
750 NEXT
760 NEXT
770 PRINT D$;TAB(16);"1 2{DOWN}"
780 PRINT TAB(14);"8{5 SPACE}3{DOWN}"
790 PRINT TAB(17);"H{DOWN}"
800 PRINT TAB(14);"7{5 SPACE}4{DOWN}"
810 PRINT TAB(16);"6 5"
820 RETURN
900 REM**INPUT MOVE**
910 FOR DE=1 TO 200:NEXT DE
920 PRINT LEFT$(D$,YO+1);TAB(2*XO);SPC(9) C1$
930 FOR DE=1 TO 200:NEXT DE
940 PRINT LEFT$(D$,YO+1);TAB(2*XO);SPC(9) C2$
950 GET I$
960 IF I$>"" THEN RETURN
970 GOTO 910
1000 REM **MAIN PROGRAM**
1010 GOSUB 400:REM RANDOM SHUFFLE
1020 DI=-1
1030 GOSUB 700:REM SCREEN
1040 GOSUB 900:REM INPUT MOVE
1050 NM=VAL(I$):IF NM=0 OR NM=9 THEN 1040
1060 GOSUB 500:REM KNIGHT'S MOVE
1070 GOTO 1040
