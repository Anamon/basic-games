100 CALL CLEAR :: DR$="DSK1." :: PR$="PIO" :: CALL CHAR(124,"0010F88484F81000",126,"00FF",139,"00") :: ON BREAK NEXT
110 CALL SCREEN(5) :: FOR C=1 TO 13 :: CALL COLOR(C,16,6) :: NEXT C :: CALL VCHAR(1,31,139,91) :: CALL COLOR(0,1,1)
120 FOR I=21 TO 24 :: CALL HCHAR(I,2,32,30) :: NEXT I :: CALL HCHAR(20,2,30,30)
130 ON WARNING NEXT :: CALL KEY(5,K,S) :: OPTION BASE 1 :: DIM A$(66) :: ON ERROR 160
140 DISPLAY AT(24,1):"Press ENTER to begin typing." :: I,M,R=1 :: GOSUB 450 :: DISPLAY AT(24,1):"Use Fctn X to exit text mode"
150 CALL KEY(5,K,S) :: DISPLAY AT(23,26):I :: ACCEPT AT(R,1)SIZE(-28):A$(I) :: M=I :: CALL SOUND(25,-1,1) :: DISPLAY AT(23,1):" "
160 CALL KEY(0,G,H) :: IF G=11 THEN 170 ELSE IF G=10 THEN 270 ELSE 210
170 IF I<=1 THEN 150 ELSE I=I-1 :: R=R-1 :: G=0
180 IF I=38 THEN RS=20 :: RE=38 :: RO=1 :: GOSUB 800 :: GOTO 150
190 IF I=19 THEN RS=1 :: RE=19 :: RO=1 :: GOSUB 800 :: GOTO 150
200 GOTO 150
210 IF I=19 THEN RS=20 :: RE=38 :: R=1 :: GOTO 240
220 IF I=38 THEN RS=39 :: RE=57 :: R=1 :: GOTO 240
230 GOTO 270
240 RO=1 :: FOR I=RS TO RE :: DISPLAY AT(RO,1):A$(I) :: RO=RO+1 :: NEXT I
250 IF K=69 THEN I=57 ELSE I=RS
260 GOTO 150
270 IF G=10 THEN GOSUB 530 :: GOSUB 450 :: GOTO 150
280 IF I=57 THEN 300
290 R=R+1 :: I=I+1 :: DISPLAY AT(24,1):"Use Fctn X to exit text mode" :: GOTO 150
300 DISPLAY AT(24,1):"     Escape Print Save." :: CALL KEY(3,S,K) :: IF K=0 THEN 300 ELSE IF S=69 THEN 440 ELSE IF S=80 THEN 310 ELSE IF S=83 THEN 340 ELSE 300
310 CALL KEY(3,K,S) :: DISPLAY AT(24,1):"Printer:";PR$ :: ACCEPT AT(24,9)SIZE(-20):PR$
320 ON ERROR 580 :: OPEN #1:PR$,OUTPUT :: DISPLAY AT(24,1):" " :: FOR I=1 TO 60 :: PRINT #1:CHR$(14);TAB(8);A$(I)
330 DISPLAY AT(24,1):"Printing line:";I :: NEXT I :: CLOSE #1 :: I=M :: GOTO 150
340 CALL KEY(3,K,S) :: DISPLAY AT(24,1):"Save:           (Enter=Exit)" :: ACCEPT AT(24,6)SIZE(-10):P$
350 ON ERROR 560 :: IF P$="" THEN 140 ELSE P$=DR$&P$
360 OPEN #2:P$,OUTPUT,DISPLAY,VARIABLE :: FOR I=1 TO 60
370 PRINT #2:A$(I) :: DISPLAY AT(23,26):I :: NEXT I :: CLOSE #2 :: I=M :: GOTO 150
380 FOR I=1 TO 19 :: DISPLAY AT(I,1):" " :: NEXT I :: P$="" :: GOTO 140
390 CALL KEY(3,K,S) :: DISPLAY AT(24,1):"Load:           (Enter=Exit)" :: ACCEPT AT(24,6)SIZE(-10):P$
400 IF P$="CAT" THEN 590
410 IF P$="" THEN 140 ELSE P$=DR$&P$
420 ON ERROR 570 :: OPEN #2:P$,INPUT,DISPLAY,VARIABLE
430 FOR I=1 TO 60 :: LINPUT #2:A$(I) :: DISPLAY AT(23,26):I :: NEXT I :: CLOSE #2
440 FOR I=1 TO 19 :: DISPLAY AT(I,1):A$(I) :: NEXT I :: GOTO 140
450 DISPLAY AT(21,1):" Clear Delete End File Home Insert Load Print Quit Save" :: CALL KEY(3,K,S)
460 IF S=0 THEN 450 ELSE IF K=1 THEN 740 ELSE IF K=67 THEN 380 ELSE IF K=68 THEN 660 ELSE IF K=69 THEN 540 ELSE IF K=70 THEN 730
470 IF K=72 THEN 550 ELSE IF K=6 THEN 790 ELSE IF K=73 THEN 670 ELSE IF K=76 THEN 390
480 IF K=80 THEN 310 ELSE IF K=81 THEN 720 ELSE IF K=61 THEN 810 ELSE IF K=83 THEN 340
490 IF K=49 THEN RS=1 :: RE=19 :: R=1 :: GOTO 240
500 IF K=50 THEN RS=20 :: RE=38 :: R=1 :: GOTO 240
510 IF K=51 THEN RS=39 :: RE=57 :: R=1 :: GOTO 240
520 RETURN
530 DISPLAY AT(24,1):" Press C,D,F,H,I,L,P,Q or S" :: RETURN
540 R=19 :: RS=39 :: RE=57 :: GOTO 240
550 I,R=1 :: RS=1 :: RE=19 :: GOTO 240
560 DISPLAY AT(23,1)BEEP:"*     Can't save file!     *" :: GOTO 140
570 DISPLAY AT(23,1)BEEP:"*     File Not Found!      *" :: GOTO 140
580 DISPLAY AT(23,1)BEEP:"*     Printer error!       *" :: GOTO 140
590 ON ERROR 650 :: OPEN #2:DR$,INPUT,RELATIVE,INTERNAL :: INPUT #2:E$,E,E,F :: RO=1
600 FOR H@=1 TO 127 :: INPUT #2:E$,D,E,F :: DISPLAY AT(RO,1):E$ :: RO=RO+1 :: IF RO>19 THEN 610 ELSE 620
610 DISPLAY AT(24,1):"Press any key..." :: CALL KEY(0,AA,BB) :: IF BB=0 THEN 610 ELSE IF AA=15 THEN 640 ELSE RO=1
620 DISPLAY AT(24,1):" " :: IF ABS(D)=0 THEN 640
630 NEXT H@
640 CLOSE #2 :: GOTO 390
650 DISPLAY AT(23,1)BEEP:"*     Drive error!         *" :: GOTO 140
660 GOSUB 700 :: FOR I=I TO 60 :: A$(I)=A$(I+1) :: DISPLAY AT(24,1):"Deleting Line." :: NEXT I :: I=I-1 :: GOSUB 680 :: GOTO 150
670 GOSUB 700 :: FOR I=60 TO I STEP-1 :: A$(I)=A$(I-1) :: DISPLAY AT(24,1):"Inserting line." :: NEXT I :: A$(M)=" " :: GOSUB 680 :: GOTO 150
680 IF RS=0 THEN RS=1 :: RE=19
690 RO=1 :: FOR I=RS TO RE :: DISPLAY AT(RO,1):A$(I) :: RO=RO+1 :: NEXT I :: CALL HCHAR(R,2,139,1) :: I=M :: GOTO 150
700 IF I=1 AND K=73 THEN 710 ELSE CALL HCHAR(R,2,124,1) :: RETURN
710 DISPLAY AT(21,1)BEEP:" ":"* Can't insert line there. *":" " :: GOTO 150
720 DISPLAY AT(24,1):"Are you sure? (Y/N)" :: CALL KEY(3,K,S) :: IF K=78 THEN 140 ELSE IF K<>89 THEN 720 :: CALL CLEAR :: END
730 DISPLAY AT(23,1):"FileName:" :: DISPLAY AT(23,10):P$ :: RETURN
740 DISPLAY AT(2,1):"Clear -erases the screen":"Delete-erases a line of text":"End   -goes to end of file":"File  -displays current file"
750 DISPLAY AT(6,1):"Home  -goes to start of file":"Insert-inserts a blank line":"Load  -loads a file"
760 DISPLAY AT(9,1):"Print -prints a file":"Quit  -exits the program":"Save  -saves current file"
770 DISPLAY AT(13,1):"1 key -displays screen 1":"2 key -displays screen 2":"3 key -displays screen 3"
780 DISPLAY AT(17,1):"Fctn 8-alters disk path":"= key -purges current file" :: GOTO 140
790 DISPLAY AT(24,1):"Data Path:";DR$ :: ACCEPT AT(24,11)SIZE(-13):DR$ :: IF DR$="" THEN 790 :: GOTO 140
800 FOR I=RS TO RE :: DISPLAY AT(RO,1):A$(I) :: RO=RO+1 :: NEXT I :: I=RE :: R=19 :: RETURN
810 DISPLAY AT(24,1):"Initializing memory..." :: RUN
