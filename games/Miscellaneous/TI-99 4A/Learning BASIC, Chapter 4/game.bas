10 QA=600
20 QZ=1
40 CALL COLOR(12,13,13)
50 CALL CHAR(102,"C0F0FCFFFFFCF0C0")
60 GOTO 430
70 FOR Q=1 TO QA
80 NEXT Q
90 RETURN
100 LC=16.03
110 R=INT(LC)
120 QQ=0
130 C=100*(LC-R)
140 FOR Q=1 TO LEN(A$)
150 QQ=QQ+1
160 IF C+QQ<31 THEN 190
170 R=R+1
180 QQ=1
190 CALL HCHAR(R,C+QQ,ASC(SEG$(A$,Q,1)))
200 NEXT Q
210 LC=LC+1
220 RETURN
230 INPUT "PRESS ENTER ":Q$
240 CALL CLEAR
250 RETURN
260 A$=AA$
270 GOSUB 110
280 FOR II=1 TO 5
290 CALL HCHAR(R,C+1,102,LEN(AA$))
300 LC=LC-1
310 GOSUB 110
320 NEXT II
330 RETURN
340 LC=24.03
350 A$="PRESS ENTER"
360 GOSUB 110
370 CALL KEY(G,KE,ST)
380 IF ST=0 THEN 370
390 GOSUB 410
400 RETURN
410 CALL HCHAR(16,1,32,290)
420 RETURN
430 CALL SCREEN(2)
440 CALL CHAR(101,"FF000000000000FF")
450 CALL COLOR(9,5,5)
460 CALL CLEAR
470 B$(0)="10 CALL CLEAR"
480 B$(1)="20 PRINT""NIM""::"
490 B$(2)="30 N=9"
500 B$(3)="40 PRINT""THERE ARE "";N;"" STONES LEFT."""
510 B$(4)="50 INPUT""TAKE 1,2,OR 3 STONES "":D"
520 B$(5)="60 PRINT""I TAKE "";4-D;"" STONES"""
530 B$(6)="70 N=N-4"
540 B$(7)="80 IF N>1 THEN 40"
550 B$(8)="90 PRINT""THERE IS ONE STONE LEFT. I WIN AGAIN"""
560 B$(9)="100 END"
600 FOR I=0 TO 9
610 PRINT B$(I)
620 NEXT I
630 PRINT: : :
640 FOR I=1 TO 7
650 CALL HCHAR(I+7,8,101,18)
660 NEXT I
670 FOR K=2 TO 16
680 FOR I=1 TO 8
690 CALL COLOR(I,18-K,17-K)
700 CALL SCREEN(5*K/6)
710 NEXT I
720 NEXT K
730 CALL SCREEN(12)
740 LC=10.09
750 A$="LEARNING BASIC"
760 GOSUB 110
770 A$=" CHAPTER FOUR "
780 GOSUB 110
790 A$=" PROGRAMMING  "
800 GOSUB 110
810 CALL COLOR(9,16,13)
820 GOSUB 70
830 CALL CLEAR
840 PRINT "O.K.  LET'S DO SOME REAL":"PROGRAMMING."
850 GOSUB 70
860 PRINT: : : :"IT MIGHT BE GOOD TO START BY":"PROGRAMMING THE COMPUTER TO":"PLAY A GAME.": : :
870 GOSUB 70
880 PRINT "LET'S TRY THE GAME CALLED":"NIM.": : :"FIRST TRY PLAYING THE GAME":"AND THEN I'LL SHOW YOU HOW"
890 PRINT "TO PROGRAM IT.": :
900 GOSUB 230
910 CALL SCREEN(8)
920 R=21
930 GOSUB 1200
940 PRINT "NIM":"THERE ARE 21 STONES":"IN A ROW.":"YOU CAN TAKE 1, 2, OR 3":"STONES IN YOUR MOVE."
950 PRINT "THEN I CAN TAKE 1, 2, OR 3":"STONES IN MY MOVE.": :"WE ALTERNATE MOVES.": :
960 PRINT "THE PLAYER WHO TAKES THE":"LAST STONE LOSES.": :"O.K. YOU GO FIRST.": :
970 INPUT "HOW MANY DO YOU TAKE? ":NI
975 IF NI<1 THEN 990
980 IF NI<4 THEN 1010
990 PRINT "NO!":"ENTER A NUMBER 1, 2, OR 3."
1000 GOTO 970
1010 CALL CLEAR
1020 R=R-NI
1030 GOSUB 1200
1040 FOR II=0 TO 500
1050 NEXT II
1060 NI=4-NI
1070 R=R-NI
1080 GOSUB 1200
1090 PRINT "I TAKE ";NI
1100 FOR II=0 TO 500
1110 NEXT II
1120 IF R>1 THEN 970
1130 PRINT "I WIN!!"
1140 CALL SOUND(1000,131,0,165,5,196,10,-3,28)
1150 CALL SOUND(1000,262,0,330,0,392,0)
1160 CALL SOUND(1000,524,0,660,0,784,0)
1170 INPUT "WANT TO PLAY AGAIN? ":YN$
1180 IF SEG$(YN$,1,1)="Y" THEN 920
1190 GOTO 1360
1200 CALL CLEAR
1210 PRINT "  221111111111000000000"
1220 PRINT "  109876543210987654321"
1230 CALL HCHAR(21,26-R,42,R)
1240 RETURN
1250 GOTO 1250
1260 CALL CLEAR
1270 PRINT "NIM": :
1280 N=9
1290 PRINT "THERE ARE ";N;" STONES LEFT."
1300 INPUT "TAKE 1,2,OR 3 STONES ":D
1310 PRINT "I TAKE ";4-D;" STONES."
1320 N=N-4
1330 IF N>1 THEN 1290
1340 PRINT "THERE IS 1 STONE LEFT. I WIN AGAIN!"
1350 END
1360 CALL SCREEN(12)
1370 CALL CLEAR
1380 PRINT "NOW LET'S SEE HOW TO PROGRAM":"THIS GAME.": :
1390 GOSUB 70
1400 PRINT "WE HAVE TO KNOW WHAT TO":"PROGRAM, SO FIRST WE MUST":"UNDERSTAND THE STRATEGY OF":"THE GAME."
1410 GOSUB 230
1420 PRINT "FOR SIMPLICITY,":"LET'S USE ONLY 9 STONES":"INSTEAD OF 21.": :
1430 C$="* * * * * * * * *"
1440 C1$="9 8 7 6 5 4 3 2 1"
1450 PRINT C1$:C$
1460 LC=7.03
1470 A$="THE OBJECT OF THE GAME IS  TO GET YOUR OPPONENT TO    TAKE THE LAST STONE."
1480 GOSUB 110
1490 CALL CHAR(100,"183C7EFF18181818")
1500 CALL COLOR(9,5,12)
1510 CALL HCHAR(24,19,100)
1520 QA=900
1530 GOSUB 70
1540 CALL HCHAR(7,3,32,100)
1550 LC=7.03
1560 A$="IF WE CAN GET HIM TO TAKE  THIS STONE, WE CAN GET HIM TO TAKE THE LAST ONE.       (THINK ABOUT IT)"
1570 GOSUB 110
1580 CALL HCHAR(24,11,100)
1590 GOSUB 70
1600 CALL HCHAR(7,3,32,128)
1610 LC=7.03
1620 A$="NOW. WE CAN GET HIM TO TAKE #5 BY HAVING HIM GO FIRST.  (THINK ABOUT THIS TOO)"
1630 GOSUB 110
1640 CALL HCHAR(24,3,100)
1650 PRINT: :
1660 GOSUB 230
1670 PRINT C1$:C$: : :
1680 PRINT "HERE IS OUR STRATEGY.":"OUR OPPONENT GOES FIRST.":"IF HE TAKES ONE STONE,":"WE TAKE 3"
1690 PRINT "(FORCING HIM TO TAKE #5).": :"IF HE TAKES 2, WE TAKE 2.":"IF HE TAKES 3, WE TAKE 1."
1700 CALL HCHAR(14,11,100)
1710 GOSUB 230
1720 PRINT "O.K. LET'S START TO PROGRAM."
1730 GOSUB 230
1740 A$=B$(0)
1750 LC=1.03
1760 GOSUB 110
1770 CALL CHAR(112,"000000FFFF000000")
1780 CALL COLOR(11,7,7)
1790 CALL HCHAR(15,1,112,32)
1800 AA$="10"
1810 LC=1.03
1820 GOSUB 260
1830 A$=CHR$(102)&" 10 IS THE LINE NUMBER.   EVERY STATEMENT SHOULD HAVEA LINE NUMBER."
1840 GOSUB 100
1850 GOSUB 340
1860 A$="IT IS USUAL TO HAVE 10 AS  THE FIRST LINE OF THE      PROGRAM, AND 20 AS THE 2ND LINE, AND SO ON."
1870 GOSUB 100
1880 GOSUB 340
1890 A$="STATEMENT 10 ERASES THE    SCREEN. CALL CLEAR MEANS   CLEAR THE SCREEN."
1900 GOSUB 100
1910 GOSUB 340
1920 LC=2.03
1930 A$=B$(1)
1940 GOSUB 110
1950 AA$="PRINT"
1960 LC=2.06
1970 GOSUB 260
1980 A$="""PRINT"" ALLOWS US TO PRINT THINGS ON THE SCREEN. TEXT INSIDE QUOTE MARKS PRINTS  ON THE SCREEN."
1990 GOSUB 100
1995 AA$="NIM"
1996 LC=2.12
1997 GOSUB 260
2000 GOSUB 340
2040 CALL COLOR(4,2,14)
2050 A$=": MEANS SKIP A LINE.  THE  TWO COLONS (:) MEANS SKIP 2LINES AFTER PRINTING ""NIM""."
2060 GOSUB 100
2070 GOSUB 340
2080 CALL COLOR(4,2,12)
2090 LC=3.03
2100 A$=B$(2)
2110 GOSUB 110
2120 A$="N=9 JUST MEANS TAKE A      VARIABLE CALLED  N  AND LETIT BE THE NUMBER 9. (N IS  THE NUMBER OF STONES.)"
2130 GOSUB 100
2140 GOSUB 340
2150 LC=4.03
2160 A$=B$(3)
2170 GOSUB 110
2180 A$="HERE IS A MORE COMPLICATED PRINT STATEMENT."
2190 GOSUB 100
2210 LC=18.03
2220 A$="SEMICOLONS (;) SEPARATE    THINGS TO BE PRINTED."
2230 GOSUB 110
2240 GOSUB 340
2250 A$="SO STATEMENT 40 PRINTS     ""THERE ARE"" AND THEN PRINTSWHAT N EQUALS AND THEN     ""STONES LEFT."""
2260 GOSUB 100
2270 GOSUB 340
2280 A$="IF WE WERE TO RUN THE      PROGRAM NOW WE WOULD GET..."
2290 GOSUB 100
2300 GOSUB 230
2310 PRINT "NIM": :
2320 PRINT "THERE ARE 9 STONES LEFT.": : : : : :
2330 GOSUB 230
2340 PRINT B$(0):B$(1):B$(2):B$(3):B$(4): : : : : : : : : : : : : : : : :
2350 CALL HCHAR(15,1,112,32)
2360 AA$="INPUT"
2370 LC=6.05
2380 GOSUB 260
2390 A$="THIS IS AN INPUT STATEMENT.IT ALLOWS YOU TO ENTER DATAFROM THE KEYBOARD."
2400 GOSUB 100
2410 A$="RIGHT AFTER THE WORD INPUT YOU CAN HAVE TEXT IN       QUOTES. THIS IS PRINTED ON THE SCREEN."
2420 LC=19.03
2430 GOSUB 110
2450 GOSUB 340
2460 A$="THIS STATEMENT SAYS THE    NUMBER OF STONES THAT YOU  ENTER FROM THE KEYBOARD    WILL BE CALLED D."
2470 GOSUB 100
2480 GOSUB 340
2490 A$="YOU NEED NOT PUT PRINTED   MATERIAL IN THE STATEMENT. IF YOU DO, THE COLON (:) ISNEEDED."
2492 CALL HCHAR(8,6,100)
2500 GOSUB 100
2510 GOSUB 340
2520 A$=B$(5)
2530 LC=8.02
2540 GOSUB 110
2550 A$="ANOTHER PRINT STATEMENT-   FIRST ""I TAKE "" IS PRINTED.THEN THE QUANTITY 4-D IS   CALCULATED."
2560 GOSUB 100
2570 LC=20.03
2580 A$="THE VALUE OF 4-D IS PRINTEDFINALLY, THE WORD " STONES"IS  PRINTED."
2590 GOSUB 110
2600 GOSUB 340
2610 A$="THIS IS OUR GAME STRATEGY. THE COMPUTER WILL TAKE 4   MINUS THE NUMBER THAT THE  PLAYER TAKES."
2620 GOSUB 100
2630 GOSUB 340
2640 LC=10.02
2650 A$=B$(6)
2660 GOSUB 110
2670 AA$="N=N-4"
2680 LC=10.05
2690 GOSUB 260
2700 A$="STATEMENT 70 DOES NOT SEEM TO MAKE SENSE. HOW CAN N BEEQUAL TO N MINUS 4?"
2710 GOSUB 100
2720 GOSUB 340
2730 A$="IN COMPUTING THE EQUAL SIGNMEANS SOMETHING DIFFERENT  THAN IN ARITHMETIC. IT     MEANS ""REPLACE""."
2740 GOSUB 100
2750 LC=20.03
2760 A$="SO N=N-4 MEANS SUBTRACT 4  FROM N AND CALL THE ANSWER N."
2770 GOSUB 110
2780 GOSUB 340
2790 A$="THE TOTAL NUMBER OF STONES REMAINING IS N."
2800 GOSUB 100
2810 GOSUB 340
2820 LC=11.02
2830 A$=B$(7)
2840 GOSUB 110
2850 A$="HOW IS THE COMPUTER TO KNOWWHEN IT HAS WON? IF N=1    (ONE STONE LEFT), THEN THE COMPUTER HAS WON."
2860 GOSUB 100
2865 GOSUB 340
2870 AA$="IF"
2880 LC=11.05
2890 GOSUB 260
2895 CALL COLOR(16,7,7)
2900 A$="THIS IS AN ""IF"" STATEMENT. IT SAYS "&CHR$(152)&"IF"&CHR$(152)&" N IS GREATER"
2910 GOSUB 100
2920 A$="THAN 1 "&CHR$(152)&"THEN"&CHR$(152)&" GO BACK TO   STATEMENT NUMBER 40."
2930 LC=18.03
2940 GOSUB 110
2950 GOSUB 340
2960 LC=12.02
2970 A$=B$(8)
2980 GOSUB 110
2990 A$="HOWEVER, IF THE COMPUTER   HAS WON (N=1), THEN THE    COMPUTER WILL TELL YOU SO."
3000 GOSUB 100
3010 GOSUB 340
3020 CALL CLEAR
3030 FOR I=0 TO 9
3040 PRINT B$(I)
3050 NEXT I
3052 PRINT: :"STATEMENT 100 ENDS OUR":"PROGRAM. IT SAYS THAT THE":"PROGRAM IS AT THE END."
3060 PRINT:"THIS IS OUR GAME PROGRAM.":"WHEN YOU ARE FINISHED":"STUDYING IT, PRESS ENTER."
3070 INPUT Q$
3075 CALL CLEAR
3080 PRINT "THIS ENDS CHAPTER 4.": :"THE GAME THAT WE PROGRAMMED":"IS STORED IN THE COMPUTER"
3090 PRINT "STARTING AT LINE NUMBER":"1260.  IF YOU WANT TO TRY":"IT, ENTER":"    RUN 1260"
