5 REM   ******************  
10 REM  *DELIVER THE CAKE*  
20 REM  *                * 
30 REM  *BY FURMAN SMITH *  
40 REM  *                * 
50 REM  *IN RECREATIONAL *  
60 REM  *   COMPUTING    *  
70 REM  *                * 
80 REM  *JULY/AUGUST,1981*  
90 REM  ******************   
300 CALL CLEAR
350 CALL SCREEN(4)
370 RANDOMIZE
390 PRINT TAB(8);"***********"
400 PRINT TAB(8);"*         *":TAB(8);"* DELIVER *":TAB(8);"*         *":TAB(8);"*   THE   *"
410 PRINT TAB(8);"*         *":TAB(8);"*  CAKE   *":TAB(8);"*         *":TAB(8);"***********": : : :
420 GOSUB 8600
430 GOSUB 9400
1110 GOSUB 9000
1200 V9=18
1210 N9=17
1220 M9=7
1230 R9=9
1250 DIM V$(18)
1260 DIM N$(17)
1270 DIM L(7)
1280 DIM N1(7)
1290 DIM D(9,4)
1300 FOR I=1 TO V9
1310 READ V$(I)
1320 NEXT I
1350 DATA TAKE,GET,GRAB,PICK
1360 DATA DROP,GIVE,LEAVE,DELIVER
1370 DATA OPEN,UNLOCK,LIST,HELP
1380 DATA GO,RELOOK,CONSULT,CLOSE
1390 DATA EAT,TASTE
1400 FOR I=1 TO N9
1410 READ N$(I)
1420 NEXT I
1450 DATA N,S,E,W
1460 DATA NORTH,SOUTH,EAST,WEST
1470 DATA INVENTORY,WORDS,DOOR
1480 DATA UMBRELLA,CAKE,KEY,MAP
1490 DATA GLOBE,WRAPPER
1500 FOR I=1 TO M9
1510 READ L(I),N1(I)
1520 NEXT I
1550 DATA 0,12,8,12,4,13
1560 DATA 9,14,2,15,1,16,0,17
1600 IF RND>.75 THEN 1610 ELSE 1630
1610 L(7)=6
1620 GOTO 1650
1630 L(7)=3
1650 FOR I=1 TO R9
1660 FOR J=1 TO 4
1670 READ D(I,J)
1680 NEXT J
1690 NEXT I
1700 DATA 0,4,-2,0,0,0,3,-1
1710 DATA 0,6,0,2,1,7,-5,0
1720 DATA 0,0,6,4,3,-9,0,-5
1730 DATA 4,0,-8,0,0,0,0,-7
1740 DATA -6,0,0,0
1750 C9=2
1760 C=0
1770 R=7
2000 ON R GOTO 2100,2200,2300,2400,2500,2600,2700,2800,2900
2100 PRINT: :"YOU ARE NOW ON THE STAGE.":"THE SEATING ROOM IS TO THE":"SOUTH.  THERE IS A DOOR TO":"THE EAST."
2150 GOTO 3000
2200 PRINT: :"YOU ARE IN THE PANTRY.":"THERE IS A DOOR TO THE WEST":"AND AN OPEN PASSAGEWAY TO":"THE EAST."
2250 GOTO 3000
2300 PRINT: :"THIS IS THE KITCHEN, BUT":"NOTHING IS HAPPENING IN":"HERE RIGHT NOW."
2350 GOTO 3000
2400 PRINT: :"YOU ARE IN THE SEATING ROOM":"OF AN AUDITORIUM.  YOU CAN":"SEE THE STAGE TO THE NORTH,"
2410 PRINT "A DOORWAY TO THE SOUTH, AND":"A DOORWAY TO THE EAST.  YOU":"NOTICE THAT IT'S RAINING IN"
2420 PRINT "THE ROOM TO THE EAST."
2450 GOTO 3000
2500 PRINT: :"THE CEILING OF THIS ROOM":"REALLY NEEDS FIXING.  RAIN":"IS FALLING THROUGH THE"
2510 PRINT "CEILING AND RUNNING THROUGH":"TINY HOLES IN THE FLOOR."
2550 GOTO 3000
2600 PRINT: :"WELCOME TO THE PARTY ROOM.":"THERE ARE DOORWAYS TO THE":"NORTH AND WEST.  THE EAST"
2610 PRINT "WALL HAS A GIANT PICTURE OF":"AN ELEPHANT.  THE SOUTH":"WALL HAS A SMALL CURTAIN"
2620 PRINT "MADE OUT OF BRIGHT GREEN":"BEADS."
2650 GOTO 3000
2700 PRINT: :"YOU ARE IN A ROOM CALLED":"THE FOYER.  THERE IS A":"SMALL DOORWAY TO THE EAST"
2710 PRINT "AND A LARGE DOORWAY TO THE":"NORTH."
2750 GOTO 3000
2800 PRINT: :"THIS ROOM IS SO DARK THAT":"IT IS CALLED THE DARK ROOM.":"YOU CAN SEE LIGHT COMING"
2810 PRINT "FROM THE WEST DOORWAY."
2850 GOTO 3000
2900 PRINT: :"YOU ARE NOW IN THE OFFICE."
2950 GOTO 3000
3000 IF R=8 THEN 3060 ELSE 3100
3060 IF L(6)<>-1 THEN 3070 ELSE 3100
3070 IF L(6)<>R THEN 3080 ELSE 3100
3080 PRINT: :"IT'S TOO DARK TO SEE":"ANYTHING ELSE IN HERE."
3090 GOTO 4000
3100 FOR M=1 TO M9
3150 IF L(M)=R THEN 3160 ELSE 3170
3160 ON M GOSUB 3250,3350,3450,3550,3650,3750,3850
3170 NEXT M
3180 GOTO 4000
3250 PRINT: :"THERE IS AN OPEN UMBRELLA":"HERE."
3300 RETURN
3350 PRINT: :"THERE IS A CLOSED UMBRELLA":"IN THE CORNER."
3400 RETURN
3450 PRINT: :"AH!  HERE IS THE CAKE THAT":"YOU ARE TO DELIVER TO THE":"PARTY ROOM."
3500 RETURN
3550 PRINT: :"THERE IS AN OLD BRASS KEY":"ON THE FLOOR."
3600 RETURN
3650 PRINT: :"HERE'S A MAP OF THIS":"CRAZY PLACE."
3700 RETURN
3750 PRINT: :"THERE IS A GLOBE IN THE":"CENTER OF THE ROOM, AND":"IT IS SOFTLY GLOWING."
3800 RETURN
3850 PRINT: :"THERE IS AN OLD GUM WRAPPER":"IN THIS ROOM."
3900 RETURN
4000 PRINT: :"WHAT DO YOU INTEND TO DO":"NOW?": :
4060 INPUT A$
4070 PRINT
4080 CALL HCHAR(24,1,45,32)
4150 I9=LEN(A$)
4160 J=I9
4200 FOR I=1 TO I9
4210 IF SEG$(A$,I,1)<>" " THEN 4220 ELSE 4250
4220 J=J-1
4230 NEXT I
4250 IF J<>0 THEN 4260 ELSE 4270
4260 VI$=SEG$(A$,1,I-1)
4265 GOTO 4300
4270 VI$=A$
4280 NI$=A$
4290 GOTO 4400
4300 FOR J=I9 TO 1 STEP-1
4310 IF SEG$(A$,J,1)<>" " THEN 4320 ELSE 4350
4320 NEXT J
4350 NI$=SEG$(A$,J+1,I9-J)
4400 V=0
4410 N=0
4450 FOR I=1 TO V9
4460 IF V$(I)=VI$ THEN 4470 ELSE 4490
4470 V=I
4480 GOTO 4500
4490 NEXT I
4500 FOR I=1 TO N9
4510 IF N$(I)=NI$ THEN 4520 ELSE 4540
4520 N=I
4530 GOTO 4550
4540 NEXT I
4550 IF 0<N THEN 4560 ELSE 4600
4560 IF N<=8 THEN 5000
4600 IF V*N>0 THEN 4610 ELSE 4650
4610 IF V<=8 THEN 6000 ELSE 7000
4650 IF V=0 THEN 4660 ELSE 4700
4660 IF N=9 THEN 8680
4700 IF N=0 THEN 4710 ELSE 4750
4710 IF V=12 THEN 7000
4720 IF V=14 THEN 7000
4750 IF VI$<>NI$ THEN 8920
4800 PRINT: :"I DON'T UNDERSTAND": :TAB(4);"'";A$;"'."
4850 PRINT: :"PERHAPS YOU SHOULD"
4900 PRINT "TYPE 'LIST WORDS'."
4950 IF RND<.95 THEN 4000
4960 PRINT: :"A VOICE SAYS 'TYPE":"<HELP> IF YOU NEED":"A LITTLE HELP.'"
4970 GOTO 4000
5000 IF N<=4 THEN 5070 ELSE 5080
5070 I=N
5075 GOTO 5090
5080 I=N-4
5090 IF D(R,I)>0 THEN 5100 ELSE 5120
5100 R=D(R,I)
5110 GOTO 2000
5120 IF D(R,I)=0 THEN 5450
5180 ON R GOTO 5240,5240,7800,5300,7800,5210,5390,5390,5390
5210 IF I=2 THEN 5390 ELSE 5300
5240 IF L(4)=-1 THEN 8320
5250 PRINT: :"YOU NEED THE KEY TO GET":"THROUGH THE LOCKED DOOR."
5260 GOTO 4000
5300 IF L(3)=-1 THEN 5330 ELSE 5360
5330 PRINT: :"THE UMBRELLA CAN PROTECT":"YOU FROM THE FALLING RAIN":"BUT NOTHING CAN PROTECT"
5335 PRINT "THE CAKE FROM THE WATER":"THAT SPATTERS UP FROM THE":"FLOOR. YOU MUST FIND"
5340 PRINT "SOME OTHER WAY TO DELIVER":"THE CAKE TO THE PARTY":"ROOM."
5350 GOTO 4000
5360 IF L(1)=-1 THEN 5365 ELSE 5375
5365 R=-D(R,I)
5370 GOTO 2000
5375 PRINT: :"NO ONE CAN ENTER THE RAIN":"ROOM WITHOUT AN OPEN":"UMBRELLA."
5380 GOTO 4000
5390 IF L(1)<>-1 THEN 5400 ELSE 5420
5400 R=-D(R,I)
5410 GOTO 2000
5420 PRINT: :"YOU CANNOT TAKE AN OPEN":"UMBRELLA THROUGH THAT":"SMALL PASSAGEWAY."
5430 GOTO 4000
5450 PRINT: :"YOU CANNOT WALK THROUGH":"WALLS."
5510 A$=""
5540 IF D(R,1)=0 THEN 5550 ELSE 5570
5550 A$=A$&"N, "
5570 IF D(R,2)=0 THEN 5580 ELSE 5600
5580 A$=A$&"S, "
5600 IF D(R,3)=0 THEN 5610 ELSE 5630
5610 A$=A$&"E, "
5630 IF D(R,4)=0 THEN 5640 ELSE 5660
5640 A$=A$&"W, "
5660 IF A$="" THEN 5670 ELSE 5690
5670 PRINT: :"POSSIBLE PASSAGES ABOUND."
5680 GOTO 4000
5690 A$=SEG$(A$,1,LEN(A$)-2)
5720 PRINT: :TAB(4);"THERE";TAB(10);
5750 IF LEN(A$)=1 THEN 5760 ELSE 5770
5760 PRINT "IS A WALL";
5765 GOTO 5780
5770 PRINT "ARE WALLS";
5780 PRINT " TO THE"
5810 PRINT TAB(4);A$;"."
5820 GOTO 4000
6000 IF V<=4 THEN 6100 ELSE 6120
6100 V=1
6105 GOTO 6200
6120 V=5
6200 M=0
6210 FOR I=1 TO M9
6250 IF N=N1(I)THEN 6260 ELSE 6280
6260 IF L(I)<>0 THEN 6270 ELSE 6280
6270 M=I
6275 GOTO 6300
6280 NEXT I
6300 IF M=0 THEN 6310 ELSE 6350
6310 IF N=9 THEN 8680
6320 PRINT: :"THAT IS NOT POSSIBLE NOW."
6330 GOTO 4000
6350 IF V=5 THEN 6650
6400 IF L(M)=-1 THEN 6450 ELSE 6500
6450 PRINT: :"YOU STILL HAVE IT."
6460 GOTO 4000
6500 IF L(M)<>R THEN 6510 ELSE 6550
6510 PRINT: :"THERE'S NO ";N$(N1(M));" HERE."
6520 GOTO 4000
6550 IF C<C9 THEN 6560 ELSE 6600
6560 L(M)=-1
6570 C=C+1
6580 PRINT: :"YOU HAVE IT NOW."
6590 GOTO 4000
6600 PRINT: :"YOU CAN ONLY CARRY ";C9;" OBJECTS."
6610 PRINT "TYPE 'INVENTORY' TO":"SEE WHAT YOU CAN DROP."
6620 GOTO 4000
6650 IF L(M)<>-1 THEN 6660 ELSE 6750
6660 PRINT: :"YOU CANNOT DROP WHAT YOU":"DO NOT HAVE."
6670 GOTO 8680
6750 L(M)=R
6760 C=C-1
6800 IF L(3)<>6 THEN 3000
6810 PRINT: : : :"YOU WON!": :"CONGRATULATIONS!"
6820 END
7000 ON V-8 GOTO 8400,8200,7880,7800,4900,2000,7440,7200,7120,7120
7120 IF N=13 THEN 7130 ELSE 8920
7130 PRINT: :"YOU ARE SUPPOSED TO DELIVER":"THE CAKE--NOT EAT IT."
7140 GOTO 4000
7200 IF N=11 THEN 7250 ELSE 7280
7250 PRINT: :"THE DOOR AUTOMATICALLY":"CLOSES AND RELOCKS ITSELF."
7260 GOTO 4000
7280 IF N<>12 THEN 8920
7320 IF L(1)=0 THEN 7330 ELSE 7360
7330 PRINT: :"THE UMBRELLA IS STILL":"CLOSED."
7340 GOTO 4000
7360 IF L(1)=R THEN 7380
7370 IF L(1)=-1 THEN 7380 ELSE 7400
7380 L(2)=L(1)
7390 L(1)=0
7392 PRINT: :"OK; DONE."
7394 GOTO 4000
7400 PRINT: :"YOU CANNOT CLOSE IT UNLESS":"YOU ARE IN THE SAME ROOM AS":"IT IS."
7410 GOTO 4000
7440 IF N<15 THEN 8920
7480 IF L(N-10)<>-1 THEN 7490 ELSE 7520
7490 PRINT: :"YOU DON'T HAVE THE ";NI$
7500 PRINT "IN YOUR HANDS."
7510 GOTO 4000
7520 IF N=17 THEN 7530 ELSE 7560
7530 PRINT: :"THE WRAPPER DOES NOT HAVE":"ANYTHING IMPORTANT WRITTEN":"ON IT.  NICE TRY."
7540 GOTO 4000
7560 IF N=16 THEN 7570 ELSE 7600
7570 PRINT: :"THE GLOBE JUST SOFTLY":"GLOWS AND GLOWS."
7580 GOTO 4000
7600 IF N<>15 THEN 8920
7640 PRINT: :"+-+-+-+-+-+-+-+-+-+-+-+-+":"+ STAGE / PANTRY/ KITCH +":"+                       +"
7645 PRINT "+       /       /       +":"+-+/ /+-+-+-+-+-+-+/ /+-+"
7680 PRINT "+A      / RAIN   /     P+":"+U                     A+":"+D      /        /     R+"
7690 PRINT "+-+/ /+-+-+-+-+-+-+/ /+-+"
7720 PRINT "+       /       +       +":"+               +       + ":"+FOYER  / DARK  + OFFICE+"
7730 PRINT "+-+-+-+-+-+-+-+-+-+-+-+-+"
7760 GOTO 4000
7800 PRINT: :"YOU ARE TO FIND THE CAKE":"AND DELIVER IT TO THE":"PARTY ROOM."
7840 GOSUB 8600
7850 PRINT: :"TYPE 'LIST WORDS' TO":"SEE HOW YOU MAY MAKE ME":"UNDERSTAND."
7860 GOTO 4000
7880 IF N<>10 THEN 8920
7960 PRINT: :"I KNOW THESE VERBS:": :
8000 FOR I=1 TO V9
8010 PRINT TAB(9);V$(I)
8020 GOSUB 8600
8030 NEXT I
8040 PRINT: :TAB(4);"I KNOW THESE NOUNS:": :
8080 FOR I=1 TO N9
8090 PRINT TAB(9);N$(I)
8100 GOSUB 8600
8110 NEXT I
8120 PRINT: :"I JUST LOOK AT THE FIRST":"AND LAST WORDS OF YOUR":"INTENTION; FOR EXAMPLE,"
8130 PRINT "SAYING'TAKE KEY' DOES":"JUST AS MUCH AS":"'TAKE THE OLD BRASS KEY.'"
8160 GOSUB 8600
8170 GOTO 4000
8200 IF N<>11 THEN 8920
8280 IF L(4)<>-1 THEN 8290 ELSE 8320
8290 PRINT: :"YOU NEED TO BE HOLDING THE":"KEY IN ORDER TO UNLOCK THE":"DOOR."
8300 GOTO 4000
8320 PRINT: :"YOU UNLOCKED THE DOOR,"
8330 GOSUB 8600
8335 PRINT "PASSED THROUGH IT,"
8340 GOSUB 8600
8345 PRINT "AND IT SHUT BEHIND YOU."
8360 GOSUB 8600
8370 R=3-R
8380 GOTO 2000
8400 IF N=11 THEN 8200
8410 IF N<>12 THEN 8920
8480 IF L(2)=0 THEN 8490 ELSE 8520
8490 PRINT: :"THE UMBRELLA IS STILL":"OPEN."
8500 GOTO 4000
8520 IF L(2)=-1 THEN 8540
8530 IF L(2)=R THEN 8540 ELSE 8560
8540 L(1)=L(2)
8545 L(2)=0
8550 PRINT: :TAB(4);"OK; DONE."
8555 GOTO 4000
8560 PRINT: :"YOU CANNOT OPEN THE":"UMBRELLA IF IT'S NOT":"AROUND."
8570 GOTO 4000
8600 FOR J=1 TO 400
8610 NEXT J
8620 RETURN
8680 IF C=0 THEN 8720 ELSE 8760
8720 PRINT: :"YOUR HANDS ARE EMPTY."
8760 IF C=1 THEN 8770 ELSE 8800
8770 PRINT: :"ONE HAND IS EMPTY AND THE":"OTHER HAS THE"
8775 GOTO 8840
8800 IF C=2 THEN 8810 ELSE 8890
8810 PRINT: :"BOTH HANDS HAVE SOMETHING;":"HERE'S WHAT YOU HAVE:"
8840 FOR M=1 TO M9
8850 IF L(M)=-1 THEN 8860 ELSE 8880
8860 PRINT: :TAB(10);N$(N1(M))
8880 NEXT M
8890 GOTO 4000
8920 PRINT: :"THE ";NI$;" IS HARD TO ";VI$;"."
8930 GOTO 4000
9000 PRINT: :"YOU ARE ABOUT TO BE PLACED":"(LET'S PRETEND) IN AN":"ENCHANTED HOUSE."
9010 GOSUB 8600
9100 PRINT: :"IT IS ALWAYS RAINING IN":"ONE OF THE ROOMS IN THIS":"HOUSE (THE RAIN ROOM)."
9110 PRINT "YOU CANNOT ENTER THE RAIN":"ROOM WITHOUT AN UMBRELLA,":"BUT YOU HAVE TO FIND":"THE UMBRELLA FIRST."
9120 GOSUB 9400
9150 GOSUB 8600
9160 PRINT: :"YOU SHOULD USE SHORT":"MESSAGES TO TELL ME WHAT":"YOU WANT TO DO."
9170 GOSUB 8600
9200 PRINT: :"TYPICAL MESSAGES ARE":"TIPTOE NORTH, GET UMBRELLA,":"TAKE THE OLD BRASS KEY,"
9210 PRINT "UNLOCK THE DOOR, GO EAST,":"DROP THE MAP, W ('W' IS THE":"SAME AS 'GO WEST'"
9220 PRINT "OR 'WEST')."
9230 GOSUB 9400
9300 PRINT: :"YOUR MISSION IS TO FIND":"THE CAKE AND DELIVER IT TO":"THE PARTY ROOM."
9310 GOSUB 8600
9350 PRINT: :"WHEN YOU ARE IN THE HOUSE,":"YOU CAN TYPE 'HELP' FOR":"MORE INFORMATION.  YOU ARE"
9360 PRINT "ABOUT TO BE PLACED IN A":"ROOM CALLED THE FOYER."
9370 GOSUB 8600
9400 GOSUB 8600
9410 PRINT: :"WHEN YOU HAVE READ THE":"MESSAGE ABOVE,PLEASE":"PRESS 'ENTER'."
9420 INPUT A$
9430 PRINT
9440 CALL HCHAR(24,1,45,32)
9450 PRINT
9460 RETURN
9470 RETURN
