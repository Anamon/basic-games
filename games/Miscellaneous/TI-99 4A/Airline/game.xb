8 C0=0 :: C1=1 :: C2=2 :: C3=3 :: C4=4 :: C5=5 :: C6=6 :: C7=7 :: C8=8 :: C9=9 :: CD=10 :: CZ=16 :: CH=100 :: CT=1000 :: CW=24
9 DIM W0(4),W1(4),W2(4),W3(4),W4(4),PU(7,7),PB(7,7),PP(7,7),SP$(11),SN$(8),SV$(30),SM$(4),SC$(6,6),ST$(5),C$(9),CC$(6)
10 DIM N(9),CT$(6,1) :: GOTO 1000
12 T=T+PU(I1,I2)*PP(I1,I2)*PU(I3,I4)*CH :: RETURN
20 W1(C1)=C1 :: W1(C2)=C1 :: W1(C3)=C2 :: W1(C4)=C1 :: W2(C1)=3 :: W2(C2)=3 :: W2(C3)=C2 :: W2(C4)=C2 :: W3(C1)=CT
21 W3(C2)=CT :: W3(C3)=CT :: W3(C4)=CT :: W0(C1)=C4 :: W0(C2)=C4 :: W0(C3)=C4 :: W0(C4)=C4 :: LC=C0
22 LP=C0 :: CP=C1 :: DISPLAY AT(22,C1):SM$(NP);" IS THINKING HARD"
23 TT=C1 :: FOR IL=C1 TO C6 :: FOR II=C1 TO C6 :: CP=CP+C1 :: DISPLAY AT(23,C3+CP/C2):":"
24 I3=CD :: I4=CD :: I5=CD :: I6=CD :: I7=C0 :: I8=C0 :: I9=C0 :: IX=C0 :: IF NT>C3 THEN W1(NP)=C0 :: W4(NP)=C0
25 IF PB(IL,II-C1)=NP THEN I7=PP(IL,II-C1)ELSE I3=W1(NP) :: IF PB(IL,II-C1)=C0 THEN I3=W0(NP)
27 IF PB(IL,II+C1)=NP THEN I8=PP(IL,II+C1)ELSE I4=W1(NP) :: IF PB(IL,II+C1)=C0 THEN I4=W0(NP)
29 IF PB(IL-C1,II)=NP THEN I9=PP(IL-C1,II)ELSE I5=W1(NP) :: IF PB(IL-C1,II)=C0 THEN I5=W0(NP)
31 IF PB(IL+C1,II)=NP THEN IX=PP(IL+C1,II)ELSE I6=W1(NP) :: IF PB(IL+C1,II)=C0 THEN I6=W0(NP)
33 LV=PU(IL,II-C1)*I3+PU(IL,II+C1)*I4+PU(IL-C1,II)*I5+PU(IL+C1,II)*I6
34 LV=LV*(IT+CD-NT)*PU(IL,II) :: IW=LV
35 IA=PU(IL,II-C1)*I7+PU(IL,II+C1)*I8+PU(IL-C1,II)*I9+PU(IL+C1,II)*IX
36 LV=CD*IA*(IT+CD-NT)*PU(IL,II)+LV
37 IF PB(IL,II)<>C0 OR PU(IL,II)*PU(IL,II)*CT>TM(NP)OR NT=IT OR LV<LC THEN 39
38 LC=LV :: ID=IL :: IE=II
39 IF PB(IL,II)<>NP OR NT<C3 THEN 45
40 FOR I7=C1 TO C5 :: IF I7*I7*3000>TM(NP)THEN 44
41 IF I7<=PP(IL,II)OR IW*CD*I7-PP(IL,II)*PP(IL,II)*3000<I7*I7*3000 THEN 44
42 LV=IW*(I7-PP(IL,II))*W2(NP)/CD :: IF LV<LP THEN 44
43 LP=LV :: IM=IL :: IG=II :: IH=I7
44 NEXT I7
45 NEXT II :: NEXT IL :: IF LC>=LP AND LC>W3(NP)THEN 49
46 IF LP<W3(NP)OR PB(IM,IG)<>NP THEN 115
47 PP(IM,IG)=IH :: TM(NP)=TM(NP)-3000*IH*IH :: GOSUB 57 :: X=IM :: Y=IG :: IA=IH :: GOSUB 1300
48 GOTO 115
49 PB(ID,IE)=NP :: TM(NP)=TM(NP)-CT*PU(ID,IE)*PU(ID,IE) :: GOSUB 57 :: X=ID :: Y=IE
50 IA=C0 :: GOSUB 1300 :: GOTO 115
57 IF SM$(NP)="." THEN RETURN
58 DISPLAY AT(CZ+NP,C5):SM$(NP) :: DISPLAY AT(CZ+NP,CZ):"$";TM(NP) :: RETURN
60 FOR A=21 TO CW :: DISPLAY AT(A,C1)SIZE(28) :: NEXT A :: RETURN
70 FOR A=C1 TO 500 :: NEXT A :: RETURN
100 FOR NT=C1 TO IT :: FOR NP=C1 TO C4 :: IF IP(NP)=C3 THEN 115
102 DISPLAY AT(C2,C9):"TURN";NT;"OF";IT
103 FOR IL=C1 TO C5 :: DISPLAY AT(CZ+NP,C5)SIZE(25)BEEP
104 GOSUB 57 :: NEXT IL
105 GOSUB 60 :: IF IP(NP)=C2 THEN 20
106 DISPLAY AT(21,C1)SIZE(15):SM$(NP);"'S TURN?"
110 IZ=C0 :: IA=C0 :: X=23 :: Y=63 :: GOSUB 10000 :: Y=(Y-15)/CZ :: IF Y=C4 THEN 105
112 GOSUB 60 :: ON Y GOSUB 200,400,300,110,450 :: CALL DELSPRITE(ALL) :: IF IZ=C0 THEN 105
115 NEXT NP
150 GOSUB 60 :: DISPLAY AT(22,11)BEEP:"FARES:"
152 FOR IL=C1 TO C4 :: T=C0 :: IF IP(IL)=C3 THEN 170
153 DISPLAY AT(CW,C0)SIZE(25):SM$(IL); :: DISPLAY AT(CW,CD):"$";STR$(TM(IL));"+";
154 FOR I1=C1 TO C6 :: FOR I2=C1 TO C6 :: IF PB(I1,I2)<>IL THEN 165
155 I4=I2 :: FOR I3=I1-C1 TO I1+C1 STEP C2
156 IF PB(I3,I4)=IL THEN GOSUB 12
157 NEXT I3
160 I3=I1 :: FOR I4=I2-C1 TO I2+C1 STEP C2
161 IF PB(I3,I4)=IL THEN GOSUB 12
162 NEXT I4
165 NEXT I2 :: NEXT I1 :: T=T-(T*C9)*(IT=NT) :: TM(IL)=TM(IL)+T :: NP=IL
167 DISPLAY AT(CW,20):"$";STR$(T);
170 GOSUB 57 :: NEXT IL :: NEXT NT
172 IA=C0 :: T=-C1 :: FOR NP=C1 TO C4 :: IF TM(NP)>T THEN T=TM(NP) :: IA=NP
173 NEXT NP
175 FOR AA=C1 TO C5 :: GOSUB 60 :: DISPLAY AT(22,C3)BEEP:"THE WINNER IS ";SM$(IA) :: NEXT AA
185 DISPLAY AT(23,C5):"PLAY AGAIN?"; :: GOSUB 6600 :: IF A$<>"Y" THEN END ELSE GOTO 1061
200 DISPLAY AT(21,C9):"WHICH CITY?" :: X=129 :: Y=57 :: GOSUB 20000 :: X=(X-33)/24 :: Y=(Y-C9)/CZ
210 DISPLAY AT(22,C7):SC$(X,Y);"  $";PU(X,Y)*PU(X,Y)*CT
215 IF PB(X,Y)<>C0 THEN DISPLAY AT(23,C6):"RIGHTS NOT FOR SALE" :: GOSUB 70 :: RETURN
220 IF PU(X,Y)*PU(X,Y)*CT>TM(NP)THEN DISPLAY AT(23,C7):"COSTS TOO MUCH" :: GOSUB 70 :: RETURN
230 DISPLAY AT(23,C5):"SPACE TO VERIFY SALE" :: GOSUB 6600
240 IF A$<>" " THEN RETURN
250 TM(NP)=TM(NP)-CT*PU(X,Y)*PU(X,Y) :: PB(X,Y)=NP :: IA=C0 :: GOSUB 1300 :: IZ=C1 :: GOSUB 57 :: RETURN
300 DISPLAY AT(22,C9):"PLANE TYPE?"
305 IA=C0 :: X=214 :: Y=63 :: GOSUB 10000 :: PT=(Y-15)/CZ :: T=PT*PT*3000 :: DISPLAY AT(23,C8):ST$(PT);"  $";T
308 IF T>TM(NP)THEN DISPLAY AT(CW,C8):"COSTS TOO MUCH" :: GOSUB 70 :: RETURN
309 DISPLAY AT(22,C9):"WHICH CITY?"
310 X=129 :: Y=57 :: GOSUB 20000 :: X=(X-33)/CW :: Y=(Y-C9)/CZ
320 IF PB(X,Y)<>NP THEN DISPLAY AT(CW,C5):"NO LANDING RIGHTS" :: GOSUB 70 :: RETURN
330 PP(X,Y)=PT :: TM(NP)=TM(NP)-T :: GOSUB 57 :: IA=PT :: GOSUB 1300 :: IZ=C1 :: RETURN
400 FOR A=C1 TO C6 :: DISPLAY AT(C3+A*C2,C6):CC$(A); :: DISPLAY AT(C3+A*C2-C1,C6):"  ˆ  ˆ  ˆ  ˆ  ˆ  "; :: NEXT A
410 DISPLAY AT(22,C6):"SPACE TO CONTINUE" :: GOSUB 6600 :: GOSUB 6800 :: RETURN
450 DISPLAY AT(22,C5):"SPACE TO SKIP TURN" :: GOSUB 6600 :: IF A$=" " THEN IZ=C1
455 RETURN
1000 FOR IL=C0 TO C7 :: FOR II=C0 TO C7 :: PU(IL,II)=C0 :: NEXT II :: NEXT IL :: FOR IL=C1 TO C4 :: READ S$ :: NEXT IL :: CALL DELSPRITE(ALL)
1020 FOR IL=C1 TO C5 :: READ ST$(IL) :: NEXT IL :: FOR II=C1 TO C6 :: FOR IL=C1 TO C6 :: READ PU(IL,II),SC$(IL,II) :: NEXT IL :: NEXT II
1028 READ A :: IF A=C0 THEN 1030
1029 READ A$ :: CALL CHAR(A,A$) :: GOTO 1028
1030 DATA WIZ KIDD,TI CHAMP,HAL,IRON MIKE
1031 DATA PROP,AIRBUS,DC-10,B. 747,CONCORD
1032 DATA 2,SEATTLE,1,BISMARK,3,MINNEAPOLIS,3,MILWAUKEE
1033 DATA 2,BUFFALO,2,BOSTON,3,PORTLAND,3,DENVER
1034 DATA 1,DES MOINES,4,DETROIT,2,PITTSBURG,8,NEW YORK
1035 DATA 2,SACRAMENTO,1,SALT LAKE CITY,2,ST. LOUIS
1036 DATA 6,CHICAGO,2,CINCINNATI,4,PHILADELPHIA
1037 DATA 4,SAN FRANCISCO,2,LAS VEGAS,2,KANSAS CITY
1038 DATA 2,CLEVELAND,2,NASHVILLE,3,WASHINGTON
1039 DATA 6,LOS ANGELES,2,PHEONIX,4,DALLAS,2,MEMPHIS
1040 DATA 4,ATLANTA,3,JACKSONVILLE,3,SAN DIEGO
1041 DATA 3,SAN ANTONIO,5,HOUSTON,3,NEW ORLEANS
1042 DATA 2,BIRMINGHAM,3,MIAMI
1044 DATA 102,00,96,AAAAAAAAAAAAAAAA,98,000102050A152A55,97,00FF00FF00FF00FF,99,00008040A050A854,100,AA552A150A050201
1046 DATA 101,AA54A850A04080,112,FFFFFFFFF0F0F0F0,113,FFFFFFFF0F0F0F0F,114,F0F0F0F0FFFFFFFF,115,0F0F0F0FFFFFFFFF
1047 DATA 116,FFFFFFFF,117,F0F0F0F0F0F0F0F0,120,3F3F3F3F3F3F0000,121,FCFCFCFCFCFC0000,122,00,123,1E1E3E3F3F3F
1048 DATA 124,000000040E0E0E1E,125,0000000000303038,126,000000000C0D0F0F,127,0000C0C0E0E0F8F8,128,00000000000C1E1E,130,000000000000001C
1049 DATA 131,C0F8F8F8FCFC,132,000C0C0D0F0F,129,7078F8F8FCFC,133,C0C0F0F0F8F8,134,000001071F1F,135,0080D8D8F8F8
1050 DATA 111,FEFED6FED6D6FEFE,118,0F0F0F0F0F0F0F0F,119,00000000FFFFFFFF
1051 DATA 103,FC84848484FC0000,140,FFFFC0C0C0C0C0C0,141,C0C0C0C0C0C0FFFF,142,FFFF030303030303,143,030303030303FFFF,0
1053 FOR A=C1 TO C9 :: READ C$(A),N(A) :: NEXT A :: C$(C0)=CHR$(111)
1054 DATA P,5,A,7,D,9,B,11,C,13,L,5,C,7,P,9,Q,13
1055 DATA 122,122,134,135,122,122,132,133,130,122,123,131,128,122,123,129,124,125,120,121,126,127,120,121
1056 DATA " 2ˆ 1ˆ 3ˆ 3ˆ 2ˆ 2"," 3ˆ 3ˆ 1ˆ 4ˆ 2ˆ 8"," 2ˆ 1ˆ 2ˆ 6ˆ 2ˆ 4"," 4ˆ 2ˆ 2ˆ 2ˆ 2ˆ 3"," 6ˆ 2ˆ 4ˆ 2ˆ 4ˆ 3"," 3ˆ 3ˆ 5ˆ 3ˆ 2ˆ 3"
1060 FOR A=C1 TO C6 :: FOR B=C0 TO C1 :: FOR C=C1 TO C2 :: READ D :: CT$(A,B)=CT$(A,B)&CHR$(D) :: NEXT C :: NEXT B :: NEXT A :: FOR A=C1 TO C6 :: READ CC$(A) :: NEXT A
1061 CALL CLEAR :: CALL SCREEN(11)
1070 FOR IL=C1 TO C4 :: DISPLAY AT(C4+IL,C1):"PLAYER" :: DISPLAY AT(C4+IL,C7):IL :: DISPLAY AT(C4+IL,C9):":        :" :: NEXT IL
1080 DISPLAY AT(CD,C2):"H = HUMAN" :: DISPLAY AT(11,C2):"C = COMPUTER" :: DISPLAY AT(12,C2):"S = SKIP"
1083 DISPLAY AT(14,C1)BEEP:"H, C, OR S FOR PLAYER"
1084 FOR IL=C1 TO C4 :: DISPLAY AT(14,22):IL :: GOSUB 6600
1087 IF A$="H" THEN IP(IL)=C1 :: S$="HUMAN" :: GOTO 1091
1088 IF A$="C" THEN IP(IL)=C2 :: S$="COMPUTER" :: GOTO 1091
1089 IF A$="S" THEN IP(IL)=C3 :: S$="SKIPPED" :: GOTO 1091 ELSE GOTO 1086
1091 DISPLAY AT(C4+IL,CD):S$; :: NEXT IL :: DISPLAY AT(CZ,C2):"PLAYERS OK?" :: GOSUB 6600 :: IF A$<>"Y" THEN 1061
1095 RESTORE :: FOR IL=C1 TO C4 :: READ S$ :: IF IP(IL)=C3 THEN SM$(IL)="." :: GOTO 1105
1096 IF IP(IL)=C2 THEN SM$(IL)=S$ :: GOTO 1105
1097 DISPLAY AT(18,C2):"ENTER NAME OF PLAYER";IL
1098 DISPLAY AT(19,C2):"9 LTR MAX"; :: GOSUB 6700
1105 DISPLAY AT(C4+IL,19):SM$(IL) :: NEXT IL :: DISPLAY AT(19,C2)SIZE(13)
1106 DISPLAY AT(18,C2)SIZE(CW) :: DISPLAY AT(CZ,C2)BEEP:"NAMES OK?"
1107 GOSUB 6600 :: IF A$="N" THEN 1095
1108 IF A$<>"Y" THEN 1107
1110 CALL CLEAR :: CALL SCREEN(13) :: INPUT "# OF TURNS?":S :: S=INT(S) :: IT=S :: IF S<C1 THEN 1110
1165 CALL CLEAR :: CALL SCREEN(CD) :: CALL COLOR(C9,13,15)
1170 FOR A=C3 TO CZ :: CALL HCHAR(A,C2,102,31) :: NEXT A
1180 CALL VCHAR(C5,C3,96,C9) :: CALL VCHAR(C5,C5,96,C9) :: CALL VCHAR(C5,27,96,C9) :: CALL VCHAR(C5,29,96,C9)
1181 A$=CHR$(98)&CHR$(97)&CHR$(99) :: B$=CHR$(CH)&CHR$(97)&CHR$(101)
1182 DISPLAY AT(C4,C1):A$; :: DISPLAY AT(C4,25):A$; :: DISPLAY AT(14,C1):B$; :: DISPLAY AT(14,25):B$; :: Z=28 :: FOR A=1 TO 9
1183 IF A>5 THEN Z=4
1184 CALL VCHAR(N(A),Z,ASC(C$(A))) :: NEXT A
1185 CALL COLOR(11,C5,C8) :: CALL VCHAR(C3,C7,112) :: CALL VCHAR(C3,25,113) :: CALL VCHAR(CZ,C7,114) :: CALL VCHAR(CZ,25,115)
1186 CALL VCHAR(C4,C7,117,12) :: CALL VCHAR(C4,25,118,12) :: CALL HCHAR(C3,C8,116,17) :: CALL HCHAR(CZ,C8,119,17) :: DISPLAY AT(C0,11):"AIRLINE"
1188 CALL COLOR(12,C2,C8) :: CALL COLOR(13,C2,C8) :: CALL COLOR(14,C2,C8) :: FOR A=C4 TO 15 :: CALL HCHAR(A,C8,122,17) :: NEXT A
1190 FOR NP=C1 TO C4 :: TM(NP)=C0-100000*(IP(NP)<C3) :: GOSUB 57 :: NEXT NP
1192 FOR IL=C0 TO C7 :: FOR II=C0 TO C7 :: PB(IL,II)=C0 :: PP(IL,II)=C0 :: NEXT II :: NEXT IL
1193 GOSUB 6800 :: GOTO 100
1300 A$=STR$(NP)&C$(PP(X,Y))
1320 DISPLAY AT(C2+C2*Y,C3+C3*X):CHR$(136);CHR$(136); :: DISPLAY AT(C2+C2*Y+C1,C3+C3*X):A$; :: RETURN
6600 CALL KEY(C0,K,S) :: IF S<C1 THEN 6600 ELSE A$=CHR$(K) :: RETURN
6700 DISPLAY AT(20,C2):"?" :: A=C1 :: N$=""
6735 GOSUB 6600 :: IF A$=CHR$(13)THEN 6799
6750 N$=N$&A$ :: DISPLAY AT(20,C2+A):A$ :: A=A+C1 :: IF A<>CD THEN 6735
6799 SM$(IL)=N$ :: DISPLAY AT(20,C2):" " :: RETURN
6800 FOR AA=C1 TO C6 :: FOR BB=C1 TO C6
6840 IF PB(AA,BB)=C0 THEN 6860
6850 X=AA :: Y=BB :: TP=NP :: NP=PB(X,Y) :: GOSUB 1300 :: NP=TP :: GOTO 6890
6860 IF PU(AA,BB)=C8 THEN C=C6 :: GOTO 6880
6865 IF PU(AA,BB)>C4 THEN C=C5 :: GOTO 6880 ELSE C=PU(AA,BB)
6880 DISPLAY AT(C2+C2*BB,C3+C3*AA):CT$(C,C0); :: DISPLAY AT(C2+C2*BB+C1,C3+C3*AA):CT$(C,C1);
6890 NEXT BB :: NEXT AA :: RETURN
7100 IF A=C1 THEN A=-C1 :: P=-CZ ELSE A=C1 :: P=CZ
7105 FOR B=Y TO Y+P STEP A :: CALL SPRITE(#1,103,C7,B,X) :: NEXT B :: Y=Y+P :: RETURN
7200 IF A=C1 OR A=C3 THEN 7240
7210 IF A=C2 THEN A=C1 :: P=CW ELSE A=-C1 :: P=-CW
7230 FOR B=X TO X+P STEP A :: CALL SPRITE(#C2,140,C5,Y,B) :: NEXT B :: X=X+P :: RETURN
7240 IF A=C1 THEN A=-C1 :: P=-CZ ELSE A=C1 :: P=CZ
7250 FOR B=Y TO Y+P STEP A :: CALL SPRITE(#C2,140,C5,B,X) :: NEXT B :: Y=Y+P :: RETURN
10000 CALL DELSPRITE(ALL) :: CALL MAGNIFY(C2) :: CALL SPRITE(#C1,103,C7,Y,X)
10005 GOSUB 6600 :: IF A$<>" " AND A$<>"E" AND A$<>"X" THEN 10005
10010 IF A$="X" AND Y=63 AND X=23 THEN A=C2 :: GOSUB 7100 :: A=C2 :: GOSUB 7100 :: GOTO 10005
10015 IF A$="X" AND Y<95 THEN A=C2 :: GOSUB 7100 :: GOTO 10005
10020 IF A$="E" AND X=23 AND Y=95 THEN A=C1 :: GOSUB 7100 :: A=C1 :: GOSUB 7100 :: GOTO 10005
10025 IF A$="E" AND Y>31 THEN A=C1 :: GOSUB 7100 :: GOTO 10005
10030 IF A$=" " THEN CALL DELSPRITE(ALL) :: RETURN
10040 GOTO 10005
20000 CALL DELSPRITE(ALL) :: CALL MAGNIFY(C3) :: CALL SPRITE(#C2,140,C5,Y,X)
20005 GOSUB 6600 :: IF A$<>" " AND A$<>"E" AND A$<>"S" AND A$<>"X" AND A$<>"D" THEN 20005
20010 IF A$=" " THEN RETURN
20015 IF A$="E" AND Y>25 THEN A=C1 :: GOTO 20050
20020 IF A$="X" AND Y<105 THEN A=C3 :: GOTO 20050
20030 IF A$="S" AND X>57 THEN A=C4 :: GOTO 20050
20040 IF A$="D" AND X<177 THEN A=C2 :: GOTO 20050 ELSE GOTO 20005
20050 GOSUB 7200 :: GOTO 20005
