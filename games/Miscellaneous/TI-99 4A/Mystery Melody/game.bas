10 DATA MYSTERY MELODY
12 DATA @1980  TEXAS INSTRUMENTS
14 DATA INCORPORATED
16 DATA INITIALIZING - PLEASE WAIT
18 REM
19 REM  (* SETUP SCREEN *)
30 DIM H(25),S(60),D(60)
35 DIM T$(99),PS(96)
40 CALL SCREEN(8)
50 CALL CLEAR
52 FOR I=1 TO 8
54 CALL COLOR(I,2,8)
56 NEXT I
58 CALL VCHAR(1,31,1,96)
60 CALL SCREEN(5)
68 DEF CTR$(X$)=SEG$("          ",1,14-LEN(X$)/2)&X$
69 CALL CHAR(64,"3C4299A1A199423C")
70 READ A$,B$,C$,D$
72 PRINT CTR$(A$): : : : :CTR$(B$): :CTR$(C$): : : : : : :CTR$(D$): : :
90 RANDOMIZE
100 REM  (* INITIALIZE *)
102 H1=2^(1/12)
105 H(0)=30000
106 H(1)=262
110 FOR I=2 TO 25
115 H(I)=H(I-1)*H1
120 NEXT I
130 T=200
140 PS$="RC1D1EF1G1A1B%.#84+2-111"
142 FOR I=1 TO LEN(PS$)
144 PS(ASC(SEG$(PS$,I,1)))=I
146 NEXT I
150 READ T$(N+1)
160 IF T$(N+1)="END" THEN 200
170 N=N+1
175 READ S$
180 GOTO 150
200 REM  (* GET PARAMETERS *)
201 CALL VCHAR(1,3,32,672)
202 PRINT STR$(N);" SONGS IN MEMORY.": : :
205 INPUT "1 OR 2 PLAYERS? ":NP
207 IF NP=-1 THEN 600
210 IF(NP<>1)*(NP<>2)THEN 205
215 INPUT "MENU SIZE (2-5)? ":M
220 IF(M<2)+(M>5)THEN 215
250 FOR Q=1 TO 10
251 NS=NS+1
252 PRINT: : :"GET READY FOR THE ";SEG$("FIRSTNEXT",1-5*(Q>1),4-(Q=1));" SONG": :
255 FOR I=1 TO M
260 P=INT(RND*N)+1
270 FOR J=1 TO I-1
275 IF P=C(J)THEN 260
280 NEXT J
285 C(I)=P
290 NEXT I
300 REM  (* ANALYZE A SONG *)
301 P=1+INT(RND*M)
305 RESTORE 900
310 FOR I=1 TO C(P)
315 READ A$,S$
320 NEXT I
325 O1=0
327 D1=2
329 K=0
330 FOR I=1 TO LEN(S$)
332 C$=SEG$(S$,I,1)
335 X=PS(ASC(C$))
340 IF X>13 THEN 360
342 IF X=0 THEN 390
345 K=K+1
350 S(K)=X-1-O1*(X>1)
355 D(K)=D1
359 GOTO 390
360 ON X-13 GOTO 362,370,362,380,380,385,380,385,390,390,380
362 IF S(K)=0 THEN 390
363 S(K)=S(K)+X-15
364 GOTO 390
370 D1=D1*1.5
372 D(K)=D1
375 GOTO 390
380 D1=X-16
382 D(K)=D1
384 GOTO 390
385 O1=-12*(X=19)
390 NEXT I
392 K=K+1
395 D(K)=2
397 S(K)=0
398 IF NP<0 THEN 500
400 REM  (* SETUP MENU *)
401 CALL VCHAR(1,3,32,672)
402 PRINT "SONG";NS;"IS WHICH ONE?": : :
405 FOR I=1 TO M
410 PRINT STR$(I);
415 IF NP=1 THEN 425
420 PRINT "/";STR$(I+5+10*(I=5));
425 PRINT TAB(5);T$(C(I))
430 NEXT I
445 PRINT: :
455 E1=1
460 E2=(NP=2)
500 REM  (* PLAY & CHECK KEYBOARD *)
502 FOR I=1 TO K
505 CALL KEY(0,CH,ST)
510 IF ST THEN 540
515 CALL SOUND(T*D(I),H(S(I)),1)
520 NEXT I
522 IF NP<0 THEN 600
525 PRINT T$(C(P))
530 PRINT "NO SCORE."
535 GOTO 800
540 W=CH-48-10*(CH=48)
545 IF E1*(W=P)+E2*(W=P+5)THEN 700
550 E1=E1*((W<1)+(W>5))
555 E2=E2*((W<5)+(W>10))
560 GOTO 515
600 REM   (* TEST MODE *) 
601 PRINT
602 PRINT "SONG TITLE?"
603 INPUT "":C$
605 IF C$="STOP" THEN 999
607 RESTORE 900
610 FOR I=1 TO N
615 READ A$,S$
620 IF A$=C$ THEN 650
625 NEXT I
630 PRINT "NOT FOUND."
635 GOTO 600
650 PRINT S$
655 GOTO 300
700 REM  (* AWARD POINTS *) 
702 G=1-(W=P+5)
705 PRINT T$(C(P))
710 PT=INT(21-19*I/K)
715 PRINT "PLAYER";G;"GETS";PT;"POINTS."
720 SC(G)=SC(G)+PT
800 REM  (* DISPLAY TOTALS *)
802 PRINT
805 PRINT:"PLAYER 1:";SC(1);
810 IF NP=1 THEN 820
815 PRINT TAB(16);"PLAYER 2:";SC(2);
820 PRINT
823 NEXT Q
825 PRINT: : : : :
826 FOR J=1 TO 1500
827 NEXT J
830 CALL VCHAR(1,3,32,672)
835 PRINT "AFTER";NS;"SONGS:"
840 PRINT "  PLAYER 1 HAS";SC(1)
842 IF NP=1 THEN 880
845 PRINT "  PLAYER 2 HAS";SC(2)
850 PRINT
855 IF SC(1)=SC(2)THEN 870
860 PRINT "PLAYER";1-(SC(2)>SC(1));"IS WINNING."
865 GOTO 880
870 PRINT "TIE GAME!"
880 PRINT: : :
885 INPUT "PLAY SOME MORE? ":Y$
890 IF SEG$(Y$,1,1)="Y" THEN 250
895 IF SEG$(Y$,1,1)="N" THEN 999 ELSE 885
900 REM  (* SONGS *)
901 DATA JINGLE BELLS,E8EEREEEREGCDE2F8FFFFEEEEDDED4G
902 DATA SILENT NIGHT,G4.A8G4E2.G4.A8G4E2.+D2D4C%2.C2C4-G2.
903 DATA DECK THE HALLS,A.G8F#4EDEF#DE8F#GEF#4.E8D4C#D2
904 DATA JOY TO THE WORLD,B#B8AG4.F8E4DC.G8A4.A8B4.B8B#1
905 DATA THE FIRST NOEL,E8DC4.D8EFG2A8BB#4BAG2A8BB#4BAGABB#GFE2
906 DATA COME ALL YE FAITHFUL,GG2D4GA2DB4ABB#B2A4GG2F#4EF#GABF#2E4.D8D1
907 DATA THE BAND PLAYED ON,B!+D-B!A+D-AGB!GF2G8B!8A2.E!2.E!2.
908 DATA OLD VIRGINNY,+E8E4F8E4.D8C4-AG8+C4.-G4+C8DEEFEEC-B+CD2
909 DATA TOWN OF BETHLEHEM,AAAA%AB#B%DGFE8FG4CA2.A4AA+DCC-B%DGFE8FA4GF2.
910 DATA LOCH LOMOND,D8EG4G8AB4A8GA4A8GE4D8E8G4GGB8+DE2D4DEE8D4-B4.+D8C-BAGE4D8EG4B8+D8E4D8-B8A2G
911 DATA DEEP RIVER,A2A8GF4GD2F4F4.F8+F4.D8C4-AA8GF4A2A8GF4GD4F4.F8FFF4F8DCDF2F2
912 DATA DRINK TO ME,G8GGA!4A!8B!A!GFGA!B!E!A!G4F8E!4.E!4R8G8GGA!4A!8B!A!GFGA!B!E!A!G4F8E!4.E!8
913 DATA MY OLD KENTUCKY HOME,B8B4BGA8BB#8B8B#+ED4R8-B#8BA4G8G4F#8GA2.R8A8B4BGA8BB#BB#+ED4-G8A8B4BA8G8BA8G2.
914 DATA WE THREE KINGS,A2G4F2D4EFED2.A2G4F2D4EFED2.F2F4G2G4A2A4B#B%AGAGF2E4D1.
915 DATA "HARK, THE HERALD ANGELS",DGG.F#8G4BBA+DDD.C8-B4AB2D4GG.F#8G4BBA+D-AAGF#ED2
916 DATA O CHRISTMAS TREE,D.G4G8G4.AB4B8B2.A4B8B#4.F#A4G8G2.
917 DATA MY WILD IRISH ROSE,G4+E2.E2D4C2.C2C4C2C4-B2A4G2.G2E8EF4GAG2E4FGAG2B8+C8-A2.+E2D4D2.D2
918 DATA THE TROUBLE I'VE SEEN,B8D4E8G4.A8BBB4B2B8D4E8G4GEDR2B8D4E8G4.A8BBB4B2+D4.-B8A4BG2G4
919 DATA REST YE MERRY GENTLEMEN,DDAAGFEDCDEFGA2.D4DAAGFEDCDEFGA2.
920 DATA POLLY WOLLY DOODLE,G8AB4BGG8AB4BGGB8BBB+CC-BBA2F#8GA4AF#F#AAF#A+D8DDDCC-AAG2.
921 DATA SING A SONG OF SIXPENCE,+G8AGEC4B#8B#BBDEF2F8GFD-B4+AGC8DE2
922 DATA AWAY IN A MANGER,CB#.B%8A4AGFFEDC2C4C.D8C4CGEDCFA2
923 DATA BATTLE HYMN,G8G4G8G4F8E4G8+C4D8E4E8E4D8C4.C4-B8A4A8A4B8B#4B8B#4A8G4A8G4E8G4
924 DATA SWING LOW SWEET CHARIOT,B4G2B4G4.G8ED4.G8GGGBB+DD1E8D-B2+D4-G4.G8ED4.G8GGGBBA4G2.
925 DATA TURKEY IN THE STRAW,+B8AG4GG-B8+CD4DDG8AB4BB8AGAB4AAB8AG4GG-B8+CD4DDG8AB4+D4D8-BGAB4AG
927 DATA BEAUTIFUL DREAMER,B#BB#G2.ED4D%DA1.G4BAAGFFEDE1.
928 DATA BRAHMS' LULLABY,E8EG2E8EG2E8GB#4B.A8A4GD8EF2D8EF2D8FBAG4BB#2
929 DATA AULD LANG SYNE,+A4G.E8E4CD.C8D4AG.E8E4GA1
930 DATA SWANEE RIVER,E2.D4C8E4D8C4.B#A8B#2B#8G2.E4.CD1.
931 DATA BICYCLE BUILT FOR TWO,B#4.AFCD8EFD4F8C2.G4.B#AFD8EFG4A8G2.
932 DATA MAN ON FLYING TRAPEZE,C8CC4FGAAAB%DDG2G8GC4EFGAGGFDC2
933 DATA ALOUETTE,G.A8B4BA8GABG4DG.A8B4BA8GABG2
934 DATA MY BONNIE,G+EDCDC-AGE1G4+EDCCC%CD1
935 DATA SHE'LL BE COMIN' 'ROUND,G8A+CCCC-AGEG+C2.C8DEEEEGGFED2.
936 DATA OH! SUSANNA,C8DE4GG.A8G4EC.D8E4EDCD2.C8DE4GG.A8G4EC.D8E4EDDC1
937 DATA THE CAMPTOWN RACES,GGGEGAGE2E4D2.E4D2G4GGEGAGE2D4E8FE4DC1
943 DATA SKIP TO MY LOU,F#F#DDF#F#A2E4EC#C#EEG2F#4F#DDF#F#A2E4F#8GF#4ED2D2
944 DATA SHENANDOAH,DG8GG4.A8B+CE4D2G8F#E2R8DEDC%DD2D4E8EE4.C%8D-BAG2.
945 DATA SWEET BETSY FROM PIKE,D8DD4F#AAGF#EDDDRDDF#A+DDDC#-AAA
946 DATA HOME ON THE RANGE,CCFGA2F8ED4.B%8B%4B%2A8B%B#2F8FF4EFG1
947 DATA RED RIVER VALLEY,CFA2A4GF2G4FDFF1C4FA2F4AB#2B%4AG1
950 DATA DIXIE,G8EC4CC8DEFG4GGEAAA.G8A4.G8AB+CDE2.C8-GB#2.G8EG2.D8EC2.
951 DATA WORKING ON THE RAILROAD,G2G8DG4D8G4A8B2.GB#G4.A4.B1.G2G8DG4D8G4A8B2.G4.B4B8B4.AABA2.
952 DATA BLOW THE MAN DOWN,+CCDC-AFA+CDC-A2.+CD-B%4AB%G2.B%4B#B%GEGB%B#B%G2.
954 DATA YANKEE DOODLE,G8GABGBADGGABG4F#G8GABB#BAGF#DEF#G4G
955 DATA AMERICA,GGAF#.G8A4BBB#B.A8G4AGF#G2.
956 DATA STARS AND STRIPES,G4GF8EE4E%8EE2.E%8EE4E%8EG4E8GF2D.
958 DATA GREENSLEEVES,A+C2D4E.F8E4D2-B4G.A8B4B#2A4A.G#8A4B2.E
959 DATA STAR SPANGLED BANNER,GE8C4.EG+C2.E4D8C4.-EF#G2.G4G8+E2E8DC4.-B2.A4B8B#4.B#GEC
960 DATA SAINTS GO MARCHING IN,C8EFG2R8CEFG2R8CEFG4ECED2R8EEDC4.C8E4GG8F4.R4E8FG4ECDC2.
999 DATA END
