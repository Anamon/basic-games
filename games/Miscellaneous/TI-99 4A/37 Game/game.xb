100 CALL CLEAR :: CALL SCREEN(16) :: DISPLAY AT(3,8):"THE `37' GAME"!by Jim Peterson
110 DISPLAY AT(5,1):" We will take turns picking":"a number from 1 to 5, but":"not the number that was just":"picked."
120 DISPLAY AT(10,1):" The numbers we pick will be":"added to the total count."
130 DISPLAY AT(13,1):" Whoever reaches 37 is the":"winner, but if you go over":"37 you lose."
140 CALL SHOW(20,1,"Press any key to start")
150 CALL KEY(0,K,S) :: IF S=0 THEN 150
160 DATA 4,11,17,24,30,37
170 DATA 262,330,392,523,523
180 DATA 1047,784,659,523,523
190 C,P=0 :: CALL CLEAR :: CALL MAGNIFY(2) :: R=10 :: FOR J=1 TO 5 :: CALL SPRITE(#J,48+J,5,R,10) :: R=R+30 :: NEXT J
200 CALL SHOW(24,1,"(Y)ou or (M)e first?") :: ACCEPT AT(24,22)VALIDATE("YM")SIZE(1):Q$ :: DISPLAY AT(24,1):""
210 IF Q$="Y" THEN CALL SHOW(22,8,"I pick 4") :: CALL COLOR(#4,1) :: P=4 :: C=4 :: CALL SHOW(3,10,"COUNT=4")
220 CALL SHOW(20,8,"Pick your number") :: ACCEPT AT(20,26)VALIDATE("12345"):N :: IF N=P THEN 220
230 IF P>0 THEN CALL COLOR(#P,5)
240 CALL COLOR(#N,1) :: P=N :: C=C+N :: CALL SHOW(3,10,"COUNT= "&STR$(C)) :: IF C=37 THEN 320 ELSE IF C>37 THEN 340
250 RESTORE 160
260 READ X :: IF C<X THEN B=X-C ELSE IF X<37 THEN 260
270 CALL SHOW(22,8,"I'm thinking...") :: FOR Y=1 TO 700 :: NEXT Y
280 IF B>5 AND B/2=INT(B/2)THEN B=B/2
290 IF B>5 OR B=P THEN B=1-(P=1)
300 CALL SHOW(22,8,"I pick "&STR$(B)) :: CALL COLOR(#P,5) :: CALL COLOR(#B,1) :: P=B :: C=C+B :: CALL SHOW(3,10,"COUNT= "&STR$(C))
310 IF C=37 THEN 340 ELSE IF C>37 THEN 320 ELSE 220
320 RESTORE 170 :: FOR J=1 TO 5 :: READ F :: CALL SOUND(100,F,5,F*1.03,5) :: NEXT J :: CALL SHOW(12,8,"YOU WIN!")
330 CALL SHOW(15,8,"Play again? (Y/N)") :: ACCEPT AT(15,26)VALIDATE("YN"):Q$ :: IF Q$="N" THEN STOP ELSE 190
340 RESTORE 180 :: FOR J=1 TO 5 :: READ F :: CALL SOUND(300,30000,30,30000,30,F,30,-4,5) :: NEXT J :: CALL SHOW(12,8,"YOU LOSE!") :: GOTO 330
350 SUB SHOW(R,C,T$) :: FOR J=1 TO 10 :: DISPLAY AT(R,C):" " :: DISPLAY AT(R,C):T$ :: NEXT J :: SUBEND
