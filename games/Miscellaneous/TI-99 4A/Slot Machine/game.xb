10 REM  TI(EXTENDED)-SLOT MACHINE
20 CALL CLEAR :: RANDOMIZE
30 DIM WF(13),R(2,13),JAC(13) :: JAC(5)=-1 :: JAC(6)=-1 :: JAC(7)=-1
40 FOR VV=1 TO 13 :: READ WF(VV),R(1,VV),R(2,VV) :: NEXT VV
50 DATA 110,0,0,116,0,0,115,0,0,109,0,0,106,8,50,105,8,40,114,7,30
60 DATA 113,5,25,104,3,20,136,3,20,137,2,10,128,2,10,112,0,0
70 DIM JPX(4),JPY(4),JF(4),JCC(4) :: FOR JO=1 TO 4 :: READ JPX(JO),JPY(JO),JF(JO) :: NEXT JO
80 DATA 11,8,110,12,8,116,14,8,115,15,8,109
90 NJ$=CHR$(138)
100 DIM WPX(3),WPY(3),V(4),HVV(3) :: WPX(1)=11 :: WPX(2)=13 :: WPX(3)=15 :: FOR I=1 TO 3 :: WPY(I)=10 :: NEXT I
110 DIM HPX(3),HPY(3),HF$(3) :: FOR I=1 TO 3 :: HPX(I)=WPX(I) :: HPY(I)=13 :: NEXT I
120 H1$=CHR$(107) :: NH$=CHR$(108) :: HH$=CHR$(96) :: HO$=H1$&HH$&H1$&HH$&H1$
130 DIM GPX(4),GPY(4),GV(4) :: GPX(1)=22 :: GPY(1)=1
140 GPX(2)=27 :: GPY(2)=2 :: GPX(3)=22 :: GPY(3)=3 :: GPX(4)=17 :: GPY(4)=2
150 NJ=4 :: NH=0 :: NG=0 :: GW=0 :: NI=0 :: NT=-1
160 GOTO 1740
170 REM  BLINK S1$/S2$ AND GEET IN
180 DISPLAY AT(PLY,PLX):S1$; :: CH=0 :: FOR DE=1 TO 10
190 IF CH=0 THEN CALL KEY(0,GET,CH)
200 NEXT DE :: DISPLAY AT(PLY,PLX):S2$; :: IF CH=1 THEN RETURN
210 FOR DE=1 TO 10 :: IF CH=0 THEN CALL KEY(0,GET,CH)
220 NEXT DE :: IF CH=1 THEN RETURN
230 GOTO 180
240 REM  AND INCREMENT TO MONEY
250 FOR AD=SGN(IN)TO IN STEP SGN(IN)
260 DISPLAY AT(1,7):MO+AD;
270 IF SGN(IN)>0 THEN 290
280 CALL SOUND(-50,200,2) :: GOTO 300
290 FOR SO=500 TO 700 STEP 100 :: CALL SOUND(-50,SO,2) :: NEXT SO
300 NEXT AD :: MO=MO+IN :: RETURN
310 REM  REMOVE DOUBLE
320 DO=0 :: CALL HCHAR(2,3,32,7)
330 FOR JO=1 TO 4 :: CALL HCHAR(JPY(JO),JPX(JO)+2,JF(JO)) :: JCC(JO)=0 :: NEXT JO
340 NJ=4 :: RETURN
350 CALL HCHAR(24,3,32,28) :: RETURN
360 REM  JACKPOT
370 FOR TI=1 TO 4 :: CALL HCHAR(4,3,32,JC)
380 FOR C=1 TO JC :: CALL SOUND(-100,150+20*C,0) :: DISPLAY AT(4,C):"J"; :: NEXT C
390 NEXT TI :: RETURN
400 REM  DEFINE CHARACTERS
410 CALL CHAR(96,"FFFFFFFFFFFFFFFF")
420 CH$="0F0F0F0F0F0F0F0F" :: CALL CHAR(97,CH$)
430 CALL CHAR(60,CH$)
440 CALL COLOR(9,6,1,10,2,16,11,7,16,12,16,6,13,11,16,14,13,16)
450 CALL CHAR(107,"FFBBBBBB83BBBBBB")
460 CALL CHAR(108,"FFFFFFFFFFFFFFFF")
470 A$="1898FF3D3C3CE404"
480 CALL CHAR(109,A$) :: CALL CHAR(115,A$)
490 A$="1819FFBC3C3C2720"
500 CALL CHAR(110,A$) :: CALL CHAR(116,A$)
510 CALL CHAR(136,"02043C566A566A3C")
520 CALL CHAR(114,"00006C7C7C381000")
530 CALL CHAR(112,"0204387C7C7C3800")
540 CALL CHAR(104,"02020C3C78783000")
550 CALL CHAR(128,"00406060703C1E00")
560 CALL CHAR(137,"020413C37C7C3800")
570 CALL CHARPAT(36,A$) :: CALL CHAR(106,A$)
580 CALL CHARPAT(63,A$) :: CALL CHAR(120,A$)
590 CALL CHAR(113,"081C3E7F3EE1C0800")
600 CALL CHAR(104,"081C2A772A080800")
610 CALL CHAR(105,"0010387C7C103800")
620 CALL CHAR(138,"0000000000000000")
630 RETURN
640 REM **DRAW SCREEN
650 CALL CLEAR :: CALL SCREEN(12)
660 CALL HCHAR(7,12,96,7)
670 FOR I=8 TO 12 :: CALL HCHAR(I,11,96,9) :: NEXT I
680 CALL VCHAR(7,20,60)
690 CALL VCHAR(8,20,97,4)
700 CALL HCHAR(12,20,96)
710 CALL HCHAR(13,12,96,7)
720 FOR I=14 TO 19 :: CALL HCHAR(I,13,96,5) :: NEXT I
730 CALL HCHAR(20,12,96,7)
740 CALL HCHAR(21,11,96,9)
750 CALL HCHAR(22,10,96,11)
760 DISPLAY AT(1,1):"TOTAL:0"
770 DISPLAY AT(10,3):CHR$(112);" = 1";
780 FOR VV=12 TO 5 STEP-1
790 DISPLAY AT(23-VV,1):"-";RPT$(CHR$(WF(VV)),2);"-=";R(1,VV);
800 DISPLAY AT(23-VV,20):RPT$(CHR$(WF(VV)),3);"=";R(2,VV);
810 IF JAC(VV)THEN DISPLAY AT(23-VV,27):"+J";
820 NEXT VV :: RETURN
830 REM  ** ADAPT VARIABLES
840 NT=NT+1 :: IF WI>0 THEN HB=-1 :: WI=0
850 FOR HO=1 TO 3 :: HF$(HO)=NH$ :: DISPLAY AT(HPY(HO),HPX(HO)):NH$; :: NEXT HO
860 IF DO THEN 910
870 FOR JO=1 TO 4 :: IF JCC(JO)=0 THEN 900
880 JCC(JO)=JCC(JO)-1 :: IF JCC(JO)>0 THEN 900
890 NJ=NJ+1 :: CALL HCHAR(JPY(JO),JPX(JO)+2,JF(JO))
900 NEXT JO :: GOTO 920
910 DC=DC-1 :: IF DC=0 THEN GOSUB 320
920 IF JA THEN DISPLAY AT(4,JC):" "; :: JC=JC-1 :: IF JC=0 THEN JA=0
930 RETURN
940 REM  ** NOT ENOUGH MONEY
950 HB=-1 :: GOSUB 320
960 JA=0 :: CALL HCHAR(4,3,32,15)
970 GOSUB 350 :: DISPLAY AT(24,1):"INSERT OR END";
980 S1$="INSERT (I)" :: S2$=RPT$("  ",10) :: PLX=1 :: PLY=2 :: GOSUB 180
990 IF GET=ASC("P")THEN 980 ELSE RETURN
1000 REM  ** HOLD POSSIBLE
1010 DISPLAY AT(24,1):"INSERT, HOLD,PLAY OR END";
1020 S1$=HO$ :: S2$=HF$(1)&HH$&HF$(2)&HH$&HF$(3)
1030 PLX=11 :: PLY=13 :: GOSUB 180
1040 IF GET<49 OR GET>51 THEN RETURN ELSE HO=GET-48
1050 NH=NH+1 :: IF HF$(HO)=NH$ THEN HF$(HO)=H1$ ELSE HF$(HO)=NH$
1060 GOTO 1020
1070 REM  ** NO HOLD
1080 GOSUB 350 :: DISPLAY AT(24,1):"INSERT,PLAY OR END";
1090 S1$=RPT$(CHR$(120),3) :: S2$=RPT$(CHR$(96),3) :: PLX=12 :: PLY=19 :: GOSUB 180
1100 RETURN
1110 REM  ** WHAT TO DO WITH WINNINGS?
1120 GOSUB 350 :: IF NOT HB THEN DISPLAY AT(24,1):"HOLD, ";
1130 DISPLAY AT(24,7):"GAMBLE OR COLLECT";
1140 GOTO 1090
1150 REM  ** SPIN GAMBLE WHEELS
1160 FOR I=7 TO 10 :: CALL HCHAR(I,20,32) :: CALL HCHAR(I+1,20,60) :: NEXT I
1170 CALL SOUND(-4000,-7,29)
1180 FOR I=10 TO 7 STEP-1 :: CALL HCHAR(I,20,60) :: CALL HCHAR(I+1,20,97) :: NEXT I
1190 FOR WD=1 TO 3 :: IF HF$(WD)=NH$ THEN DISPLAY AT(WPY(WD),WPX(WD)):CHR$(138);
1200 NEXT WD
1210 FOR WD=1 TO 3 :: IF HF$(WD)=H1$ THEN 1240
1220 FI=INT(RND*100+1) :: IF FI<5 THEN IF JCC(FI)<>0 THEN V(WD)=13 ELSE V(WD)=FI :: JCC(FI)=-1 :: GOTO 1240
1230 V(WD)=5-(FI>7)-(FI>10)-(FI>13)-(FI>23)-(FI>36)-(FI>49)-(FI>68)-(FI>87)
1240 FOR DE=2 TO 300 :: NEXT DE :: DISPLAY AT(WPY(WD),WPX(WD)):CHR$(138);
1250 CALL SOUND(-100,300,2) :: CALL HCHAR(WPY(WD),WPX(WD)+2,WF(V(WD)))
1260 CALL SOUND(4000,-7,29) :: NEXT WD :: CALL SOUND(-1,-2,30) :: RETURN
1270 REM  TAKE CARE OF JOKERS
1280 J=0 :: FOR WD=1 TO 3 :: VV=V(WD)
1290 IF VV>4 THEN FV=VV :: GOTO 1320
1300 JW=WD :: J=J+1 :: IF JCC(VV)>0 THEN 1320
1310 DISPLAY AT(JPY(VV),JPX(VV)):NJ$; :: JCC(VV)=20 :: NJ=NJ+1
1320 NEXT WD :: DO=(NJ=0) :: IF DO THEN DC=15 :: DISPLAY AT(2,1):"DOUBLE!";
1330 RETURN
1340 REM  ** COMPUTE WINNINGS
1350 HV=13 :: ON J+1 GOTO 1360,1380,1360,1410
1360 IF NOT(JA AND FV>10)THEN HV=FV
1370 GOTO 1410
1380 V(0)=V(3) :: V(4)=V(1) :: IF JA AND V(JW-1)<>V(JW+1)THEN 1410
1390 V(0)=15 :: V(4)=15
1400 IF V(JW+1)>V(JW-1)THEN HV=V(JW-1)ELSE HV=V(JW+1)
1410 FOR WD=1 TO 3 :: IF V(WD)<5 THEN HVV(WD)=HV ELSE HVV(WD)=V(WD)
1420 NEXT WD :: IF HVV(1)<>HVV(2)OR HVV(2)<>HVV(3)THEN 1440
1430 IF JAC(HVV(1))THEN JA=-1 :: JC=15 :: GOSUB 370
1440 FOR WD=1 TO 3 :: IF HVV(WD)=13 THEN WI=WI+1
1450 NEXT WD :: IF JA THEN WI=10*WI
1460 NS=-(HVV(1)=HVV(2))-(HVV(2)=HVV(3)) :: IF NS>0 THEN WI=WI+R(NS,HVV(2))
1470 IF DO THEN WI=2*WI
1480 RETURN
1490 REM  *** GAMBLE ROUTINE
1500 DT=1 :: GV(1)=2*WI :: GV(2)=0 :: GV(3)=INT(3*WI/2) :: GV(4)=INT(WI/2)
1510 KEY=0 :: NG=NG+1 :: GOSUB 350 :: DISPLAY AT(24,1):"STOP";
1520 FOR LI=1 TO 3 :: CALL HCHAR(LI,16,96,17) :: NEXT LI
1530 RR=RR+1 :: IF RR>4 THEN RR=1
1540 DISPLAY AT(GPY(RR),GPX(RR)):STR$(GV(RR));
1550 CALL SOUND(-4000,150+50*RR,2)
1560 IF KEY<>ASC("S")THEN CALL KEY(0,KEY,CH) :: GOTO 1580
1570 DT=(1+RND*2)*DT :: FOR DE=1 TO DT :: NEXT DE :: IF DT>150 THEN 1590
1580 CALL HCHAR(GPY(RR),GPX(RR)+2,96,4) :: GOTO 1530
1590 FOR LI=1 TO 3 :: CALL HCHAR(LI,16,32,17) :: NEXT LI
1600 GW=GW+GV(RR)-WI :: WI=GV(RR) :: CALL SOUND(-1,150+50*RR,2) :: RETURN
1610 REM  *** END OF GAME
1620 CALL CLEAR :: CALL CHARSET :: CALL SCREEN(8)
1630 DISPLAY AT(5,1):"AMOUNT OF MONEY:"
1640 IMAGE " $ ####.##"
1650 DISPLAY AT(7,3):"PUT IN:"; :: DISPLAY AT(7,18):USING 1640:NI
1660 DISPLAY AT(8,3):"GOT BACK:"; :: DISPLAY AT(8,18):USING 1640:MO/4
1670 DISPLAY AT(9,3):"MAX AT ONE TIME:"; :: DISPLAY AT(9,18):USING 1640:MM-4
1680 DISPLAY AT(10,3):"WON BY GAMBLING:"; :: DISPLAY AT(10,18):USING 1640:GW/4
1690 DISPLAY AT(12,1):"NUMBER OF HOLDS:  ";NH
1700 DISPLAY AT(13,1):"NUMBER OF GAMBLES:";NG
1710 DISPLAY AT(14,1):"NUMBER OF TURNS:  ";NT
1720 RETURN
1730 REM  MAIN PROGRAM
1740 GOSUB 410!DEF CHAR
1750 GOSUB 650!SCREEN
1760 GOSUB 840!ADAPT
1770 ON-2*HB-(MO>1)+1 GOSUB 950,1010,950,1080
1780 DISPLAY AT(5,9) :: CA=-(GET=ASC("I"))-2*(GET=ASC("P"))-3*(GET=ASC("E"))
1790 ON CA+1 GOTO 1770,1800,1820,2020
1800 NI=NI+1 :: IN=4 :: GOSUB 250
1810 GOTO 1770
1820 IN=-2 :: GOSUB 250
1830 GOSUB 1160!SPIN WHEELS
1840 IF DO THEN 1860
1850 GOSUB 1280!JOKERS
1860 GOSUB 1350!WINNINGS
1870 IF HF$(1)=H1$ OR HF$(2)=H1$ OR HF$(3)=H1$ THEN 1890
1880 HB=0 :: LW=WI :: IF WI>0 THEN 1910 ELSE 1760
1890 HB=-1 :: IF WI>LW THEN 1910
1900 DISPLAY AT(5,9):" YOU LOST" :: CALL SOUND(-600,200,2) :: GOTO 1760
1910 DISPLAY AT(5,9):"YOU WON"; :: DISPLAY AT(5,17):WI; :: FOR DU=1 TO WI :: CALL SOUND(-50,500,1) :: CALL SOUND(1,500,30) :: NEXT DU
1920 IF MO<2 THEN HB=-1
1930 GOSUB 1120!GET INSTRUCTION
1940 CA=-(GET=ASC("H")AND NOT HB)-2*(GET=ASC("G"))-3*(GET=ASC("C"))
1950 ON CA+1 GOTO 1930,2010,1990,1960
1960 IN=WI :: GOSUB 250
1970 IF WI>MM THEN MM=WI
1980 GOTO 1760
1990 HB=-1 :: GOSUB 1500!GAMBLE
2000 IF WI>0 THEN 1910 ELSE 1900
2010 WI=0 :: GOTO 1760
2020 GOSUB 1620!END
2030 END
