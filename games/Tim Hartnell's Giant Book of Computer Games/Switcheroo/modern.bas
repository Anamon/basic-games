10 REM Switcheroo
20 CLS: COLOR 15
30 PRINT: PRINT "Please stand by..."
40 GOSUB 100
50 GOSUB 230
60 IF A$ = "123456789" THEN 400
70 M = M + 1
80 GOTO 50
90 END
100 RANDOMIZE TIMER
110 M = 1: X = 0
120 A$ = ""
130 FOR T = 1 TO 9
140 L = INT(RND * 9) + 49
150 Q = 1
160 IF MID$(A$, Q, 1) = CHR$(L) THEN 140
170 IF Q < T THEN Q = Q + 1: GOTO 160
180 A$ = A$ + CHR$(L)
190 PRINT A$
200 PRINT
210 NEXT T
220 RETURN
230 REM Print out
240 CLS
250 PRINT: PRINT: PRINT
260 COLOR 12: PRINT TAB(33); "Move number"; M: COLOR 15
270 PRINT: PRINT: PRINT
280 COLOR 11: PRINT TAB(35); A$: COLOR 15
290 PRINT: PRINT: COLOR 10: PRINT TAB(26); "Which number to switcheroo?": COLOR 15
300 IF INKEY$ <> "" THEN 300
310 C$ = INKEY$
320 R = VAL(C$)
330 IF R < 1 OR R > 8 THEN 310
340 B$ = ""
350 FOR T = 9 TO R STEP -1
360 B$ = B$ + MID$(A$, T, 1)
370 NEXT T
380 A$ = LEFT$(A$, R - 1) + B$
390 RETURN
400 PRINT: PRINT: PRINT
430 COLOR 12: PRINT "You did it, champ!": COLOR 15
440 PRINT: PRINT "And it took just"; M; "moves..."
450 END

