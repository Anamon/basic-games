5 DEFINT A-Z: DIM B(3), C(9), E(3), K(100)
10 REM ELECTRONIC BRAIN - 3 digits
20 GOSUB 510: REM INITIALISE
30 REM MAKE A GUESS
40 IF GUESS < 3 THEN FOR Z = 1 TO 3: B(Z) = Z + 3 * GUESS: NEXT Z: GOTO 70
60 GOSUB 280
70 CLS
80 GUESS = GUESS + 1
90 PRINT: PRINT
100 PRINT "Guess number"; GUESS
110 PRINT
120 PRINT "My guess is"; B(1); B(2); B(3)
130 PRINT: PRINT
140 INPUT "How many blacks"; B
150 IF B = 3 THEN GOTO 600
160 IF B = 2 THEN 190
170 PRINT: PRINT
180 INPUT "And how many whites"; W
190 IF W + B = 3 THEN Q = 3: C(1) = B(1): C(2) = B(2): C(3) = B(3)
200 IF B + W = 0 THEN C(B(1)) = 0: C(B(2)) = 0: C(B(3)) = 0: GOTO 30
210 IF B > AID THEN FOR Z = 1 TO 3: E(Z) = B(Z): NEXT Z: AID = B
220 FOR Z = 1 TO 9
230 FOR D = 1 TO 3
240 IF B(D) = C(Z) THEN C(Z) = C(Z) + (B + W) * 100 + W * 10
250 NEXT D
260 NEXT Z
270 GOTO 30
280 REM Pick three numbers
290 FOR Z = 1 TO 3
300 D1 = C(INT(RND * Q) + 1)
310 IF D1 = 0 THEN 300
320 D2 = C(INT(RND * Q) + 1)
330 IF D2 = 0 THEN 320
340 IF INT(D1 / 10) > INT(D2 / 10) THEN B(Z) = D1
350 IF INT(D1 / 10) < INT(D2 / 10) THEN B(Z) = D2
360 IF INT(D1 / 10) = INT(D2 / 10) THEN B(Z) = D1
370 IF B(Z) > 100 THEN B(Z) = B(Z) - 100 * INT(B(Z) / 100): GOTO 370
380 IF B(Z) > 10 THEN B(Z) = B(Z) - 10 * INT(B(Z) / 10): GOTO 380
390 NEXT Z
400 IF B(1) = B(2) OR B(1) = B(3) OR B(2) = B(3) THEN 290
410 IF AID <= 0 THEN 430
415 COUNT = 0: FOR Z = 1 TO 3: IF B(Z) = E(Z) THEN COUNT = COUNT + 1
420 NEXT Z: IF COUNT < AID THEN 290
430 M = 100 * B(1) + 10 * B(2) + B(3)
440 K(GUESS) = M
450 IF GUESS < 3 THEN 500
460 COUNT = 1
470 COUNT = COUNT + 1
480 IF K(COUNT) = M THEN 290
490 IF COUNT < GUESS - 1 THEN 470
500 RETURN
510 REM INITIALISE
530 GUESS = 0: Q = 9: AID = 0
540 RANDOMIZE VAL(RIGHT$(TIME$, 2))
560 FOR Z = 1 TO 9
570 C(Z) = Z
580 NEXT Z
590 RETURN
600 PRINT: PRINT
610 PRINT "I guessed your code of"; B(1); B(2); B(3)
620 PRINT TAB(5); "in just"; GUESS; "guesses"
630 END

