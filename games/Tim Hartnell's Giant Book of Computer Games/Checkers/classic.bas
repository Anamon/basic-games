5 DIM Q(99), N(4), S(10)
6 WIDTH 40
10 REM CHECKERS
20 GOSUB 1010
30 REM DELETE LINE 50 FOR HUMAN
40 REM TO HAVE FIRST MOVE
50 GOTO 90
60 REM *********************************
70 GOSUB 600
80 GOSUB 760
90 GOSUB 600
100 GOSUB 130
110 GOTO 70
120 REM ********************************
130 FOR X = 1 TO 10: S(X) = 0: NEXT X
140 SC = 0: A = 89
150 A = A - 1
160 IF Q(A) <> C AND Q(A) <> CK THEN 240
170 B = 0: IF A < 29 THEN B = 2
180 B = B + 1
190 M = A + N(B)
200 IF M > 88 OR M < 11 THEN 240
210 IF (Q(M) = H OR Q(M) = HK) AND Q(M + N(B)) = E THEN 280
220 IF Q(M) = E THEN IF (Q(M - 11) <> H AND Q(M - 11) <> HK) THEN IF (Q(M - 9) <> H AND Q(M - 9) <> HK) AND Q(M + 9) <> HK THEN IF ((Q(M + 22) <> HK OR Q(M + 18) <> HK) AND (Q(M + 9) <> C OR Q(M + 9) <> CK OR Q(M + 11) = C OR Q(M + 11) = CK)) AND Q(M + 11) <> HK THEN GOSUB 400
230 IF B < 2 OR (Q(A) = CK AND B < 4) THEN 180
240 IF A > 11 THEN 150
250 FL = 0: IF Q(22) = C OR Q(24) = C OR Q(26) = C OR Q(28) = C THEN GOSUB 1270
260 IF FL = 1 THEN 570
270 GOTO 420
280 Q(M + N(B)) = Q(A): Q(M) = E: Q(A) = E
290 CO = CO + 1
300 GOSUB 600
310 A = M + N(B)
320 B = 0
330 B = B + 1
340 IF (A + 2 * N(B) < 11 OR A + 2 * N(B) > 88) AND B < 4 THEN 330
350 M = A + N(B)
360 IF Q(M) = C AND B > 3 THEN RETURN
370 IF (Q(M) = H OR Q(M) = HK) AND Q(M + N(B)) = E THEN 280
380 IF B < 2 OR (Q(A) = CK AND B < 4) THEN 330
390 RETURN
400 IF SC < 10 THEN SC = SC + 1
410 S(SC) = 100 * A + B + 20: RETURN
420 IF SC = 0 THEN 470
430 XC = INT(RND * SC) + 1
440 A = INT(S(XC) / 100)
450 M = A + N(S(XC) - 100 * A - 20)
460 GOTO 570
470 SC = SC + 1: A = INT(RND * 88) + 1
480 IF Q(A) <> C AND Q(A) <> CK THEN 550
490 B = 0
500 B = B + 1
510 M = A + N(B)
520 IF M > 88 OR M < 11 THEN 540
530 IF Q(M) = E THEN 570
540 IF B < 2 OR Q(A) = CK AND B < 4 THEN 500
550 IF SC < 300 THEN 470
560 PRINT: PRINT "I concede the game": END
570 Q(M) = Q(A): Q(A) = E
580 RETURN
590 REM ********************************
600 CLS: PRINT: PRINT
610 PRINT "      COMPUTER:"; CO
620 PRINT "         HUMAN:"; HU: PRINT: PRINT
630 PRINT "    1 2 3 4 5 6 7 8"
640 FOR F = 80 TO 10 STEP -10
650 PRINT F / 10; " ";
660 FOR G = 1 TO 8: PRINT CHR$(Q(F + G)); " ";: NEXT G
670 PRINT F / 10: NEXT F
680 PRINT "    1 2 3 4 5 6 7 8"
690 IF CO = 12 OR HU = 12 THEN 710
700 RETURN
710 IF HU = 12 THEN PRINT "You have won"
720 IF CO = 12 THEN PRINT "I have won"
730 PRINT "Thanks for the game": END
740 REM ********************************
750 REM 99 TO CONCEDE
760 PRINT: PRINT
770 PRINT "Enter your move"
780 INPUT "From"; A
790 IF A = 99 THEN GOTO 730
800 IF Q(A) <> H AND Q(A) <> HK THEN 780
810 INPUT "To"; B
820 IF Q(B) <> E THEN 810
830 Q(B) = Q(A): Q(A) = E
840 REM ********************************
850 FOR T = 11 TO 17: IF Q(T) = C THEN Q(T) = CK
860 NEXT T
870 FOR T = 82 TO 88: IF Q(T) = H THEN Q(T) = HK
880 NEXT T
890 REM ********************************
900 IF ABS(A - B) < 12 THEN RETURN
910 TY = RND
920 IF TY < .3 THEN PRINT "Good move": GOSUB 1340
930 IF TY > .7 THEN PRINT "Got mel": GOSUB 1340
940 HU = HU + 1: Q((A + B) / 2) = E: GOSUB 600
950 FOR T = 82 TO 88: IF Q(T) = H THEN Q(T) = HK
960 NEXT T

970 PRINT: INPUT "Can you jump again (Y or N)"; A$
980 IF A$ <> "y" AND A$ <> "Y" THEN RETURN
990 A = B: GOTO 800
1000 REM *******************************
1010 REM Initialise
1020 CLS: PRINT "Press any key": N = 1
1030 N = N + 1: IF INKEY$ = "" THEN 1030
1040 CLS
1050 PRINT "Please stand by"
1070 H = ASC("H"): HK = ASC("K")
1080 C = ASC("C"): CK = ASC("$")
1090 E = 32: OF = -99
1100 FOR M = 1 TO 99: Q(M) = OF: NEXT M
1110 FOR M = 1 TO 64
1120 READ D: READ G
1130 Q(D) = G: NEXT M
1140 DATA 81,46,82,67,83,46,84,67,85,46,86,67,87,46
1150 DATA 88,67,71,67,72,46,73,67,74,46,75,67,76,46
1160 DATA 77,67,78,46,61,46,62,67,63,46,64,67
1170 DATA 65,46,66,67,67,46,68,67,51,32,52,46
1180 DATA 53,32,54,46,55,32,56,46,57,32,58,46
1190 DATA 41,46,42,32,43,46,44,32,45,46,46,32
1200 DATA 47,46,48,32,31,72,32,46,33,72,34,46,35,72
1210 DATA 36,46,37,72,38,46,21,46,22,72,23,46,24,72
1220 DATA 25,46,26,72,27,46,28,72,11,72,12,46,13,72
1230 DATA 14,46,15,72,16,46,17,72,18,46
1240 FOR M = 1 TO 4: READ X: N(M) = X: NEXT M
1250 DATA -11,-9,11,9
1260 CO = 0: HU = 0: RETURN
1270 IF Q(22) = C AND Q(11) = E THEN A = 22: M = 11: FL = 1: RETURN
1280 IF Q(22) = C AND Q(13) = E THEN A = 22: M = 13: FL = 1: RETURN
1290 IF Q(24) = C AND Q(13) = E THEN A = 24: H = 13: FL = 1: RETURN
1300 IF Q(24) = C AND Q(15) = E THEN A = 24: M = 15: FL = 1: RETURN
1310 IF Q(26) = C AND Q(15) = E THEN A = 26: M = 15: FL = 1: RETURN
1320 IF Q(26) = C AND Q(17) = E THEN A = 26: H = 17: FL = 1: RETURN
1330 RETURN
1340 FOR O = 1 TO 1000: NEXT O: RETURN

