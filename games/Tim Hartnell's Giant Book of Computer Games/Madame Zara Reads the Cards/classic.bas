5 DIM A$(10), B$(10), C$(10), D$(10)
10 REM MADAME ZARA READS THE CARDS
20 GOSUB 770
30 PRINT: PRINT "WELCOME "; A$; ", TO ZARA'S HOUSE"
40 PRINT "OF MYSTICISM...WHERE THE VEIL OVER"
50 PRINT "THE FUTURE IS MOVED BACK..."
60 PRINT TAB(7); "...JUST FOR A MOMENT"
70 XX = 1.0: GOSUB 2000
80 PRINT: PRINT "PLEASE TELL ME WHEN YOU"
90 PRINT "WERE BORN...AS NUMBERS"
100 PRINT "THE MONTH FIRST (1 - JANUARY TO"
110 INPUT "12 - DECEMBER)"; B1: IF B1 < 1 OR B1 > 12 THEN 80
120 PRINT: PRINT "THANK YOU, "; A$
130 INPUT "NOW WHAT DAY OF THE MONTH WERE YOU BORN"; B2
140 IF B2 < 1 OR B2 > 31 THEN 120
150 B = 100 * B1 + B2
160 CLS
170 FOR Y = 1 TO 32: PRINT TAB(Y); "*": NEXT Y
180 PRINT: PRINT A$; ", YOU WERE BORN UNDER"
190 PRINT "THE SIGN OF ";
200 IF B >= 101 AND B <= 120 OR B >= 1223 AND B <= 1231 THEN PRINT "CAPRICORN"
210 IF B >= 121 AND B <= 219 THEN PRINT "AQUARIUS"
220 IF B >= 220 AND B <= 321 THEN PRINT "PISCES"
230 IF B >= 332 AND B <= 420 THEN PRINT "ARIES"
240 IF B >= 421 AND B <= 521 THEN PRINT "TAURUS"
250 IF B >= 522 AND B <= 621 THEN PRINT "GEMINI"
260 IF B >= 622 AND B <= 723 THEN PRINT "CANCER"
270 IF B >= 724 AND B <= 823 THEN PRINT "LEO"
280 IF B >= 824 AND B <= 923 THEN PRINT "VIRGO"
290 IF B >= 924 AND B <= 1023 THEN PRINT "LIBRA"
300 IF B >= 1024 AND B <= 1122 THEN PRINT "SCORPIO"
310 IF B >= 1123 AND B <= 1222 THEN PRINT "SAGITTARIUS"
320 FOR Y = 1 TO 32: PRINT TAB(Y); "*": XX = 0.1: GOSUB 2000: NEXT Y
330 PRINT "NOW I SHALL GAZE INTO THE FUTURE..."
340 FOR Y = 1 TO 32: PRINT TAB(Y); "*": XX = 0.1: GOSUB 2000: NEXT Y
350 PRINT "I AM TURNING UP THE CARDS FROM"
360 PRINT "THE HARTNELL ARCANA..."
370 XX = 0.7: GOSUB 2000
380 FOR Y = 1 TO 4
390 A = INT(RND(1) * 6)
400 PRINT "----------------------------------"
410 PRINT "THIS IS ";
420 IF A = 0 THEN PRINT "THE ANCHOR ";
430 IF A = 1 THEN PRINT "THE TOWER ";
440 IF A = 2 THEN PRINT "THE ELVLING ";
450 IF A = 3 THEN PRINT "THE KNAVE ";
460 IF A = 4 THEN PRINT "THE CLERIC ";
470 IF A = 5 THEN PRINT "THE SOVEREIGN ";
480 A = INT(RND(1) * 6)
490 IF A = 0 THEN PRINT "OF STARS"
500 IF A = 1 THEN PRINT "OF STONES"
510 IF A = 2 THEN PRINT "OF SHADOWS"
520 IF A = 3 THEN PRINT "OF GEMS"
530 IF A = 4 THEN PRINT "OF DUST"
540 IF A = 5 THEN PRINT "OF ECHOES"
550 PRINT "----------------------------------"
560 XX = 2.0: GOSUB 2000
570 NEXT Y
580 XX = 2.5: GOSUB 2000
590 PRINT: PRINT "AND THESE CARDS TELL ME"
600 XX = 1.3: GOSUB 2000
610 PRINT: PRINT A$((INT(RND(1) * 10) + 1))
620 XX = 1.3: GOSUB 2000
630 PRINT: PRINT B$((INT(RND(1) * 10) + 1))
640 XX = 1.3: GOSUB 2000
650 PRINT: PRINT C$((INT(RND(1) * 10) + 1))
660 XX = 1.3: GOSUB 2000
670 PRINT: PRINT D$((INT(RND(1) * 10) + 1))
680 XX = 1.3: GOSUB 2000
690 PRINT "----------------------------------"
700 PRINT: PRINT "THANK YOU FOR CONSULTING THE"
710 PRINT "THE WISDOM OF ZARA...."
720 XX = 2.0: GOSUB 2000
730 PRINT "----------------------------------"
740 PRINT: PRINT "I WOULD LIKE TO SEE YOU"
750 PRINT "AGAIN SOME TIME, "; A$
760 END
770 REM
780 RANDOMIZE TIMER
790 CLS
800 FOR T = 1 TO 10
810 PRINT "* * * * * * * * * * * * * * * * * *"
820 PRINT "MADAME ZARA READS THE STARS...."
830 NEXT T
840 FOR T = 1 TO 20
850 PRINT TAB(T); "* FOR YOU ... "
860 NEXT T
870 XX = 1.3: GOSUB 2000
880 INPUT "WHAT IS YOUR NAME, MY DEAR"; A$
890 CLS
900 FOR T = 1 TO 10: READ A$(T): READ B$(T)
910 READ C$(T): READ D$(T): NEXT T
920 RETURN
930 DATA "A SUDDEN SHOCK","GREAT JOY IS NEAR","A PARK, AND A SPECIAL FRIENDSHIP","DISAPPOINTMENT IS ON ITS WAY","YOU ARE CLOSELY PROTECTED FROM HARM"
940 DATA "THERE IS A CHANCE TO STOP ILL-FEELING","GOOD FRIENDS ARE NEAR","DISTRUST THOSE WHO WOULD ADVISE YOU","THE MOON IS IN A GOOD ASPECT FOR YOU"
950 DATA "SUCCESS IN SPECULATION IS LIKELY","CONFIDE IN THOSE NEAR YOU","SUCCESS IN ALL VENTURES IS INDICATED"
960 DATA "A MYSTERY WILL SOON BE SOLVED","A LETTER WILL BRING YOU STRANGE NEWS","GOOD LUCK WILL BE YOURS TODAY"
970 DATA "HIGH HONOR IS ON ITS WAY","DOMESTIC HAPPINESS IS THREATENED","THE CLOUDS ARE GATHERING","STRENGTH IS ON YOUR SIDE"
980 DATA "SOMEONE IS WATCHING YOU WITH LONGING","FAMILY QUARRELS LIKELY","THE SUN IS RISING ON YOUR HOPES"
990 DATA "EVIL CONSEQUENCES MAY BE AVERTED","THERE IS A CHANCE TO MAKE UP A QUARREL","YOUR STRUGGLES ARE TO BE REWARDED"
1000 DATA "YOU WILL BE TREATED GENTLY TODAY","A LONG-AWAITED WISH IS ABOUT TO COME TRUE","SUSPICION IS FOCUSED ON YOU"
1010 DATA "GOOD HEALTH WILL BE YOURS","FULFILLMENT OF A DREAM IS JUST AROUND THE CORNER"
1020 DATA "BEAUTY COMES A-CALLING","GOOD TIDINGS ARE ON THEIR WAY","THE CURRENT BAD SITUATION WILL NOT LAST LONG"
1030 DATA "LOSS OF PROPERTY IS PROBABLE","YOU GET THE CHANCE TO WARD OFF EVIL"
1040 DATA "RICHES THROUGH PROPERTY INDICATED","A MINOR FAILURE WILL TURN TO SUCCESS","I SEE A SAFE BULGING WITH MONEY FOR YOU"
1050 DATA "GOOD NEWS IS ON ITS WAY","BEWARE OF FLATTERY"
2000 ST = TIMER + XX
2010 IF TIMER < ST THEN 2010
2020 RETURN

