5 RANDOMIZE TIMER: DIM A(10, 10)
10 REM REVERSI
20 GOTO 740
30 PRINT "My move..."
40 S = O: T = X: H = 0
50 FOR A = 2 TO 9: FOR B = 2 TO 9
60 IF A(A, B) <> 46 THEN 210
70 Q = 0
80 FOR C = -1 TO 1: FOR D = -1 TO 1
90 K = 0: F = A: G = B
100 IF A(F + C, G + D) <> S THEN 130
110 K = K + 1: F = F + C: G = G + D
120 GOTO 100
130 IF A(F + C, G + D) <> T THEN 150
140 Q = Q + K
150 NEXT D: NEXT C
160 IF A = 2 OR A = 9 OR B = 2 OR B = 9 THEN Q = Q * 2
170 IF A = 3 OR A = 8 OR B = 3 OR B = 8 THEN Q = Q / 2
180 IF (A = 2 OR A = 9) AND (B = 3 OR B = 8) OR (A = 3 OR A = 8) AND (B = 2 OR B = 9) THEN Q = Q / 2
190 IF Q < H OR (RND < .3 AND Q = H) THEN 210
200 H = Q: M = A: N = B
210 NEXT B: NEXT A
220 IF H = 0 AND R = 0 THEN 690
230 IF H = 0 THEN 250
240 GOSUB 580
250 GOSUB 370
260 COLOR 10: INPUT "Enter your move (0 to pass)"; R: COLOR 15
270 REM ENTER 0 TO PASS
280 S = X: T = O: REM LETTER 0
290 IF R = 0 THEN 350
300 IF R < 11 OR R > 88 THEN 260
310 R = R + 11
320 M = INT(R / 10)
330 N = R - 10 * M
340 GOSUB 580
350 GOSUB 370
360 GOTO 30
370 REM PRINT BOARD
380 C = 0: H = 0
390 CLS: REM OR 'HOME'
400 PRINT
410 PRINT TAB(32); "Computer is ";: COLOR 12: PRINT "X": COLOR 15
420 PRINT TAB(32); "   Human is ";: COLOR 11: PRINT "O": COLOR 15
430 PRINT
440 COLOR 13: PRINT TAB(32); "1 2 3 4 5 6 7 8": COLOR 15
450 FOR B = 2 TO 9: COLOR 13: PRINT TAB(28); B - 1; " ";: COLOR 15
460 FOR D = 2 TO 9
465 C$ = CHR$(A(B, D))
466 IF C$ = "X" THEN COLOR 12: ELSE IF C$ = "O" THEN COLOR 11: ELSE COLOR 15
470 PRINT C$; " ";
480 IF A(B, D) = X THEN C = C + 1
490 IF A(B, D) = O THEN H = H + 1
500 NEXT D
510 COLOR 13: PRINT B - 1: COLOR 15
520 NEXT B
530 COLOR 13: PRINT TAB(32); "1 2 3 4 5 6 7 8": COLOR 15
540 PRINT
550 PRINT TAB(34); "Computer:";: COLOR 12: PRINT C: COLOR 15
560 PRINT TAB(34); "   Human:";: COLOR 11: PRINT H: COLOR 15: PRINT: PRINT
570 RETURN
580 FOR C = -1 TO 1: FOR D = -1 TO 1
590 F = M: G = N
600 IF A(F + C, G + D) <> S THEN 630
610 F = F + C: G = G + D
620 GOTO 600
630 IF A(F + C, G + D) <> T THEN 670
640 A(F, G) = T
650 IF M = F AND N = G THEN 670
660 F = F - C: G = G - D: GOTO 640
670 NEXT D: NEXT C
680 RETURN
690 GOSUB 370
700 IF C > H THEN PRINT "I'm the champ!"
710 IF H > C THEN PRINT "You're the champ!"
720 IF H = C THEN PRINT "It's a draw!"
730 END
740 CLS
750 X = ASC("X"): O = ASC("O"): REM LETTER O NOT ZERO
770 FOR B = 1 TO 10: FOR C = 1 TO 10
780 IF B <> 1 AND C <> 1 AND B <> 10 AND C <> 10 THEN A(B, C) = ASC(".")
790 NEXT C: NEXT B
800 A(5, 5) = X: A(6, 6) = X
810 A(6, 5) = O: A(5, 6) = O
820 P = 0
830 COLOR 10: PRINT "Do you want the first move (Y/N)";
850 INPUT W$: COLOR 15
860 GOSUB 370
870 IF W$ = "Y" OR W$ = "y" THEN GOTO 260
880 GOTO 30

