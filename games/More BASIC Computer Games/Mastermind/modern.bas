1 CLS: COLOR 12
5 RANDOMIZE TIMER
10 PRINT TAB(35); "Mastermind"
20 PRINT TAB(31); "Creative Computing"
30 PRINT TAB(29); "Morristown, New Jersey"
40 PRINT: PRINT: PRINT: COLOR 15
100 PRINT "The Game of Mastermind"
110 PRINT
130 PRINT "Color codes:"
140 PRINT ,: COLOR 12: PRINT "R=Red",: COLOR 11: PRINT "C=Cyan",: COLOR 14: PRINT "Y=Yellow"
150 PRINT ,: COLOR 10: PRINT "G=Green",: COLOR 9: PRINT "B=Blue",: COLOR 13: PRINT "M=Magenta"
160 PRINT
170 DIM B$(10), Y(10), Z(10)
180 C(0) = 4
190 FOR N = 1 TO 4
200 C(N) = INT(6 * RND(1) + 1)
210 NEXT N
220 FOR N = 1 TO 4
230 X = C(N)
240 GOSUB 730
250 C(N) = X
260 NEXT N
270 P$ = ""
273 FOR X1 = 1 TO 4
275 P$ = P$ + CHR$(C(X1))
277 NEXT X1
280 FOR P = 1 TO 10
290 PRINT: COLOR 15
300 PRINT "Move number"; P; "(or BOARD/QUIT)";
310 INPUT G$
320 IF G$ = "BOARD" OR G$ = "board" THEN 910
330 IF G$ = "QUIT" OR G$ = "quit" THEN 440
335 G1$ = "": FOR Z = 1 TO LEN(G$)
336 Z$ = MID$(G$, Z, 1): IF Z$ >= "a" AND Z$ <= "z" THEN G1$ = G1$ + CHR$(ASC(Z$) - 32) ELSE G1$ = G1$ + Z$
337 NEXT Z: G$ = G1$
340 B$(P) = G$
350 GOSUB 520
360 IF B = 4 THEN 1010
370 GOSUB 600
380 PRINT B; "black pegs"
390 Y(P) = B
400 PRINT W; "white pegs"
410 Z(P) = W
420 NEXT P
430 PRINT "Sorry, you lose!"
440 PRINT "The correct code was: "; P$
450 COLOR 10: PRINT: PRINT "Want to play again (Y/N)";
460 INPUT A$: COLOR 15
480 IF LEFT$(A$, 1) = "Y" OR LEFT$(A$, 1) = "y" THEN 190
490 PRINT
500 END
510 REM COMPUTE BLACK PEGS
520 FOR X1 = 1 TO 4
523 G(X1) = ASC(MID$(G$, X1, 1))
525 NEXT X1
530 B = 0
540 FOR K = 1 TO 4
550 IF G(K) <> C(K) THEN 570
560 B = B + 1
570 NEXT K
580 RETURN
590 REM COMPUTE WHITE PEGS
600 FOR X1 = 1 TO 4
603 R(X1) = ASC(MID$(P$, X1, 1))
605 NEXT X1
610 W = 0
620 FOR I = 1 TO 4
630 FOR J = 1 TO 4
640 IF G(I) <> R(J) THEN 680
650 W = W + 1
660 R(J) = 0
670 GOTO 690
680 NEXT J
690 NEXT I
700 W = W - B
710 RETURN
720 REM TRANSLATE COLOR CODES TO NUMERICS
730 IF X <> 1 THEN 760
740 X = ASC("R")
750 RETURN
760 IF X <> 2 THEN 790
770 X = ASC("C")
780 RETURN
790 IF X <> 3 THEN 820
800 X = ASC("Y")
810 RETURN
820 IF X <> 4 THEN 850
830 X = ASC("G")
840 RETURN
850 IF X <> 5 THEN 880
860 X = ASC("B")
870 RETURN
880 X = ASC("M")
890 RETURN
900 REM PRINT BOARD SUMMARY
910 V = P - 1
920 PRINT: PRINT "Guess", "Blacks", "Whites"
930 PRINT "-----", "------", "------"
960 FOR I = 1 TO V
970 FOR J = 1 TO LEN(B$(I)): Z$ = MID$(B$(I), I, 1)
971 IF Z$ = "R" THEN COLOR 12
972 IF Z$ = "C" THEN COLOR 11
973 IF Z$ = "Y" THEN COLOR 14
974 IF Z$ = "G" THEN COLOR 10
975 IF Z$ = "B" THEN COLOR 9
976 IF Z$ = "M" THEN COLOR 13
978 PRINT Z$;: NEXT J: COLOR 15
980 PRINT , Y(I), Z(I)
990 NEXT I
1000 GOTO 290
1010 PRINT "You win!"
1020 GOTO 450
9999 END


