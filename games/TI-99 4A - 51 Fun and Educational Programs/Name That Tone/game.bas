100 REM -NAME THAT TONE
110 FOR I=1 TO 7
120 READ F(I),T$(I)
130 NEXT I
140 DATA 880,A,988,B,1047,C
150 DATA 1175,D,1319,E,1397,F,1568,G
160 CALL CLEAR
170 PRINT "**** NAME THAT TONE ****"
180 PRINT : :
190 PRINT "I AM GOING TO PLAY ALL THE":"NOTES FROM A TO G"
200 PRINT : :"TRY TO MEMORIZE EACH TONE"
210 PRINT : :"LATER I WILL PLAY THEM BACK ":"TO SEE IF YOU CAN REMEMBER THEM"
220 PRINT : :"CAN YOU IDENTIFY 10 IN A": "ROW?"
230 PRINT : : :
240 PRINT "TO PROCEED, HIT ENTER"
250 INPUT RESPONSE$
260 CALL CLEAR
270 FOR I=1 TO 7
280 CALL SOUND(1000,F(I),2)
290 PRINT "THIS IS THE NOTE: ";T$(I)
300 PRINT
310 NEXT I
320 PRINT : : :"TO REPEAT, HIT THE R KEY AND ENTER"
330 PRINT :"TO PROCEED, JUST HIT ENTER"
340 PRINT : :
350 INPUT RESPONSE$
360 IF RESPONSE$="R" THEN 260
370 CALL CLEAR
380 RANDOMIZE
390 N=INT(RND*7)+1
400 PRINT : : : : : : :
410 PRINT "WHAT NOTE DID I PLAY"
420 CALL SOUND(700,F(N),2)
430 PRINT "ENTER A,B,C,D,E,F,OR G"
440 INPUT ANS$
450 IF ANS$<"A" THEN 430
460 IF ANS$>"G" THEN 430
470 IF ANS$=T$(N)THEN 540
480 REM -WRONG
490 PRINT
500 PRINT "SORRY - WRONG ANSWER"
510 PRINT "THAT NOTE WAS: ";T$(N)
520 R=0
530 GOTO 580
540 REM -RIGHT
550 PRINT
560 PRINT "CORRECT - VERY GOOD"
570 R=R+1
580 REM -SCORE
590 PRINT
600 PRINT
610 PRINT "CORRECT ANSWERS IN A ROW=";R
620 IF R=10 THEN 630 ELSE 380
630 REM -FINAL SCORE
640 PRINT : : : : : : :
650 PRINT "CONGRATULATIONS": :"YOU HAVE WON THE GAME"
660 END
