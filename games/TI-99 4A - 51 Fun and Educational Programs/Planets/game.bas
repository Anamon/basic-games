100 REM -"PLANETS"
110 CALL CLEAR
120 PRINT "*******  PLANETS  *******"
130 PRINT
140 PRINT
150 PRINT "NAME THE PLANETS IN "
160 PRINT "ORDER FROM THE SUN"
170 GOSUB 470
180 READ PLANET$,FACT
190 IF PLANET$="END" THEN 680
200 GOSUB 520
210 GOSUB 470
220 PRINT "NAME THE NEXT PLANET "
230 INPUT ANS$
240 IF ANS$="HELP" THEN 400
250 IF ANS$<>PLANET$ THEN 310
260 PRINT
270 PRINT
280 PRINT "CORRECT-VERY GOOD"
290 GOSUB 470
300 GOTO 180
310 REM =WRONG
320 GOSUB 470
330 PRINT "SORRY-THAT'S WRONG"
340 PRINT "KEEP TRYING"
350 PRINT
360 PRINT "IF YOU STILL CAN'T THINK"
370 PRINT "OF IT. TYPE  HELP"
380 PRINT
390 GOTO 200
400 GOSUB 470
410 PRINT "THE ANSWER IS >>> ";PLANET$
420 PRINT
430 PRINT
440 PRINT
450 GOTO 180
460 RETURN
470 REM =SKIP 3 LINES
480 FOR I=1 TO 3
490 PRINT
500 NEXT I
510 RETURN
520 REM =DASHES
530 FOR I=1 TO 27
540 PRINT "-";
550 NEXT I
560 PRINT
570 RETURN
580 DATA MERCURY,0.28
590 DATA VENUS,0.85
600 DATA EARTH,1
610 DATA MARS,0.38
620 DATA JUPITER,2.6
630 DATA SATURN,1.2
640 DATA URANUS,1.1
650 DATA NEPTUNE,1.4
660 DATA PLUTO,.6
670 DATA END,0
680 REM =WEIGHTS
690 FOR I=1 TO 700
700 NEXT I
710 CALL CLEAR
720 PRINT "PLEASE ENTER YOUR WEIGHT"
730 PRINT "ON EARTH AND FIND OUT"
740 PRINT "HOW MUCH YOU WEIGH ON OTHER PLANETS"
750 GOSUB 470
760 INPUT "ENTER YOUR WEIGHT  ":WEARTH
770 CALL CLEAR
780 PRINT "PLANET","WEIGHT"
790 GOSUB 470
800 RESTORE
810 READ PLANET$,FACT
820 IF PLANET$="END" THEN 860
830 PW=INT(WEARTH*FACT)
840 PRINT PLANET$,PW
850 GOTO 810
860 END
