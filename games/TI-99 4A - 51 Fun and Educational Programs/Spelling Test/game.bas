100 REM -"SPELLING TEST"
110 CALL CLEAR
120 PRINT "*** SPELLING TEST ***"
130 GOSUB 360
140 READ PH$,SP$
150 IF PH$="END" THEN 470
160 N=N+1
170 GOSUB 360
180 PRINT "SPELL >";PH$
190 INPUT ANS$
200 IF ANS$=SP$ THEN 290
210 REM =WRONG
220 PRINT
230 PRINT "SORRY WRONG ANSWER"
240 PRINT "THE CORRECT ANSWER IS"
250 PRINT
260 PRINT " >>> ";SP$
270 GOSUB 410
280 GOTO 140
290 REM =RIGHT
300 PRINT
310 PRINT
320 PRINT "CORRECT-VERY GOOD"
330 RIGHT=RIGHT+1
340 GOSUB 410
350 GOTO 140
360 REM =SKIP 3 LINES
370 FOR I=1 TO 4
380 PRINT
390 NEXT I
400 RETURN
410 REM =DASHES
420 FOR I=1 TO 26
430 PRINT "-";
440 NEXT I
450 PRINT
460 RETURN
470 REM = FINAL SCORE
480 PCNT=INT(RIGHT/N*1000)/10
490 CALL CLEAR
500 PRINT "---- FINAL SCORE ---"
510 GOSUB 360
520 GOSUB 360
530 PRINT "YOU SPELLED CORRECTLY"
540 PRINT RIGHT;"WORDS OUT OF ";N
550 PRINT
560 PRINT "THAT'S ";PCNT;"%"
570 GOSUB 360
580 END
590 DATA ADRES,ADDRESS
600 DATA REECEEV,RECEIVE
610 DATA HITE,HEIGHT
620 DATA BIZEE,BUSY
630 DATA FRAYT,FREIGHT
640 DATA NAYBOR,NEIGHBOR
650 DATA LAFF,LAUGH
660 DATA BYUROH,BUREAU
670 DATA KOMITEE,COMMITTEE
680 DATA END,END
