100 REM -PROGRAM "BOMB"
110 T=90
120 CALL CLEAR
130 PRINT "EMERGENCY !!!"
140 PRINT
150 PRINT "A BOMB HAS BEEN"
160 PRINT "PLACED IN A HOTEL"
170 PRINT "WITH 100 ROOMS."
180 PRINT "YOU HAVE A BOMB"
190 PRINT "DETECTOR TO HELP"
200 PRINT "YOU FIND IT."
210 PRINT
220 PRINT "THE CLOSER YOU GET"
230 PRINT "TO THE BOMB"
240 PRINT "THE HIGHER THE"
250 PRINT "STRENGTH READING."
260 PRINT
270 PRINT "YOU HAVE";T;"SECONDS"
280 PRINT "TO FIND IT"
290 PRINT "BEFORE IT BLOWS UP."
300 PRINT
310 PRINT "GOOD LUCK!"
320 PRINT
330 PRINT "HIT ENTER TO START"
340 INPUT ENTER$
350 CALL CLEAR
360 FOR I=1 TO 3
370 PRINT
380 NEXT I
390 RANDOMIZE
400 B=INT(RND*100)+1
410 PRINT "WHICH ROOM TO SEARCH? (ENTER ROOM NUMBER 1-100)"
420 INPUT M
430 PRINT
440 IF M<101 THEN 480
450 PRINT "ONLY 100 ROOMS"
460 PRINT
470 GOTO 410
480 IF M=B THEN 630
490 D=ABS(B-M)
500 V=100-D
510 S=V^2
520 T=T-10
530 IF T=0 THEN 740
540 PRINT "DETECTOR READING= ";S
550 PRINT "SECONDS LEFT= " ;T
560 FOR I=1 TO 27
570 PRINT "-";
580 NEXT I
590 PRINT
600 CALL SOUND(500,500,2)
610 GOTO 410
620 GOTO 820
630 REM =FOUND IT
640 CALL CLEAR
650 FOR I=1 TO 7
660 CALL SOUND(-20,150*I,2)
670 NEXT I
680 PRINT
690 PRINT
700 PRINT "YOU FOUND THE BOMB"
710 PRINT "AND SAVED THE HOTEL"
720 PRINT "YOU ARE A BRAVE HERO"
730 GOTO 820
740 REM =BOOM
750 CALL CLEAR
760 CALL SOUND(1500,120,1)
770 PRINT
780 PRINT
790 PRINT "B O O M !!!"
800 PRINT "THE BOMB BLEW UP"
810 PRINT "IN ROOM ";B
820 END
