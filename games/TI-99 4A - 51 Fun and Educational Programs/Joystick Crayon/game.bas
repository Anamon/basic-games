100 REM -JOYSTICK CRAYON
110 CALL CLEAR
120 PRINT "***** JOYSTICK CRAYON *****"
130 PRINT
140 PRINT
150 PRINT "USE ONE OF YOUR JOYSTICKS"
160 PRINT "TO DRAW LINES ON THE SCREEN."
170 PRINT
180 PRINT "HOLD THE FIRE BUTTON DOWN TO"
190 PRINT "ERASE OR MOVE WITHOUT DRAWING A LINE."
200 PRINT
210 PRINT "BEFORE STARTING, MAKE SURE"
220 PRINT "ALPHA-LOCK BUTTON ON KEYBOARD IS UP."
230 PRINT
240 PRINT
250 PRINT "HIT ENTER TO START"
260 INPUT ENTER$
270 CALL CLEAR
280 W$="FFFFFFFFFFFFFFFF"
290 CALL CHAR(140,W$)
300 R=12
310 C=16
320 CALL JOYST(1,X,Y)
330 CALL KEY(1,KEY,STAT)
340 IF KEY<>18 THEN 360
350 CALL HCHAR(R,C,32)
360 IF X>0 THEN 470
370 IF Y>0 THEN 420
380 IF Y<0 THEN 520
390 IF X<0 THEN 570
400 CALL VCHAR(R,C,140)
410 GOTO 320
420 REM -JOY UP
430 R=R-1
440 IF R>1 THEN 460
450 R=1
460 GOTO 400
470 REM -JOY RIGHT
480 C=C+1
490 IF C<32 THEN 510
500 C=32
510 GOTO 400
520 REM -JOY DOWN
530 R=R+1
540 IF R<24 THEN 560
550 R=24
560 GOTO 400
570 REM -JOY LEFT
580 C=C-1
590 IF C>1 THEN 610
600 C=1
610 GOTO 400
620 END
