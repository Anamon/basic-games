100 REM CREATE AND TEST SPECIAL CHARACTER
110 CALL CLEAR
120 PRINT "---- SPECIAL CHARACTERS ----"
130 PRINT
140 PRINT
150 PRINT "TO CREATE CODE: TYPE CODE"
160 PRINT
170 PRINT "TO TEST CHARACTER: TYPE TEST"
180 PRINT
190 PRINT "TO END: TYPE END"
200 INPUT CHOICE$
210 IF CHOICE$="CODE" THEN 260
220 IF CHOICE$="TEST" THEN 740
230 IF CHOICE$="END" THEN 920
240 PRINT "ERROR-PLEASE ENTER AGAIN"
250 GOTO 130
260 CALL CLEAR
270 PRINT "SPECIAL CHARACTER CODE GENERATOR"
280 PRINT
290 PRINT
300 DIM HEX$(16)
310 FOR I=0 TO 9 
320 HEX$(I)=STR$(I)
330 NEXT I
340 HEX$(10)="A"
350 HEX$(11)="B"
360 HEX$(12)="C"
370 HEX$(13)="D"
380 HEX$(14)="E"
390 HEX$(15)="F"
400 PRINT
410 PRINT
420 PRINT
430 PRINT "ENTER PATTERN OF 8 ONES AND ZEROS (WHEN DONE: TYPE DONE)"
440 INPUT X$
450 IF X$="DONE" THEN 110
460 IF LEN(X$)<>8 THEN 400
470 REM  DECODE RIGHT 4 BITS
480 N=0
490 V=0
500 FOR I=8 TO 5 STEP -1
510 B$=SEG$(X$,I,1)
520 IF B$="0" THEN 540
530 V=V+2^N
540 N=N+1
550 NEXT I
540 N=N+1
550 NEXT I
560 V2$=HEX$(V)
570 REM  DECODE LEFT 4 BITS
580 N=0
590 V=0
600 FOR I=4 TO 1 STEP -1
610 B$=SEG$(X$,I,1)
620 IF B$="0" THEN 640
630 V=V+2^N
640 N=N+1
650 NEXT I
660 V1$=HEX$(V)
670 CODE$=V1$&V2$
680 PRINT
690 PRINT "----------------------------"
700 PRINT X$;"=";CODE$
710 PRINT
720 PRINT "----------------------------"
730 GOTO 400
740 REM  TEST CHARACTER
750 CALL CLEAR
760 PRINT "**** TEST CHARACTER ****"
770 PRINT
780 PRINT
790 PRINT "ENTER 16-CHARACTER   TEST CODE (EXAMPLE: FF818199998181FF)"
800 INPUT PAT$
810 IF LEN(PAT$)=16 THEN 860
820 PRINT
830 PRINT "ERROR: MUST ENTER 16 CHAR"
840 PRINT
850 GOTO 790
860 CALL CHAR(140,PAT$)
870 CALL CLEAR
880 CALL VCHAR(12,17,140)
890 PRINT "WHEN DONE HIT ENTER"
900 INPUT ENTER$
910 GOTO 110
920 END
