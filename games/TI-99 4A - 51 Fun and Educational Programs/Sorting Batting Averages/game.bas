100 REM -"SORTED BATNG AVG"
110 CALL CLEAR
120 PRINT "*** BATTING AVERAGES ***"
130 PRINT
140 PRINT
150 PRINT "THIS PROGRAM COMPUTES"
160 PRINT "A BATTING AVERAGE FOR"
170 PRINT "EACH PLAYER AND FOR"
180 PRINT "THE TEAM. THE AVERAGES"
190 PRINT "ARE THEN SORTED."
200 GOSUB 710
210 GOSUB 710
220 PRINT "HIT ENTER TO START"
230 INPUT ENTER$
240 DIM N$(50),R(50)
250 CALL CLEAR
260 GOSUB 710
270 PRINT "ENTER NAME,AT-BAT,HITS"
280 PRINT "(TO END TYPE 0,0,0)"
290 INPUT NAME$,AB,H
300 IF AB>=H THEN 350
310 PRINT
320 PRINT "ERROR-MORE HITS THAN AT BATS"
330 PRINT
340 GOTO 270
350 TEAMAB=TEAMAB+AB
360 TEAMH=TEAMH+H
370 IF NAME$="0" THEN 430
380 Z=Z+1
390 AVG=INT(H/AB*1000)
400 N$(Z)=NAME$
410 R(Z)=AVG
420 GOTO 260
430 REM =SORT
440 FOR I=1 TO Z
450 FOR J=I+1 TO Z
460 IF R(I)>R(J)THEN 530
470 H$=N$(I)
480 H2=R(I)
490 N$(I)=N$(J)
500 R(I)=R(J)
510 N$(J)=H$
520 R(J)=H2
530 NEXT J
540 NEXT I
550 CALL CLEAR
560 PRINT "NAME","AVERAGE"
570 PRINT
580 FOR I=1 TO Z
590 PRINT N$(I),R(I)
600 LINES=LINES+1
610 IF LINES<15 THEN 660
620 PRINT
630 PRINT "HIT ENTER TO CONTINUE"
640 INPUT ENTER$
650 LINES=0
660 NEXT I
670 TAVG=INT(TEAMH/TEAMAB*1000)
680 PRINT
690 PRINT "TEAM AVG",TAVG
700 GOTO 760
710 REM =SKIP 3 LINES
720 FOR I=1 TO 3
730 PRINT
740 NEXT I
750 RETURN
760 END
