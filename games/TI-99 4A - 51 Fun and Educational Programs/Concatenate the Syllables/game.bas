100 REM -CONCATENATE THE SYLLABLES
110 REM -UP TO 9 SYLLABLES MAY BE PLACED IN DATA STATEMENT. LAST SYLLABLE MUST BE END.
120 REM -MAIN
130 GOSUB 220
140 GOSUB 300
150 GOSUB 400
160 GOSUB 510
170 PRINT "TO CONTINUE, HIT  ENTER."
180 PRINT "TO END, TYPE  END."
190 INPUT RESPONSE$
200 IF RESPONSE$="END" THEN 570
210 GOTO 140
220 REM -READ SYLLABLES
230 FOR I=1 TO 9
240 READ LET$
250 IF LET$="END" THEN 280
260 SYL$(I)=LET$
270 NEXT I
280 Z=I-1
290 RETURN
300 REM -PRINT SCREEN
310 CALL CLEAR
320 PRINT "-CONCATENATE THE SYLLABLES-"
330 GOSUB 510
340 FOR K=1 TO Z
350 PRINT K;SYL$(K)
360 NEXT K
370 GOSUB 510
380 PRINT "CREATE WORDS BY ENTERING HE NUMBER OF THE SYLLABLES YOU WISH TO CONCATENATE       (EXAMPLE: 857)"
390 RETURN
400 REM -CREATE AND PRINT
410 INPUT NUM$
420 L=LEN(NUM$)
430 WORD$=" "
440 FOR I=1 TO L
450 X=VAL(SEG$(NUM$,I,1))
460 WORD$=WORD$&SYL$(X)
470 NEXT I
480 GOSUB 510
490 PRINT "YOUR WORD IS: ";WORD$
500 RETURN
510 REM -SKIP 3 LINES
520 FOR J=1 TO 3
530 PRINT
540 NEXT J
550 RETURN
560 DATA IN,ER,G,R,T,E,AND,S,CA,END
570 END
