100 REM -"PAPER-ROCK-SCISSORS"
110 CALL CLEAR
120 PRINT "*** PAPER-ROCK-SCISSORS ***"
130 PRINT
140 PRINT
150 PRINT "CHOOSE PAPER, ROCK OR"
160 PRINT "SCISSORS. THE COMPUTER"
170 PRINT "WILL DO THE SAME."
180 PRINT
190 PRINT
200 PRINT "REMEMBER PAPER COVERS ROCK."
210 PRINT "ROCK SMASHES SCISSORS."
220 PRINT "SCISSORS CUT PAPER."
230 PRINT
240 PRINT
250 PRINT "WHEN ENTERING YOUR CHOICE"
260 PRINT "TYPE P, R, OR S"
270 PRINT
280 PRINT "WHEN FINISHED, TYPE END."
290 PRINT
300 PRINT
310 PRINT "HIT ENTER TO CONTINUE"
320 PRINT
330 INPUT ENTER$
340 CALL CLEAR
350 INPUT "ENTER P,R,S, OR END:":H$
360 IF H$="END" THEN 920
370 FOR K=1 TO 4
380 IF H$=SEG$("PRSX",K,1)THEN 400
390 NEXT K
400 IF K>3 THEN 350
410 GOSUB 560
420 GOSUB 640
430 CALL CLEAR
440 PRINT "YOUR CHOICE WAS- ";H$
450 PRINT "MY CHOICE WAS- " ;C$
460 PRINT W$(R(N))
470 GOSUB 870
480 PRINT "YOUR SCORE=";HSCORE
490 PRINT "MY SCORE=";CSCORE
500 FOR I=1 TO 26
510 PRINT "-";
520 NEXT I
530 GOSUB 870
540 GOSUB 870
550 GOTO 350
560 REM =COMPUTER CHOICE
570 RANDOMIZE
580 RAND=INT(RND*3)+1
590 X$(1)="P"
600 X$(2)="R"
610 X$(3)="S"
620 C$=X$(RAND)
630 RETURN
640 REM =RESULTS & SCORE
650 RESTORE
660 FOR J=1 TO 9
670 READ Q$(J)
680 NEXT J
690 FOR J=1 TO 9
700 READ R(J)
710 NEXT J
720 DATA PP,PR,PS,RP,RR,RS,SP,SR,SS
730 DATA 3,1,2,2,3,1,1,2,3
740 W$(1)="YOU WON"
750 W$(2)="I WON"
760 W$(3)="WE TIED"
770 CALL CLEAR
780 M$=H$&C$
790 FOR N=1 TO 9
800 IF M$=Q$(N)THEN 820
810 NEXT N
820 ON R(N)GOTO 830,850,860
830 HSCORE=HSCORE+1
840 GOTO 860
850 CSCORE=CSCORE+1
860 RETURN
870 REM =SKIP 3 LINES
880 FOR I=1 TO 3
890 PRINT
900 NEXT I
910 RETURN
920 REM =FINAL SCORE
930 CALL CLEAR
940 PRINT "*** FINAL SCORE ***"
950 GOSUB 870
960 PRINT "YOUR SCORE IS ";HSCORE
970 PRINT
980 PRINT "MY SCORE IS   ";CSCORE
990 GOSUB 870
1000 GOSUB 870
1010 END
