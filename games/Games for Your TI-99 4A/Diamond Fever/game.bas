10 REM  DIAMOND FEVER
20 CALL CLEAR
30 INPUT "DO YOU WANT INSTRUCTIONS ?":HUH$
40 IF SEG$(HUH$,1,1)<>"Y" THEN 290
50 PRINT "        DIAMOND FEVER"
60 PRINT "          ~~~~~~"
70 PRINT :"        written by":
80 PRINT :"       Kevin Burfitt"
90 PRINT "           1983"
100 PRINT : : : : : : : : :
110 FOR I=1 TO 1000
120 NEXT I
130 CALL CLEAR
140 PRINT "       You are alone, on an asteroid in deepest space.  While scanning the rock you"
150 PRINT "notice it contains an abnor-mally high amount of metals,so you venture down to see"
160 PRINT "if you can mine any   WHEN  SUDDENLY   you realise that you are not inside an"
170 PRINT "asteroid but an evil VOGON  spacecraft !!": :
180 PRINT "How can you escape from the maze of rock and rubble?    Fate was on your side, you  got a look at the diamond"
190 PRINT "seam in the rock, but can   you reach the exit before   the "
200 PRINT :"   Vogon gets you ????": :
210 PRINT :"PRESS ANY KEY TO CONTINUE"
220 CALL KEY(0,KEY,STATUS)
230 IF STATUS THEN 240 ELSE 220
240 CALL SOUND(100,440,0)
250 PRINT : :"level 1 is easiest > level 5 for the champions ": :"you have your trusty blaster"
260 PRINT "with you and it's running   out of steam (uranium       actually) but it will cut   through rock": :"N.B. YOU ARE RUNNING OUT OF"
270 PRINT :"  gasp'  AIR  gasp'": :
280 PRINT "Use the arrow keys to move":"Your blaster will  'bleep'  when you try to cut through rock"
290 HIGHEST_SCORER$="HIGH SCORE"
300 HIGH_SCORE=50
310 CALL CHAR(132,"995A3C3C3C3C2424")
320 DIAMOND=3
330 ROCK=1
340 SCORE=0
350 CALL CHAR(136,"55AA55AA55AA55AA")
360 DIM A(19,19)
370 FOR I=1 TO 19
380 FOR J=1 TO 19
390 A(I,J)=0
400 NEXT J
410 NEXT I
420 RANDOMIZE
430 INPUT "LEVEL OF DIFFICULTY ?":L
440 SC1=0
450 CALL CLEAR
460 IF (L<0)+(L>5)THEN 430
470 LEVEL=L
480 CALL COLOR(10,3,3)
490 CALL COLOR(11,2,2)
500 CALL COLOR(13,2,3)
510 CALL COLOR(14,2,16)
520 CALL CHAR(133,"0018245A18240000")
530 CALL CLEAR
540 CALL SCREEN(L+4)
550 AX=10
560 PRINT : : : : : :"  DIAMOND SEAM ": :
570 AY=INT(RND*4)+2
580 FOR N=1 TO 30+(LEVEL)
590 DIR=INT(RND*5)+1
600 AX=AX+(DIR=1)-(DIR=3)
610 AY=AY-(DIR=2)-(DIR=4)+(DIR=5)
620 AX=AX-(AX<1)+(AX>19)
630 AY=AY-(AY<1)+(AY>19)
640 CALL HCHAR(AX,AY,136)
650 A(AX,AY)=DIAMOND
660 NEXT N
670 FOR N=1 TO 80+4*LEVEL
680 Y=INT(RND*19)+1
690 X=INT(RND*18)+2
700 IF A(Y,X)=DIAMOND THEN 760
710 IF A(Y,X)=ROCK THEN 770
720 A(Y,X)=0
730 IF ((Y=9)+(Y=10))*((X=18)+(X=19))THEN 770
740 A(Y,X)=ROCK
750 GOTO 770
760 A(Y,X)=0
770 NEXT N
780 CALL CLEAR
790 Y=10
800 X=1
810 PRINT "LEVEL -> ";LEVEL:
820 R=Y
830 FOR I=1 TO LEN(STR$(SCORE))
840 CALL HCHAR(2,22+I,ASC(SEG$(STR$(SCORE),I,I)))
850 NEXT I
860 V=19
870 FOR I=1 TO LEN(STR$(HIGH_SCORE))
880 CALL HCHAR(6,22+I,ASC(SEG$(STR$(HIGH_SCORE),I,I)))
890 NEXT I
900 C=0
910 FOR I=1 TO LEN(HIGHEST_SCORER$)
920 CALL HCHAR(5,22+I,ASC(SEG$(HIGHEST_SCORER$,I,I)))
930 NEXT I
940 CALL HCHAR(21,1,112,21)
950 CALL HCHAR(1,1,112,21)
960 CALL VCHAR(1,21,112,21)
970 CALL VCHAR(1,1,112,21)
980 FOR I=2 TO 20
990 CALL HCHAR(I,2,136,19)
1000 NEXT I
1010 CALL HCHAR(11,21,32)
1020 FOR N=1 TO 8-L
1030 C=C+1
1040 IF C>300+L*100 THEN 1910
1050 IF 50*LEVEL-C>0 THEN 1110
1060 TEST=TEST=0
1070 IF TEST=0 THEN 1090
1080 CALL SCREEN(16)
1090 IF TEST=-1 THEN 1110
1100 CALL SCREEN(L+4)
1110 IF C<20+L*100 THEN 1130
1120 CALL SOUND(-200,770,0,769,0,771,0)
1130 Z=X
1140 T=Y
1150 CALL KEY(0,KEY,STATUS)
1160 Y=Y+(KEY=ASC("E"))-(KEY=ASC("X"))
1170 X=X+(KEY=ASC("S"))-(KEY=ASC("D"))
1180 IF (Y=10)*(X=20)THEN 1560
1190 Y=Y-(Y<1)+(Y>19)
1200 X=X-(X<1)+(X>19)
1210 IF (R=Y)*(V=X)THEN 1910
1220 IF A(Y,X)=0 THEN 1330
1230 A(Y,X)=A(Y,X)-0.1
1240 IF A(Y,X)>0 THEN 1290
1250 SCORE=SCORE+LEVEL
1260 FOR I=1 TO LEN(STR$(SCORE))
1270 CALL HCHAR(2,22+I,ASC(SEG$(STR$(SCORE),I,I)))
1280 NEXT I
1290 X=Z
1300 Y=T
1310 CALL SOUND(-50,880,0)
1320 GOTO 1360
1330 REM
1340 CALL HCHAR(T+1,Z+1,111)
1350 CALL HCHAR(Y+1,X+1,132)
1360 REM
1370 NEXT N
1380 E=R
1390 S=V
1400 V=V-(V<X)+(V>X)
1410 IF A(R,V)=0 THEN 1440
1420 V=S
1430 GOTO 1470
1440 CALL HCHAR(E+1,S+1,111)
1450 CALL HCHAR(R+1,V+1,133)
1460 S=V
1470 R=R-(R<Y)+(R>Y)
1480 IF A(R,V)=0 THEN 1520
1490 R=E
1500 V=S
1510 GOTO 1540
1520 CALL HCHAR(E+1,S+1,111)
1530 CALL HCHAR(R+1,V+1,133)
1540 IF (R=Y)*(V=X)THEN 1910
1550 GOTO 1020
1560 CHECK=50*LEVEL-C
1570 IF CHECK<0 THEN 1640
1580 IF CHECK<300 THEN 1600
1590 CHECK=300
1600 SCORE=SCORE+CHECK+50
1610 IF SCORE>0 THEN 1640
1620 SCORE=0
1630 SC1=0
1640 GOSUB 2130
1650 SC2=SCORE-SC1
1660 SC1=SCORE
1670 PRINT "YOU GOT ";SC2;" THAT TIME";
1680 GOSUB 2010
1690 LEVEL=LEVEL+1
1700 L=L+1
1710 FOR I=1 TO 19
1720 FOR J=1 TO 19
1730 A(I,J)=0
1740 NEXT J
1750 NEXT I
1760 IF L<8 THEN 530
1770 L=1
1780 DIAMOND=DIAMOND+0.5
1790 ROCK=ROCK+0.2
1800 GOTO 530
1810 IF SCORE>HIGH_SCORE THEN 1850
1820 PRINT "THE HIGH SCORE IS ";HIGH_SCORE;" AND YOU GOT ";SCORE
1830 PRINT : : : : : :
1840 GOTO 310
1850 HIGH_SCORE=SCORE
1860 PRINT "THE HIGH SCORE IS ";HIGH_SCORE;" AND YOU GOT IT"
1870 INPUT "WHAT IS YOUR NAME ?":HIGHEST_SCORER$
1880 IF LEN(HIGHEST_SCORER$)<11 THEN 310
1890 PRINT "NOT THAT LONG"
1900 GOTO 1870
1910 GOSUB 1960
1920 FOR I=1 TO 1000
1930 NEXT I
1940 CALL CLEAR
1950 GOTO 1810
1960 FOR I=1 TO 3
1970 CALL SOUND(100,220,0,219,0,218,0)
1980 CALL SOUND(1,110,30)
1990 NEXT I
2000 CALL SOUND(1500,185,0,184,0,183,0)
2010 FOR I=1 TO 19
2020 FOR J=1 TO 19
2030 IF A(I,J)=0 THEN 2090
2040 IF A(I,J)<ROCK+0.1 THEN 2070
2050 CALL HCHAR(I+1,J+1,112)
2060 GOTO 2100
2070 CALL HCHAR(I+1,J+1,136)
2080 GOTO 2100
2090 CALL HCHAR(I+1,J+1,111)
2100 NEXT J
2110 NEXT I
2120 RETURN
2130 FOR I=135 TO 216 STEP 10
2140 CALL SOUND(100,I,0,I+1,0,I+2,0)
2150 NEXT I
2160 CALL SOUND(200,40000,30)
2170 CALL SOUND(300,110,0,111,0,112,0)
2180 RETURN
