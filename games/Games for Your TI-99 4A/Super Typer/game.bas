10 REM  SUPER TYPER MARK II
20 REM
30 REM  K BURFITT
40 REM
50 HS$="SUPER TYPER MARK "&CHR$(127)
60 HSCORE=400
70 CALL CHAR(127,"FF424242424242FF")
80 CALL CLEAR
90 RANDOMIZE
100 GOTO 540
110 FOR I=0 TO 9
120 A(I)=INT(RND*26)+65
130 B(I)=23
140 NEXT I
150 FOR I=0 TO 9
160 CALL HCHAR(B(I),I*2+5,A(I))
170 NEXT I
180 CALL KEY(0,K,S)
190 IF S<1 THEN 320
200 FOR I=0 TO 9
210 IF A(I)<>K THEN 310
220 CALL VCHAR(1,I*2+5,32,24)
230 SCORE=SCORE+5
240 CALL SOUND(-200,-6,0)
250 A(I)=INT(RND*26)+65
260 B(I)=23
270 FOR II=1 TO LEN(STR$(SCORE))
280 CALL HCHAR(8+II,2,ASC(SEG$(STR$(SCORE),II,1)))
290 NEXT II
300 CALL HCHAR(B(I),I*2+5,A(I))
310 NEXT I
320 X=INT(RND*10)
330 B(X)=B(X)-1-SCORE/100
340 IF B(X)<0.5 THEN 410
350 CALL HCHAR(B(X)+1+SCORE/100,X*2+5,32)
360 FOR I=B(X)+1+SCORE/100 TO B(X)STEP -1
370 CALL HCHAR(I+1,X*2+5,32)
380 CALL HCHAR(I,X*2+5,A(X))
390 NEXT I
400 GOTO 180
410 PRINT "YOU GET A SCORE OF ";SCORE;
420 FOR I=1 TO 1500
430 NEXT I
440 CALL CLEAR
450 IF SCORE<HSCORE THEN 540
460 PRINT "YOU HAVE TOP SCORE "
470 PRINT "THE PREVIOUS BEST WAS";HSCORE;"BY    ";HS$
480 INPUT "WHAT IS YOUR NAME ?":HS$
490 IF LEN(HS$)<25 THEN 520
500 PRINT "ONLY 24 CHARACTERS LONG, NOT";LEN(HS$);" CHARACTERS PLEASE"
510 GOTO 480
520 HSCORE=SCORE
530 CALL CLEAR
540 FOR I=1 TO LEN(HS$)
550 CALL HCHAR(I,26,ASC(SEG$(HS$,I,1)))
560 NEXT I
570 S$=STR$(HSCORE)
580 FOR I=1 TO LEN(S$)
590 CALL HCHAR(I,27,ASC(SEG$(S$,I,1)))
600 NEXT I
610 Q$="PRESS A KEY TO START"
620 FOR I=1 TO LEN(Q$)
630 CALL HCHAR(4,I+2,ASC(SEG$(Q$,I,1)))
640 NEXT I
650 CALL KEY(0,K,S)
660 IF S=0 THEN 650
670 CALL HCHAR(4,2,32,22)
680 SCORE=0
690 GOTO 110
