30 REM MINEFIELD FOR C-64
45 POKE53280,0:POKE53281,0
50 GOSUB 1130
60 REM ---INITIALIZE VARIABLES---
70 DIM BT(37),B3(37),B4(37),BP(37),BS(37),XM(4),YM(4),BC(25)
80 DEF FNY(X)=INT((X-1024)/40)
90 DEF FNX(X)=(X-40*FNY(X))-1024
100 DEF FNS(X)=1024+PX+40*PY
110 DEF FNP(X)=1307+INT(34*RND(1))+40*INT(15*RND(1))
120 DEF FNN(X)=PEEK(FNS(X))
130 FORJ=1 TO 4:READ XM(J),YM(J):NEXT
140 DATA 0,-1,0,1,-1,0,1,0
150 SC=0:BT=1680:NB=4:NW=0:D=54272
160 PRINT"{CLR}";:POKE 53272,21
170 PRINT"{RVS}{WHT}MINE****{S-} SCORE: 0"
180 PRINT"{RVS}{WHT}********{S-}{RIGHT}HI SCORE:";HS
190 PRINT"{RVS}{WHT}***FIELD{S-}{RIGHT}WAVE: 1"
200 PRINT"{RVS}{WHT}{8 SPACE}{S-}{RIGHT}";:IF NL<>1 THEN FORJ=1 TO NL-1:PRINT"z";:NEXT
210 FORJ=1024 TO 1183:IFPEEK(J)=32 THEN POKE J,160:POKEJ+D,1
220 NEXT
230 XP$="{RED}u{S-}i{DOWN}{4 LEFT}uu{S-}ii{DOWN}{6 LEFT}uuu{S-}iii{DOWN}{7 LEFT}{3 S*}*{3 S*}{DOWN}{7 LEFT}jjj{S-}kkk"
235 XP$=XP$+"{RED}{DOWN}{6 LEFT}jj{S-}kk{DOWN}{4 LEFT}j{S-}k"
240 S$="{HOME}{24 DOWN}"
250 Q$="{WHT}{40 RIGHT}"
260 XR$="{WHT}{3 SPACE}{DOWN}{4 LEFT}{5 SPACE}{DOWN}{6 LEFT}{7 SPACE}{DOWN}{7 LEFT}{3 SPACE}*{3 SPACE}{DOWN}{7 LEFT}{7 SPACE}"
265 XR$=XR$+"{DOWN}{6 LEFT}{5 SPACE}{DOWN}{4 LEFT}{3 SPACE}"
270 REM ---SET UP NEXT WAVE---
280 BG=0:NW=NW+1:IF NW>11 THEN 310
290 NB=NB+1.5:IF NW=1 THEN 330
300 IF NW<6 THEN BT=BT-180
310 PRINT"{HOME}{2 DOWN}{RVS}";TAB(15);NW
320 POKE FNS(1),32:FORJ=1 TO NB:POKEBP(J),32:NEXT
325 FORJ=1 TO 25:POKE BC(J),32:NEXT
330 BN=INT(NB):FORJ=1 TO NB:BS(J)=1:NEXT
340 FORJ=1 TO NB
350 BT(J)=(.4+INT(61*RND(1))/100)*BT
360 B3(J)=BT(J)+.5*BT(J):B4(J)=B3(J)+.25*BT(J)
370 NEXT
380 PX=19:PY=15:POKE FNS(1),90:POKEFNS(1)+D,1
390 FORJ=1 TO NB
400 BP(J)=FNP(1):IF PEEK(BP(J))<>32 THEN 400
410 POKE BP(J),87:POKEBP(J)+D,8:NEXT:NN=0
415 FORJ=1 TO 25
416 BC(J)=FNP(1):IF PEEK(BC(J))<>32 THEN 416
417 IF PEEK(BC(J)+1)=87 THEN 416
419 POKEBC(J),86:POKEBC(J)+D,5:NEXT
420 GET R$:IF R$<>"" THEN 420
430 DR=0:TX=TI
440 REM ---GET COMMANDS---
450 R=(15-(PEEK(56321)AND15))*2
460 IFR<>0THENDR=LOG(R)/LOG(2)
470 IFR=0THEN490
480 REM ---MOVE TRUCK---
490 IF DR=0 THEN 600
500 POKE FNS(1),32:PX=PX+XM(DR):PY=PY+YM(DR)
510 IF PX<0 THEN PX=39
520 IF PX>39 THEN PX=0
530 IF PY<4 THEN PY=24
540 IF PY>24 THEN PY=4
550 X=FNN(1)
560 IF X=32 THEN POKE FNS(1),90:POKEFNS(1)+D,1:GOTO 600
570 IF X=42 OR X=86 THEN 960
580 GOTO 890
590 REM ---UPDATE BOMBS---
600 NN=NN+1:IF NN>INT(NB)THEN NN=1
610 IF BS(NN)=0 THEN 600
620 TG=TI-TX
630 IF TG>B4(NN) THEN N1=NN:GOTO 720
640 IF BS(NN)>2 THEN 690
650 IF TG>BT(NN) THEN BS(NN)=2
660 IF TG>B3(NN) THEN BS(NN)=3
670 IF BS(NN)=1 THEN 450
680 IF BS(NN)=2 THEN POKE BP(NN),215:POKEBP(NN)+D,1:GOTO 450
690 IF BS(NN)=3 THEN POKE BP(NN),87:POKEBP(NN)+D,1:BS(NN)=4:GOTO 450
700 IF BS(NN)=4 THEN POKE BP(NN),215:POKEBP(NN)+D,1:BS(NN)=3:GOTO 450
710 REM ---BOMB EXPLODES---
720 TQ=TI:PD=0
725 X$="{OFF}"+LEFT$(S$,FNY(BP(N1))-2)+LEFT$(Q$,FNX(BP(N1))-1)
730 BS(N1)=0:N2=0:PRINTX$;XP$;
740 FORJ=1 TO NB:X=PEEK(BP(J)):IF BS(J)=0 THEN 760
750 IF X<>87 AND X<>215 AND X<>218 THEN N2=J
760 NEXT:IF FNN(1)<>90 AND FNN(1)<>218 THEN PD=1
770 PRINTX$;XR$;:GR=129:GOSUB2000
780 FORJ=1TONB:IF PEEK(BP(J))=32 AND BS(J)<>0 THEN POKE BP(J),87-128*(BS(J)>1)
790 NEXT:BN=BN-1
800 IF PD=1 THEN 960
810 IF BN=0 THEN 840
820 IF N2=0 THEN TX=TX+(TI-TQ):GOTO 450
830 N1=N2:GOTO 725
840 PRINT"{HOME}{2 DOWN}{RVS}";TAB(20);
850 FORJ=1 TO 20:PRINT"{RVS}COMPLETED{9 LEFT}";:FORK=1 TO 100:NEXT
860 PRINT"{RVS}{9 SPACE}{9 LEFT}";:FORK=1 TO 100:NEXT:NEXT
870 SC=SC-10*(INT(NB)-BG):IF SC<0 THEN SC=0
880 PRINT"{4 LEFT}{3 UP}{10 SPACE}{10 LEFT}";SC:GOTO 280
885 REM ---BOMB GATHERED---
890 BG=BG+1:TQ=TI:POKE FNS(1),218
895 FORJ=1 TO NB:IF PEEK(BP(J))=218 THEN AJ=BS(J):BS(J)=0
900 NEXT
910 IF AJ=4 THEN AJ=3
920 SC=SC+10*AJ:PRINT"{HOME}{RVS}";TAB(16);SC
930 GR=33:GOSUB2000:BN=BN-1:IF BN=0 THEN 840
940 TX=TX+(TI-TQ):GOTO 450
950 REM ---PLAYER DESTROYED---
960 GR=129:GOSUB2000
961 TQ=TI:FORJ=1 TO 20:POKE FNS(1),42:FORK=1 TO 25:NEXT:POKE FNS(1),170
970 FORK=1 TO 25:NEXT:NEXT:POKE FNS(1),32:NL=NL-1
980 POKE 1153+NL,160:DR=0:PX=19:PY=15
990 IF NL=0 THEN 1045
1000 IF BN=0 THEN 840
1010 GET R$:IF R$<>"" THEN 1010
1020 FORJ=1TONB:IF PEEK(BP(J))=32 AND BS(J)<>0 THEN POKE BP(J),87-128*(BS(J)>1)
1030 NEXT
1040 POKE FNS(1),90:TX=TX+(TI-TQ):GOTO 450
1045 IF SC>HS THEN HS=SC:PRINT"{HOME}{DOWN}{RVS}";TAB(19);HS
1050 FORJ=1 TO 1500:NEXT:PRINT"{HOME}{WHT}{2 DOWN}{RVS}";TAB(20);"GAME OVER{DOWN}{WHT}{9 LEFT}PLAY AGAIN?";
1060 PRINT"(Y/N){4 LEFT}";
1080 PRINT"{RVS}Y/{OFF}N{3 LEFT}";
1081 FORJ=1 TO 99:NEXT
1082 PRINT"{OFF}Y{RVS}/N{3 LEFT}";
1083 FORJ=1 TO 99:NEXT
1084 GET R$:IF R$="Y" THEN 1110
1090 IF R$<>"N" THEN 1080
1100 PRINT"{CLR}{WHT}LATER ON!":END
1110 GOSUB 1130:GOTO 150
1120 REM ---INSTRUCTIONS---
1130 PRINT"{CLR}{RVS}{WHT}m{SSPACE}i{SSPACE}n{SSPACE}e{SSPACE}f{SSPACE}i{SSPACE}e{SSPACE}l{SSPACE}d":POKE 53272,23
1140 PRINT"{WHT}dO YOU NEED INSTRUCTIONS (y/n)"
1150 GET R$:IF R$="N" THEN 1410
1160 IF R$<>"Y" THEN 1150
1180 PRINT"{CLR}{WHT}{DOWN}tHE OBJECT OF THIS GAME IS TO PICK UP"
1190 PRINT"{WHT}AS MANY BOMBS AS YOU CAN BEFORE THEY"
1200 PRINT"{WHT}EXPLODE. tO PICK UP A BOMB, JUST RUN"
1210 PRINT"{WHT}OVER IT WITH YOUR TRUCK."
1220 PRINT"{WHT}bOMBS WILL EXPLODE AFTER A SHORT TIME."
1230 PRINT"{WHT}iF A BOMB TURNS REVERSE-FIELD, BE CARE-"
1240 PRINT"{WHT}FUL WITH IT. iF IT STARTS TO BLINK, IT"
1250 PRINT"{WHT}WILL VERY SHORTLY EXPLODE-WATCH OUT!!"
1260 PRINT"{WHT}bOMBS WILL CHAIN-REACT; ONE BOMB CAUGHT"
1270 PRINT"{WHT}IN ANOTHER'S EXPLOSION WILL ALSO BLOW"
1280 PRINT"{WHT}UP. iF YOU ARE CAUGHT IN A BOMB'S "
1290 PRINT"{WHT}EXPLOSION, YOU WILL BE BLOWN UP."
1300 PRINT"{WHT}aLSO, DO NOT RUN INTO BOMB CRATERS (*)"
1310 PRINT"{WHT}OR MINES (x) OR YOU'LL BE TOTALLED."
1320 PRINT"{WHT}tHE CONTOLS ARE: 1 TO GO UP"
1330 PRINT"{WHT}{17 SPACE}CTRL TO GO LEFT"
1340 PRINT"{WHT}{17 SPACE}2 TO GO RIGHT"
1350 PRINT"{WHT}{17 SPACE}{<-} TO GO DOWN"
1355 PRINT"{WHT}OR YOU CAN USE A JOYSTICK IN PORT 1."
1360 PRINT"{WHT}yOUR TRUCK CANNOT STOP ONCE YOU BEGIN"
1370 PRINT"{WHT}MOVING. iT CAN WRAP-AROUND BOTH THE"
1380 PRINT"{WHT}THE TOP AND SIDES OF THE SCREEN."
1390 PRINT"{DOWN}{WHT}p{WHT}RESS return TO CONTINUE";
1400 GET R$:IF R$<>CHR$(13) THEN 1400
1410 PRINT"{CLR}s{WHT}ELECT SKILL SETTING (0-3)"
1420 GET R$:IF R$<"0" OR R$>"3" THEN 1420
1430 NL=4-VAL(R$):RETURN
1900 END
2000 REM SOUND OF EXPLOSION
2010 QW=54272
2020 FORS=QWTOQW+24:POKES,0:NEXT
2025 POKEQW+24,47
2030 POKEQW+5,64+7 :POKEQW+6,240
2050 POKEQW+4,GR :POKEQW+1,36:POKEQW,85
2060 FORT=1TO250:NEXT
2070 FORT=15TO0STEP-1:POKEQW+24,INT(T):NEXT
2080 RETURN
