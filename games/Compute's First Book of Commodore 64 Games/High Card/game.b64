10 REM*NECESSARY INITIAL INFORMATION*
20 DOLLARS=500
30 PRINT " WITH WHOM DO I HAVE THE PLEASURE"
40 PRINT " OF PLAYING HIGH CARD SLICE?"
50 INPUT NAME$
60 PRINT " HIGH CARD WINS IN THIS GAME!"
70 DIM SUIT$(4),CARD$(14):FOR I=11 TO 14:READ CARD$(I):NEXT I
80 FOR I=1 TO 4: READ SUIT$(I): NEXT I
90 REM
100 REM*MAIN PROGRAM LOOP*
110 PRINT: PRINT" YOU HAVE $" DOLLARS
120 IF DOLLARS<=0 THEN PRINT" THE GAME IS OVER. YOU ARE OUT OF CASH.":END
130 PRINT"WHAT IS YOUR BET";:INPUT BET
140 IF DOLLARS<BET THEN PRINT" YOU ONLY HAVE $"DOLLARS" TO BET, "NAME$:GOTO 130
150 YOURCARD=0:YURSUIT=0
160 PLAYER$=NAME$
170 GOSUB300
180 YOURCARD=CARD:YURSUIT=SUIT
190 PLAYER$=" THE COMPUTER"
200 GOSUB300
210 IF CARD>YOURCARD THEN GOTO 240
220 IF CARD=YOURCARD THEN PRINT " A TIE!":GOTO 100
230 PRINT NAME$ " WINS": DOLLARS = DOLLARS + BET:GOTO 100
240 PRINT " THE COMPUTER WINS": DOLLARS= DOLLARS-BET:GOTO 100
290 REM
300 REM* SUBROUTINE TO DRAW THE CARDS*
310 CARD=INT(RND(5)*13)+2:SUIT=INT(RND(5)*4)+1
320 IF CARD=YOURCARD AND SUIT=YURSUIT THEN 300:REM NO IDENTICAL DRAWS
330 IF CARD<11 THEN CARD$(CARD)=STR$(CARD)
340 PRINT PLAYER$ " DRAWS THE " CARD$(CARD) " OF " SUIT$(SUIT)
350 RETURN
490 REM
500 REM* DATA TABLE*
510 DATA JACK,QUEEN,KING,ACE
520 DATA CLUBS,DIAMONDS,HEARTS,SPADES
