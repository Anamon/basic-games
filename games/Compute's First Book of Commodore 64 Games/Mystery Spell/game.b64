100 GOSUB 2660
110 X=RND(-TI)
120 DIM W(20),W$(500)
130 GOSUB 1190 :REM DRAW HOUSE
140 PRINT"{HOME}{BLU}PLEASE WAIT...
150 GOSUB 1380 :REM POKE IN SPRITES
160 GOSUB 1970 :REM GET WORDS
170 GOSUB 690 :REM SET UP SPRITES
180 PRINT"{HOME}{14 SPACE}"
190 W$=W$(RND(1)*N+1)
200 GOSUB 650
210 L$=" ABCDEFGHIJKLMNOPQRSTUVWXYZ"
220 PRINT"{HOME}{17 DOWN}{8 RIGHT}";
230 FOR I=2 TO 14
240 PRINTMID$(L$,I,1)"{RIGHT}";
250 NEXT
260 PRINT:PRINT"{DOWN}{8 RIGHT}";
270 FOR I=15TO 27
280 PRINTMID$(L$,I,1)"{RIGHT}";
290 NEXT
300 PRINT"{HOME}{4 DOWN}"SPC(18-LEN(G$));
310 FOR I=1TO LEN(G$)
320 PRINTMID$(G$,I,1)"{RIGHT}";
330 NEXT
340 IF COUNT<>LEN(W$) THEN420
350 POKE 198,0
360 FOR DL=1TO100:NEXTDL:CL=CL+1:IFCL=3THENCL=1
370 PRINTMID$("{BLK}{CYN}",CL,1);
380 PRINT"{HOME}{14 SPACE}YOU WIN !!!!"
390 GETA$:IFA$=""THEN 360
400 GOTO 2610
410 GOSUB 2000
420 GETA$:IFA$<"A"ORA$>"Z"ANDA$<>"{<-}"THEN410
430 IF A$="{<-}"THEN 760
440 P=ASC(A$)-64
450 IF MID$(L$,P+1,1)<>" "THEN540
460 PRINT"{HOME}{4 DOWN}{8 SPACE}LETTER ALREADY CHOSEN{10 SPACE}"
470 FOR I=1 TO 800:NEXTI
480 PRINT"{HOME}{4 DOWN}{38 SPACE}"
490 PRINT"{HOME}{4 DOWN}"SPC(18-LEN(G$));
500 FOR I=1TO LEN(G$)
510 PRINTMID$(G$,I,1)"{RIGHT}";
520 NEXT
530 GOTO 420
540 L$=LEFT$(L$,P)+" "+MID$(L$,P+2)
550 RF=0 :REM FLAG FOR CORRECT GUESS
560 FOR I=1 TO LEN(W$) 
570 IF MID$(W$,I,1)<>A$ THEN 610
580 G$=LEFT$(G$,I)+MID$(W$,I,1)+MID$(G$,I+2)
590 RF=RF+1
600 COUNT=COUNT+1
610 NEXT I
620 IF RF=0 THEN GOSUB 1030
630 IF RF THEN GOSUB 2070
640 GOTO 220
650 G$=" "
660 FOR I=1 TO LEN(W$):G$=G$+"-":W(I)=0:NEXT
670 RETURN
680 I=I+1:GOTO1980
690 REM SET UP SPRITES
700 V=53248
710 FOR I=0 TO 15:POKE V+I,0:NEXT
720 POKE V+21,255
730 FOR I=V+39 TO V+46:POKE I,0:NEXT
740 X=0:Y=60:S=251
750 RETURN
760 PRINT"{HOME}{BLU}ENTER YOUR GUESS: ";
770 POKE V+21,PEEK(V+21)AND254
780 FOR I=1 TO LEN(W$):PRINT"{C@}";:NEXT
790 PRINT"{HOME}{18 RIGHT}";GU$;
800 IF LEN(GU$)<LEN(W$)THENPRINT"{C+}";
810 IF LEN(GU$)<LEN(W$)-1 THEN FOR I=2 TO LEN(W$)-LEN(GU$):PRINT"{C@}";
820 GET K$:IF K$=""THEN 820
830 IF K$=CHR$(20) AND LEN(GU$)>0 THEN GU$=LEFT$(GU$,LEN(GU$)-1):GOTO790
840 IF K$=CHR$(13) AND LEN(GU$)=LEN(W$) THEN 870
850 IF K$>="A" AND K$<="Z" AND LEN(GU$)<LEN(W$) THEN GU$=GU$+K$
860 GOTO 790
870 IF GU$<>W$ THEN 930
880 PRINT"{HOME}{38 SPACE}"
890 PRINT"{HOME}{4 DOWN}"SPC(18-LEN(" "+W$));
900 FOR I=1TO LEN(" "+W$)
910 PRINTMID$(" "+W$,I,1)"{RIGHT}";
920 NEXT:GOTO350
930 PRINT"{HOME}{BLK}{13 SPACE}SORRY... YOU LOSE{5 SPACE}"
940 PRINT"{BLK}THE WORD WAS ..."
950 PRINT"{HOME}{4 DOWN}"SPC(18-LEN(" "+W$));
960 FOR I=1TO LEN(" "+W$)
970 PRINTMID$(" "+W$,I,1)"{RIGHT}";
980 FOR D=1 TO 200:NEXT
990 NEXT
1000 POKE 198,0
1010 GETA$:IFA$=""THEN1010
1020 GOTO 2610
1030 DB=DB+1:S=S-3
1040 DX=32*DB+16:DY=225
1050 IF DB=8 THEN DB=0
1060 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKE V+1,Y:POKE2040,S
1070 IF X=0 THEN POKE V+21,PEEK(V+21)OR1
1080 FLAG=0
1090 IFABS(X-DX)>1THENX=X+3:FLAG=1:IFX>344THEN X=0:POKEV+21,PEEK(V+21)AND254
1100 IF Y<DY THEN Y=Y+2:FLAG=1
1110 S=S+1:IFS=251THENS=248
1120 IF FLAG THEN 1060
1130 X=DX:Y=DY
1140 POKEV+2*DB,XAND255:POKEV+16,PEEK(V+16)OR(2^DB)*(-(X>255))
1150 POKEV+2*DB+1,Y:POKE2040+DB,254
1160 IF DB<>0 THEN POKE V+21,PEEK(V+21)AND254
1170 X=0:Y=60:IF DB=0 THEN 930
1180 RETURN
1190 POKE 53281,3:POKE 53280,4
1200 PRINT"{CYN}{CLR}
1210 PRINT"{4 DOWN}
1220 PRINT 
1230 PRINT"{5 SPACE}{GRN}{3 SPACE}{RVS}{2 SPACE}{OFF}{10 SPACE}{WHT}{CD}{UP}{RVS}{CB}{OFF}{DOWN}{6 SPACE}{GRN}
1240 PRINT"{6 SPACE}{RVS}{CK}{4 SPACE}{OFF}{CJ}{6 SPACE}{RVS}{YEL}{S#}{C*}{BLK}{OFF}{2 CG}{3 SPACE}{GRN} {RVS}{CJ} {CL}{OFF}
1250 PRINT"{6 SPACE}{RVS}{CJ}{4 SPACE}{CL}{OFF}{5 SPACE}{RVS}{YEL}{S#}{2 SPACE}{C*}{OFF}{BLK}{CG}{3 SPACE}{GRN} {RVS}{3 SPACE}{OFF}
1260 PRINT"{6 SPACE}{RVS}{CG}{4 SPACE}{CN}{OFF}{4 SPACE}{RVS}{YEL}{S#}{4 SPACE}{C*}{OFF}{GRN}{3 SPACE}{RVS}{CJ}{3 SPACE}{CL}{OFF}
1270 PRINT"{6 SPACE}{RVS}{6 SPACE}{OFF}{4 SPACE}{RVS}{RED}{4 SPACE}{C#} {OFF}{GRN}{3 SPACE}{RVS}{5 SPACE}{OFF}
1280 PRINT"{6 SPACE}{C5}{2 SPACE}{RVS}{2 SPACE}{OFF}{6 SPACE}{RVS}{RED} {C#}{4 SPACE}{OFF}{2 SPACE}{GRN}{3 SPACE}{RVS}{C5} {OFF}
1290 PRINT"{RVS}{C6}{8 SPACE}{C5}{2 SPACE}{C6}{6 SPACE}{RED}{2 SPACE}{CI}{CF} {C#}{C6}{5 SPACE}{C5} {C6}{12 SPACE}";
1300 PRINT"{8 SPACE}{C5}{2 SPACE}{C6}{6 SPACE}{RED}{2 SPACE}{OFF} {RVS}{CK}{2 SPACE}{C6}{5 SPACE}{C5} {C6}{12 SPACE}";
1310 PRINT"{C6}{RVS}";
1320 FOR I=0 TO 8:PRINT"{40 SPACE}";:NEXT
1330 FOR I=1 TO 8 : L=1024+23*40+I*4 :POKE L,114:POKEL+54272,0:NEXT
1340 FOR I=0 TO 39:POKE 1024+24*40+I,160:POKE 55296+24*40+I,13:NEXT
1350 PRINT"{HOME}
1360 PRINT"{BLK}
1370 RETURN
1380 I=15872:IFPEEK(I+1)=96THENFORI=1TO64*6+2:READA:NEXT:RETURN
1390 READ A:IF A=256 THEN 1410
1400 POKE I,A:I=I+1:GOTO 1390
1410 FOR I=0 TO 63:POKE 254*64+I,PEEK(249*64+I):NEXT:RETURN
1420 DATA 0,96,0,0,113,224,0
1430 DATA 121,176,0,125,252,117,193
1440 DATA 192,127,255,192,113,255,128
1450 DATA 0,252,0,0,24,0,0
1460 DATA 24,0,0,102,0,0,102
1470 DATA 0,0,0,0,0,0,0
1480 DATA 0,0,0,0,0,0,0
1490 DATA 0,0,0,0,0,0,0
1500 DATA 0,0,0,0,0,0,0
1510 DATA 0,0,0,0,0,1,224
1520 DATA 0,1,176,0,127,252,117
1530 DATA 193,192,127,255,192,113,255
1540 DATA 128,0,252,0,0,24,0
1550 DATA 0,24,0,0,102,0,0
1560 DATA 102,0,0,0,0,0,0
1570 DATA 0,0,0,0,0,0,0
1580 DATA 0,0,0,0,0,0,0
1590 DATA 0,0,0,0,0,0,0
1600 DATA 0,0,0,0,0,0,1
1610 DATA 224,0,1,176,112,127,252
1620 DATA 127,221,192,115,185,192,1
1630 DATA 179,128,0,172,0,0,24
1640 DATA 0,0,24,0,0,102,0
1650 DATA 0,102,0,0,0,0,0
1660 DATA 0,0,0,0,0,0,0
1670 DATA 0,0,0,0,0,0,0
1680 DATA 0,0,0,0,0,0,0
1690 DATA 0,0,0,0,96,0,0
1700 DATA 113,224,0,121,176,0,125
1710 DATA 252,117,193,192,127,255,192
1720 DATA 113,255,128,0,252,0,0
1730 DATA 0,0,0,0,0,0,0
1740 DATA 0,0,0,0,0,0,0
1750 DATA 0,0,0,0,0,0,0
1760 DATA 0,0,0,0,0,0,0
1770 DATA 0,0,0,0,0,0,0
1780 DATA 0,0,0,0,0,0,0
1790 DATA 0,1,224,0,1,176,0
1800 DATA 127,252,117,193,192,127,255
1810 DATA 192,113,255,128,0,252,0
1820 DATA 0,0,0,0,0,0,0
1830 DATA 0,0,0,0,0,0,0
1840 DATA 0,0,0,0,0,0,0
1850 DATA 0,0,0,0,0,0,0
1860 DATA 0,0,0,0,0,0,0
1870 DATA 0,0,0,0,0,0,0
1880 DATA 0,0,1,224,0,1,176
1890 DATA 112,127,252,127,221,192,115
1900 DATA 185,192,1,179,128,0,172
1910 DATA 0,0,112,0,0,0,0
1920 DATA 0,0,0,0,0,0,0
1930 DATA 0,0,0,0,0,0,0
1940 DATA 0,0,0,0,0,0,0
1950 DATA 0,0,0,0,0,0,0
1960 DATA 0,0,0,0,0,0,0,256
1970 I=1
1980 READ W$(I):IFW$(I)="*"THENN=I-1:RETURN
1990 I=I+1:GOTO1980
2000 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKE V+1,Y:POKE2040,S
2010 IF X=0 THEN POKE V+21,PEEK(V+21)OR1
2020 X=X+3:IFX>344 THEN X=0:POKEV+21,PEEK(V+21)AND254
2030 Y=Y-1+RND(1)*2:IFY>100THENY=99
2040 IF Y<50 THEN Y=50
2050 S=S+1:IFS=254THENS=251
2060 RETURN
2070 DX=INT(P+13*(P>13))*16+24+40
2080 DY=173+INT(P/14)*24:IF S>250 THEN S=S-3
2090 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKEV+1,Y:POKE2040,S
2100 IF X=0 THEN POKE V+21,PEEK(V+21)OR1
2110 FLAG=0
2120 IFABS(X-DX)>2THENX=X+3:FLAG=1:IFX>344THENX=0:POKEV+21,PEEK(V+21)AND254
2130 IF Y<DY THEN Y=Y+2:FLAG=1
2140 S=S+1:IFS=251THENS=248
2150 IF FLAG THEN 2090
2160 X=DX:Y=DY
2170 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKEV+1,Y:POKE2040,249
2180 POKE 56334,PEEK(56334)AND254
2190 POKE 1,PEEK(1)AND251
2200 FOR I=0 TO 7
2210 B=PEEK(53248+8*P+I)
2220 FOR J=248 TO 250
2230 POKE J*64+40+I*3,B
2240 NEXT J,I
2250 POKE 1,PEEK(1)OR4
2260 POKE 56334,PEEK(56334)OR1
2270 PRINT"{HOME}{17 DOWN}{8 RIGHT}";
2280 FOR I=2 TO 14
2290 PRINTMID$(L$,I,1)"{RIGHT}";
2300 NEXT
2310 PRINT:PRINT"{DOWN}{8 RIGHT}";
2320 FOR I=15TO 27
2330 PRINTMID$(L$,I,1)"{RIGHT}";
2340 NEXT 
2350 DX=160-8*LEN(G$):DY=69
2360 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKEV+1,Y:POKE2040,S
2370 IF X=0 THEN POKE V+21,PEEK(V+21)OR1
2380 FLAG=0
2390 IFABS(X-DX)>2THENX=X+3:FLAG=1:IFX>344THEN X=0:POKEV+21,PEEK(V+21)AND254
2400 IF Y>DY THEN Y=Y-2:FLAG=1
2410 S=S+1:IFS=251THENS=248
2420 IF FLAG THEN 2360
2430 X=DX:Y=DY
2440 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKEV+1,Y:POKE2040,249
2450 PRINT"{HOME}{4 DOWN}"SPC(18-LEN(G$));
2460 FOR I=1TO LEN(G$)
2470 IF MID$(G$,I,1)=A$ THEN PRINT A$;:RF=RF-1:IFRF=0 THEN GOSUB 2560
2480 IF MID$(G$,I,1)<>A$ THEN PRINT"{RIGHT}";
2490 PRINT"{RIGHT}";
2500 IF RF=0 THEN I=100:GOTO2540
2510 FOR J=0 TO 15:X=X+1:S=S+1:IFS=251THENS=248
2520 POKEV,XAND255:POKEV+16,PEEK(V+16)AND254OR-(X>255):POKE2040,S
2530 NEXT J
2540 NEXT I
2550 RETURN
2560 FOR K=0 TO 7
2570 FOR J=248 TO 250
2580 POKE J*64+40+K*3,0
2590 NEXT J,K
2600 RETURN
2610 PRINT "{CLR}{7 DOWN}{BLK}DO YOU WISH TO PLAY AGAIN (Y/N) ?"
2615 POKE V+21,PEEK(V+21)AND254
2620 PRINT"{10 DOWN}YOU MISSED THIS MANY :"
2630 GETA$:IFA$<>"N"AND A$<>"Y"THEN2630
2640 IF A$="Y"THENPOKE V+21,0:RUN110
2650 END
2660 POKE 53281,0:POKE 53280,0
2670 PRINT"{CLR}{YEL}{13 SPACE}INSTRUCTIONS
2680 PRINT"{2 DOWN}{WHT}{4 SPACE}CHOOSE LETTERS TO GUESS THE WORD.
2690 PRINT"{DOWN}IF YOU CHOOSE A WRONG LETTER, THE BIRD
2700 PRINT"{DOWN}WILL LAND ON ITS PERCH.
2710 PRINT"{DOWN}{4 SPACE}WHEN ALL THE PERCHES ARE FULL, OR
2720 PRINT"{DOWN}YOU GUESSED THE WORD, THE GAME IS OVER
2730 PRINT"{2 DOWN}{4 SPACE}YOU CAN HIT THE "CHR$(34)"{<-}"CHR$(34)" KEY ANYTIME TO
2740 PRINT"{DOWN}GUESS THE WORD. IF YOU GET IT WRONG,{DOWN}{4 SPACE}YOU LOSE.
2750 PRINT"{3 DOWN}{9 RIGHT}{YEL}HIT A KEY TO BEGIN"
2760 GETA$:IFA$=""THEN2760
2770 RETURN
2780 DATA HAPPY,BRIDGE,FAMILY,CHILDREN
2790 DATA WINDOW,TRAIN,DWARF,BIRDS
2800 DATA SUPERMAN,CONCERT,PEOPLE,MAGIC
2810 DATA SPACE,SCIENCE,PLANETS,GALAXY,STARS
2820 DATA ROOMS,TEACHER,CHALK,BLACKBOARD
2830 DATA SCREEN,COMPUTER,KEYBOARD,PROGRAM
2840 DATA SPELLING,WORDS,COLORS,LETTERS
2850 DATA MARKET,STREETS,SQUARE,TRIANGLE
2860 DATA MOVIE,SPACESHIP,LASER,AIRPLANE,BOAT
2870 DATA STICK,ROCK,PAPER,WIN,PLACE,SHOW
2880 DATA CHANNEL,EXECUTIVE,MONEY,SHIRT
2890 DATA QUIET,LOUD,BILLBOARD,YACHT,MOTORCYCLE,*
