5 POKE53280,0:POKE53281,0:GOSUB190:PRINT"{CLR}":GOTO85
10 POKEROW,X:POKECOL,Y:PRINT"{UP}";:RETURN
11 POKEROW,A:POKECOL,B:PRINT"{UP}";:RETURN
12 POKEROW,Z:POKECOL,B:PRINT"{UP}";:RETURN
13 GOSUB10:PRINTG1$;
14 GOTO38
16 TT=TT+1:R=1+INT(RND(1)*10):IFTT>TDTHENIFR=10GOTO43
18 IFPEEK(197)=6THEN29
19 IFPEEK(197)=5THEN23
20 IFPEEK(197)=3THEN26
21 GOTO16
23 X=X-1:IFX<1THENX=1
24 GOSUB10:PRINTG1$;:GOTO16
26 X=X+1:IFX>21THENX=21
27 GOSUB10:PRINTG2$;:GOTO16
29 GOSUB180
30 X=X+1:Y=3:GOSUB10:FORI=1TO185STEP5:PRINT"{PUR}>";:NEXT::GOSUB10
31 FORI=1TO37:PRINT" ";:NEXT:X=X-1:Y=0
33 IFX+1=ATHEN60
34 IFX+1=A+1THEN60
35 IFX+1=A+2THEN60
36 GOTO16
38 A=1+INT(RND(1)*21):IFA<3THENA=3
39 IFA>19THENA=19
41 GOSUB11:PRINTIN$:GOTO16
43 GOSUB170:Z=A+1:B=B-1:GOSUB12:FORI=1TO72STEP2:PRINT"{RED}{<-}{2 LEFT}";:NEXT
45 PRINT"{RIGHT}{UP}n{2 DOWN}{LEFT}m":GOSUB12:FORI=1TO36:PRINT" {2 LEFT}";:NEXT:PRINT"{RIGHT}{UP} {2 DOWN}{LEFT} ":B=B+1
47 HT=SR+((Z-1)*40):RD=PEEK(HT)
48 IFRD=160THENRN=1:GOTO57
49 IFRD=231THENRN=2:GOTO57
50 IFRD=234THENRN=3:GOTO57
51 IFRD=246THENRN=4:GOTO57
52 IFRD=97THENRN=5:GOTO57
53 IFRD=117THENRN=6:GOTO57
54 IFRD=116THENRN=7:GOTO57
55 IFRD=101THENRN=8:GOTO57
56 IFRD=32THENRN=8:GOTO68
57 FORI=1TORN:READFE:NEXT:POKEHT,FE:RESTORE:GOTO16
58 DATA 231,234,246,97,117,116,101,32
60 GOSUB11:PRINT"{RED}{2 LEFT}{<-}{UP}{YEL}+{2 DOWN}{3 LEFT} {DOWN}{2 LEFT}{C*}{DOWN}{RVS}{S#}"
61 FORI=1TO20:NEXT:GOSUB11:PRINT"{2 LEFT} {UP} {2 DOWN}{3 LEFT} {DOWN}{2 LEFT} {DOWN} "
62 GOSUB11:PRINT"{2 UP}{LEFT}{S#}{2 DOWN}{3 LEFT}{RED}m{YEL}^{2 DOWN}{3 LEFT}{YEL}{<-}{DOWN}{LEFT} {DOWN}{LEFT}"
63 FORI=1TO20:NEXT:GOSUB11:PRINT"{2 UP}{LEFT} {2 DOWN}{3 LEFT}{2 SPACE}{2 DOWN}{3 LEFT} {DOWN}{LEFT} {DOWN}{LEFT}":GOSUB160
64 FORI=1TO20:NEXT
65 GOSUB11:PRINT" {LEFT}{DOWN} {LEFT}{DOWN} {LEFT}{DOWN}":GOSUB77
67 SC=SC+1:TT=0:GOTO38
68 FORI=1TO500:NEXT
70 PRINT"{CLR}{WHT}{3 DOWN}{10 SPACE}YOU HIT";SC;"INVADERS":GOSUB170:GOSUB170:GOSUB160
71 GOSUB160:GOSUB160:GOSUB160:PRINT"{3 DOWN}{14 SPACE}TRY AGAIN?{3 SPACE}"
72 GOSUB170:GOSUB160:GETC$:IFC$=""THEN72
73 IFC$<>"Y"ANDC$<>"N"THEN72
74 IFC$="N"THENPRINT"{CLR}":END
75 SC=0:GOTO123
76 REM------GENERATE STARS------------
77 SR=SR-2:P=46
78 POKESR+15,P:POKESR+28,P:POKESR+127,P:POKESR+158,P:POKESR+175,P:POKESR+226,P
79 POKESR+330,P:POKESR+460,P:POKESR+474,P:POKESR+390,P:POKESR+575,P
80 POKESR+605,P:POKESR+628,P:POKESR+703,P:POKESR+715,P:POKESR+730,P
81 POKESR+806,P:POKESR+819,P:POKESR+837,P:POKESR+868,P:POKESR+883,P
82 POKESR+904,P:POKESR+928,P:POKESR+947,P:POKESR+964,P:POKESR+992,P
83 SR=SR+2:RETURN
84 REM-------SET VARIABLES------------
85 ROW=214:COL=211:X=5:Y=.:IN$="{YEL}{RVS}{S#}{DOWN}{LEFT}{RVS}{<-}{OFF}{DOWN}{LEFT}{C*}{WHT}":B=39:G1$="{CYN}{RVS}v{OFF}{DOWN}{LEFT}{S+}{DOWN}{LEFT}{RVS}v{OFF}{DOWN}{LEFT} "
86 G2$="{CYN}{UP} {DOWN}{LEFT}{RVS}v{OFF}{DOWN}{LEFT}{S+}{DOWN}{LEFT}{RVS}v{OFF}":SR=1026:M=21:I=RND(-T1)
87 GOSUB77:GOTO91
88 REM--------LOCATION ROUTINE--------
89 POKEROW,M:POKECOL,Y:PRINT"{UP}";:RETURN
90 REM-------TITLES TO 118-----------
91 M=M-1:GOSUB89:PRINTG1$;
92 IFM>7THEN91
93 FORI=1TO200:NEXT:GOSUB89
94 PRINT"{DOWN}{2 RIGHT}";:FORI=1TO110STEP10:PRINT"{GRN}>";:NEXT:PRINT"{RVS} LASER GUNNER {OFF}";
95 GOSUB180:FORI=1TO120STEP10:PRINT">";:NEXT
96 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO11:PRINT" ";:NEXT:PRINT"{14 RIGHT}";
97 FORI=1TO12:PRINT" ";:NEXT:GOSUB160
98 GOSUB89:M=M+1:PRINT"{DOWN}"G2$;
99 IFM<12THEN98
100 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO200:NEXT
101 FORI=1TO170STEP10:PRINT"{RED}>";:NEXT:GOSUB170:PRINT"{PUR}AN";
102 FORI=1TO170STEP10:PRINT"{RED}>";:NEXT:GOSUB170
103 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO17:PRINT" ";:NEXT:PRINT"{2 RIGHT}";:FORI=1TO17
104 PRINT" ";:NEXT
105 GOSUB89:M=M+1:PRINT"{DOWN}"G2$;
106 IFM<16THEN105
107 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO200:NEXT
108 FORI=1TO112STEP10:PRINT"{PUR}>";:NEXT:GOSUB170:PRINT"{YEL}ACTION GAME";
109 FORI=1TO110STEP10:PRINT"{PUR}>";:NEXT:GOSUB 170
110 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO12:PRINT" ";:NEXT:PRINT"{11 RIGHT}";
111 FORI=1TO13:PRINT" ";:NEXT
112 GOSUB89:M=M+1:PRINT"{DOWN}"G2$;
113 IFM<22THEN112
114 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO200:NEXT
115 FORI=1TO90STEP10:PRINT"{RED}>";:NEXT:GOSUB180:PRINT"{CYN}{RVS} WANT INSTRUCTIONS?{OFF}";
116 FORI=1TO100STEP10:PRINT"{RED}>";:NEXT:GOSUB160
117 GOSUB89:PRINT"{DOWN}{2 RIGHT}";:FORI=1TO9:PRINT" ";:NEXT:PRINT"{19 RIGHT}";
118 FORI=1TO10:PRINT" ";:NEXT
119 REM-----WANT INSTRUCTIONS-------
120 GETC$:IFC$=""THENGOSUB170:GOSUB170:GOSUB170:GOSUB160:GOTO114
121 IFC$="Y"THEN134
122 REM------PICK SKILL LEVEL---------
123 PRINT"{CLR}{YEL}{6 DOWN}{5 SPACE}{RVS}{2 SPACE}PICK SKILL LEVEL{5 SPACE}(1-3){OFF}"
124 GOSUB170:GETC:IFC=0THEN124
125 IFC>3THEN124
126 PRINT"{CLR}{YEL}{6 DOWN}{8 SPACE}{RVS} PRESS [SPACE] TO BEGIN {OFF}"
127 GOSUB160:GETC$:IFC$=""THEN127
128 IFC=1THENTD=15
129 IFC=2THENTD=8
130 IFC=3THENTD=0
131 REM------BUILD FORCE FIELD-------
132 PRINT"{CLR}":GOSUB77:PRINT"{HOME}";:FORI=0TO23:PRINTTAB(2)"{RVS}{GRN} {OFF}":NEXT:GOTO13
133 REM---------INSTRUCTIONS--------
134 PRINT"{CLR}{DOWN}{2 SPACE}{RVS}{CYN} YOU ARE LASER GUNNER ON A STARSHIP "
135 PRINT"{DOWN} YOU ARE UNDER ATTACK BY ALIEN INVADERS"
136 PRINT"{DOWN}{5 SPACE}YOU MUST MOVE YOUR LASER INTO"
137 PRINT"{4 SPACE}POSITION, AND FIRE IT TO DESTROY"
138 PRINT"{11 SPACE}THE ALIEN SHIP"
141 PRINT"{2 DOWN}{3 SPACE}YOU ARE PROTECTED BY A FORCE FIELD"
142 PRINT"{4 SPACE}BUT THE FORCE FIELD IS WEAKENED"
143 PRINT"{5 SPACE}WITH EVERY HIT BY AN INVADER"
144 PRINT"{2 DOWN}{5 SPACE}A HIT IN A HOLE ENDS THE GAME"
145 PRINT"{2 DOWN}{7 SPACE}TO MOVE UP----HIT {RVS} F3 {OFF} KEY"
146 PRINT"{7 SPACE}TO FIRE-------HIT {RVS} F5 {OFF} KEY"
147 PRINT"{7 SPACE}TO MOVE DOWN--HIT {RVS} F7 {OFF} KEY"
148 PRINT"{2 DOWN}{8 SPACE}{RVS}PRESS SPACE TO CONTINUE{OFF}"
149 GETC$:IFC$=""THEN149
150 GOTO123
155 REM--------SOUND SUBROUTINES-------
160 POKEW1,21:POKEW2,129:FORZ=20TO1STEP-2:POKEH1,Z:POKEL1,Z
161 POKEH2,INT(RND(1)*70)+3:POKEL2,Z:NEXT:POKEW1,0:POKEW2,0:RETURN
170 POKEW1,17:POKEW2,129:FORZ=35TO0STEP-7:POKEH1,Z:POKEL1,Z:POKEL2,Z
171 POKEH2,INT(RND(1)*70):NEXT:POKEW1,0:POKEW2,0:RETURN
180 POKEW1,21:FORZ=1TO3:FORZX=0TO100STEP15:POKEH1,ZX:POKEL1,ZX:NEXT:NEXT
181 POKEW1,0:RETURN
190 POKE54296,15:POKE54277,15:POKE54291,65 :W1=54276:W2=54290:H1=54273:L1=54272
191 H2=54287:L2=54286:RETURN
