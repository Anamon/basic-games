100 POKE53280,14:POKE53281,6:POKE55,176:POKE56,29:CLR:POKE54276,8:POKE54283,8
110 POKE54277,0:POKE54278,255:POKE54284,0:POKE54285,255:POKE54296,15
120 S=1355:SC=S+54272:DIMA$(16)
130 PRINT"{CLR}":G=1632:X=0:DX=1:P=55904:S1=54276:S2=54283:AD=1232:R=14
140 PRINT"{CLR}{DOWN}"TAB(11)" MOSAIC PUZZLE"TAB(50)"{17 CY}{DOWN}"
150 :
160 :
170 REM FIND TIME LIMIT, MOVE KEYS
180 PRINT"{C7} DO YOU WANT A TIME LIMIT? ";:GOSUB 270
190 IFIN$<>"Y"THEN240
200 H=1:INPUT"{HOME}{6 DOWN} HOURS MINS SECS (6 DIGITS)";T$:IFLEN(T$)<>6THEN200
210 IFLEFT$(T$,2)>"23"ORLEFT$(T$,2)<"0"THEN200
220 IFMID$(T$,3,2)>"59"ORMID$(T$,3,2)<"0"THEN200
230 IFRIGHT$(T$,2)>"59"ORRIGHT$(T$,2)<"0"THEN200
240 PRINT"{DOWN} KEY FOR UP: ";:GOSUB270:U$=IN$:PRINT"{DOWN} FOR DOWN: ";:GOSUB270:D$=IN$
250 PRINT"{DOWN} FOR LEFT: ";:GOSUB270:L$=IN$:PRINT"{DOWN} FOR RIGHT: ";:GOSUB270:R$=IN$
260 GOTO310
270 PRINT"{C+}";:WAIT198,255:GETIN$:PRINT"{LEFT}";:POKE216,1:PRINTIN$:RETURN
280 :
290 :
300 REM FIND GOAL ORDER
310 PRINT"{CLR}"TAB(43)"ENTER GOAL SETUP"
320 PRINT"{DOWN}{3 SPACE}1 2 3 4 5 6 7 8 9"SPC(23)"A B C D E F {RVS}SPACE"
330 PRINTTAB(5)"{DOWN}IN ANY ORDER":PRINTTAB(248)"GOAL
340 FORK=0TO3:POKE1592+K,100:POKE1792+K,99:POKE55864+K,R:POKE56064+K,R
350 POKE1631+K*40,103:POKE1636+K*40,101:POKE55903+K*40,R:POKE55908+K*40,R:NEXT
360 FORI=1TO16:POKEG+X,63:POKEP+X,1
370 WAIT198,255:GETA$(I):FORL=I-1TO0STEP-1:IFA$(I)=A$(L)THEN370
380 NEXT:IFA$(I)=" "THENFORK=0TO4:POKE55471+K,15:NEXT:B2=32:GOTO420
390 IF(A$(I)<"1"ORA$(I)>"F")OR(A$(I)>"9"ANDA$(I)<"A")THEN370
400 B=VAL(A$(I)):B2=B+48:IFBTHENPOKE55417+2*B,15:GOTO420
410 B=ASC(A$(I))-64:B2=B:POKE55457+2*B,15
420 POKEG+X,B2:X=X+DX:IFX=4THENG=G+40:P=P+40:X=0
430 NEXT
440 :
450 :
460 REM SET UP WORK AREA
470 PRINT"{HOME}":FORI=0TO64:PRINT"{4 SPACE}";:NEXT:PRINT"{HOME}"TAB(127)"PUZZLE"
480 FORK=0TO3:POKE1192+K,100:POKE55464+K,R:POKE1392+K,99:POKE55664+K,R
490 POKE1231+K*40,103:POKE55503+K*40,R:POKE1236+K*40,101:POKE55508+K*40,R:NEXT
500 READA,B,C:IFA>=0THENPOKEAD+A,B:POKE55504+A,C:GOTO500
510 FORI=1TO500:NEXT:POKES1-3,80:POKES1,33:PRINT"{HOME}"TAB(28)"{10 DOWN}{RED}{WHT}!GO!{C7}"
520 FORT=1TO300:NEXT:PRINT"{HOME}"TAB(28)"{10 DOWN}{4 SPACE}":POKES1,8:TI$="000000"
530 PRINT"{HOME}"TAB(25)"LIMIT:{CYN}";:IFT$=""THENPRINT"NONE":GOTO580
540 PRINTLEFT$(T$,2)":"MID$(T$,3,2)":"RIGHT$(T$,2)"{C7}"
550 :
560 :
570 REM LOOP MAIN CONTROL
580 PRINT"{HOME}TIME ELAPSED:{WHT}"LEFT$(TI$,2)":"MID$(TI$,3,2)":"RIGHT$(TI$,2)"{C7}"
590 IFH=1ANDT$<=TI$THEN750
600 GETB$:J=31-PEEK(56320)AND31:IFB$=""ANDJ=0THEN580
610 IFB$=CHR$(13)ORJ=16THENWN=0:GOTO780
620 IFB$=D$OR(JAND2)THENDR=-40:CK=100:GOTO660
630 IFB$=L$OR(JAND4)THENDR=1:CK=101:GOTO660
640 IFB$=R$OR(JAND8)THENDR=-1:CK=103:GOTO660
650 DR=40:CK=99:IFB$<>U$AND(JAND1)=0THEN580
660 IFPEEK(S+DR)=CKTHEN580
670 POKES,PEEK(S+DR):POKESC,PEEK(SC+DR):POKES+DR,32:S=S+DR:SC=SC+DR
680 FORM=0TO120STEP40:FORN=0TO3:W=PEEK(AD+M+N)AND127:IFW<>PEEK(1632+M+N)THEN580
690 NEXT:NEXT:PRINT"{HOME}"TAB(24)"{5 DOWN}{CYN}{RVS}YOU WIN!{C7}":POKES1-3,0:POKES1,33:WN=1
700 READN1,N2,D:IFN1=-1THENPOKES1,8:GOTO780
710 POKES1-4,N1:POKES1-3,N2:FORT=1TOD:NEXT:GOTO700
720 :
730 :
740 REM END OF GAME
750 PRINT"{HOME}"TAB(23)"{5 DOWN}{WHT}{RVS}!YOU LOSE!{C7}":POKES1-3,10:POKES1,17:WN=1
760 POKES2-3,60:POKES2,129:FORT=1TO300:NEXT:POKES2,8:POKES1,8
770 :
780 TM$=TI$:PRINT"{HOME}"TAB(21)"{9 DOWN}(1) RESET
790 PRINTTAB(21)"{DOWN}(2) QUIT":IFWN=0THENPRINTTAB(21)"{DOWN} (3) AS YOU LEFT IT"
800 GETV$:IFV$<"1"ORV$>"3"THEN800
810 IFV$="1"THENRUN
820 IFV$="2"THENEND
830 IFWNTHEN800
840 PRINT"{HOME}{8 DOWN}":FORI=1TO6:PRINTTAB(21)"{18 SPACE}":NEXT
850 TI$=TM$:GOTO580
860 :
870 :
880 REM SETUP AND MUSIC DATA
890 DATA0,49,1,1,178,3,2,51,1,3,180,3
900 DATA40,53,1,41,182,3,42,55,1,43
910 DATA184,3,80,57,1,81,129,3,82,2,1
920 DATA83,131,3,120,4,1,121,133,3,122
930 DATA6,1,123,32,3,-1,-1,-1
940 DATA 96,22,150,0,0,50,96,22,75,0,0,50,96,22,75,49,28,175,96,22,115,49,28
950 DATA175,135,33,250,0,0,0,-1,-1,-1
