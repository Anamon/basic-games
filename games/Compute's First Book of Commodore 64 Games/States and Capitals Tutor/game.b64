5 OPEN 1,8,0,"STATES"
10 DIM ST$(49,1)
15 FOR I=0 TO 49
20 FOR J=0 TO 1
25 INPUT#1,ST$(I,J)
30 NEXT J
35 NEXT I
40 CLOSE 1
45 K=0:RT%=0:WR%=0:HE%=0
48 PRINTCHR$(147)
50 PRINT"STATES TUTOR"
55 PRINT:PRINT"THIS PROGRAM TUTORS THE STUDENT IN{6 SPACE}STATES AND CAPITALS"
60 PRINT:PRINT"IF YOU DON'T KNOW AN{2 SPACE}ANSWER,TYPE 'HELP'"
65 PRINT:PRINT "PRESS ANY KEY TO CONTINUE"
70 GET A$:IF A$=""THEN70
100 R1%=INT((50-K)*RND(-RND(0)))
105 R2%=INT(2*RND(1))
110 PRINTCHR$(147)
115 IF R2%=0 THEN 130
120 PRINT"THE CAPITAL OF ":PRINTST$(R1%,0);" IS"
125 GOTO 140
130 PRINTST$(R1%,1):PRINT:PRINT"IS THE CAPITAL OF WHAT STATE?"
140 INPUT AN$
145 IF AN$=ST$(R1%,R2%)THEN170
150 IF AN$="HELP"THEN220
155 GOTO 250
170 RT%=RT%+1
175 PRINT"THAT'S RIGHT!"
180 FOR I=0TO1
185 I$=ST$((49-K),I)
190 ST$((49-K),I)=ST$(R1%,I)
195 ST$(R1%,I)=I$
200 NEXTI
205 K=K+1
210 GOTO 300
220 HE%=HE%+1
225 PRINT:PRINT"THE ANSWER IS..."
230 PRINTSPC(5)ST$(R1%,R2%)
235 GOTO 300
250 WR%=WR%+1
255 PRINT:PRINT"SORRY.THE CORRECT ANSWER IS "
260 PRINT:PRINTSPC(5)ST$(R1%,R2%)
300 PRINT:PRINT:PRINT:PRINT
305 PRINT"YOUR SCORE IS:"
310 PRINTSPC(5)RT%;" RIGHT"
315 PRINTSPC(5)WR%;" WRONG"
320 PRINTSPC(5)HE%;" HELPS"
325 IF RT%=50THEN400
330 PRINT:PRINT"PRESS ANY KEY TO CONTINUE "
335 GET A$:IF A$=""THEN 335
340 GO TO 100
400 IF WR%+HE%=0THEN430
405 PRINT"THAT'S ALL. BUT NOT ALL YOUR ANSWERS"
406 PRINT "WERE CORRECT OR I HAD TO HELP YOU."
408 PRINT "PRESS ANY KEY TO START OVER"
410 GET A$:IF A$=""THEN410
415 GOTO45
430 PRINT:PRINT"YOU DID IT!!!!!"
435 PRINT"A PERFECT SCORE AND I DIDN'T HELP"
440 PRINT:PRINT"PRESS ANY KEY TO START OVER"
445 GET A$:IF A$=""THEN 445
450 GOTO 45
