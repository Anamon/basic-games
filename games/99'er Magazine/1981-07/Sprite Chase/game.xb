100 REM  ****************
110 REM  * SPRITE CHASE *
120 REM  ****************
130 REM  99'ER VERSION 7.81.1XB
140 REM  BY RON BINKOWSK1
150 REM
160 REM
170 CALL CLEAR
180 PRINT "USE THE FOUR ARROW KEYS AND W,R,Z,C KEYS TO CHASE THE   LETTERS OR NUMBERS." :: PRINT
190 PRINT "YOU MUST CATCH THEM IN ALPHA NUMERIC SEQUENCE." :: PRINT
200 PRINT "PRESS 'L' FOR LARGE TARGET. 'S' FOR SMALL TARGET." :: PRINT
210 CALL KEY(0,GOT,STATUS)
220 IF STATUS=0 THEN 210
230 IF GOT=76 THEN T=16 :: CALL MAGNIFY(2)ELSE IF GOT=83 THEN T=8 ELSE 210
240 PRINT "FOR NUMBERS PRESS'N'":"FOR LETTERS PRESS 'L'." :: PRINT
250 CALL KEY(0,GOT,STATUS)
260 IF STATUS=0 THEN 250
270 IF GOT=78 THEN TARGS=10 :: CH=47 ELSE IF GOT=76 THEN TARGS=26 :: CH=64 ELSE 250
280 CALL CLEAR
290 RANDOMIZE
300 COUNT=0
310 CALL COLOR(3,2,9) :: CALL COLOR(4,2,9)
320 CALL CHAR(96,"FFFFFFFFFFFFFFFF")
330 CALL SPRITE(#28,96,2,90,120,0,0)
340 FOR A=1 TO TARGS
350 CALL SPRITE(#A,A+CH,2,90,120,INT(RND*50-25),INT(RND*50-25))
360 NEXT A
370 CALL SOUND(100,555,0)
380 FOR A=1 TO TARGS
390 CALL COLOR(#A,16)
400 CALL KEY(0,GOT,STATUS)
410 COUNT=COUNT+1
420 DISPLAY AT(24,1)SIZE(6):COUNT
430 CALL COINC(#28,#A,T,HIT)
440 IF HIT=-1 THEN 620
450 IF STATUS=0 THEN 400
460 IF GOT=69 THEN CALL MOTION(#28,-30,0) :: GOTO 540
470 IF GOT=88 THEN CALL MOTION(#28,30,0) :: GOTO 540
480 IF GOT=68 THEN CALL MOTION(#28,0,30) :: GOTO 540
490 IF GOT=83 THEN CALL MOTION(#28,0,-30) :: GOTO 540
500 IF GOT=87 THEN CALL MOTION(#28,-30,-30) :: GOTO 540
510 IF GOT=82 THEN CALL MOTION(#28,-30,30) :: GOTO 540
520 IF GOT=90 THEN CALL MOTION(#28,30,-30) :: GOTO 540
530 IF GOT=67 THEN CALL MOTION(#28,30,30) :: GOTO 540
540 CALL KEY(0,GOT,STATUS)
550 CALL COINC(#28,#A,9,HIT)
560 IF HIT=-1 THEN 620
570 COUNT=COUNT+1
580 DISPLAY AT(24,1)SIZE(6):COUNT
590 IF STATUS=-1 THEN 540
600 CALL MOTION(#28,0,0)
610 GOTO 400
620 CALL DELSPRITE(#A)
630 CALL SOUND(100,-7,0)
640 CALL MOTION(#28,0,0)
650 NEXT A
660 CALL CHARSET
670 PRINT "YOUR SCORE IS ";COUNT
680 PRINT "ENTER 'Y' TO PLAY AGAIN: "
690 CALL KEY(0,GOT,STATUS)
700 IF STATUS=0 THEN 690
710 IF GOT=89 THEN 280
720 PRINT "      BYE"
