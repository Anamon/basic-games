100 REM  BLACKJACK
110 CALL CLEAR
120 PRINT "WELCOME TO": : : : : : :"THE CYBERNETIC CASINO!": : : : : : :
130 OPTION BASE 1
140 CALL CHAR(128,"FF818181818181FF")
150 DIM DECK(52),HAND(4,7),X(3),Y(3),VALUES$(13),SUM(4)
160 FOR I=1 TO 13
170 READ VALUES$(I)
180 NEXT I
190 DATA "A","2","3","4","5","6","7","8","9","T","J","Q","K"
200 CALL CLEAR
210 FOR I=1 TO 3
220 READ X(I),Y(I)
230 NEXT I
240 DATA 4,2,20,2,4,8
250 PRINT "HOW MANY PLAYERS?(1-3):";
260 CALL KEY(3,KEY,STATUS)
270 IF STATUS=0 THEN 260
280 IF (KEY<49)+(KEY>51)THEN 260
290 NUM_PLAYERS=KEY-47
300 PRINT NUM_PLAYERS-1
310 FOR I=1 TO NUM_PLAYERS-1
320 CALL HCHAR(Y(I),X(I)-1,ASC("["))
330 CALL HCHAR(Y(I),X(I),48+I)
340 CALL HCHAR(Y(I),X(I)+1,ASC("]"))
350 NEXT I
360 FOR I=1 TO 6
370 CALL HCHAR(8,18+I,ASC(SEG$("DEALER",I,1)))
380 NEXT I
390 FOR I=1 TO 52
400 RANDOMIZE
410 DECK(I)=I-INT(I/13)*13+1
420 R=INT(RND*I)+1
430 TEMP=DECK(R)
440 DECK(R)=DECK(I)
450 DECK(I)=TEMP
460 NEXT I
470 FOR I=1 TO 2
480 FOR J=1 TO NUM_PLAYERS
490 CARD_COUNT=CARD_COUNT+1
500 HAND(J,I)=DECK(CARD_COUNT)
510 SUM(J)=SUM(J)-HAND(J,I)*(HAND(J,I)<11)-10*(HAND(J,I)>10)
520 IF J=NUM_PLAYERS THEN 550
530 T=ASC(VALUES$(HAND(J,I)))
540 CALL HCHAR(Y(J)+2,X(J)+I+I-3,T)
550 NEXT J
560 NEXT I
570 CALL HCHAR(10,19,ASC(VALUES$(HAND(NUM_PLAYERS,1))))
580 CALL HCHAR(10,21,128)
590 FOR I=1 TO NUM_PLAYERS-1
600 C=2
610 FOR J=1 TO 8
620 CALL HCHAR(14,J+3,ASC(SEG$("PLAYER #",J,1)))
630 NEXT J
640 CALL HCHAR(14,12,I+48)
650 CALL KEY(3,KEY,STATUS)
660 IF STATUS=0 THEN 650
670 KEY$=CHR$(KEY)
680 IF KEY$="H" THEN 980
690 IF KEY$<>"S" THEN 650
700 FOR J=1 TO 6
710 CALL HCHAR(14,13+J,ASC(SEG$("STANDS",J,1)))
720 NEXT J
730 FOR T=1 TO 500
740 NEXT T
750 CALL HCHAR(14,1,32,20)
760 FOR J=1 TO 7
770 IF HAND(I,J)=0 THEN 810
780 IF (HAND(I,J)<>1)+(SUM(I)>11)THEN 800
790 SUM(I)=SUM(I)+10
800 NEXT J
810 NEXT I
820 C=2
830 CALL HCHAR(10,21,ASC(VALUES$(HAND(NUM_PLAYERS,C))))
840 IF SUM(NUM_PLAYERS)>16 THEN 1080
850 IF SUM(NUM_PLAYERS)>11 THEN 900
860 FOR I=1 TO C
870 IF (HAND(NUM_PLAYERS,I)<>1)+(SUM(NUM_PLAYERS)+10<18)THEN 890
880 SUM(NUM_PLAYERS)=SUM(NUM_PLAYERS)+10
890 NEXT I
900 C=C+1
910 CARD_COUNT=CARD_COUNT+1
920 HAND(NUM_PLAYERS,C)=DECK(CARD_COUNT)
930 CALL HCHAR(10,17+C+C,ASC(VALUES$(HAND(NUM_PLAYERS,C))))
940 SUM(NUM_PLAYERS)=SUM(NUM_PLAYERS)-(HAND(NUM_PLAYERS,C)<11)*HAND(NUM_PLAYERS,C)-10*(HAND(NUM_PLAYERS,C)>10)
950 IF SUM(NUM_PLAYERS)>21 THEN 1160
960 GOTO 840
970 STOP
980 C=C+1
990 CARD_COUNT=CARD_COUNT+1
1000 HAND(I,C)=DECK(CARD_COUNT)
1010 CALL HCHAR(Y(I)+2,X(I)+C+C-3,ASC(VALUES$(HAND(I,C))))
1020 SUM(I)=SUM(I)-HAND(I,C)*(HAND(I,C)<11)-10*(HAND(I,C)>10)
1030 IF SUM(I)>21 THEN 1040 ELSE 650
1040 FOR J=1 TO 12
1050 CALL HCHAR(Y(I)+2,X(I)+J-3,ASC(SEG$(" YOU BUST   ",J,1)))
1060 NEXT J
1070 GOTO 810
1080 FOR I=1 TO NUM_PLAYERS-1
1090 IF SUM(I)>21 THEN 1140
1100 IF SUM(I)<=SUM(NUM_PLAYERS)THEN 1180
1110 FOR J=1 TO 8
1120 CALL HCHAR(Y(I)+2,X(I)+J-2,ASC(SEG$(" YOU WIN",J,1)))
1130 NEXT J
1140 NEXT I
1150 END
1160 SUM(NUM_PLAYERS)=0
1170 GOTO 1080
1180 FOR J=1 TO 8
1190 CALL HCHAR(Y(I)+2,X(I)+J-2,ASC(SEG$("YOU LOSE",J,1)))
1200 NEXT J
1210 GOTO 1140
