100 CALL CLEAR
110 PRINT "QUIZ ALERT! REV UP YOUR     BRAIN!": : : : : : : : : :
120 FOR I=1 TO 6
130 FOR J=2 TO 10 STEP 2
140 CALL SOUND(50,-4,0,(3340-J*110),0,(1620-J*110),2,J*110,0)
150 CALL SCREEN(J)
160 NEXT J
170 NEXT I
180 CALL SCREEN(13)
190 CALL CLEAR
200 RANDOMIZE
210 PRINT "THIS IS A TEST"
220 PRINT "OF YOUR ARITHMETIC ABILITY"
230 PRINT
240 PRINT "1 ... ADDITION"
250 PRINT "2 ... SUBTRACTION"
260 PRINT "3 ... MULTIPLICATION"
270 PRINT "4 ... DIVISION"
280 PRINT
290 RIGHT=0
300 INPUT "WHICH ONE? ":CHOICE
310 IF (CHOICE<1)+(CHOICE>4)+(CHOICE<>INT(CHOICE))THEN 300
320 CALL CLEAR
330 FOR Q=1 TO 10
340 PRINT :"QUESTION #";STR$(Q): :
350 ON CHOICE GOSUB 430,490,550,610
360 NEXT Q
370 PRINT : :"YOUR SCORE IS";RIGHT*10;"%": :
380 INPUT "ANOTHER QUIZ? ":QUERY$
390 IF SEG$(QUERY$,1,1)="Y" THEN 180
400 IF SEG$(QUERY$,1,1)<>"N" THEN 380
410 CALL CLEAR
420 END
430 REM ADDITION ROUTINE
440 A=INT(RND*100)+1
450 B=INT(RND*100)+1
460 ANSWER=A+B
470 PRINT A;"+";B;"=";
480 GOTO 670
490 REM SUBTRACTION ROUTINE
500 A=INT(RND*100)+1
510 B=INT(RND*100)+1
520 ANSWER=A-B
530 PRINT A;"-";B;"=";
540 GOTO 670
550 REM MULTIPLICATION ROUTINE
560 A=INT(RND*25)+1
570 B=INT(RND*25)+1
580 ANSWER=A*B
590 PRINT A;"*";B;"=";
600 GOTO 670
610 REM DIVISION ROUTINE
620 A=INT(RND*25)+1
630 B=INT(RND*25)+1
640 ANSWER=A
650 A=A*B
660 PRINT A;"/";B;"=";
670 REM GET THE RESPONSE
680 INPUT RESPONSE
690 IF RESPONSE=ANSWER THEN 730
700 CALL SOUND(400,110,0,120,0,130,0,-4,0)
710 PRINT :"SORRY, THE CORRECT ANSWER   WAS";ANSWER: :
720 RETURN
730 FOR I=1 TO 5
740 CALL SOUND(50,110*I,I,220*I,I,440*I,I)
750 NEXT I
760 PRINT :"THAT'S CORRECT!": :
770 RIGHT=RIGHT+1
780 RETURN
