100 REM SOUND/SIGHT SIMON
110 CALL CLEAR
120 RANDOMIZE
130 PRINT "PREPARE YOURSELF FOR...": : : : : : : : : : : : :
140 PRINT " SSSS IIII M   M  OO  N   N"
150 PRINT "S      II  MM MM O  O NN  N"
160 PRINT " SSS   II  M M M O  O N N N"
170 PRINT "    S  II  M   M O  O N  NN"
180 PRINT "SSSS  IIII M   M  OO  N   N": : : : : : : : : : : : : : : :
190 DIM SEQ(23),NOTE(9)
200 FOR I=1 TO 9
210 READ NOTE(I)
220 NEXT I
230 DATA 446,502,560,598,664,744,832,890,999
240 PRINT "TYPE THE DIFFICULTY LEVEL"
250 INPUT "(1 IS LOW,9 IS HIGH) ":LEVEL
260 IF (LEVEL<1)+(LEVEL>9) THEN 250
270 FOR L=LEVEL TO 9
280 CALL CLEAR
290 PRINT "YOU ARE NOW ON LEVEL";L
300 TIME=100*(10-L)
310 MAX_NOTES=L*2+5
320 FOR I=1 TO MAX_NOTES
330 SEQ(I)=INT(RND*9)+1
340 FOR J=1 TO I
350 CALL SOUND(TIME,NOTE(SEQ(J)),0)
360 CALL HCHAR(12,15,48+SEQ(J))
370 FOR T=1 TO TIME/3.7
380 NEXT T
390 NEXT J
400 CALL HCHAR(12,15,32)
410 FOR J=1 TO I
420 CALL KEY(3,A,X)
430 IF X=0 THEN 420
440 A=A-48
450 CALL SOUND(TIME,NOTE(A),0)
460 CALL HCHAR(12,15,A+48)
470 IF A<>SEQ(J) THEN 580
480 FOR T=1 TO TIME/3.7
490 NEXT T
500 NEXT J
510 CALL HCHAR(12,15,32)
520 FOR T=1 TO TIME/2
530 NEXT T
540 NEXT I
550 NEXT L
560 PRINT "YOU WIN"
570 END
580 CALL SOUND(1000,110,0)
590 PRINT "SORRY, YOU LOSE."
