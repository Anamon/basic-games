100 REM ROULETTE
110 CALL CLEAR
120 DIM COLOR$(3)
130 COLOR$(1)="RED"
140 COLOR$(2)="BLACK"
150 COLOR$(3)="GREEN"
160 MATCH$="RBGOEQ"
170 PRINT "WELCOME TO THE CASINO!": : :"HERE IS THE ROULETTE TABLE": :
180 INPUT "WHAT IS YOUR NAME? ":NAME$
190 PRINT : :"WOULD YOU CARE FOR SOME     CREDIT, ";NAME$;
200 INPUT LOAN$
210 IF SEG$(LOAN$,1,1)="Y" THEN 250
220 IF SEG$(LOAN$,1,1)<>"N" THEN 200
230 PRINT "IF YOU DON'T WANT TO PLAY,  GET OUT YOU BUM."
240 END
250 PRINT : :"WELL, ";NAME$;
260 IF DEBT>499 THEN 870
270 INPUT "HOW MUCH WOULD YOU LIKE TO  BORROW? ":LOAN
280 IF DEBT+LOAN<=5000 THEN 310
290 PRINT "SORRY, THAT EXCEEDS YOUR  CREDIT LIMIT"
300 GOTO 270
310 DEBT=DEBT+LOAN
320 CASH=LOAN+CASH
330 PRINT : :"O.K. ";NAME$:"TAKE A SEAT": :
340 PRINT "YOU HAVE $";CASH: :
350 INPUT "WHAT ARE YOU BETTING ON?    (RED, BLACK, GREEN, ODD,    EVEN, 1-36, 0, 00 OR QUIT): ":BET$
360 RANDOMIZE
370 IF SEG$(BET$,1,1)="Q" THEN 770
380 IF POS(MATCH$,SEG$(BET$,1,1),1) THEN 410
390 IF (ASC(BET$)<48)+(ASC(BET$)>57)THEN 350
400 IF (VAL(BET$)<0)+(VAL(BET$)>36)THEN 350
410 INPUT "HOW MUCH ARE YOU BETTING    (MIN = $5, MAX = $500) : ":BET
420 IF BET=0 THEN 340
430 IF (BET<5)+(BET>500)THEN 410
440 IF BET>CASH THEN 190
450 IF BET=INT(BET)THEN 480
460 PRINT "WE DON'T DEAL IN SMALL      CHANGE HERE."
470 GOTO 410
480 CASH=CASH-BET
490 R=INT(RND*74)+1
500 R1=R
510 COLOR=1-(R>36)
520 R=R-INT(R/36)*36+1
530 ODD=INT(R/2)<>R/2
540 EVEN=(ODD=0)
550 SPIN$=STR$(R)
560 IF R1<73 THEN 610
570 COLOR=3
580 SPIN$="0"
590 IF R1<74 THEN 610
600 SPIN$="00"
610 PRINT : :SPIN$;" ";COLOR$(COLOR): :
620 IF (BET$=SPIN$)+(BET$="0")*ODD+(BET$="E")*EVEN THEN 660
630 IF SEG$(BET$,1,1)=SEG$(COLOR$(COLOR),1,1) THEN 660
640 PRINT "YOU LOSE SUCKER!": :
650 GOTO 340
660 PRINT "YOU WIN!!!"
670 FOR I=1 TO 10
680 CALL SOUND(100,I*110,0)
690 NEXT I
700 IF (ASC(BET$)>47)*(ASC(BET$)<58)THEN 750
710 CASH=CASH+BET*2
720 IF SEG$(BET$,1,1)<>"G" THEN 340
730 CASH=CASH+BET*16
740 GOTO 340
750 CASH=CASH+BET*36
760 GOTO 340
770 CASH=CASH-DEBT
780 ON SGN(CASH)+2 GOTO 790,830,840
790 PRINT "YOU WILL REPAY YOUR DEBT OF $";ABS(CASH)
800 PRINT "AT 20% INTEREST -- PER WEEK."
810 PRINT "IF YOU DON'T LIKE THE TERMS SPEAK TO MY RIGHT HAND MAN  -- GUIDO THE BLADE.": :
820 PRINT "PLEASANT DREAMS..."
830 END
840 PRINT "CONGRATULATIONS! YOU'RE ONE OF THE ONLY PEOPLE TO HAVE  COME OUT SOLVENT."
850 PRINT "YOU CAME AWAY WITH $";CASH
860 END
870 PRINT "YOU HAVE REACHED YOUR CREDITLIMIT. NOW GET OUT."
880 GOTO 770
