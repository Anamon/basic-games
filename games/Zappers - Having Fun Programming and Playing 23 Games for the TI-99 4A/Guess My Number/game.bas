100 CALL CLEAR
110 PRINT "THIS GAME IS GUESS MY NUMBER"
120 PRINT "YOU SPECIFY THE RANGE OF"
130 PRINT "NUMBERS AND I'LL PICK A     RANDOM ONE AND ASK YOU TO   GUESS WHAT IT IS."
140 PRINT
150 PRINT
160 PRINT "ENTER THE RANGE"
170 INPUT "FROM -->":LOW
180 INPUT "TO   -->":HI
190 RANGE=HI-LOW+1
200 RANDOMIZE
210 X=INT(RND*RANGE)+LOW
220 P2=INT(LOG(RANGE)/LOG(2))+1
230 PRINT : :"YOU HAVE";P2;"GUESSES": :
240 FOR I=1 TO P2
250 PRINT : :
260 INPUT "PLEASE ENTER YOUR GUESS: ":GUESS
270 IF GUESS=X THEN 360
280 IF GUESS<X THEN 390 ELSE 410
290 NEXT I
300 PRINT : :"SORRY, YOU LOSE."
310 PRINT "THE ANSWER WAS ";X
320 PRINT : :"DO YOU WANT TO PLAY AGAIN?"
330 INPUT "(Y-YES, N-NO): ":MORE$
340 IF SEG$(MORE$,1,1)="Y" THEN 100
350 END
360 PRINT : :"CORRECT! YOU WIN."
370 PRINT "YOU GUESSED IT IN ";I;" TRIES"
380 GOTO 320
390 PRINT : :"TOO SMALL"
400 GOTO 290
410 PRINT : :"TOO LARGE"
420 GOTO 290
