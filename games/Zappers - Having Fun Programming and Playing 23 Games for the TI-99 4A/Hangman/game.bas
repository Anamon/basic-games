100 REM HANGMAN
110 CALL CLEAR
120 RANDOMIZE
130 OPTION BASE 1
140 DIM WORDS$(100),TEMP(20),FLAG(26)
150 N=10
160 FOR I=1 TO N
170 READ WORDS$(I)
180 NEXT I
190 FOR I=1 TO 20
200 TEMP(I)=95
210 NEXT I
220 PRINT "THIS IS HANGMAN! HERE IS    YOUR WORD: ";
230 TEMP$=WORDS$(INT(RND*N)+1)
240 L=LEN(TEMP$)
250 FOR I=1 TO L
260 PRINT CHR$(TEMP(I));
270 NEXT I
280 PRINT : :"[";SEG$("HANGMAN",1,DEATH);"]": :"LETTERS USED:";USED$: :
290 INPUT "ENTER YOUR LETTER: ":LETTER$
300 IF (LETTER$<"A")+(LETTER$>"Z")+(LEN(LETTER$)<>1)THEN 290
310 IF FLAG(ASC(LETTER$)-64)THEN 290
320 FLAG(ASC(LETTER$)-64)=-1
330 USED$=USED$&LETTER$
340 F=0
350 FOR I=1 TO L
360 IF LETTER$=SEG$(TEMP$,I,1)THEN 390
370 NEXT I
380 IF F=0 THEN 450 ELSE 250
390 F=-1
400 REM
410 TEMP(I)=ASC(LETTER$)
420 RIGHT=RIGHT+1
430 IF RIGHT=L THEN 520
440 GOTO 370
450 DEATH=DEATH+1
460 IF DEATH<7 THEN 280
470 PRINT : :"SORRY CHUMP! YOU LOSE"
480 PRINT : :"THE WORD WAS: ";TEMP$
490 END
500 DATA "KEYBOARD","COMPUTER","GAME","VERIFICATION","EDUCATE","INDUSTRIALIZE","RECREATIONAL","INTERVIEW"
510 DATA "ZONES","PRODUCT","QUOTIENT"
520 PRINT : :"YOU WIN! CONGRATULATIONS!"
530 PRINT : :"THE WORD IS:";TEMP$
