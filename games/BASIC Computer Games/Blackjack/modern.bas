1 CLS: COLOR 12
2 PRINT TAB(35); "Black Jack"
4 PRINT TAB(31); "Creative Computing"
5 PRINT TAB(29); "Morristown, New Jersey"
6 PRINT: PRINT: PRINT: RANDOMIZE TIMER: COLOR 15
10 'DEF FNA(Q)=Q+11*(Q>=22)
20 DIM P(15, 12), Q(15), C(52), D(52), T(8), S(7), B(15)
30 DIM R(15)
40 REM--P(I,J) IS THE JTH CARD IN HAND I, Q(I) IS TOTAL OF HAND I
50 REM--C IS THE DECK BEING DEALT FROM, D IS THE DISCARD PILE,
60 REM--T(I) IS THE TOTAL FOR PLAYER I, S(I) IS THE TOTAL THIS HAND FOR
70 REM--PLAYER I, B(I) IS TH BET FOR HAND I
80 REM--R(I) IS THE LENGTH OF P(I,*)
90 GOTO 1500
100 REM--SUBROUTINE TO GET A CARD.  RESULT IS PUT IN X.
110 IF C < 51 THEN 230
120 PRINT "Reshuffling..."
130 FOR D = D TO 1 STEP -1
140 C = C - 1
150 C(C) = D(D)
160 NEXT D
170 FOR C1 = 52 TO C STEP -1
180 C2 = INT(RND(1) * (C1 - C + 1)) + C
190 C3 = C(C2)
200 C(C2) = C(C1)
210 C(C1) = C3
220 NEXT C1
230 X = C(C)
240 C = C + 1
250 RETURN
300 REM--SUBROUTINE TO EVALUATE HAND I.  TOTAL IS PUT INTO
310 REM--Q(I). TOTALS HAVE THE FOLLOWING MEANING:
320 REM--  2-10...HARD 2-10
330 REM-- 11-21...SOFT 11-21
340 REM-- 22-32...HARD 11-21
350 REM--  33+....BUSTED
360 Q = 0
370 FOR Q2 = 1 TO R(I)
380 X = P(I, Q2)
390 GOSUB 500
400 NEXT Q2
410 Q(I) = Q
420 RETURN
500 REM--SUBROUTINE TO ADD CARD X TO TOTAL Q.
510 X1 = X: IF X1 > 10 THEN X1 = 10: REM  SAME AS X1=10 MIN X
520 Q1 = Q + X1
530 IF Q >= 11 THEN 590
540 IF X > 1 THEN 570
550 Q = Q + 11
560 RETURN
570 Q = Q1 - 11 * (Q1 >= 11)
580 RETURN
590 Q = Q1 - (Q <= 21 AND Q1 > 21)
600 IF Q < 33 THEN 620
610 Q = -1
620 RETURN
700 REM--CARD PRINTING SUBROUTINE
710 REM  D$ DEFINED ELSEWHERE
720 PRINT MID$(D$, 3 * X - 2, 3);
740 RETURN
750 REM--ALTERNATIVE PRINTING ROUTINE
760 PRINT " "; MID$(D$, 3 * X - 1, 2);
770 PRINT " ";
780 RETURN
800 REM--SUBROUTINE TO PLAY OUT A HAND.
810 REM--NO SPLITTING OR BLACKJACKS ALLOWED
820 H1 = 5
830 GOSUB 1410
840 H1 = 3
850 ON H GOTO 950, 930
860 GOSUB 100
870 B(I) = B(I) * 2
880 PRINT "Received a";
890 GOSUB 700
900 GOSUB 1100
910 IF Q > 0 THEN GOSUB 1300
920 RETURN
930 GOSUB 1320
940 RETURN
950 GOSUB 100
960 PRINT "Received ";
970 GOSUB 700
980 GOSUB 1100
990 IF Q < 0 THEN 940
1000 COLOR 10: PRINT " ...hit or stand (H/S)";
1010 GOTO 830
1100 REM--SUBROUTINE TO ADD A CARD TO ROW I
1110 R(I) = R(I) + 1
1120 P(I, R(I)) = X
1130 Q = Q(I)
1140 GOSUB 500
1150 Q(I) = Q
1160 IF Q >= 0 THEN 1190
1170 PRINT " ...busted."
1180 GOSUB 1200
1190 RETURN
1200 REM--SUBROUTINE TO DISCARD ROW I
1210 IF R(I) <> 0 THEN 1230
1220 RETURN
1230 D = D + 1
1240 D(D) = P(I, R(I))
1250 R(I) = R(I) - 1
1260 GOTO 1210
1300 REM--PRINTS TOTAL OF HAND I
1310 PRINT
1320 AA = Q(I): GOSUB 3400
1325 PRINT "Total is"; STR$(AA); "."
1330 RETURN
1400 REM--SUBROUTINE TO READ REPLY
1410 REM  I$ DEFINED ELSEWHERE
1420 INPUT H$: COLOR 15: H$ = LEFT$(H$, 1)
1430 FOR H = 1 TO H1 STEP 2
1435 ZU$ = MID$(I$, H, 1): IF ZU$ >= "A" AND ZU$ <= "Z" THEN ZL$ = CHR$(ASC(ZU$) + 32) ELSE ZL$ = ZU$
1440 IF H$ = ZU$ OR H$ = ZL$ THEN 1480
1450 NEXT H
1460 PRINT "Type "; MID$(I$, 1, H1 - 1); " or "; MID$(I$, H1, 2); " please."
1470 GOTO 1420
1480 H = (H + 1) / 2
1490 RETURN
1500 REM--PROGRAM STARTS HERE
1510 REM--INITIALIZE
1520 D$ = " A  2  3  4  5  6  7  8  9  10 J  Q  K  "
1530 I$ = "H,S,D,/,"
1540 FOR I = 1 TO 13
1550 FOR J = 4 * I - 3 TO 4 * I
1560 D(J) = I
1570 NEXT J
1580 NEXT I
1590 D = 52
1600 C = 53
1610 COLOR 10: PRINT "Do you want instructions (Y/N)";
1620 INPUT H$: COLOR 15
1630 IF LEFT$(H$, 1) = "N" OR LEFT$(H$, 1) = "n" THEN 1760
1635 PRINT: COLOR 14
1640 PRINT "This is the game of 21.  As many as 7 players may play the game.  On each deal,"
1650 PRINT "bets will be asked for, and the players' bets should be typed in.  The cards"
1660 PRINT "will then be dealt, and each player in turn plays their hand.  The first"
1670 PRINT "response should be one of:"
1680 PRINT: COLOR 11
1690 PRINT "    D - indicating that the player is doubling down"
1700 PRINT "    S - indicating that they are standing"
1710 PRINT "    H - indicating they want another card"
1720 PRINT "    / - indicating they want to split their cards"
1730 PRINT: COLOR 14
1740 PRINT "After the initial respose, all further resposes should be 'S' or 'H', unless"
1745 PRINT "cards were split, in which case doubling down is again permitted.  In order to"
1750 PRINT "collect for blackjack, the initial response should be 'S'."
1755 PRINT: COLOR 15
1760 COLOR 10: PRINT "Number of players (1-7)";
1770 INPUT N: COLOR 15
1775 PRINT
1780 IF N < 1 OR N > 7 OR N > INT(N) THEN 1760
1790 FOR I = 1 TO 8: T(I) = 0: NEXT I
1800 D1 = N + 1
1810 IF 2 * D1 + C >= 52 THEN GOSUB 120
1820 IF C = 2 THEN C = C - 1
1830 FOR I = 1 TO N: Z(I) = 0: NEXT I
1840 FOR I = 1 TO 15: B(I) = 0: NEXT I
1850 FOR I = 1 TO 15: Q(I) = 0: NEXT I
1860 FOR I = 1 TO 7: S(I) = 0: NEXT I
1870 FOR I = 1 TO 15: R(I) = 0: NEXT I
1880 PRINT
1890 FOR I = 1 TO N: ZZ$ = STR$(I): ZZ$ = RIGHT$(ZZ$, LEN(ZZ$) - 1): COLOR 10: PRINT "Bet #"; ZZ$;: INPUT Z(I): COLOR 15: NEXT I
1900 FOR I = 1 TO N
1910 IF Z(I) <= 0 OR Z(I) > 500 THEN 1880
1920 B(I) = Z(I)
1930 NEXT I
1940 PRINT
1950 FOR I = 1 TO N
1960 PRINT TAB(I * 10); "Player"; I;
1970 NEXT I
1980 PRINT TAB(N * 10 + 10); "Dealer"
1990 FOR J = 1 TO 2
2010 FOR I = 1 TO D1
2020 GOSUB 100
2030 P(I, J) = X
2040 IF J = 1 OR I <= N THEN PRINT TAB(I * 10);: COLOR 13: GOSUB 750: COLOR 15
2050 NEXT I
2060 PRINT
2070 NEXT J
2080 FOR I = 1 TO D1
2090 R(I) = 2
2100 NEXT I
2110 REM--TEST FOR INSURANCE
2120 IF P(D1, 1) > 1 THEN 2240
2130 PRINT: COLOR 10: PRINT "Any insurance (Y/N)";
2140 INPUT H$: COLOR 15
2150 IF LEFT$(H$, 1) <> "Y" AND LEFT$(H$, 1) <> "y" THEN 2240
2160 REM
2170 FOR I = 1 TO N: ZZ$ = STR$(I): ZZ$ = RIGHT$(ZZ$, LEN(ZZ$) - 1): COLOR 10: PRINT "Insurance Bet #"; ZZ$;: INPUT Z(I): COLOR 15: NEXT I
2180 FOR I = 1 TO N
2190 IF Z(I) < 0 OR Z(I) > B(I) / 2 THEN 2160
2200 NEXT I
2210 FOR I = 1 TO N
2220 S(I) = Z(I) * (3 * (-(P(D1, 2) >= 10)) - 1)
2230 NEXT I
2240 REM--TEST FOR DEALER BLACKJACK
2250 L1 = 1: L2 = 1
2252 IF P(D1, 1) = 1 AND P(D1, 2) > 9 THEN L1 = 0: L2 = 0
2253 IF P(D1, 2) = 1 AND P(D1, 1) > 9 THEN L1 = 0: L2 = 0
2254 IF L1 <> 0 OR L2 <> 0 THEN 2320
2260 PRINT: PRINT "Dealer has a"; MID$(D$, 3 * P(D1, 2) - 2, 3); " in the hole for blackjack."
2280 FOR I = 1 TO D1
2290 GOSUB 300
2300 NEXT I
2310 GOTO 3140
2320 REM--NO DEALER BLACKJACK
2330 IF P(D1, 1) > 1 AND P(D1, 1) < 10 THEN 2350
2340 PRINT: PRINT "No dealer blackjack."
2350 REM--NOW PLAY THE HANDS
2360 FOR I = 1 TO N
2370 COLOR 10: PRINT: PRINT "Player"; STR$(I); " (D,S,H,/)";
2380 H1 = 7
2390 GOSUB 1410
2400 ON H GOTO 2550, 2410, 2510, 2600
2410 REM--PLAYER WANTS TO STAND
2420 GOSUB 300
2430 IF Q(I) <> 21 THEN 2490
2440 PRINT "Blackjack!"
2450 S(I) = S(I) + 1.5 * B(I)
2460 B(I) = 0
2470 GOSUB 1200
2480 GOTO 2900
2490 GOSUB 1320
2500 GOTO 2900
2510 REM--PLAYER WANTS TO DOUBLE DOWN
2520 GOSUB 300
2530 GOSUB 860
2540 GOTO 2900
2550 REM--PLAYER WANTS TO BE HIT
2560 GOSUB 300
2570 H1 = 3
2580 GOSUB 950
2590 GOTO 2900
2600 REM--PLAYER WANTS TO SPLIT
2610 L1 = P(I, 1): IF P(I, 1) > 10 THEN L1 = 10
2612 L2 = P(I, 2): IF P(I, 2) > 10 THEN L2 = 10
2614 IF L1 = L2 THEN 2640
2620 PRINT "Splitting not allowed."
2630 GOTO 2370
2640 REM--PLAY OUT SPLIT
2650 I1 = I + D1
2660 R(I1) = 2
2670 P(I1, 1) = P(I, 2)
2680 B(I + D1) = B(I)
2690 GOSUB 100
2700 PRINT "First hand receives a";
2710 GOSUB 700
2720 P(I, 2) = X
2730 GOSUB 300
2740 PRINT
2750 GOSUB 100
2760 PRINT "Second hand receives a";
2770 I = I1
2780 GOSUB 700
2790 P(I, 2) = X
2800 GOSUB 300
2810 PRINT
2820 I = I1 - D1
2830 IF P(I, 1) = 1 THEN 2900
2840 REM--NOW PLAY THE TWO HANDS
2850 PRINT "Hand"; 1 - (I > D1);
2860 GOSUB 800
2870 I = I + D1
2880 IF I = I1 THEN 2850
2890 I = I1 - D1
2900 NEXT I
2910 GOSUB 300
2920 REM--TEST FOR PLAYING DEALER'S HAND
2930 FOR I = 1 TO N
2940 IF R(I) > 0 OR R(I + D1) > 0 THEN 3010
2950 NEXT I
2960 PRINT "Dealer had a";
2970 X = P(D1, 2)
2980 GOSUB 700
2990 PRINT "concealed."
3000 GOTO 3140
3010 PRINT: PRINT "Dealer has "; MID$(D$, 3 * P(D1, 2) - 2, 3); "concealed ";
3020 I = D1
3030 AA = Q(I): GOSUB 3400
3035 PRINT "for a total of"; STR$(AA); "."
3040 IF AA > 16 THEN 3130
3050 PRINT "draws";
3060 GOSUB 100
3070 GOSUB 750
3080 GOSUB 1100
3090 AA = Q: GOSUB 3400
3095 IF Q > 0 AND AA < 17 THEN 3060
3100 Q(I) = Q - (Q < 0) / 2
3110 IF Q < 0 THEN 3140
3120 AA = Q: GOSUB 3400
3125 PRINT "---Total is"; STR$(AA); "."
3130 PRINT
3140 REM--TALLY THE RESULT
3150 REM
3160 Z$ = "loses pushes wins "
3165 PRINT
3170 COLOR 11: FOR I = 1 TO N
3180 AA = Q(I): GOSUB 3400
3182 AB = Q(I + D1): GOSUB 3410
3184 AC = Q(D1): GOSUB 3420
3186 S(I) = S(I) + B(I) * SGN(AA - AC) + B(I + D1) * SGN(AB - AC)
3188 B(I + D1) = 0
3200 PRINT "Player"; I;
3210 PRINT MID$(Z$, SGN(S(I)) * 6 + 7, 6); " ";
3220 IF S(I) <> 0 THEN 3250
3230 PRINT "      ";
3240 GOTO 3260
3250 PRINT ABS(S(I));
3260 T(I) = T(I) + S(I)
3270 PRINT "Total ="; T(I)
3280 GOSUB 1200
3290 T(D1) = T(D1) - S(I)
3300 I = I + D1
3310 GOSUB 1200
3320 I = I - D1
3330 NEXT I
3340 PRINT "Dealer's total ="; T(D1)
3345 PRINT: COLOR 15
3350 GOSUB 1200
3360 GOTO 1810
3400 AA = AA + 11 * (AA >= 22): RETURN
3410 AB = AB + 11 * (AB >= 22): RETURN
3420 AC = AC + 11 * (AC >= 22): RETURN

