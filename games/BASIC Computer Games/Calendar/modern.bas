5 CLS: COLOR 12
10 PRINT TAB(36); "Calendar"
20 PRINT TAB(31); "Creative Computing"
25 PRINT TAB(29); "Morristown, New Jersey"
30 PRINT: PRINT: PRINT: COLOR 15
40 COLOR 13: PRINT "(Press any key to generate a 1979 monthly calendar.)"
50 Z$ = INKEY$: IF Z$ = "" THEN 50
100 REM     VALUES FOR 1979 - SEE NOTES
110 DIM M(12)
120 FOR I = 1 TO 6: PRINT CHR$(10);: NEXT I
130 D = -1: REM 1979 STARTS ON MONDAY (0=SUN, -1=MON, -2=TUES...)
140 S = 0
150 REM     READ DAYS OF EACH MONTH
160 FOR N = 0 TO 12: READ M(N): NEXT N
170 REM
180 FOR N = 1 TO 12
190 CLS: PRINT: PRINT: S = S + M(N - 1)
200 COLOR 11: PRINT CHR$(205); CHR$(181); S; TAB(8);
210 PRINT CHR$(198); STRING$(16, 205); CHR$(181);
220 ON N GOTO 230, 240, 250, 260, 270, 280, 290, 300, 310, 320, 330, 340
230 PRINT " January ";: GOTO 350
240 PRINT " February";: GOTO 350
250 PRINT "  March  ";: GOTO 350
260 PRINT "  April  ";: GOTO 350
270 PRINT "   May   ";: GOTO 350
280 PRINT "   June  ";: GOTO 350
290 PRINT "   July  ";: GOTO 350
300 PRINT "  August ";: GOTO 350
310 PRINT "September";: GOTO 350
320 PRINT " October ";: GOTO 350
330 PRINT " November";: GOTO 350
340 PRINT " December";
350 PRINT CHR$(198); STRING$(16, 205); CHR$(181);
360 PRINT 365 - S; CHR$(198); CHR$(205): COLOR 15
370 REM   366-S;     ON LEAP YEARS
380 COLOR 10: PRINT: PRINT: PRINT "       S       M       T       W";
390 PRINT "       T       F       S"
410 PRINT STRING$(59, 196): COLOR 15
420 REM
430 FOR W = 1 TO 6
440 PRINT
460 REM
470 FOR G = 1 TO 7
480 D = D + 1
490 D2 = D - S
500 IF D2 > M(N) THEN 580
510 IF D2 > 0 THEN PRINT TAB(8 * G - 1); D2;
530 NEXT G: PRINT
540 REM
550 IF D2 = M(N) THEN 590
560 NEXT W
570 REM
580 D = D - G
585 COLOR 13: PRINT: PRINT: PRINT: PRINT "(Press any key.)": COLOR 15
586 Z$ = INKEY$: IF Z$ = "" THEN 586
590 NEXT N
600 REM
610 FOR I = 1 TO 6: PRINT CHR$(10);: NEXT I
620 DATA 0,31,28,31,30,31,30,31,31,30,31,30,31
630 REM  0,31,29,  ..., ON LEAP YEARS
640 END

